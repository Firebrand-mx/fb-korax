//------------------------------------------------------------------------------
actor KRPGDragon : IntelligentActor replaces Dragon
{
	Health 640
	Radius 20
	Height 65
	Mass 99999
	Speed 10
	PainChance 128
	Monster
	+NoGravity
	+Float
	+NoBlood
	+Boss
	+NoTarget
	+FullVolActive
	+NoIceDeath
	SeeSound "DragonSight"
	ActiveSound "DragonActive"
	AttackSound "DragonAttack"
	PainSound "DragonPain"
	DeathSound "DragonDeath"
	Obituary "$ob_dragon"

	action native A_DragonInitFlight();
	action native A_DragonFlapAI();
	action native A_DragonFlightAI();
	action native A_DragonPain();

	states
	{
	Spawn:
		DRAG D 10 A_LookAI
		Loop
	See:
		DRAG A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		DRAG CB 5
		DRAG A 5 A_DragonInitFlight
		DRAG B 3 A_DragonFlapAI
		DRAG BCCDDCCBBAA 3 A_DragonFlightAI
		Goto See+3
	SeeDamaged:
		DRGD CB 5
		DRGD A 5 A_DragonInitFlight
		DRGD B 3 A_DragonFlapAI
		DRGD BCCDDCCBBAA 3 A_DragonFlightAI
		Goto SeeDamaged+3
	Missile:
		DRAG A 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		DRAG E 8 A_CustomMissile("DragonFireball", 32, 0)
		Goto See+3
	MissileDamaged:
		DRGD E 8 A_CustomMissile("DragonFireball", 32, 0)
		Goto SeeDamaged+3
	Pain:
		DRGD A 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		DRAG F 10 A_DragonPain
		Goto See+3
	PainDamaged:
		DRGD F 10 A_DragonPain
		Goto SeeDamaged+3
	Death:
		DRAG G 5 A_Scream
		DRAG H 4 A_NoBlocking
		DRAG I 4
		DRAG J 4 A_CheckFloor("Crash")
		Wait
	Crash:
		DRAG KL 5
		DRAG M -1
		Stop
	}
}
