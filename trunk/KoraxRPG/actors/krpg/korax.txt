//------------------------------------------------------------------------------
actor KRPGKorax : IntelligentActor replaces Korax
{
	RenderStyle Translucent
	Alpha 0.4
	+Shadow
	+Ghost
	Health 50000
	Radius 65
	Height 115
	Mass 2000
	Speed 10
	PainChance 20
	Damage 150
	Experience 50000
	Monster
	+FloorClip
	+Telestomp
	+Boss
//	+NoTarget
//	+FullVolActive
	+NoIceDeath
	+CanJump
	+CanLeap
	+CanBackpedal
	+CanSidemove
	+CanBerserk

	SeeSound "KoraxSight"
	ActiveSound "KoraxActive"
	AttackSound "KoraxAttack"
	PainSound "KoraxPain"
	DeathSound "KoraxDeath"
	Obituary "$ob_korax"

	action native A_KoraxChase();
	action native A_KoraxDecide();
	action native A_KoraxMissile();
	action native A_KoraxCommand();
	action native A_KoraxBonePop();

	states
	{
	Spawn:
		KORX A 5 A_LookAI
		Loop
	See:
		KORX A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		KORX A 0 A_PlaySoundEx("KoraxStep", "Body", 0, 1)
		KORX A 3 A_ChaseAI
		KORX AAA 3 A_KoraxChase
		KORX B 3 A_ChaseAI
		KORX BBB 3 A_KoraxChase
		KORX C 0 A_PlaySoundEx("KoraxStep", "Body", 0, 1)
		KORX C 3 A_ChaseAI
		KORX CCC 3 A_KoraxChase
		KORX D 3 A_ChaseAI
		KORX DDD 3 A_KoraxChase
		Loop
	SeeDamaged:
		KOD1 A 0 A_PlaySoundEx("KoraxStep", "Body", 0, 1)
		KOD1 A 3 A_ChaseAI
		KOD1 AAA 3 A_KoraxChase
		KOD1 B 3 A_ChaseAI
		KOD1 BBB 3 A_KoraxChase
		KOD1 C 0 A_PlaySoundEx("KoraxStep", "Body", 0, 1)
		KOD1 C 3 A_ChaseAI
		KOD1 CCC 3 A_KoraxChase
		KOD1 D 3 A_ChaseAI
		KOD1 DDD 3 A_KoraxChase
		Loop
	Pain:
		KORX A 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		KORX H 5 A_KRPGPain
		KORX H 5
		Goto See+2
	PainDamaged:
		KORX H 5 A_KRPGPain
		KORX H 5
		Goto SeeDamaged+1
	Missile:
		KORX A 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		KORX E 2 Bright A_FaceTarget
		KORX E 5 Bright A_KoraxDecide
		Wait
	MissileDamaged:
		KOD1 E 2 Bright A_FaceTarget
		KOD1 E 5 Bright A_KoraxDecide
		Wait
	Attack:
		KORX A 0 A_JumpIf(Health < default_health / 2, "AttackDamaged")
		KORX E 4 Bright A_FaceTarget
		KORX F 8 Bright A_KoraxMissile
		KORX E 8 Bright
		Goto See+2
	AttackDamaged:
		KOD1 E 4 Bright A_FaceTarget
		KOD1 F 8 Bright A_KoraxMissile
		KOD1 E 8 Bright
		Goto SeeDamaged+1
	Command:
		KORX A 0 A_JumpIf(Health < default_health / 2, "CommandDamaged")
		KORX E 5 Bright A_FaceTarget
		KORX W 10 Bright A_FaceTarget
		KORX G 15 Bright A_KoraxCommand
		KORX W 10 Bright
		KORX E 5 Bright
		Goto See+2
	CommandDamaged:
		KOD1 E 5 Bright A_FaceTarget
		KOD1 W 10 Bright A_FaceTarget
		KOD1 G 15 Bright A_KoraxCommand
		KOD1 W 10 Bright
		KOD1 E 5 Bright
		Goto SeeDamaged+1
	Death:
		KORX I 5
		KORX J 5 A_FaceTarget
		KORX K 5 A_Scream
		KORX LMNOP 5
		KORX Q 10
		KORX R 5 A_KoraxBonePop
		KORX S 5 A_NoBlocking
		KORX TU 5
		KORX V -1
		Stop
	}
}
