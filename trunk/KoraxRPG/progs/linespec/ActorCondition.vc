//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ActorCondition : SpellInstance
	abstract;


// notified events
enum
{
	notify_RecepientDead,	// Actor = killer, param - unused
	notify_ManaUsed,		// Actor - unused, param = mana amount
	notify_LevelChange		// Actor - self, param - unused
};


Actor	ARecepient;
bool	bDoFromActorTick;	// this AC's DoDelegate delegate should be called
							// by Actor inside Actor::Tick.
bool	bPlayerSpecific;	// this AC has a specific action over Player actors;
							// in particular, this means that for players
							// DoDelegate delegate should be called from
							// Player::Tick and not from Actor::Tick.

delegate void DoDelegate(float deltaTime);

string  CastMessage;

float	MaxAccumulatedLifePerLevel;

enum
{
	CSHIFT_OP_NONE,
	CSHIFT_OP_SET,
	CSHIFT_OP_OR,
	CSHIFT_OP_AND
};

int		CShiftOperation;		// Logical operation to use when modifying player's CShift
int		CShift;					// CShift value to set when active
bool	bCShiftFadeOverTime;	// decrease cshift amount all the time
bool	bCShiftFadeBeforeDeath;	// decrease cshift amount just before spell dies

//==========================================================================
//
//	Tick
//
//==========================================================================

void Tick(float deltaTime)
{
	if (!ARecepient)
	{
		//FatalError("ActorCondition::Tick : no reference to Recepient actor, bad algorythm?");
		dprint("ActorCondition::Tick %n : no reference to Recepient actor, bad algorythm?", GetClassName(Class));
		//Dispell();
		bShouldDestroy = true;
		//return;
	}

	if (ConditionMark)
	{
		StatMark(ConditionMark).LifeTime = LifeTime;
	}

	::Tick(deltaTime);
}

//==========================================================================
//
//	CanBeCastUpon
//
//==========================================================================

bool CanBeCastUpon(Actor recepient)
{
	return recepient.Health > 0;
}

//==========================================================================
//
//	MakeCShift
//
//==========================================================================

void MakeCShift(int * pCShift)
{
	int Amount;

	if (bCShiftFadeOverTime)
		Amount = ftoi(LifeTime / LifeDuration * 256.0);
	else if (bCShiftFadeBeforeDeath && LifeTime / LifeDuration <= 0.25)
		Amount = ftoi((LifeTime / (LifeDuration * 0.25) * 256.0));
	else
		Amount = 0xFF;

	if (Amount > ((CShift >> 24) & 0x000000FF))
		Amount = (CShift >> 24) & 0x000000FF;
	Amount <<= 24;
	Amount |= CShift & 0x00FFFFFF;

	if (CShiftOperation != CSHIFT_OP_NONE)
	{
		switch (CShiftOperation)
		{
		case CSHIFT_OP_SET: *pCShift = Amount; break;
		case CSHIFT_OP_OR: *pCShift |= Amount; break;
		case CSHIFT_OP_AND: *pCShift &= Amount; break;
		}
	}
}

//==========================================================================
//
//	Notify
//
//==========================================================================

void Notify(int notification, EntityEx E, int param)
{
	switch (notification)
	{
		// Common AC behavior is to dispell when recepient dies
		case notify_RecepientDead:
			//Dispell();
			Deactivate();
			break;
		case notify_LevelChange:
			Deactivate();
			break;
	}
}

//==========================================================================
//
//	SpawnConditionMark
//
//==========================================================================

void SpawnConditionMark()
{
	int i, j = 1;
	float x = 0.0;
	float z = 0.0;

	// [FB] Before spawning the StatMark, we will check for all the current
	//		conditions affecting this actor, so that we "order" the spawn
	//		origin first
	for (i = 0; i < ARecepient.Conditions.Num; i++)
	{
		if (j > 4)
		{
			// [FB] Reset the j counter to 1 and increase z offset
			z += 8.0;
			j = 1;
		}

		// Check how many conditions we have, so that we determine the order
		// in which status marks will appear
		switch(j)
		{
			case 1:
			{
				x = -8.0;
				j++;
				break;
			}
			case 2:
			{
				x = 0.0;
				j++;
				break;
			}
			case 3:
			{
				x = 8.0;
				j++;
				break;
			}
			case 4:
			{
				x = 16.0;
				j++;
				break;
			}
		}

		if (ARecepient.Conditions[i] == self)
		{
			// [FB] Spawn the StatMark above the actors head, this mark will follow
			//		the actor for the duration of the spell
			if (AInstigator)
			{
				ConditionMark = Spawn(FindClassLowerCase(StrToName(strlwr(SMark))),
					vector(AInstigator.Origin.x, AInstigator.Origin.y,
					((AInstigator.Origin.z + AInstigator.Height + 2.0) - AInstigator.FloorClip)));

				if (ConditionMark)
				{
					StatMark(ConditionMark).Owner = ARecepient;
					StatMark(ConditionMark).LifeTime = LifeTime;
					StatMark(ConditionMark).LifeDuration = LifeDuration;
					StatMark(ConditionMark).OffsetX = x;
					StatMark(ConditionMark).OffsetZ = z;
				}
			}
		}
	}
}

//==========================================================================
//
//	Activate
//
//==========================================================================

void Activate()
{
	::Activate();

	// Set default DoDelegate
	if (bDoFromActorTick)
	{
		DoDelegate = Do;
	}

	// [FB] Disabled by Ramborc's request...
	/*if (!ConditionMark)
	{
		SpawnConditionMark();
	}*/
}

/*//==========================================================================
//
//	Dispell
//
//==========================================================================

void Dispell()
{
	if (!ARecepient)
		FatalError("ActorCondition::Dispell : no reference to Recepient actor, bad algorythm?");

	Deactivate();
	Destroy();
}*/


defaultproperties
{
	bDontCallDo = true;	// Each AC should have this flag explicitly set to false if Do calls are needed

	CastMessage = "";
	MaxAccumulatedLifePerLevel = -1.0;
}
