//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		    ######  #####   ######
//**	            ##   ## ##  ## ##    ##
//**	     	    ##   ## ##  ## ##   
//**	            #####   #####  ##  ####
//**	            ## ###  ##     ###   ##
//**	     	    ##  ### ##      ######
//**
//**    $Id: template.vc 1638 2008-08-28 19:10:17Z firebrand_kh $
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class KRPGMenuScreenCharacter : MenuScreen;

int i_speed;
int i_health;
int i_spiritpower;
int i_strength;
int i_vitality;
int i_intelligence;
int i_wisdom;

int Class;

string sp_wording[HexenDefs::NUMCLASSES];

//==========================================================================
//
//	OnDraw
//
//==========================================================================

void OnDraw()
{
	int i;
	int currentY = ChoicesStartY;

	::OnDraw();

	SetFont('bigfont');
	DrawText(ChoicesStartX, currentY, va("%s", sp_wording[Class]));
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_spiritpower));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "HEALTH:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_health));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "SPEED:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_speed));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "STRENGTH:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_strength));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "VITALITY:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_vitality));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "INTELLIGENCE:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_intelligence));
	currentY += 17;
	DrawText(ChoicesStartX, currentY, "WISDOM:");
	DrawText(ChoicesStartX * 3, currentY, va("%i", i_wisdom));
}

//==========================================================================
//
//	SetClass
//
//==========================================================================

void SetClass(int PClass)
{
	Class = PClass;
}

//==========================================================================
//
//	DefaultRollStates
//
//==========================================================================

void DefaultRollStats(Object Sender)
{
	// [FB] Using a value of 0 will make the stats be built with the default values
	i_speed = 0;
	i_health = 0;
	i_spiritpower = 0;
	i_strength = 0;
	i_vitality = 0;
	i_intelligence = 0;
	i_wisdom = 0;
}

//==========================================================================
//
//	RerollStats
//
//==========================================================================

void RerollStats(Object Sender)
{
	switch(Class)
	{
		case HexenDefs::PCLASS_FIGHTER:
			i_speed = 6 + (P_Random() & 6);
			i_health = 100 - i_strength - i_vitality - i_speed;
			i_spiritpower = 3 + (P_Random() & 2);
			i_strength = 6 + (P_Random() & 3);
			i_vitality = 1 + (P_Random() & 2);
			i_intelligence = 1 + (P_Random() & 2);
			i_wisdom = 1 + (P_Random() & 2);
			break;
		case HexenDefs::PCLASS_CLERIC:
			i_speed = 2 + (P_Random() & 6);
			i_health = 66 - i_strength - i_vitality - i_speed;
			i_spiritpower = 80 - i_health;
			i_strength = 2 + (P_Random() & 4);
			i_vitality = 2 + (P_Random() & 4);
			i_intelligence = 2 + (P_Random() & 4);
			i_wisdom = 2 + (P_Random() & 4);
			break;
		case HexenDefs::PCLASS_MAGE:
			i_speed = 1 + (P_Random() & 5);
			i_health = 40 - i_strength - i_vitality - i_speed;
			i_spiritpower = 80 - i_health;
			i_strength = 1 + (P_Random() & 3);
			i_vitality = 6 + (P_Random() & 6);
			i_intelligence = 6 + (P_Random() & 6);
			i_wisdom = 6 + (P_Random() & 6);
			break;
	}
}

//==========================================================================
//
//	BuildCharacter
//
//==========================================================================

void BuildCharacter(Object Sender)
{
	KRPGPlayerReplicationInfo RepInfo;

	// [FB] We need to store these somewhere, it's not the most suited place IMO,
	//	but here it goes....
	if (ClGame.Game)
	{
		MainGameInfo(ClGame.Game).i_health = i_health;
		MainGameInfo(ClGame.Game).i_spiritpower = i_spiritpower;
		MainGameInfo(ClGame.Game).i_strength = i_strength;
		MainGameInfo(ClGame.Game).i_vitality = i_vitality;
		MainGameInfo(ClGame.Game).i_intelligence = i_intelligence;
		MainGameInfo(ClGame.Game).i_wisdom = i_wisdom;
		MainGameInfo(ClGame.Game).i_speed = i_speed;
	}
	PClassItemActivate(Sender);
}

//==========================================================================
//
//	CreateChoices
//
//==========================================================================

void CreateChoices()
{
	MenuTextButton		Btn;
	MenuChoiceEnum		Enum;
	int YPos = ChoicesStartY + (17 * 8);

	SetFont('BigFont');
	RerollStats(self);

	Btn = MenuTextButton(NewChild(MenuBigTextButton));
	Btn.SetOrigin(ChoicesStartX, YPos);
	Btn.Text = "Default Build";
	Btn.TextColourFocus = CR_YELLOW;
	Btn.Activated = DefaultRollStats;
	YPos += Btn.Height;
	
	Btn = MenuTextButton(NewChild(MenuBigTextButton));
	Btn.SetOrigin(ChoicesStartX, YPos);
	Btn.Text = "Reroll";
	Btn.TextColourFocus = CR_YELLOW;
	Btn.Activated = RerollStats;
	YPos += Btn.Height;

	Enum = MenuChoiceEnumBig(NewChild(MenuChoiceEnumBig));
	Enum.SetOrigin(ChoicesStartX, YPos);
	Enum.Text = "Experience Modifier";
	Enum.bLeftAlign = false;
	Enum.AddItem("50%");
	Enum.AddItem("100%");
	Enum.AddItem("125%");
	Enum.AddItem("150%");
	Enum.AddItem("200%");
	Enum.SetInitialValue(GetCvar('ExpModifier'));
	Enum.ValueChanging = UpdateModifier;
	YPos += Enum.Height;

	Btn = MenuTextButton(NewChild(MenuBigTextButton));
	Btn.SetOrigin(ChoicesStartX, YPos);
	Btn.Text = "OK";
	Btn.TextColourFocus = CR_YELLOW;
	Btn.Activated = BuildCharacter;
}

//==========================================================================
//
//	UpdateModifier
//
//==========================================================================

bool UpdateModifier(Object Sender, int newValue)
{
	SetCvar('ExpModifier', newValue);
	return true;
}

//==========================================================================
//
//	PClassItemActivate
//
//==========================================================================

void PClassItemActivate(Object Sender)
{
	ClientGameShared(ClGame).SetPClass(Class);
}

defaultproperties
{
	ChoicesStartX = 66;
	ChoicesStartY = 30;
	Height = 260;
	SelectorType = MenuSelector_Big;
	Title = "GENERATE YOUR CHARACTER:";

	sp_wording[0] = "BATTLE RAGE";
	sp_wording[1] = "FAITH";
	sp_wording[2] = "MAGIC";
	sp_wording[3] = "";
}
