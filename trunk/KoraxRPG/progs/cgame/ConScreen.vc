//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ConScreen : KRPGMenuScreen;


struct cue_t
{
	string				SpeakerName;
	//string			SpeechText;
	array<SplitLine>	SpeechLines;
};

//	Background
string				NPCName;
int					hBackPic;	// registered pic handle
name				Flat;

//	Speech
array<cue_t>		Cues;
int					MaximalCueLines;

// Choices
ConScrContinue		ChoiceContinue;
array<ConScrChoice>	Choices;

// Customization
bool				bDontDrawBigCharacterName;
bool				bDontDuplicateNPCName;
int					CueLinesSpacing;


//==========================================================================
//
//	Init
//
//==========================================================================

void Init()
{
	ClGame.GRoot.SetMouse(false);
	ChoiceContinue = ConScrContinue(NewChild(ConScrContinue));
	ChoiceContinue.ActivateSound = '';
	ChoiceContinue.SetPos(100, 440 - 1 * 18 + 0 * 18);
}

//==========================================================================
//
//	OnDraw
//
//==========================================================================

void OnDraw()
{
	int i, j, y, fontheight;

	fontheight = TextHeight("SAMPLE STRING");

	if (hBackPic != -1)
	{
		DrawFullScreenPic(hBackPic);
	}
	if (Flat)
	{
		FillRectWithFlat(0, 0, 640, 480, Flat);
	}
	else if (!Flat)
	{
		ShadeRect(60, 45, 515,
			// 96,
			MaximalCueLines * ((Cues.Num > 1 ? Cues.Num - 1 : 1) * (fontheight + CueLinesSpacing)) + ((64 - 46) << 1) + 
				(Cues.Num > 1 ? GetCvar('ShowSpeakerName') == 1 ? 76 : 46 : 0),
			0.6);
	}

	if (Choices.Num)
		ShadeRect(60, 438 - Choices.Num * 22, 501, (Choices.Num) * 22, 0.6);
	else
		ShadeRect(60, 438 - 22, 501, 22, 0.6);
	
	SetTextAlign(hleft, vtop);
	if (!bDontDrawBigCharacterName)
	{
		SetFont('bigfont');
		DrawText(64, 23, NPCName);
	}
	SetFont('smallfont');

	y = 64;
	for (i = 0; i < Cues.Num; i++)
	{
		if (Cues[i].SpeakerName)
		{
			// if this is enabled, the name of the current speaker is displayed above the text spoken - this should be done by using cvar 'ShowSpeakerName'
			
			if (GetCvar('ShowSpeakerName') == 1)
			{
				DrawText(64, y - 12, Cues[i].SpeakerName, CR_RED);
				/*y += 24;*/
				y += fontheight + CueLinesSpacing;
			}
						
			int color;
			if (strcmp(Cues[i].SpeakerName, NPCName) == 0)
				color = CR_GREEN;
			else
				color = CR_WHITE;

			for (j = 0; j < Cues[i].SpeechLines.Num; j++)
			{
				DrawText(64, y, Cues[i].SpeechLines[j].Text, color);
				y += fontheight + CueLinesSpacing;
			}
			y += 32;
		}
		else
		{
			// Possibly a narrator's commentary
			for (j = 0; j < Cues[i].SpeechLines.Num; j++)
			{
				DrawText(64, y, Cues[i].SpeechLines[j].Text, CR_BRICK);
				y += fontheight + CueLinesSpacing;
			}
			y += 32;
		}
	}
}

//==========================================================================
//
//  CleanScene
//
//==========================================================================

void CleanScene()
{
	StopLocalSounds();
	Cues.Num = 0;
}

//==========================================================================
//
//  CleanChoices
//
//==========================================================================

void CleanChoices()
{
	int i;
	for (i = 0; i < NumItems; i++)
		Items[i] = none;
	NumItems = 0;
	for (i = 0; i < Choices.Num; i++)
	{
		Choices[i].Destroy();
		Choices[i] = none;
	}
	Choices.Num = 0;

	ChoiceContinue.Hide();
	ChoiceContinue.Focusable = false;
}

//==========================================================================
//
//  AddContinue
//
//==========================================================================

void AddContinue()
{
	Items[0] = ChoiceContinue;
	NumItems = 1;
	ChoiceContinue.Show();
	ChoiceContinue.Focusable = true;
}

//==========================================================================
//
//  AddChoice
//
//==========================================================================

void AddChoice(string Text, string Name, int Index)
{
	int i,y;
	Choices.Num = Choices.Num + 1;
	Choices[Choices.Num - 1] = ConScrChoice(NewChild(ConScrChoice));
	Choices[Choices.Num - 1].Text = Text;
	Choices[Choices.Num - 1].Name = Name;
	Choices[Choices.Num - 1].Index = Index;
	Choices[Choices.Num - 1].TextColour = CR_TAN;
	Choices[Choices.Num - 1].TextColourFocus = CR_GOLD;
	Choices[Choices.Num - 1].ActivateSound = '';
	
	/*
	for (i = Choices.Num - 1, y = 438; i >= 0; i--, y -= 22)
	{
		Choices[i].Y = y;
	}
	*/
	for (i = 0; i < Choices.Num; i++)
	{
		Choices[i].SetPos(100, 440 - Choices.Num * 18 + i * 18);
	}
}

//==========================================================================
//
//  PlayCue
//
//==========================================================================

void PlayCue(string Who, string ToWhom, string Cue, name Voice)
{
	int i;

	SetFont('smallfont');
	Cues.Num = Cues.Num + 1;
	Cues[Cues.Num - 1].SpeakerName = Who;
	SplitText(Cue, Cues[Cues.Num - 1].SpeechLines, Width - 120 - 40);

	if (Cues.Num > 5)
	{
		Cues.Remove(0);
	}

	MaximalCueLines = 0;
	for (i = 0; i < Cues.Num; i++)
	{
		int j;
		for (j = 0; j < Cues[i].SpeechLines.Num; j++)
		{
			if (strlen(Cues[i].SpeechLines[j].Text) > 0)
			{
				if (MaximalCueLines < Cues[i].SpeechLines.Num)
					MaximalCueLines += 1;
			}
		}
		/*if (MaximalCueLines < Cues[i].SpeechLines.Num)
			MaximalCueLines = Cues[i].SpeechLines.Num;*/
	}
	
	if  (GetCvar('PlayConSounds') == 1)
	{
		if (Voice)
		{
			LocalSound(Voice);
		}
	}
}

//==========================================================================
//
//	OnKeyDown
//
//==========================================================================

bool OnKeyDown(int key)
{
	switch (key)
	{
	case K_DOWNARROW:
	case K_MWHEELDOWN:
		CycleNextChoice();
		LocalSound('');
		return true;

	case K_UPARROW:
	case K_MWHEELUP:
		CyclePrevChoice();
		LocalSound('');
		return true;

	case K_ESCAPE:
	case K_MOUSE2:
		if (!ChoiceContinue.IsVisible() && !Choices.Num)
		{
			StopLocalSounds();
			ClGame.PopMenu();
		}
		return true;
	}
	return ::OnKeyDown(key);
}

//==========================================================================
//
//	OnMouseEnter
//
//==========================================================================

void OnMouseEnter()
{
	SetFocus();
	LocalSound('');
}

defaultproperties
{
	X = 0;
	Y = 0;
	Width = 640;
	Height = 480;
	SelectorType = MenuSelector_SmallLeft;
	CueLinesSpacing = 4;
	NPCName = "Person";
	hBackPic = -1;
	Flat = '';
	ActivateSound = '';
	DeactivateSound = '';
	ClearMenusSound = '';
}