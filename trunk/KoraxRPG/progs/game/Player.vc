//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : KRPGPlayerBase;

enum
{
	BT_DUCK	= 0x10	// key input value for ducking
};

const int MAXMORPHHEALTH	= 30;

BotPlayer Bot;

// [FB] Player atributes for leveling system
int experience;
int exp_level;
int next_level;
int prev_level;
int possTics;               // FOV effect for possession
int money;

bool inited;

//
//  Other stats (for status bar)
//
int max_health;
int maxhealth_old;
int av_points;
int maxsp_power;
int sp_power;
int sp_power_old;

int sp_recharge_tics; // [FB] Determine when to charge SP

replication
{
	reliable if (!bIsClient)
		experience, exp_level, next_level, prev_level, money,
		max_health, maxhealth_old, sp_power, maxsp_power;

	unreliable if (!bIsClient)
		ParticleEffectEx;

	reliable if (!bIsClient)
		ClientUpgradeScreen, ClientArsenalScreen, ClientJournalScreen,
		ClientConversation, ConCleanScene, ConSetPersonInfo, ConPlayCue,
		ConCleanChoices, ConAddChoice, ConSuspend, ConWait, ConCloseScene;

	reliable if (bIsClient)
		ConChoice, ConContinue;
}

//==========================================================================
//
//  CreateBot
//
//==========================================================================

void CreateBot()
{
	Bot = BotPlayer(SpawnObject(BotPlayer));
	Bot.player = self;
	Bot.OnBeginPlay();
	bAutoAim = true;
}

//==========================================================================
//
//  GiveExperience
//
//==========================================================================

void GiveExperience(int Exp, optional bool bIgnoreExpModifier)
{
	// [FB] Multiply experience by game's exp modifier
	experience += Exp;
	if(experience > 2100000666)
	{
		experience = 2100000666;
		next_level = 2100000999;
	}
	Actor(MO).experience = experience;
	while (experience >= next_level && PClass != HexenDefs::PCLASS_PIG)
	{
		ActivateLevel(exp_level + 1);
	}
}

//==========================================================================
//
//  GiveStrength
//
//==========================================================================

void GiveStrength(int levels)
{
	strength += levels;
}

//==========================================================================
//
//  GiveConstitution
//
//==========================================================================

void GiveConstitution(int levels)
{
	constitution += levels;
}

//==========================================================================
//
//  GiveDexterity
//
//==========================================================================

void GiveDexterity(int levels)
{
	dexterity += levels;
}

//==========================================================================
//
//  GiveIntelligence
//
//==========================================================================

void GiveIntelligence(int levels)
{
	intelligence += levels;
}

//==========================================================================
//
//  GiveWisdom
//
//==========================================================================

void GiveWisdom(int levels)
{
	wisdom += levels;
}

//==========================================================================
//
//  GiveCharisma
//
//==========================================================================

void GiveCharisma(int levels)
{
	charisma += levels;
}

//==========================================================================
//
//  GiveLevels
//
//==========================================================================

void GiveLevels(int levels)
{
	// [CW] This should give extra experience enouph to reach next levels
	// regardless of game experience modifier;
	// the experience earned since last level should be still kept, otherwise
	// players may think it is unfair and try to cheat;
	int exp_bak = experience;
	while (levels--)
	{
		GiveExperience(next_level - experience, true);
	}
	// [FB] Reset experience counter to zero, but add any remaining exp
	// 		points to it...
	experience = 0;
	experience += (next_level - exp_bak);
}


//============================================================================
//
//  ActivateLevel
//
//============================================================================

void ActivateLevel(int to_lvl)
{
	int addhealth;
	int i;
	int skillp_table[5];

	// [FB] AutoUpdate logic comes here
	if (GetCvar('AutoUpdate'))
	{
		int str_val = 0;
		int dex_val = 0;
		int con_val = 0;
		int int_val = 0;
		int wis_val = 0;
		int cha_val = 0;
		bool updated[6];

		for (i = 0; i < 7; i++)
		{
			updated[i] = false;
		}

		while(av_points > 0)
		{
			// [FB] NOTE: Don't remove the commented lines
			//		until more testing is done!!
/*			if (Level.World.GameSkill < 3)
			{*/
				if (!updated[0] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxStrength == -1 ? true :
					strength < KRPGPlayerPawn(MO).MaxStrength*/)
				{
					if (av_points > 0)
					{
						str_val++;
						av_points -= 1;
					}
					updated[0] = true;
				}
				if (!updated[1] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxDexterity == -1 ? true :
					dexterity < KRPGPlayerPawn(MO).MaxDexterity*/)
				{
					if (av_points > 0)
					{
						dex_val++;
						av_points -= 1;
					}
					updated[1] = true;
				}
				if (!updated[2] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxConstitution == -1 ? true :
					constitution < KRPGPlayerPawn(MO).MaxConstitution*/)
				{
					if (av_points > 0)
					{
						dex_val++;
						av_points -= 1;
					}
					updated[2] = true;
				}
				if (!updated[3] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxIntelligence == -1 ? true :
					intelligence < KRPGPlayerPawn(MO).MaxIntelligence*/)
				{
					if (av_points > 0)
					{
						int_val++;
						av_points -= 1;
					}
					updated[3] = true;
				}
				if (!updated[4] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxWisdom == -1 ? true :
					wisdom < KRPGPlayerPawn(MO).MaxWisdom*/)
				{
					if (av_points > 0)
					{
						wis_val++;
						av_points -= 1;
					}
					updated[4] = true;
				}
				if (!updated[5] && av_points > 0 /*&& KRPGPlayerPawn(MO).MaxCharisma == -1 ? true :
					charisma < KRPGPlayerPawn(MO).MaxCharisma*/)
				{
					if (av_points > 0)
					{
						cha_val++;
						av_points -= 1;
					}
					updated[5] = true;
				}
/*			}
			else
			{
				if (exp_level % 2 == 0)
				{
					if (!updated[0] && av_points > 0 && KRPGPlayerPawn(MO).MaxStrength == -1 ? true :
						strength < KRPGPlayerPawn(MO).MaxStrength)
					{
						if (av_points > 0)
						{
							str_val++;
							av_points -= 1;
						}
						updated[0] = true;
					}
					if (!updated[1] && av_points > 0 && KRPGPlayerPawn(MO).MaxDexterity == -1 ? true :
						dexterity < KRPGPlayerPawn(MO).MaxDexterit)
					{
						if (av_points > 0)
						{
							dex_val++;
							av_points -= 1;
						}
						updated[1] = true;
					}
					if (!updated[2] && av_points > 0 && KRPGPlayerPawn(MO).MaxConstitution == -1 ? true : 
						constitution < KRPGPlayerPawn(MO).MaxConstitution)
					{
						if (av_points > 0)
						{
							dex_val++;
							av_points -= 1;
						}
						updated[2] = true;
					}
				}
				else
				{
					if (!updated[3] && av_points > 0 && KRPGPlayerPawn(MO).MaxIntelligence == -1 ? true : 
						intelligence < KRPGPlayerPawn(MO).MaxIntelligence)
					{
						if (av_points > 0)
						{
							int_val++;
							av_points -= 1;
						}
						updated[3] = true;
					}
					if (!updated[4] && av_points > 0 && KRPGPlayerPawn(MO).MaxWisdom == -1 ? true : 
						wisdom < KRPGPlayerPawn(MO).MaxWisdom)
					{
						if (av_points > 0)
						{
							wis_val++;
							av_points -= 1;
						}
						updated[4] = true;
					}
					if (!updated[5] && av_points > 0 && KRPGPlayerPawn(MO).MaxCharisma == -1 ? true : 
						charisma < KRPGPlayerPawn(MO).MaxCharisma)
					{
						if (av_points > 0)
						{
							cha_val++;
							av_points -= 1;
						}
						updated[5] = true;
					}
				}
			}*/

			for (i = 0; i < 7; i++)
			{
				if (updated[i] && av_points > 0)
				{
					updated[i] = false;					
				}
			}
		}

		UpdateStats(str_val, dex_val, con_val, int_val,	wis_val, cha_val);
	}
	else
	{
		for (i = 0; i < 6; i++)
		{
/*			if (i <= 1)
			{
				skillp_table[i] = 12;
			}
			else if (i == 2)
			{*/
				skillp_table[i] = 6;
/*			}
			else
			{
				skillp_table[i] = 3;
			}*/
		}

		exp_level++;
		experience -= next_level;

		next_level = 1000 * exp_level;
		addhealth = Health  / max_health;
		maxhealth_old = (maxhealth_old != max_health ? maxhealth_old : max_health);
		max_health += constitution;
		sp_power_old = (sp_power_old != sp_power ? sp_power_old : sp_power);

		maxsp_power += intelligence;
		sp_power = maxsp_power;

		Health = max_health + addhealth;
		MO.Health = Health;

		av_points += skillp_table[Level.World.GameSkill];

		// [FB] For multiplayer??
	/*	switch (PClass)
		{
			case HexenDefs::PCLASS_FIGHTER:
				strength += skillp_table[Level.World.GameSkill];
				break;
			case HexenDefs::PCLASS_CLERIC:
				strength += skillp_table[Level.World.GameSkill] / 2;
				wisdom += skillp_table[Level.World.GameSkill] -
						  (skillp_table[Level.World.GameSkill] / 2);
				break;
			case HexenDefs::PCLASS_MAGE:
				wisdom += skillp_table[Level.World.GameSkill];
				break;
			}
		}*/

		cprint("YOU REACHED LEVEL %d", exp_level);
		MO.PlaySound('levelup', CHAN_AUTO);
	}
}

//============================================================================
//
//  InitiateLevel
//
//============================================================================

void InitiateLevel()
{
	// [FB]  First, we need to get the stats we choose at the character build screen
	MainGameInfo(Level.Game).PassInitialStats(MO);
	
	max_health = KRPGPlayerPawn(MO).InitialHealth;
	sp_power = KRPGPlayerPawn(MO).InitialSpiritPower;
	maxsp_power = KRPGPlayerPawn(MO).InitialSpiritPower;

	exp_level = 1;
	next_level =  1000 * exp_level;
	prev_level = 1000 * (exp_level - 1);
	Health = max_health;
	MO.Health = Health;
	money = 0;
	UpdateStats(KRPGPlayerPawn(MO).InitialStrength, KRPGPlayerPawn(MO).InitialConstitution,
			KRPGPlayerPawn(MO).InitialDexterity, KRPGPlayerPawn(MO).InitialIntelligence,
			KRPGPlayerPawn(MO).InitialWisdom, KRPGPlayerPawn(MO).InitialCharisma);
}

//==========================================================================
//
//  UpgradeScreen
//
//==========================================================================

void UpgradeScreen()
{
	// [FB] Only if we are not berserking
	if (!Actor(MO).GetCondition(ACSpellBerserk))
	{
		ClientUpgradeScreen();
	}
}

//==========================================================================
//
//  ClientUpgradeScreen
//
//==========================================================================

void ClientUpgradeScreen()
{
	KRPGClientGameBase(ClGame).ShowUpgradeScreen();
}

//==========================================================================
//
//  JournalScreen
//
//==========================================================================

void JournalScreen()
{
	// [FB] Only if we are not berserking
	if (!Actor(MO).GetCondition(ACSpellBerserk))
	{
		ClientJournalScreen();
	}
}

//==========================================================================
//
//  ClientJournalScreen
//
//==========================================================================

void ClientJournalScreen()
{
	KRPGClientGameBase(ClGame).ShowJournalScreen();
}

//==========================================================================
//
//  UpdateStats
//
//==========================================================================

void UpdateStats(int n_strength, int n_constitution, int n_dexterity, int n_intelligence,
		int n_wisdom, int n_charisma)
{
	strength += n_strength;
	constitution += n_constitution;
	dexterity += n_dexterity;
	intelligence += n_intelligence;
	wisdom += n_wisdom;
	charisma += n_charisma;
	
	//	Update armor save percentages.
	BasicArmor Armor = BasicArmor(EntityEx(MO).FindInventory(BasicArmor));
	if (Armor && Armor.Amount)
	{
		Armor.SavePercent = 0.335 + itof(constitution) * 0.4;
		if (Armor.SavePercent > 0.75)
		{
			Armor.SavePercent = 0.75;
		}
	}

	//	Update player's weapon speed.
	float Value;
	if (MorphTime)
	{
		Value = (itof(strength) * 0.25) / 100.0;
	}
	else
	{
		Value = (itof(strength) * 0.5) / 100.0;
	}
	PlayerPawn(MO).ForwardMove1 += Value;
	PlayerPawn(MO).ForwardMove2 += Value;
	PlayerPawn(MO).SideMove1 += Value;
	PlayerPawn(MO).SideMove2 += Value;
}

//==========================================================================
//
//  ArsenalScreen
//
//==========================================================================

void ArsenalScreen()
{
	ClientArsenalScreen();
}

//==========================================================================
//
//  ClientArsenalScreen
//
//==========================================================================

void ClientArsenalScreen()
{
	KRPGClientGameBase(ClGame).ShowArsenalScreen();
}

//==========================================================================
//
//  GetPoints
//
//==========================================================================

int GetPoints()
{
	return av_points;
}

//==========================================================================
//
//  SetPoints
//
//==========================================================================

void SetPoints(int value)
{
	if (av_points < 0)
	{
		av_points = 0;
	}
	else
	{
		av_points = value;
	}
}

//==========================================================================
//
//  NetGameReborn
//
//	Respawn at the start
//
//==========================================================================

void NetGameReborn()
{
	int old_experience;
	int old_strength;
	int old_constitution;
	int old_dexterity;
	int old_intelligence;
	int old_wisdom;
	int old_charisma;
	int old_max_health;
	int old_maxhealth_old;
	int old_exp_level;
	int old_prev_level;
	int old_next_level;
	int old_maxsp_power;
	int old_sp_power_old;
	int old_money;
	EntityEx OldMo = EntityEx(MO);
	
	// [FB] Retrieve experience level and attributes we have made...
	old_experience = Player(OldMo.Player).experience;
	old_strength = Player(OldMo.Player).strength;
	old_constitution = Player(OldMo.Player).constitution;
	old_dexterity = Player(OldMo.Player).dexterity;
	old_intelligence = Player(OldMo.Player).intelligence;
	old_wisdom = Player(OldMo.Player).wisdom;
	old_charisma = Player(OldMo.Player).charisma;
	old_exp_level = Player(OldMo.Player).exp_level;
	old_next_level = Player(OldMo.Player).next_level;
	old_prev_level = Player(OldMo.Player).prev_level;
	old_max_health = Player(OldMo.Player).max_health;
	old_maxhealth_old = Player(OldMo.Player).maxhealth_old;
	old_maxsp_power = Player(OldMo.Player).maxsp_power;
	old_sp_power_old = Player(OldMo.Player).sp_power_old;
	old_money = Player(OldMo.Player).money;
	
	// [FB] ...and update them accordingly
	experience = old_experience;
	strength = old_strength;
	constitution = old_constitution;
	dexterity = old_dexterity;
	intelligence = old_intelligence;
	wisdom = old_wisdom;
	charisma = old_charisma;
	exp_level = old_exp_level;
	next_level = old_next_level;
	prev_level = old_prev_level;
	max_health = old_max_health;
	maxhealth_old = old_maxhealth_old;
	maxsp_power = old_maxsp_power;
	sp_power_old = old_sp_power_old;
	money = old_money;
	
	::NetGameReborn();
}	

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine
//
//==========================================================================

void MovePsprites(float deltaTime)
{
	if (dexterity < 6)
		deltaTime *= 0.8;
	else if (dexterity > 34)
		deltaTime *= 3.5;
	else if (dexterity > 24)
		deltaTime *= 2.5;
	else if (dexterity > 14)
		deltaTime *= 1.5;
	::MovePsprites(deltaTime);
}

//==========================================================================
//
//  OnNetReborn
//
//==========================================================================

void OnNetReborn(EntityEx OldMO)
{
	Weapon bestWeapon;
	Inventory Item;

	// Restore keys and weapons when reborn in co-op
	bestWeapon = none;
	if (OldMO)
	{
		for (Item = OldMO.Inventory; Item; )
		{
			Inventory Next = Item.Inventory;
			if (Weapon(Item))
			{
				if (!bestWeapon || bestWeapon.SelectionOrder >
					Weapon(Item).SelectionOrder)
				{
					bestWeapon = Weapon(Item);
				}
				Item.AttachToOwner(EntityEx(MO));
			}
			else if (FourthWeaponHolder(Item) || Key(Item))
			{
				Item.AttachToOwner(EntityEx(MO));
			}
			Item = Next;
		}
		OldMO.DestroyAllInventory();
	}
	GiveAmmo(Mana1, 0);
	GiveAmmo(Mana2, 0);
	GiveAmmo(RedMana, 0);
	EntityEx(MO).FindInventory(Mana1).Amount = 25;
	EntityEx(MO).FindInventory(Mana2).Amount = 25;
	EntityEx(MO).FindInventory(RedMana).Amount = 25;
	if (bestWeapon)
	{
		// Bring up the best weapon
		PendingWeapon = bestWeapon;
	}
}

//==========================================================================
//
//  DestroyBot
//
//==========================================================================

void DestroyBot()
{
	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
}

//==========================================================================
//
//  ResetPlayerOnSpawn
//
//==========================================================================

void ResetPlayerOnSpawn()
{
	::ResetPlayerOnSpawn();

//[Korax]
	if (!inited)
	{
		InitiateLevel();
		inited = true;
	}
}

//==========================================================================
//
//  BotOnSpawn
//
//==========================================================================

void BotOnSpawn()
{
	Bot.OnSpawn();
}

//==========================================================================
//
//  DoClearPlayer
//
//==========================================================================

void DoClearPlayer()
{
	BotPlayer b;

	b = Bot;
	//::DoClearPlayer();
	ClearPlayer();
	Bot = b;
}

//==========================================================================
//
//	GetMaxHealth
//
//==========================================================================

int GetMaxHealth()
{
	if (MorphTime)
	{
		return MAXMORPHHEALTH;
	}
	return max_health;
}

//==========================================================================
//
//  GetRebornHealth
//
//==========================================================================

int GetRebornHealth()
{
	return max_health;
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;
	int i;

	impulse = Impulse;
	Impulse = 0;
	Inventory Arti = none;

	switch (impulse)
	{
	case KRPGDefs::IMPULSE__TEST__TRAVEL_MAP:
		Level.Completed(-1, 0, 0);
		break;
	}

	/*---------------------------------------
	-	[CW] Actor Conditions Test      -
	---------------------------------------*/
	switch (impulse)
	{
	case KRPGDefs::IMPULSE__TEST_AC__CAST_ICE_DAMAGER:
		{
			ACDamageDealer ac = ACDamageDealer(Actor(MO).CastCondition(ACDamageDealer, false, none, 1, 5.0));
			if (ac)
			{
				ac.DamageValue = 10;
				ac.DamageDelay = 1.5;
				ac.DamageType = 'Ice';
			}
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AC__CAST_POISON_DAMAGER:
		{
			Actor(MO).CastCondition(ACPoisoned, true, none, 1, 8.0);
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AC__CAST_SPELL_MANA_BURN:
		{
			Actor(MO).CastCondition(ACSpellManaBurn, true, none, 2, 20.0);
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AC__CAST_SPELL_CONFUSION:
		{
			Actor(MO).CastCondition(ACSpellConfusion, true, none, 1, 20.0);
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AC__CAST_SPELL_BLIND:
		{
			Actor(MO).CastCondition(ACSpellBlind, true, none, 8, 10.0);
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AC__CAST_SPELL_TERROR:
		{
			Actor(MO).CastCondition(ACSpellTerror, true, none, 10, 20.0);
		}
		break;
	}

	/*---------------------------------------
	-	[CW] Spells Test      -
	---------------------------------------*/

	switch (impulse)
	{
	case KRPGDefs::IMPULSE__TEST_SPELL__CAST_SPELL_GRAVITATOR:
		{
			SpellGravitator SG = SpellGravitator(Level.Spawn(SpellGravitator));
			SG.Level = 2;
			SG.LifeDuration = 10.0;
			SG.Origin = MO.Origin;
			SG.Activate();
		}
		break;
	case KRPGDefs::IMPULSE__TEST_SPELL__CAST_SPELL_GOLDEN_STORM:
		{
			SpellGoldenStorm SGS = SpellGoldenStorm(Level.Spawn(SpellGoldenStorm));
			SGS.Level = 2;
			SGS.LifeDuration = 20.0;
			SGS.Origin = MO.Origin;
			SGS.Target = EntityEx(MO);
			SGS.Activate();
		}
		break;
	case KRPGDefs::IMPULSE__TEST_SPELL__CAST_SPELL_GLOBAL_SHRINKER:
		{
			Actor KA;
			foreach Level.AllThinkers(Actor, KA)
			{
				if (KA.bMonster)
				{
					KA.CastCondition(ACSpellShrink, true, Actor(MO), 4, 5.0);
				}
			}
		}
		break;
	}

	/*---------------------------------------
	-	[CW] Actor Tasks Test      -
	---------------------------------------*/
	switch (impulse)
	{
	case KRPGDefs::IMPULSE__TEST_AT__ORDER_EVERYONE_CANCELCURRENT:
		{
			IntelligentActor IA;
			foreach Level.AllThinkers(IntelligentActor, IA)
			{
				if (IA.bMonster)
				{
					IA.CancelCurrentOrder();
				}
			}
		}
	case KRPGDefs::IMPULSE__TEST_AT__ORDER_EVERYONE_STANDSTILL:
		{
			IntelligentActor IA;
			foreach Level.AllThinkers(IntelligentActor, IA)
			{
				if (IA.bMonster)
				{
					IA.OrderStandStill(false, 0, 5.0);
				}
			}
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AT__ORDER_EVERYONE_MOVETO_PLAYER:
		{
			IntelligentActor IA;
			foreach Level.AllThinkers(IntelligentActor, IA)
			{
				if (IA.bMonster)
				{
					IA.OrderWalkTo(false, 0, 0.0, MO.Origin);
				}
			}
		}
		break;
	case KRPGDefs::IMPULSE__TEST_AT__ORDER_EVERYONE_ATTACKALL:
		{
			IntelligentActor IA;
			foreach Level.AllThinkers(IntelligentActor, IA)
			{
				if (IA.bMonster)
				{
					IA.OrderAttackAll(false, 0, 0.0);
				}
			}
		}
		break;
	};


	if (impulse < KRPGDefs::NUMWEAPONSLOTS)
	{
		ChangeWeaponEx(impulse);
	}
	else if (impulse == KRPGDefs::IMPULSE__INVENTORY_PREV)
	{
		InventoryLeft();
	}
	else if (impulse == KRPGDefs::IMPULSE__INVENTORY_NEXT)
	{
		InventoryRight();
	}
	else if (impulse == KRPGDefs::IMPULSE__INVENTORY_UP)
	{
		InventoryUp();
	}
	else if (impulse == KRPGDefs::IMPULSE__INVENTORY_DOWN)
	{
		InventoryDown();
	}
	else if (impulse == KRPGDefs::IMPULSE__INVENTORY_USE)
	{
		InventoryUse();
	}
	else if (impulse == KRPGDefs::IMPULSE__SETNEXTSPELL)
	{
		NextSpell();
	}
	else if (impulse == KRPGDefs::IMPULSE__SETPREVIOUSSPELL)
	{
		PreviousSpell();
	}
	else if (impulse == KRPGDefs::IMPULSE__USESPELL)
	{
		SpellUse();
	}
	else if (impulse == KRPGDefs::IMPULSE__SUICIDE)
	{
		Actor(MO).Damage(none, none, 10000);
	}
	else if (impulse == KRPGDefs::IMPULSE__WEAPON_PREV)
	{
		PrevWeapon();
	}
	else if (impulse == KRPGDefs::IMPULSE__WEAPON_NEXT)
	{
		NextWeapon();
	}
	else if (impulse == KRPGDefs::IMPULSE__UPGRADESCREEN)
	{
		UpgradeScreen();
	}
	else if (impulse == KRPGDefs::IMPULSE__ARSENAL)
	{
		ArsenalScreen();
	}
	else if (impulse == KRPGDefs::IMPULSE__JOURNALSCREEN)
	{
		JournalScreen();
	}
	// [CW] TODO: replace by using KRPG conversation system
	else if (impulse >= 200 && impulse <= 205)
	{
		LineSpecialLevelInfo(Level).ConChoiceImpulse(impulse - 200);
	}
/*
	// Hexen artifact hot keys
	if (!Arti)
	{
		if (impulse == 12 && (Health < max_health))
		{
			Arti = EntityEx(MO).FindInventory(ArtiHealth);
		}
		else if (impulse == 10)
		{
			Arti = EntityEx(MO).FindInventory(ArtiPoisonBag);
		}
		else if (impulse == 9)
		{
			Arti = EntityEx(MO).FindInventory(ArtiBlastRadius);
		}
		else if (impulse == 8)
		{
			Arti = EntityEx(MO).FindInventory(ArtiTeleport);
		}
		else if (impulse == 7)
		{
			Arti = EntityEx(MO).FindInventory(ArtiTeleportOther);
		}
		else if (impulse == 6)
		{
			Arti = EntityEx(MO).FindInventory(ArtiPork);
		}
		else if (impulse == 5 &&
			!EntityEx(MO).FindInventory(PowerInvulnerable))
		{
			Arti = EntityEx(MO).FindInventory(ArtiInvulnerability);
		}
	}
*/
	if (Arti)
	{
		// Use an artifact
		EntityEx(MO).UseInventory(Arti);
	}
}

//==========================================================================
//
//  PlayerExitMap
//
//  Called when a player completes a level.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	::PlayerExitMap(clusterChange);

	int i;
	for (i = 0; i < Actor(MO).Conditions.Num; i++)
	{
		// [FB] Deactivate berserker if we are changing maps
		if (Actor(MO).Conditions[i] && Actor(MO).Conditions[i].Class == ACSpellBerserk)
		{
			Actor(MO).Conditions[i].Deactivate();
		}
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	// Process ACs that need it before anything else
	int i = 0;
	for (i = 0; i < Actor(MO).Conditions.Num; i++)
	{
		if (Actor(MO).Conditions[i] && Actor(MO).Conditions[i].bDoFromActorTick &&
			Actor(MO).Conditions[i].bPlayerSpecific)
		{
			Actor(MO).Conditions[i].DoDelegate(deltaTime);
		}
	}

	if (bIsBot)
	{
		BotTick(deltaTime);
	}

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (Actor(MO).bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		Actor(MO).bJustAttacked = false;
	}

	//	You can only press use while totally frozen
	if (bTotallyFrozen)
	{
		Buttons &= BT_USE;
		Impulse = 0;
		ViewAngles = MO.Angles;
		ForwardMove = 0.0;
		SideMove = 0.0;
		FlyMove = 0.0;
	}
	else if (bFrozen)
	{
		ForwardMove = 0.0;
		SideMove = 0.0;
		FlyMove = 0.0;
	}

	sp_recharge_tics++;
	// [FB] Slowly increase spiritual power
	if (PlayerState == PST_LIVE && sp_power < maxsp_power)
	{
		if (sp_recharge_tics >= 18)
		{
			if (wisdom <= 6 && (sp_recharge_tics % (8 * 35) == 0))
			{
				sp_power++;
			}
			else if (wisdom <= 12 && (sp_recharge_tics % (6 * 35) == 0))
			{
				sp_power++;
			}
			else if (wisdom > 24 && (sp_recharge_tics % (4 * 35) == 0))
			{
				sp_power++;
			}
		}
	}
	if (sp_recharge_tics > (10 * 35))
	{
		sp_recharge_tics = 0; // [FB] Restart counter
	}
	WorldTimer += deltaTime;
	InventoryTick(deltaTime);
	SpellTick(deltaTime);

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	// Copy view angles
	if (MO == Camera)
	{
		MO.Angles = ViewAngles;
	}

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime <= 0.0)
		{
			JumpTime = 0.0;
		}
	}
	if (MorphTime)
	{
		PlayerPawn(MO).MorphPlayerThink();
	}

	// KRPG
	// Duck
	if (ClassIsChildOf(MO.Class, KRPGPlayerPawn))
	{
		float oldHeight = MO.Height;
		if (Buttons & BT_DUCK)
		{
			KRPGPlayerPawn(MO).BendDown(deltaTime);
		}
		else
		{
			KRPGPlayerPawn(MO).StandUp(deltaTime);
		}
		KRPGPlayerPawn(MO).ViewHeight += (MO.Height - oldHeight);
	}

	//	Move around.
	//	ReactionTime is used to prevent movement for a bit after a teleport.
	if (Actor(MO).ReactionTime)
	{
		Actor(MO).ReactionTime -= deltaTime;
		if (Actor(MO).ReactionTime <= 0.0)
			Actor(MO).ReactionTime = 0.0;
		if (Actor(MO).bWaterJump)
			WaterJump();
	}
	else
	{
		bool movement_done = false;
		if (ClassIsChildOf(MO.Class, KRPGPlayerPawn))
		{
			if (MO.Height < KRPGPlayerPawn(MO).StandHeight)
			{
				ForwardMove /= 4.0;
				SideMove /= 7.0;
			}
			// KRPG
			if (KRPGPlayerPawn(MO).RidePartner && KRPGPlayerPawn(MO).bIsMountedOn && KRPGPlayerPawn(MO).bRideControl)
			{
				PlayerRide(deltaTime);
				movement_done = true;
			}
		}

		if (movement_done);
		else if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
		if (EntityEx(MO).FindInventory(PowerSpeed) &&
			!(Level.XLevel.TicTime & 1) && Length(MO.Velocity) > 12.0 * 35.0)
		{
			SpawnSpeedEffect();
		}
	}

	CalcHeight(deltaTime);

	if (MO.Sector->special || MO.Sector->Damage)
	{
		PlayerInSpecialSector(deltaTime);
	}
	PlayerOnSpecialFlat(Actor(MO).GetFloorType());
	PlayerInContents(deltaTime);

	if (MO.Velocity.z <= -35.0 * 35.0 && MO.Velocity.z >= -40.0 * 35.0 &&
		!MorphTime && MO.WaterLevel == 0 &&
		!GetSoundPlayingInfo(MO, GetSoundID('*falling')))
	{
		MO.PlaySound('*falling', CHAN_VOICE);
	}

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			//[KoraxRPG]
			//P_UseLines();
			if (MO.IsA('KRPGPlayerPawn'))
			{
				KRPGPlayerPawn(MO).TryUse(USERANGE, USETHINGRANGE);
			}
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	if (!ReadyWeapon && PendingWeapon)
	{
		SetWeapon(PendingWeapon);
		BringUpWeapon();
	}

	// Morph counter
	if (MorphTime)
	{
		if (ChickenPeck)
		{
			// Chicken attack counter
			ChickenPeck -= 3;
		}
		MorphTime -= deltaTime;
		if (MorphTime <= 0.0)
		{
			// Attempt to undo the chicken/pig
			MorphTime = 0.0;
			UndoPlayerMorph(false, self);
		}
	}

	//	Cycle psprites
	MovePsprites(deltaTime);

	//	Counters.
	if (PoisonCount && Level.XLevel.Time - LastPoisonTime >= 0.5)
	{
		PoisonCount -= 5;
		if (PoisonCount < 0)
		{
			PoisonCount = 0;
		}
		LastPoisonTime = Level.XLevel.Time;
		Actor(MO).PoisonDamage(Poisoner, Poisoner, 1, true);
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	if (HazardTime)
	{
		HazardTime -= deltaTime;
		if (HazardTime <= 0.0)
			HazardTime = 0.0;
		if (Level.XLevel.Time - LastHazardTime >= 32.0 / 35.0 &&
			HazardTime > 16.0)
		{
			LastHazardTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
	}
}

//==========================================================================
//
//  PlayerRide
//
//==========================================================================

void PlayerRide(float deltaTime)
{
	float forward = ForwardMove;
	float side = SideMove;
	float vertical = (Buttons & BT_JUMP) ? deltaTime : 0.0;

	KRPGPlayerPawn(MO).RidePartner.RideCommand(forward * deltaTime, side * deltaTime, vertical);
}

//==========================================================================
//
//  PaletteFlash
//
//  Sets the new palette based upon the current values of
// Player.DamageFlash and Player.BonusFlash.
//
//==========================================================================

void PaletteFlash()
{
	float r = 0.0;
	float g = 0.0;
	float b = 0.0;
	float a = 0.0;

	int CShiftPowerPositive, CShiftPowerNegative;

	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			AddBlend(r, g, b, a, RGBA(130, 80, 50, 128));
			break;

		case CONTENTS_LAVA:
			AddBlend(r, g, b, a, RGBA(255, 80, 0, 150));
			break;

		case CONTENTS_NUKAGE:
			AddBlend(r, g, b, a, RGBA(50, 255, 50, 150));
			break;

		case CONTENTS_SLIME:
			AddBlend(r, g, b, a, RGBA(0, 25, 5, 150));
			break;

		case CONTENTS_HELLSLIME:
			AddBlend(r, g, b, a, RGBA(255, 80, 0, 150));
			break;

		case CONTENTS_BLOOD:
			AddBlend(r, g, b, a, RGBA(160, 16, 16, 150));
			break;

		case CONTENTS_SLUDGE:
			AddBlend(r, g, b, a, RGBA(128, 160, 128, 150));
			break;

		case CONTENTS_HAZARD:
			AddBlend(r, g, b, a, RGBA(128, 160, 128, 128));
			break;
		}
	}

	if (EntityEx(MO).DamageType == 'Ice')
	{
		// Frozen player
		AddBlend(r, g, b, a, RGBA(2, 2, 255, 113));
	}
	else
	{
		if (DamageFlash)
		{
			int Amount = ftoi(114.0 * DamageFlash);
			if (Amount >= 228)
			{
				Amount = 228;
			}
			// KRPG
			// [CW] TODO: need extra parameter in DoSpecialDamage to use this
			if (Actor(MO).DamageType == 'Ice')
				AddBlend(r, g, b, a, RGBA(2, 2, 255, Amount));
			else
				AddBlend(r, g, b, a, RGBA(255, 0, 0, Amount));
		}

		if (BonusFlash)
		{
			int Amount = ftoi(256.0 * BonusFlash);
			if (Amount >= 128)
			{
				Amount = 128;
			}
			AddBlend(r, g, b, a, RGBA(215, 186, 68, Amount));
		}

		//
		// KRPG
		//
		/*if (PoisonCount)
		{
			int Amount = PoisonCount * 160 / 32;
			if (Amount >= 160)
			{
				Amount = 160;
			}
			AddBlend(r, g, b, a, RGBA(56, 118, 46, Amount));
		}*/

		//FIXME add hazard flash.
		if (HazardTime > 16.0 || ftoi(4.0 * HazardTime) & 1)
		{
			AddBlend(r, g, b, a, RGBA(0, 255, 0, 32));
		}
	}

	Inventory Item;
	for (Item = EntityEx(MO).Inventory; Item; Item = Item.Inventory)
	{
		int Blend = Item.GetBlend();
		if (Blend)
		{
			AddBlend(r, g, b, a, Blend);
		}
	}

	//
	// KRPG
	//
	KRPGPlayerPawn kpp = KRPGPlayerPawn(MO);
	if (kpp && kpp.Conditions.Num)
	{
		int i;
		for (i = 0; i < kpp.Conditions.Num; i++)
		{
			ActorCondition ac = kpp.Conditions[i];
			if (ac)
			{
				if (ac.bIsNegative)
				{
					if (!MO.bCorpse)
						ac.MakeCShift(&CShiftPowerNegative);
				}
				else
					ac.MakeCShift(&CShiftPowerPositive);
			}
		};
	}

	if (r > 1.0)
	{
		r = 1.0;
	}
	if (g > 1.0)
	{
		g = 1.0;
	}
	if (b > 1.0)
	{
		b = 1.0;
	}
	CShift = RGBA(ftoi(r * 255.0), ftoi(g * 255.0), ftoi(b * 255.0),
		ftoi(a * 255.0));
	//
	// KRPG
	CShift |= CShiftPowerPositive;
	CShift |= CShiftPowerNegative;
	// [CW] OMG, what a mess :)
}

//==========================================================================
//
//  BotTick
//
//==========================================================================

void BotTick(float deltaTime)
{
	Bot.BotTick(deltaTime);
}

//==========================================================================
//
//  ParticleEffectEx
//
//==========================================================================

void ParticleEffectEx(int count, int type1, int type2, TVec origin, TVec ornd,
	TVec velocity, TVec vrnd, TVec acceleration, float grav, int colour, float duration, float ramp,
	int Pattern)
{
	KRPGLevelInfo(Level).ParticleEffectEx(count, type1, type2, origin, ornd, velocity, vrnd, acceleration,
				grav, colour, duration, ramp, Pattern);
}

//==========================================================================
//
//  SpawnSpeedEffect
//
//==========================================================================

void SpawnSpeedEffect()
{
	Actor speedA = HexenLevelInfo(Level).Spawn(PlayerSpeed, MO.Origin);
	if (speedA)
	{
		speedA.Angles.yaw = MO.Angles.yaw;
		speedA.Translation = MO.Translation;
		speedA.Target = Actor(MO);
		speedA.Special1 = PClass;
		if (speedA.Special1 > 2)
		{
			speedA.Special1 = 0;
		}
		int Idx;
		switch (PClass)
		{
		case HexenDefs::PCLASS_FIGHTER:
			Idx = 0;
			break;
		case HexenDefs::PCLASS_CLERIC:
			Idx = 2;
			break;
		case HexenDefs::PCLASS_MAGE:
			Idx = 4;
			break;
		case HexenDefs::PCLASS_PIG:
			Idx = 6;
			break;
		}
		speedA.SetState(GetStatePlus(speedA.IdleState, Idx, true));
		speedA.FloorClip = MO.FloorClip;
#ifdef FIXME
		if (self == GPlayers[consoleplayer])
		{
			speedA.bHidden = true;
		}
#endif
	}
}

//==========================================================================
//
//  Cheat_Gimme
//
//==========================================================================

void Cheat_Gimme()
{
	class<Inventory>			Cls;
	class<HexenWeapon>			WpnCls;
	class<FourthWeaponPiece>	PieceCls;
	Inventory					Inv;

	if (Cmd_CheckParm("weapons"))
	{
		int i;

		HexenArmor HArmor = HexenArmor(EntityEx(MO).FindInventory(
			HexenArmor));
		for (i = 0; i < 4; i++)
		{
			HArmor.Slots[i] = HArmor.SlotsIncrement[i];
		}
		foreach AllClasses(HexenWeapon, WpnCls)
		{
			if (!FindClassState(WpnCls, 'Ready'))
			{
				//	Abstract class.
				continue;
			}
			WpnCls = class<HexenWeapon>(GetClassReplacement(WpnCls));
			if (WpnCls)
			{
				HexenWeapon Wpn = HexenWeapon(Level.Spawn(WpnCls,,,, false));
				Wpn.AmmoGive1 = 0;
				Wpn.AmmoGive2 = 0;
				Wpn.AmmoGive3 = 0;
				if (!Wpn.TryPickup(EntityEx(MO)))
				{
					Wpn.Destroy();
				}
				else
				{
					dprint("Weapon given: %n", GetClassName(Wpn.Class));
				}
			}
			else
			{
				continue;
			}
		}
		foreach AllClasses(Ammo, Cls)
		{
			//	Only direct descendants.
			if (GetClassParent(Cls) != Ammo)
			{
				continue;
			}
			Inventory AmmoItem = EntityEx(MO).FindInventory(Cls);
			if (!AmmoItem)
			{
				Cls = class<Inventory>(GetClassReplacement(Cls));
				AmmoItem = Level.Spawn(Cls,,,, false);
				if (!AmmoItem.TryPickup(EntityEx(MO)))
				{
					AmmoItem.Destroy();
					AmmoItem = none;
				}
			}
			if (AmmoItem)
			{
				AmmoItem.Amount = AmmoItem.MaxAmount;
			}
		}

		FourthWeaponHolder Hold = FourthWeaponHolder(
			EntityEx(MO).FindInventory(FourthWeaponHolder));
		if (!Hold)
		{
			Hold = Level.Spawn(FourthWeaponHolder,,,, false);
			Hold.AttachToOwner(EntityEx(MO));
		}
		Hold.PieceMask = 7;

		cprint("ALL WEAPONS");
		EntityEx(MO).Inventory.Dump();
	}
	if (Cmd_CheckParm("armor"))
	{
		int i;
		HexenArmor HArmor = HexenArmor(EntityEx(MO).FindInventory(
			HexenArmor));
		for (i = 0; i < 4; i++)
		{
			HArmor.Slots[i] = HArmor.SlotsIncrement[i];
		}
	}
	if (Cmd_CheckParm("mana") || Cmd_CheckParm("ammo"))
	{
		foreach AllClasses(Ammo, Cls)
		{
			//	Only direct descendants.
			if (GetClassParent(Cls) != Ammo)
			{
				continue;
			}
			Inventory AmmoItem = EntityEx(MO).FindInventory(Cls);
			if (!AmmoItem)
			{
				Cls = class<Inventory>(GetClassReplacement(Cls));
				AmmoItem = Level.Spawn(Cls,,,, false);
				if (!AmmoItem.TryPickup(EntityEx(MO)))
				{
					AmmoItem.Destroy();
					AmmoItem = none;
				}
			}
			if (AmmoItem)
			{
				AmmoItem.Amount = AmmoItem.MaxAmount;
			}
		}
	}
	if (Cmd_CheckParm("health"))
	{
		if (MorphTime)
		{
			MO.Health = MAXMORPHHEALTH;
			Health = MO.Health;
		}
		else
		{
			MO.Health = max_health;
			Health = MO.Health;
		}
		cprint("FULL HEALTH");
	}
	if (Cmd_CheckParm("keys"))
	{
		foreach AllClasses(Key, Cls)
		{
			if (!FindClassState(Cls, 'Spawn'))
			{
				//	Abstract class
				continue;
			}
			Cls = class<Inventory>(GetClassReplacement(Cls));
			EntityEx(MO).GiveInventoryType(Cls);
		}
		cprint("ALL KEYS");
	}
	if (Cmd_CheckParm("artifacts"))
	{
		// All artifacts
		foreach AllClasses(Inventory, Cls)
		{
			if (!Cls.default.bInvBar || !Cls.default.MaxAmount)
			{
				//	Not an artifact
				continue;
			}
			if (!FindClassState(Cls, 'Spawn'))
			{
				//	Abstract class
				continue;
			}
			if (ClassIsChildOf(Cls, PuzzleItem))
			{
				//	Not puzzle items.
				continue;
			}
			if (!AreStateSpritesPresent(FindClassState(Cls, 'Spawn')))
			{
				//	From other game or not available in shareware.
				continue;
			}
			Cls = class<Inventory>(GetClassReplacement(Cls));
			Inv = Level.Spawn(Cls,,,, false);
			Inv.Amount = Inv.MaxAmount;
			if (!Inv.TryPickup(EntityEx(MO)))
			{
				Inv.Destroy();
			}
		}
		cprint("ALL ARTIFACTS");
	}
	if (Cmd_CheckParm("puzzles"))
	{
		foreach AllClasses(PuzzleItem, Cls)
		{
			if (!FindClassState(Cls, 'Spawn'))
			{
				//	Abstract class
				continue;
			}
			Cls = class<Inventory>(GetClassReplacement(Cls));
			EntityEx(MO).GiveInventoryType(Cls);
		}
		cprint("ALL ARTIFACTS");
	}
	if (Cmd_CheckParm("exp"))
	{
		GiveExperience(next_level - experience, true);
		cprint("LOTS OF EXP");
	}
	if (Cmd_CheckParm("gold") || Cmd_CheckParm("money") || Cmd_CheckParm("cash") || Cmd_CheckParm("coins"))
	{
		Cls = class<Inventory>(FindClass('Coin'));
		Inv = Level.Spawn(Cls,,,, false);
		Inv.Amount = (Inv.Amount+999);
		if (!Inv.TryPickup(EntityEx(MO)))
		{
			Inv.Destroy();
		}
		cprint("KA CHING!");
	}
	if (Cmd_CheckParm("spells"))
	{
		foreach AllClasses(SpellItem, Cls)
		{
			if (!FindClassState(Cls, 'Spawn'))
			{
				//	Abstract class
				continue;
			}
			Cls = class<Inventory>(GetClassReplacement(Cls));
			EntityEx(MO).GiveInventoryType(Cls);
		}
		cprint("ALL SPELLS");	
	}
	// [CW] Could not help myself not doing this >8}=}
	if (Cmd_CheckParm("peace"))
	{
		if (MO)
			Actor(MO).Kill(,,,,true);
		cprint("REST IN PEACE...");
	}
	if (Cmd_CheckParm("life"))
	{
		FatalError("Get yourself a life!");
	}
}

//==========================================================================
//
//  Cheat_Morph
//
//==========================================================================

void Cheat_Morph()
{
	if (MorphTime)
	{
		UndoPlayerMorph(false, self);
	}
	else
	{
		Actor(MO).MorphPlayer(PorkFX.default.PlayerClass, 40.0,
			PorkFX.default.MorphStyle, none, none, Actor(MO));
	}
	cprint("SQUEAL!!");
}

//==========================================================================
//
//  Cheat_Class
//
//==========================================================================

void Cheat_Class()
{
	int i;
	int pclass;
	Inventory Item;

	if (MorphTime)
	{
		// don't change class if the player is morphed
		cprint("CAN'T CHANGE CLASS WHILE MORPHED");
		return;
	}
	if (Cmd_CheckParm("fighter"))
	{
		pclass = HexenDefs::PCLASS_FIGHTER;
	}
	else if (Cmd_CheckParm("cleric"))
	{
		pclass = HexenDefs::PCLASS_CLERIC;
	}
	else if (Cmd_CheckParm("mage"))
	{
		pclass = HexenDefs::PCLASS_MAGE;
	}
	else
	{
		cprint("INVALID PLAYER CLASS");
		return;
	}
	//FIXME it doesn't work like this.
	PClass = pclass;
	HexenArmor HArmor = HexenArmor(EntityEx(MO).FindInventory(HexenArmor));
	for (i = 0; i < 4; i++)
	{
		HArmor.Slots[i] = 0.0;
	}

	// Remove all weapons
	for (Item = EntityEx(MO).Inventory; Item; Item = Item.Inventory)
	{
		if (Weapon(Item) || FourthWeaponHolder(Item))
		{
			Item.Destroy();
		}
	}
	BaseClass = pclass;
//	PostMorphWeapon(HexenDefs::WP_FIRST);
	Actor(MO).MorphPlayer(class<PlayerPawn>(Level.Game.PlayerClasses[PClass]), 0.0,
		PorkFX.default.MorphStyle, none, none, Actor(MO));
	SetCvar('class', pclass);
	SB_Start();
}

//==========================================================================
//
//  IsWeaponAlwaysExtremeDeath
//
//==========================================================================

bool IsWeaponAlwaysExtremeDeath()
{
	// Always extreme death from fourth weapon
	return ReadyWeapon.bExtremeDeath;
}

//==========================================================================
//
//  ClientConversation
//
//==========================================================================

void ClientConversation(string Speaker, string SpeakingTo)
{
	MainGameInfo(Level.Game).GameConParser.CleanScene = ConCleanScene;
	MainGameInfo(Level.Game).GameConParser.SetPersonInfo = ConSetPersonInfo;
	MainGameInfo(Level.Game).GameConParser.PlayCue = ConPlayCue;
	MainGameInfo(Level.Game).GameConParser.CleanChoices = ConCleanChoices;
	MainGameInfo(Level.Game).GameConParser.AddChoice = ConAddChoice;
	MainGameInfo(Level.Game).GameConParser.Suspend = ConSuspend;
	MainGameInfo(Level.Game).GameConParser.Wait = ConWait;
	MainGameInfo(Level.Game).GameConParser.CloseScene = ConCloseScene;

	//MainGameInfo(Level.Game).GameConParser.PlayerName = PlayerName;
	MainGameInfo(Level.Game).GameConParser.ConInfo.Game = MO.Level.Game;
	MainGameInfo(Level.Game).GameConParser.ConInfo.World = MO.Level.Game.WorldInfo;
	MainGameInfo(Level.Game).GameConParser.ConInfo.Level = MO.Level;
	MainGameInfo(Level.Game).GameConParser.ConInfo.Player = self;

	KRPGClientGameBase(ClGame).ConversationStart();

	MainGameInfo(Level.Game).GameConParser.Begin(Speaker, SpeakingTo);
}

//==========================================================================
//
//  ConCleanScene
//
//==========================================================================

void ConCleanScene()
{
	KRPGClientGameBase(ClGame).ConCleanScene();
}

//==========================================================================
//
//  ConSetPersonInfo
//
//==========================================================================

void ConSetPersonInfo(string Name, name Pic, name Flat)
{
	KRPGClientGameBase(ClGame).ConSetPersonInfo(Name, Pic, Flat);
}

//==========================================================================
//
//  ConPlayCue
//
//==========================================================================

void ConPlayCue(string Who, string ToWhom, string Cue, name Voice)
{
	KRPGClientGameBase(ClGame).ConPlayCue(Who, ToWhom, Cue, Voice);
}

//==========================================================================
//
//  ConCleanChoices
//
//==========================================================================

void ConCleanChoices()
{
	KRPGClientGameBase(ClGame).ConCleanChoices();
}

//==========================================================================
//
//  ConAddChoice
//
//==========================================================================

void ConAddChoice(string Text, string Name, int Index)
{
	KRPGClientGameBase(ClGame).ConAddChoice(Text, Name, Index);
}

//==========================================================================
//
//  ConSuspend
//
//==========================================================================

void ConSuspend()
{
	KRPGClientGameBase(ClGame).ConSuspend();
}

//==========================================================================
//
//  ConWait
//
//==========================================================================

void ConWait()
{
}

//==========================================================================
//
//  ConCloseScene
//
//==========================================================================

void ConCloseScene()
{
	KRPGClientGameBase(ClGame).ConversationEnd();
	KRPGLevelInfo(Level).StopSpeech2();	// some post processing
}

//==========================================================================
//
//  ConChoice
//
//==========================================================================

void ConChoice(string Name, int Index)
{
	MainGameInfo(Level.Game).GameConParser.ExecuteChoice(Name, Index);
}

//==========================================================================
//
//  ConContinue
//
//==========================================================================

void ConContinue()
{
	MainGameInfo(Level.Game).GameConParser.Continue();
}

//==========================================================================
//
//	KilledActor
//
//==========================================================================

void KilledActor(EntityEx Victim)
{
	// Check for frag changes
	if (Victim.bIsPlayer)
	{
		if (bIsBot)
		{
			Bot.Killed(Victim);
		}

		if (Victim.Player.bIsBot)
		{
			Player(Victim.Player).Bot.Died(EntityEx(MO));
		}

		if (Victim == MO)
		{
			// Self-frag
			Frags--;
		}
		else
		{
			Frags++;
		}
	}
}

//==========================================================================
//
//	PlayerMorphed
//
//==========================================================================

void PlayerMorphed(EntityEx OldMO)
{
	PClass = HexenDefs::PCLASS_PIG;
}

//==========================================================================
//
//	SpiritsWithin
//
//	[FB] Make fallen foe's spirits help us out
//
//==========================================================================

final bool A_SpiritsWithin(float limit)
{
	int i;
	EntityEx tail;
	EntityEx next;
	EntityEx mo;
	EntityEx g;
	float dist;

	foreach EntityEx(MO).AllThinkers(EntityEx, mo)
	{
		 // Not a corpse
		if (!mo.bCorpse)
		{
			continue;
		}
		dist = EntityEx(MO).DistTo(mo);

		if (dist > limit)
		{
			continue;
		}
		if (sp_power >= 5)
		{
			sp_power -= 5;
		}
		else
		{			
			return false;
		}
		g = Level.Spawn(HolySpirit, mo.Origin + vector(0.0, 0.0, 32.0));
		if (!g)
		{
			continue;
		}
		g.Angles.yaw = AngleMod360(mo.Angles.yaw +
			(45.0 + 45.0 / 2.0));
		g.Thrust(g.Angles.yaw, g.Speed * Level.Game.frametime);
		g.Target = EntityEx(MO);
		g.Args[0] = 10;	// initial turn value
		g.Args[1] = 0;	// initial look angle
		if (Level.Game.deathmatch)
		{
			// Ghosts last slightly less longer in DeathMatch
			g.Health = 85;
		}
		if (EntityEx(MO).Tracer)
		{
			g.Tracer = EntityEx(MO).Tracer;
			//	Don't colide with world but colide with things, i.e explode
			g.bColideWithWorld = false;
			g.bSkullFly = true;
			g.bMissile = false;
		}
		tail = Level.Spawn(HolyTail, g.Origin);
		tail.Target = g;	// parent
		for (i = 1; i < 3; i++)
		{
			next = Level.Spawn(HolyTailTrail, g.Origin);
			tail.Tracer = next;
			tail = next;
		}
		g.PlaySound(g.ActiveSound, CHAN_VOICE);
		mo.SetState(mo.FindState('FreeTargMobj'));
	}

	return true;
}

defaultproperties
{
	SpellSize = 6;
	InvSize = 56;
}
