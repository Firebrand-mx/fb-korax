//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id: template.vc 508 2006-07-10 19:02:43Z firebrand_kh $
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ScoreBoardScreen : Window;

const int
	SHADE_LEFT		= 24,
	SHADE_TOP		= 40,
	SHADE_WIDTH		= 592,
	SHADE_HEIGHT		= 400,

	PORTRAIT_HEIGHT		= 25,

	TABLE_LEFT_X		= 48,
	TABLE_DATA_LEFT_X	= 96,
	TABLE_HEAD_Y		= 48,
	TABLE_TOP_Y		= 82,
	TABLE_HEADER_MARGIN_TOP = 10,
	TABLE_HEADER_MARGIN_BOTTOM = 10,
	TABLE_STRING_DELTA	= 14,
	TABLE_ENTRY_DELTA	= 28,
	TABLE_COL2		= 240,
	TABLE_COL3		= 302,
	TABLE_COL4		= 386,
	TABLE_COL5		= 446,
	TABLE_COL6		= 494,
	TABLE_VALINDENT4	= 16,
	TABLE_VALINDENT5	= 12,
	TABLE_VALINDENT6	= 18;

ClientGameBase ClGame;

int colourmark[HexenDefs::MAXPLAYERTEAMS];

//[CW] may be those should be put into some shared resources module
string colournames[HexenDefs::MAXPLAYERTEAMS];
string gamemodenames[HexenDefs::NUMGAMES];
string playerclassnames[HexenDefs::NUMCLASSES];

int pic_portraits[8];
int pic_portraits_heights[8];


//==========================================================================
//
//  StartMap
//
//==========================================================================

void StartMap()
{
	//	initialise pics
	int i;
	picinfo_t info;

	for (i = 0; i < 8; i++)
	{
		pic_portraits[i] = R_RegisterPic(StrToName(va("icon0%d", i)));
		R_GetPicInfo(pic_portraits[i], &info);
		pic_portraits_heights[i] = info.height;
	}
}

//==========================================================================
//
//  DrawWindow
//
//==========================================================================

void DrawWindow()
{
	int j, y;
	int gamemode;
	int current_team;
	int current_team_idx;
	int current_player_inteam_idx;
	bool team_player_skipped;
	string tmp;
	KArenaPlayerReplicationInfo RepInfo;

	ShadeRect(SHADE_LEFT, SHADE_TOP, SHADE_WIDTH, SHADE_HEIGHT, 0.6);

	// Game mode

	gamemode = GetCvar('deathmatch');

	T_SetAlign(hleft, vtop);
	T_SetFont(font_yellow);
	T_DrawText(TABLE_LEFT_X, TABLE_HEAD_Y , va("game mode:   %s", gamemodenames[gamemode]));

	/*--------------------------------------
		General Scoring Table
	--------------------------------------*/

	/* Table Head		*/

	T_DrawText(TABLE_LEFT_X, TABLE_HEAD_Y + TABLE_STRING_DELTA , "player");
	T_DrawText(TABLE_COL2, TABLE_HEAD_Y + TABLE_STRING_DELTA, "score");
	T_DrawText(TABLE_COL3, TABLE_HEAD_Y + TABLE_STRING_DELTA, "ctrl");

	// Those are mode dependent
	T_DrawText(TABLE_COL4, TABLE_HEAD_Y + TABLE_STRING_DELTA, "frags");
	T_DrawText(TABLE_COL5, TABLE_HEAD_Y + TABLE_STRING_DELTA, "kills");
	T_DrawText(TABLE_COL6, TABLE_HEAD_Y + TABLE_STRING_DELTA, "deaths");
	//

	T_SetFont(font_small);

	y = TABLE_TOP_Y;
	
	current_team = -1;
	current_team_idx = 0;
	current_player_inteam_idx = 0;


	while(current_team_idx < HexenDefs::MAXTEAMSINGAME)
	{
		RepInfo = none;
		team_player_skipped = false;

		
		foreach ClGame.GLevel.LevelInfo.AllThinkers(KArenaPlayerReplicationInfo, RepInfo)
		{
			if (RepInfo.teampos != current_team_idx ||
					RepInfo.playerpos < current_player_inteam_idx)
				continue;
			
			if (RepInfo.playerpos > current_player_inteam_idx)
			{
				team_player_skipped = true;
				continue;
			}
		
			// Insert team string if a new team encountered
			if (gamemode >= HexenDefs::NUMGAMESNOTEAM && RepInfo.team != current_team)
			{
				if (current_team != -1)
					y += TABLE_HEADER_MARGIN_TOP;
				R_FillRect(SHADE_LEFT + 8, y - 2, SHADE_WIDTH - 16, 13, colourmark[RepInfo.team]);
				T_DrawText(TABLE_LEFT_X, y, va("%s team,   score:   %d", colournames[RepInfo.team],
									Player(ClGame.cl).teamscores[current_team_idx]));
				y += TABLE_HEADER_MARGIN_BOTTOM;
				current_team = RepInfo.team;
			}

			// Mark current client with special background colour
			if (ClGame.cl.ClientNum == RepInfo.PlayerNum)
			{
				if (gamemode < HexenDefs::NUMGAMESNOTEAM)
					R_FillRect(SHADE_LEFT + 8, y + 6, SHADE_WIDTH - 16, PORTRAIT_HEIGHT, colourmark[RepInfo.team]);
				else
				{
					T_SetFont(font_yellow);
				}
			}

			// Player portrait
			DrawPic(TABLE_LEFT_X + 20, y + PORTRAIT_HEIGHT - ((PORTRAIT_HEIGHT - pic_portraits_heights[RepInfo.plclass]) >> 1),
								pic_portraits[RepInfo.plclass]);

			y += 6;

			// Player's name
			T_DrawText(TABLE_DATA_LEFT_X, y, RepInfo.PlayerName);

			// Level + class
			switch (RepInfo.level)
			{
				case 1: tmp = "st"; break;
				case 2: tmp = "nd"; break;
				case 3: tmp = "rd"; break;
				default: tmp = "th";
			}
			T_DrawText(TABLE_DATA_LEFT_X, y + TABLE_STRING_DELTA, va("%d%s %s",
				RepInfo.level, tmp, playerclassnames[RepInfo.plclass]));

			y += TABLE_STRING_DELTA >> 1;

			// Player's score
			T_DrawText(TABLE_COL2, y, va("%d", RepInfo.score));

			// Player is human/cpu
			if (RepInfo.IsBot)
				T_DrawText(TABLE_COL3, y, "bot");
			else
				T_DrawText(TABLE_COL3, y, "hum");

			// Those are mode dependent
			T_DrawText(TABLE_COL4 + TABLE_VALINDENT4, y, va("%d", RepInfo.Frags));
			T_DrawText(TABLE_COL5 + TABLE_VALINDENT5, y, va("%d", RepInfo.KillCount));
			T_DrawText(TABLE_COL6 + TABLE_VALINDENT6, y, va("%d", RepInfo.deaths));

			y += TABLE_ENTRY_DELTA - (TABLE_STRING_DELTA >> 1);

			if (gamemode >= HexenDefs::NUMGAMESNOTEAM)
				T_SetFont(font_small);

			current_player_inteam_idx++;
		}
		
		if (!team_player_skipped)
		{
			current_team_idx++;
			current_player_inteam_idx = 0;
		}
	}
}

defaultproperties
{
	Width = 640;
	Height = 480;

	colourmark[0] = 0xff0000aa;
	colourmark[1] = 0xffaa0000;
	colourmark[2] = 0xffaaaa00;
	colourmark[3] = 0xff55aa00;
	colourmark[4] = 0xff008800;
	colourmark[5] = 0xff888888;
	colourmark[6] = 0xff884400;
	colourmark[7] = 0xff880088;

	colournames[0] = "blue";
	colournames[1] = "red";
	colournames[2] = "gold";
	colournames[3] = "lime";
	colournames[4] = "green";
	colournames[5] = "gray";
	colournames[6] = "brown";
	colournames[7] = "magenta";

	gamemodenames[0] = "unused game mode";
	gamemodenames[1] = "deathmatch";
	gamemodenames[2] = "kill the heresiarch";
	gamemodenames[3] = "castle sweep";
	gamemodenames[4] = "team deathmatch";
	gamemodenames[5] = "capture the flag";
	gamemodenames[6] = "team castle sweep";
	gamemodenames[7] = "football";

	playerclassnames[0] = "fighter";
	playerclassnames[1] = "cleric";
	playerclassnames[2] = "mage";
	playerclassnames[3] = "heretic";
	playerclassnames[4] = "marine";
	playerclassnames[5] = "hero";
	playerclassnames[6] = "pig";
	playerclassnames[7] = "heresiarch";
}
