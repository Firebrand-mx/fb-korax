//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		    ##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MenuScreenNewNetGame : MenuScreen;

array<int>			MapLumpIdx;
array<name>			MapLumpNames;

// [CW] an order of game modes in list
int					GameTypes[HexenDefs::NUMGAMES - HexenDefs::GAMEFIRSTUSED];

MenuChoiceEnum		Monsters;
MenuChoiceEnum		ScoreLimit;
MenuChoiceEnum		TimeLimit;
MenuChoiceEnum		Level;

// Extra options
MenuChoiceEnum		SurvivalStartingWave;
MenuChoiceEnum		SurvivalMonsterAmount;
MenuChoiceEnum		SurvivalMonsterHealth;


int					LevelPic;		// level shot thumbnail
picinfo_t			LevelPicInfo;
string				LevelInfo;		// level description
int					ChoicesHeight;
int					factor;			// [FB] Global factor value

//==========================================================================
//
//	CreateChoices
//
//==========================================================================

void CreateChoices()
{
	int i;
	MenuTextButton		Btn;
	MenuChoiceEnum		Enum;
	int					currentY;

	currentY = ChoicesStartY;

	Btn = MenuTextButton(NewChild(MenuSmallTextButton));
	Btn.SetOrigin(ChoicesStartX, currentY);
	Btn.Text = "Start Game";
	Btn.Activated = ActivateStartNewGame;
	currentY += Btn.Height;

	Btn = MenuTextButton(NewChild(MenuSmallTextButton));
	Btn.SetOrigin(ChoicesStartX, currentY);
	Btn.Text = "Server Settings";
	Btn.Activated = ActivateServerSettings;
	currentY += Btn.Height;

	Btn = MenuTextButton(NewChild(MenuSmallTextButton));
	Btn.SetOrigin(ChoicesStartX, currentY);
	Btn.Text = "Team Settings";
	Btn.Activated = ActivateTeamSettings;
	currentY += Btn.Height;

	Enum = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	Enum.SetOrigin(ChoicesStartX, currentY);
	Enum.Text = "Max players";
	Enum.AddItem("1");
	Enum.AddItem("2");
	Enum.AddItem("3");
	Enum.AddItem("4");
	Enum.AddItem("5");
	Enum.AddItem("6");
	Enum.AddItem("7");
	Enum.AddItem("8");
	Enum.SetInitialValue(7);
	Enum.ValueChanging = ChangeMaxPlayers;
	currentY += Enum.Height;

	Enum = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	Enum.SetOrigin(ChoicesStartX, currentY);
	Enum.Text = "Skill";
	for (i = 0; i < P_GetNumSkills(); i++)
	{
		Enum.AddItem(P_GetSkillDef(i)->Name);
	}
	Enum.SetInitialValue(2);
	Enum.ValueChanging = ChangeSkill;
	currentY += Enum.Height;

	Enum = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	Enum.SetOrigin(ChoicesStartX, currentY);
	Enum.Text = "Bots";
	Enum.AddItem("0");
	Enum.AddItem("1");
	Enum.AddItem("2");
	Enum.AddItem("3");
	Enum.AddItem("4");
	Enum.AddItem("5");
	Enum.AddItem("6");
	Enum.AddItem("7");
	Enum.SetInitialValue(0);
	Enum.ValueChanging = ChangeBots;
	currentY += Enum.Height;

	Monsters = MenuChoiceEnum(NewChild(MenuChoice_OnOff));
	Monsters.SetOrigin(ChoicesStartX, currentY);
	Monsters.Text = "Monsters";
	Monsters.ValueChanging = ChangeMonsters;
	currentY += Monsters.Height;

	Enum = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	Enum.SetOrigin(ChoicesStartX, currentY);
	Enum.Text = "Game type";
	for (i = 0; i < HexenDefs::NUMGAMES - HexenDefs::GAMEFIRSTUSED; i++)
	{
		switch (GameTypes[i])
		{
		case HexenDefs::game_dm:
			Enum.AddItem("Deathmatch"); break;
		case HexenDefs::game_kth:
			Enum.AddItem("Kill the Heresiarch"); break;
		case HexenDefs::game_sweep:
			Enum.AddItem("Castle Sweep"); break;
		case HexenDefs::game_survival:
			Enum.AddItem("Survival"); break;
		case HexenDefs::game_xmas:
			Enum.AddItem("XMas Havoc"); break;
		case HexenDefs::game_dmteam:
			Enum.AddItem("Team Deathmatch"); break;
		case HexenDefs::game_ctf:
			Enum.AddItem("Capture The Flag"); break;
		case HexenDefs::game_domination:
			Enum.AddItem("Domination"); break;
		case HexenDefs::game_sweepteam:
			Enum.AddItem("Team Castle Sweep"); break;
		case HexenDefs::game_football:
			Enum.AddItem("Football"); break;
		}
	}
	Enum.SetInitialValue(0);
	Enum.ValueChanging = ChangeDeathmatch;
	currentY += Enum.Height;

	ScoreLimit = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	ScoreLimit.SetOrigin(ChoicesStartX, currentY);
	ScoreLimit.Text = "Score limit";
	ScoreLimit.ValueChanging = ChangeScoreLimit;
	currentY += ScoreLimit.Height;

	TimeLimit = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	TimeLimit.SetOrigin(ChoicesStartX, currentY);
	TimeLimit.Text = "Time limit";
	TimeLimit.AddItem("no limit");
	for (i = 1; i <= 12; i++)
	{
		TimeLimit.AddItem(va("%i", i * 5));
	}
	TimeLimit.ValueChanging = ChangeTimeLimit;
	currentY += TimeLimit.Height;

	Level = MenuChoiceEnum(NewChild(MenuChoiceEnumWrapped));
	Level.SetOrigin(ChoicesStartX, currentY);
	Level.Text = "Level";
	Level.Width = Level.Width - 16;
	Level.ValueChanging = ChangeLevel;
	currentY += (Level.Height << 1); // [CW] two lines height (can be increased later if needed)

	int topExtraChoices = currentY + 12;
	int maxExtraY = 0;

	//
	// Survival extra choices:
	currentY = topExtraChoices;

	SurvivalStartingWave = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	SurvivalStartingWave.SetOrigin(ChoicesStartX, currentY);
	SurvivalStartingWave.Text = "Starting Wave";
	for (i = 0; i < /* GameSurvival.default.MonsterClasses.Num */ 50; i++)
	{
		SurvivalStartingWave.AddItem(va("%i", i + 1));
	}
	SurvivalStartingWave.SetInitialValue(GetCvar('StartingWave'));
	SurvivalStartingWave.ValueChanging = ChangeSurvivalStartingWave;
	currentY += SurvivalStartingWave.Height;

	SurvivalMonsterAmount = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	SurvivalMonsterAmount.SetOrigin(ChoicesStartX, currentY);
	SurvivalMonsterAmount.Text = "Monster Amount";
	SurvivalMonsterAmount.AddItem("really not so much");
	SurvivalMonsterAmount.AddItem("not much");
	SurvivalMonsterAmount.AddItem("less");
	SurvivalMonsterAmount.AddItem("normal");
	SurvivalMonsterAmount.AddItem("more");
	SurvivalMonsterAmount.AddItem("much more");
	SurvivalMonsterAmount.AddItem("roaring hordes");
	SurvivalMonsterAmount.AddItem("rolling armies");
	SurvivalMonsterAmount.AddItem("hellish infestation");
	SurvivalMonsterAmount.SetInitialValue(GetCvar('MonsterAmount'));
	SurvivalMonsterAmount.ValueChanging = ChangeSurvivalMonsterAmount;
	currentY += SurvivalMonsterAmount.Height;

	SurvivalMonsterHealth = MenuChoiceEnum(NewChild(MenuChoiceEnum));
	SurvivalMonsterHealth.SetOrigin(ChoicesStartX, currentY);
	SurvivalMonsterHealth.Text = "Monster Health";
	SurvivalMonsterHealth.AddItem("crippled");
	SurvivalMonsterHealth.AddItem("puny");
	SurvivalMonsterHealth.AddItem("weaker");
	SurvivalMonsterHealth.AddItem("normal");
	SurvivalMonsterHealth.AddItem("stronger");
	SurvivalMonsterHealth.AddItem("powerfull");
	SurvivalMonsterHealth.AddItem("tough as rock");
	SurvivalMonsterHealth.AddItem("robust as a tank");
	SurvivalMonsterHealth.AddItem("korax'es offspring");
	SurvivalMonsterHealth.SetInitialValue(GetCvar('MonsterHealth'));
	SurvivalMonsterHealth.ValueChanging = ChangeSurvivalMonsterHealth;
	currentY += SurvivalMonsterHealth.Height;

	if (currentY > maxExtraY)
		maxExtraY = currentY;

	ChangeMaxPlayers(none, 7);
	ChangeSkill(none, 2);
	ChangeDeathmatch(none, 0);
	ChangeBots(none, 0);
	ChangeTimeLimit(none, TimeLimit.Value);

	ChoicesHeight = maxExtraY - ChoicesStartY;
}

//==========================================================================
//
//	OnDraw
//
//==========================================================================

void OnDraw()
{
	int x = 300;
	int y = 16;

	FillRectWithFlat(4, FirstChild.Y - 8, 320,
		ChoicesStartY - FirstChild.Y + ChoicesHeight + 16, 'kaf_012');

	FillRectWithFlat(x - 8, y - 8, 336, 480 - (y - 8) - 8, 'kaf_010');

	if (LevelPic)
	{	
		R_DrawPic(x, y, LevelPic);
		y += LevelPicInfo.height + 16;
	}

	SetFont('smallfont');
	SetTextAlign(hleft, vtop);
	DrawText(x, y, LevelInfo);
}

//==========================================================================
//
//	ActivateStartNewGame
//
//==========================================================================

void ActivateStartNewGame(Object Sender)
{
	CmdBuf_AddText("Disconnect\n");
	ClientGame(ClGame).LoadingScreenStart(MapLumpNames[Level.Value], Level.EnumNames[Level.Value]);
}

//==========================================================================
//
//	ActivateServerSettings
//
//==========================================================================

void ActivateServerSettings(Object Sender)
{
	ClGame.PushMenuScreen(ClGame.SpawnMenu(MenuScreenServerSettings));
}

//==========================================================================
//
//	ActivateTeamSettings
//
//==========================================================================

void ActivateTeamSettings(Object Sender)
{
	ClGame.PushMenuScreen(ClGame.SpawnMenu(MenuScreenTeamSettings));
}

//==========================================================================
//
//	ChangeMaxPlayers
//
//==========================================================================

bool ChangeMaxPlayers(Object Sender, int newValue)
{
	CmdBuf_AddText("MaxPlayers %d\n", newValue + 1);
	return true;
}

//==========================================================================
//
//	ChangeSkill
//
//==========================================================================

bool ChangeSkill(Object Sender, int newValue)
{
	SetCvar('Skill', newValue);
	return true;
}

//==========================================================================
//
//	ChangeBots
//
//==========================================================================

bool ChangeBots(Object Sender, int newValue)
{
	SetCvar('BotsAutoSpawn', newValue);
	return true;
}

//==========================================================================
//
//	ChangeMonsters
//
//==========================================================================

bool ChangeMonsters(Object Sender, int newValue)
{
	SetCvar('NoMonsters', !newValue);
	return true;
}

//==========================================================================
//
//	ChangeDeathmatch
//
//==========================================================================

bool ChangeDeathmatch(Object Sender, int newValue)
{
	CmdBuf_AddText("DeathMatch %d\n", GameTypes[newValue]);
	InitMapList(GameTypes[newValue]);
	InitScoreLimit(GameTypes[newValue]);
	InitMonsters(GameTypes[newValue]);

	if (GameTypes[newValue] == HexenDefs::game_survival)
	{
		SurvivalStartingWave.Visible = true;
		SurvivalStartingWave.Focusable = true;
		SurvivalMonsterAmount.Visible = true;
		SurvivalMonsterAmount.Focusable = true;
		SurvivalMonsterHealth.Visible = true;
		SurvivalMonsterHealth.Focusable = true;
	}
	else
	{
		SurvivalStartingWave.Visible = false;
		SurvivalStartingWave.Focusable = false;
		SurvivalMonsterAmount.Visible = false;
		SurvivalMonsterAmount.Focusable = false;
		SurvivalMonsterHealth.Visible = false;
		SurvivalMonsterHealth.Focusable = false;
	}
	return true;
}

//==========================================================================
//
//	ChangeScoreLimit
//
//==========================================================================

bool ChangeScoreLimit(Object Sender, int newValue)
{
	SetCvar('ScoreLimit', newValue * factor);
	return true;
}

//==========================================================================
//
//	ChangeTimeLimit
//
//==========================================================================

bool ChangeTimeLimit(Object Sender, int newValue)
{
	SetCvar('TimeLimit', newValue * 5);
	return true;
}

//==========================================================================
//
//	ChangeLevel
//
//==========================================================================

bool ChangeLevel(Object Sender, int newValue)
{
	string picname = va("%nth", MapLumpNames[newValue]);
	LevelPic = R_RegisterPic(StrToName(picname));
	if (LevelPic)
		R_GetPicInfo(LevelPic, &LevelPicInfo);
	string lvlname = strlwr(StrReplace(Level.EnumNames[newValue], " ", ""));
	lvlname = strlwr(StrReplace(lvlname, ",", ""));
	lvlname = strlwr(StrReplace(lvlname, ".", ""));
	lvlname = strlwr(StrReplace(lvlname, "!", ""));
	lvlname = strlwr(StrReplace(lvlname, "?", ""));
	LevelInfo = GetLangString(StrToName(va("%s_info", lvlname)));
	LevelInfo = SplitTextWithNewlines(LevelInfo, 320);
	return true;
}

//==========================================================================
//
//	ChangeSurvivalStartingWave
//
//==========================================================================

bool ChangeSurvivalStartingWave(Object Sender, int newValue)
{
	SetCvar('StartingWave', newValue);
	return true;
}

//==========================================================================
//
//	ChangeSurvivalMonsterAmount
//
//==========================================================================

bool ChangeSurvivalMonsterAmount(Object Sender, int newValue)
{
	SetCvar('MonsterAmount', newValue);
	return true;
}

//==========================================================================
//
//	ChangeSurvivalMonsterHealth
//
//==========================================================================

bool ChangeSurvivalMonsterHealth(Object Sender, int newValue)
{
	SetCvar('MonsterHealth', newValue);
	return true;
}

//==========================================================================
//
//	InitMapList
//
// [CW] Load and parse maps table for selected game mode
//
//==========================================================================

void InitMapList(int GameType)
{
	ScriptsParser sc = ScriptsParser(SpawnObject(ScriptsParser));

	switch (GameType)
	{
	case HexenDefs::game_dm:
		sc.OpenLumpName('gm_dm');
		break;
	case HexenDefs::game_dmteam:
		sc.OpenLumpName('gm_tdm');
		break;
	case HexenDefs::game_kth:
		sc.OpenLumpName('gm_kth');
		break;
	case HexenDefs::game_domination:
		sc.OpenLumpName('gm_dom');
		break;
	case HexenDefs::game_ctf:
		sc.OpenLumpName('gm_ctf');
		break;
	case HexenDefs::game_sweep:
		sc.OpenLumpName('gm_swp');
		break;
	case HexenDefs::game_sweepteam:
		sc.OpenLumpName('gm_tswp');
		break;
	case HexenDefs::game_survival:
		sc.OpenLumpName('gm_srv');
		break;
	case HexenDefs::game_football:
		sc.OpenLumpName('gm_ftb');
		break;
	case HexenDefs::game_xmas:
		sc.OpenLumpName('gm_xmas');
		break;
	default:
		Error("MenuChoice_Level::InitMapList : unknown game type %i", GameType);
		return;
	}

	if (sc.AtEnd())
	{
		Error("MenuChoice_Level::InitMapList : maps table missing for game type %i", GameType);
		return;
	}

	//
	// Begin parsing...
	//
	Level.EnumNames.Num = 0;
	MapLumpIdx.Num = 0;
	MapLumpNames.Num = 0;

	while (!sc.AtEnd())
	{
		sc.ExpectString();
		if (StrEndsWith(sc.String, "*"))
		{
			AddMapsToList(substr(sc.String, 0, strlen(sc.String) - 1), true);
		}
		else
		{
			AddMapsToList(sc.String, false);
		}
	}
	sc.Destroy();

	//
	// Sorting alphabetically
	//
	int isorted, iraw;
	string dummy;
	string leftmost;
	int leftmost_idx;

	MapLumpNames.Num = MapLumpIdx.Num;
	for (isorted = 0; isorted < MapLumpNames.Num; isorted++)
	{
		leftmost = "";
		for (iraw = 0; iraw < MapLumpIdx.Num; iraw++)
		{
			if (MapLumpIdx[iraw] >= 0)
			{
				dummy = P_GetMapName(MapLumpIdx[iraw]);
				if (!strlen(leftmost) || stricmp(dummy, leftmost) < 0)
				{
					leftmost = dummy;
					leftmost_idx = iraw;
				}
			}
		}

		MapLumpNames[isorted] = P_GetMapLumpName(MapLumpIdx[leftmost_idx]);
		Level.AddItem(P_GetMapName(MapLumpIdx[leftmost_idx]));
		MapLumpIdx[leftmost_idx] = -1;
	}

	Level.SetInitialValue(0);
	ChangeLevel(none, Level.Value);
}

//==========================================================================
//
//	AddMapsToList
//
//==========================================================================

void AddMapsToList(string mapLumpName, bool useAsPrefix)
{
	int i, count, len;
	string testmapname;

	mapLumpName = strlwr(mapLumpName);
	for (i = 0, count = 0; i < 99; i++)
	{
		// If the lump found after any others is the first possible map, it means
		// no appropriate lump may be found any more, so cycle should end here 
		if (i > 0 && P_GetMapLumpName(i) == P_GetMapLumpName(0))
			break;

		testmapname = va("%n", P_GetMapLumpName(i));
		len = strlen(mapLumpName);
		if (strlen(testmapname) < len)
			continue;

		if (useAsPrefix ? StrStartsWith(testmapname, mapLumpName) :
			!strcmp(testmapname, mapLumpName))
		{
			count++;
			MapLumpIdx.Num = MapLumpIdx.Num + 1;
			MapLumpIdx[MapLumpIdx.Num - 1] = i;
			
			if (!useAsPrefix)
				break;
		}
	}
}

//==========================================================================
//
//	InitMonsters
//
//==========================================================================

void InitMonsters(int GameType)
{
	switch (GameType)
	{
	case HexenDefs::game_sweep:	//	Castle Sweep
	case HexenDefs::game_sweepteam: //	Team Castle Sweep
	case HexenDefs::game_survival:
		Monsters.Value = 1;
		break;
	case HexenDefs::game_dm:	//	Deathmatch
	case HexenDefs::game_dmteam:	//	Team deathmatch.
	case HexenDefs::game_kth:
	case HexenDefs::game_domination:	
	case HexenDefs::game_ctf: //	Capture the Flag
	case HexenDefs::game_football: //	Football
	case HexenDefs::game_xmas:
		Monsters.Value = 0;
		break;
	default:
		Error("MenuChoice_Level::InitMonsters : unknown game type %i", GameType);
	}

	ChangeMonsters(none, Monsters.Value);
}

//==========================================================================
//
//	InitScoreLimit
//
//==========================================================================

void InitScoreLimit(int GameType)
{
	int i, count;
	ScoreLimit.EnumNames.Num = 0;

	switch (GameType)
	{
	case HexenDefs::game_sweep:	//	Castle Sweep
	case HexenDefs::game_sweepteam: //	Team Castle Sweep
	case HexenDefs::game_dm:	//	Deathmatch
	case HexenDefs::game_dmteam:	//	Team deathmatch.
	case HexenDefs::game_kth:
	case HexenDefs::game_survival:
		factor = 5;
		count = 20;
		break;
	case HexenDefs::game_domination:
		factor = 25;
		count = 20;
		break;
	case HexenDefs::game_ctf: //	Capture the Flag
		factor = 1;
		count = 20;
		break;
	case HexenDefs::game_football: //	Football
		factor = 1;
		count = 20;
		break;
	case HexenDefs::game_xmas:
		factor = 25;
		count = 20;
		break;
	default:
		Error("MenuChoice_Level::InitScoreLimit : unknown game type %i", GameType);
	}

	ScoreLimit.AddItem("no limit");
	for (i = 1; i <= count; i++)
	{
		ScoreLimit.AddItem(va("%i", i * factor));
	}
	ScoreLimit.Value = 0;
	ChangeScoreLimit(none, ScoreLimit.Value);
}

defaultproperties
{
	X = 0;
	Y = 0;
	Width = 640;
	Height = 480;
	TitleX = 150;
	TitleY = 104;
	ChoicesStartX = 132;
	ChoicesStartY = 120;
	SelectorType = MenuSelector_SmallRight;
	Title = "NEW GAME";

	GameTypes[0] = HexenDefs::game_dm;
	GameTypes[1] = HexenDefs::game_dmteam;
	GameTypes[2] = HexenDefs::game_kth;
	GameTypes[3] = HexenDefs::game_survival;
	GameTypes[4] = HexenDefs::game_ctf;
	GameTypes[5] = HexenDefs::game_domination;
	GameTypes[6] = HexenDefs::game_sweep;
	GameTypes[7] = HexenDefs::game_sweepteam;
	GameTypes[8] = HexenDefs::game_football;
	GameTypes[9] = HexenDefs::game_xmas;
}
