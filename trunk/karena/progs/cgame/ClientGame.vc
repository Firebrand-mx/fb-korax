//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ClientGame : ClientGameBase;

enum
{
	MB_None,
	MB_Control,
	MB_Quit,
	MB_EndGame
};

enum
{
	pt_static,
	pt_explode,
	pt_explode2,
	pt_chunks,
	pt_chunks2
};

const int MAX_MENU_STACK		= 32;

TitleScreen			GTitle;
StatusBarScreen		GSBar;
ScoreBoardScreen	GScBoard;	// [CW]
IntermissionScreen	GImission;
FinaleScreen		GFinale;

MenuScreenTeamSelect	TeamSelectScreen;	//[CW]

LocalSoundQueue		Announcer;		// [CW]

MenuScreen menu_stack[MAX_MENU_STACK];
int menu_sp;

MenuScreen CurrentMenuScreen;

MenuControlKey CurrentCtl;
int ControlColumn;	// menu column for controls

int MenuPClass;
name MenuStartMap;
class<MenuScreen> SkillMenuType;

//
//  MESSAGE BOX
//
bool messageToPrint;
int msgBoxX;
int msgBoxY;
int msgBoxW;
int msgBoxH;
string messageString;	// ...and here is the message string!
bool messageNeedsInput;	// timed message = no input from user
int messageType;

int pic_bordt;
int pic_bordb;
int pic_bordl;
int pic_bordr;
int pic_bordtl;
int pic_bordtr;
int pic_bordbl;
int pic_bordbr;

int ramp1[16];
int ramp2[16];
int ecolours[8];


bool IsInGame;

//==========================================================================
//
//  RootWindowCreated
//
//==========================================================================

void RootWindowCreated()
{
	//[CW]
	CreateCvar('BodyQueSize', "32", CVAR_ARCHIVE);
	CreateCvar('CorpseQueSize', "64", CVAR_ARCHIVE);
	CreateCvar('BotsAutoSpawn', "0", CVAR_ARCHIVE);
	CreateCvar('TargetNameHint', "1", CVAR_ARCHIVE | CVAR_USERINFO);

	GTitle = TitleScreen(GRoot.NewChild(TitleScreen));
	GSBar = StatusBarScreen(GRoot.NewChild(StatusBarScreen));
	GScBoard = ScoreBoardScreen(GRoot.NewChild(ScoreBoardScreen));		// [CW]
	GImission = IntermissionScreen(GRoot.NewChild(IntermissionScreen));
	GFinale = FinaleScreen(GRoot.NewChild(FinaleScreen));
	Announcer = LocalSoundQueue(SpawnObject(LocalSoundQueue));
	GTitle.ClGame = self;
	GSBar.cl = Player(cl);
	GSBar.ClGame = self;
	GSBar.Hide();

	/* scoreboard by [CW]	*/
	GScBoard.cl = Player(cl);
	GScBoard.Hide();

	GImission.ClGame = self;
	GImission.Hide();
	GFinale.ClGame = self;
	GFinale.Hide();
}

//==========================================================================
//
//  Connected
//
//==========================================================================

void Connected()
{
	GTitle.Hide();
	GTitle.StopDemoLoop();
	GSBar.Show();
	GImission.Hide();
	GFinale.Hide();

	IsInGame = true;
}

//==========================================================================
//
//  Disconnected
//
//==========================================================================

void Disconnected()
{
	GTitle.Show();
	GSBar.Hide();
	GScBoard.Hide();	// [CW]
	GImission.Hide();
	GFinale.Hide();

	IsInGame = false;
}

//==========================================================================
//
//  DemoPlaybackStarted
//
//==========================================================================

void DemoPlaybackStarted()
{
	GTitle.Hide();
	GSBar.Show();
	GImission.Hide();
	GFinale.Hide();
}

//==========================================================================
//
//  DemoPlaybackStopped
//
//==========================================================================

void DemoPlaybackStopped()
{
	GTitle.Show();
	GSBar.Hide();
	GScBoard.Hide();	// [CW]
	GImission.Hide();
	GFinale.Hide();
}

//==========================================================================
//
//  OnHostEndGame
//
//==========================================================================

void OnHostEndGame()
{
	GTitle.OnHostEndGame();
}

//==========================================================================
//
//  OnHostError
//
//==========================================================================

void OnHostError()
{
	GTitle.OnHostError();
}

//==========================================================================
//
//  StatusBarStartMap
//
//==========================================================================

void StatusBarStartMap()
{
	GSBar.Show();
	GSBar.StartMap();
	GScBoard.StartMap();	// [CW]
	GImission.Hide();
	GFinale.Hide();
}

//==========================================================================
//
//  StatusBarDrawer
//
//==========================================================================

void StatusBarDrawer(int sb_view)
{
	GSBar.SB_Drawer(sb_view);
}

//==========================================================================
//
//  StatusBarUpdateWidgets
//
//==========================================================================

void StatusBarUpdateWidgets()
{
	GSBar.SB_UpdateWidgets();

	if (Announcer)
		Announcer.Continue(time - oldtime);
}

//==========================================================================
//
//  IintermissionStart
//
//==========================================================================

void IintermissionStart()
{
	GSBar.Hide();
	GScBoard.Hide();	// [CW]
	GImission.Show();
	GImission.Start();
	GFinale.Hide();
}

//==========================================================================
//
//  StartFinale
//
//==========================================================================

void StartFinale(name FinaleType)
{
	GSBar.Hide();
	GScBoard.Hide();	// [CW]
	GImission.Hide();
	GFinale.Show();
	GFinale.StartFinale(FinaleType);
}

//===========================================================================
//
//  FinaleResponder
//
//===========================================================================

bool FinaleResponder(event_t * event)
{
	return false;
}

//==========================================================================
//
//  DeactivateMenu
//
//==========================================================================

void DeactivateMenu()
{
	ForceMenuOff();
}

//==========================================================================
//
//  MenuResponder
//
//==========================================================================

bool MenuResponder(event_t * event)
{
	if (!CurrentMenuScreen)
	{
		// Pop-up menu?
		if (event->type == ev_keydown && event->data1 == K_ESCAPE)
		{
			//PushMenuScreen(SpawnMenu(MenuScreenMain));
			SpawnMainMenu();
			return true;
		}
		return false;
	}

	if (event->type != ev_keydown)
	{
		return false;
	}
	return CurrentMenuScreen.Key(event->data1);
}

//==========================================================================
//
//  MenuActive
//
//==========================================================================

bool MenuActive()
{
	return CurrentMenuScreen != none;
}

//==========================================================================
//
//	SetMenu
//
//==========================================================================

void SetMenu(string Name)
{
	if (!stricmp(Name, "Main"))
	{
		//PushMenuScreen(SpawnMenu(MenuScreenMain));
		SpawnMainMenu();
		return;
	}
	/*if (!stricmp(Name, "Save"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenSaveGame));
		return;
	}
	if (!stricmp(Name, "Load"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenLoadGame));
		return;
	}*/
	if (!stricmp(Name, "PSetup"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenPlayerSetup));
		return;
	}
	if (!stricmp(Name, "Options"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenOptions));
		return;
	}
	if (!stricmp(Name, "Controls"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenControls));
		return;
	}
	if (!stricmp(Name, "Controls2"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenControls2));
		return;
	}
	if (!stricmp(Name, "Mouse"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenMouseOptions));
		return;
	}
	if (!stricmp(Name, "Resolution"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenScreenResolution));
		return;
	}
	if (!stricmp(Name, "Video"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenVideoOptions));
		return;
	}
	if (!stricmp(Name, "Sound"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenSoundOptions));
		return;
	}
	if (!stricmp(Name, "Help"))
	{
		PushMenuScreen(SpawnMenu(MenuScreenHelp));
		return;
	}
	if (!stricmp(Name, "Quit"))
	{
		menu_quit();
		return;
	}
	//PushMenuScreen(SpawnMenu(MenuScreenMain));
	SpawnMainMenu();
}

//==========================================================================
//
//  SpawnMenu
//
//==========================================================================

MenuScreen SpawnMenu(class<MenuScreen> CID)
{
	MenuScreen m;

	m = MenuScreen(GRoot.NewChild(CID));
	m.ClGame = self;
	return m;
}

//==========================================================================
//
//	SpawnMainMenu
//
//	[CW]
//
//==========================================================================

void SpawnMainMenu()
{
	MenuScreenMain mm = MenuScreenMain(SpawnMenu(MenuScreenMain));
	mm.InitWindow();
	PushMenuScreen(mm);
}

//**************************************************************************
//**
//**    Menu stack handling
//**
//**************************************************************************

//==========================================================================
//
//  PushMenuScreen
//
//==========================================================================

void PushMenuScreen(MenuScreen menu)
{
	int i;

	// avoid stacking menus invoked by hotkeys
	for (i = 0; i < menu_sp; i++)
	{
		if (menu_stack[i].Class == menu.Class)
		{
			int j;

			for (j = i; j < menu_sp; j++)
			{
				menu_stack[j].Destroy();
			}
			menu_sp = i;
			CurrentMenuScreen = none;
			break;
		}
	}

	if (menu_sp >= MAX_MENU_STACK)
		FatalError("PushMenu: menu stack overflow");

	menu_stack[menu_sp++] = menu;

	if (!CurrentMenuScreen)
	{
/*		if (cl.paused)
		{
			S_ResumeSound();
		}
		C_ClearNotify();*/
		CmdBuf_AddText("HideConsole\n");
		LocalSound('menu/activate');
	}
	else
	{
		CurrentMenuScreen.Hide();
	}

	CurrentMenuScreen = menu;
}

//==========================================================================
//
//  PopMenu
//
//==========================================================================

void PopMenu()
{
	menu_sp--;

	if (menu_sp < 0)
		FatalError("PopMenu: menu stack underflow");

	//[CW]
	if (CurrentMenuScreen == TeamSelectScreen)
		TeamSelectScreen.OnCancel();

	menu_stack[menu_sp].Destroy();
	if (menu_sp)
	{
		CurrentMenuScreen = menu_stack[menu_sp - 1];
		CurrentMenuScreen.Show();
		CurrentMenuScreen.Raise();
		LocalSound('menu/backup');
	}
	else
	{
		CurrentMenuScreen = none;
		LocalSound('menu/clear');
	}
}

//==========================================================================
//
//  ForceMenuOff
//
//==========================================================================

void ForceMenuOff()
{
	int i;

	//[CW]
	if (TeamSelectScreen && CurrentMenuScreen == TeamSelectScreen)
		TeamSelectScreen.OnCancel();

	for (i = 0; i < menu_sp; i++)
	{
		menu_stack[i].Destroy();
	}
	menu_sp = 0;
	CurrentMenuScreen = none;
}

//==========================================================================
//
//  MessageBoxStartMessage
//
//==========================================================================

void MessageBoxStartMessage(string text, int type, bool input)
{
	messageToPrint = true;
	messageString = text;
	messageType = type;
	messageNeedsInput = input;

	T_SetFont(font_small);
	msgBoxW = (T_TextWidth(messageString) + 16) & ~15;
	msgBoxH = (T_TextHeight(messageString) + 16) & ~15;
	if (msgBoxW > 304)
		msgBoxW = 304;
	if (msgBoxH > 184)
		msgBoxH = 184;
	msgBoxX = (640 - msgBoxW) / 2;
	msgBoxY = (480 - msgBoxH) / 2;

	CmdBuf_AddText("HideConsole\n");
}

//==========================================================================
//
//  MessageBoxDrawer
//
//==========================================================================

void MessageBoxDrawer()
{
	// Horiz. & Vertically centre string and print it.
	if (messageToPrint)
	{
		R_FillRectWithFlat(msgBoxX, msgBoxY, msgBoxW, msgBoxH, 'f_019');
		DrawBorder(msgBoxX, msgBoxY, msgBoxW, msgBoxH);
		T_SetFont(font_small);
		T_SetAlign(hcentre, vcentre);
		T_DrawText(320, 240, messageString);
	}
}

//==========================================================================
//
//  MessageBoxResponder
//
//==========================================================================

bool MessageBoxResponder(event_t * event)
{
	// Take care of any messages that need input
	if (messageToPrint && event->type == ev_keydown)
	{
		if (messageNeedsInput &&
			event->data1 != K_SPACE &&
			event->data1 != K_n &&
			event->data1 != K_y &&
			event->data1 != K_ESCAPE &&
			event->data1 != K_MOUSE1 && event->data1 != K_MOUSE2)
		{
			return true;
		}

		messageToPrint = false;
		switch (messageType)
		{
		case MB_None:
			break;
		case MB_Control:
			ControlResponse(event->data1);
			break;
		case MB_Quit:
		case MB_EndGame:
			QuitResponse(messageType, event->data1);
			break;
			break;
		}

//      S_StartSound(sfx1_swtchx);
		return true;
	}
	return false;
}

//==========================================================================
//
//  MessageBoxActive
//
//==========================================================================

bool MessageBoxActive()
{
	return messageToPrint;
}

//==========================================================================
//
//  menu_quit
//
//==========================================================================

void menu_quit()
{
	LocalSound('Chat');
	MessageBoxStartMessage("ARE YOU SURE YOU WANT TO QUIT?", MB_Quit, true);
}

//==========================================================================
//
//  QuitResponse
//
//==========================================================================

void QuitResponse(int type, int ch)
{
	if (ch != K_y)
		return;

	switch (type)
	{
	case MB_Quit:
		CmdBuf_AddText("Quit\n");
		break;
	case MB_EndGame:
		CmdBuf_AddText("Disconnect\n");
		break;
	};
}

//==========================================================================
//
//  ControlResponse
//
//==========================================================================

void ControlResponse(int key)
{
	string kb_down;
	string kb_up;
	int key1;
	int key2;

	if (key != K_ESCAPE)
	{
		kb_down = CurrentCtl.CommandOn;
		kb_up = CurrentCtl.CommandOff;
		IN_GetBindingKeys(kb_down, &key1, &key2);
		if (ControlColumn == 0)
		{
			IN_SetBinding(key1, "", "");
			IN_SetBinding(key, kb_down, kb_up);
		}
		if (ControlColumn == 1)
		{
			IN_SetBinding(key2, "", "");
			IN_SetBinding(key, kb_down, kb_up);
		}
	}
}

//==========================================================================
//
//  NewGame
//
//==========================================================================

void NewGame(int SkillLevel)
{
	SetCvar('Skill', SkillLevel);
	SetCvar('Class', MenuPClass);

	CmdBuf_AddText("Disconnect\n");
	CmdBuf_AddText("MaxPlayers 1\n");
	CmdBuf_AddText("Map %n\n", MenuStartMap);

	ForceMenuOff();
}

//==========================================================================
//
//  InitBorder
//
//==========================================================================

void InitBorder()
{
	pic_bordt = R_RegisterPic('bordt');
	pic_bordb = R_RegisterPic('bordb');
	pic_bordl = R_RegisterPic('bordl');
	pic_bordr = R_RegisterPic('bordr');
	pic_bordtl = R_RegisterPic('bordtl');
	pic_bordtr = R_RegisterPic('bordtr');
	pic_bordbl = R_RegisterPic('bordbl');
	pic_bordbr = R_RegisterPic('bordbr');
}

//==========================================================================
//
//  DrawBorder
//
//  Draws border
//
//==========================================================================

void DrawBorder(int x, int y, int w, int h)
{
	int i;

	if (!pic_bordb)
	{
		InitBorder();
	}

	for (i = 0; i < w; i += 16)
		R_DrawPic(x + i, y - 4, pic_bordt);

	for (i = 0; i < w; i += 16)
		R_DrawPic(x + i, y + h, pic_bordb);

	for (i = 0; i < h; i += 16)
		R_DrawPic(x - 4, y + i, pic_bordl);

	for (i = 0; i < h; i += 16)
		R_DrawPic(x + w, y + i, pic_bordr);

	// Draw beveled edge.
	R_DrawPic(x - 4, y - 4, pic_bordtl);
	R_DrawPic(x + w, y - 4, pic_bordtr);
	R_DrawPic(x - 4, y + h, pic_bordbl);
	R_DrawPic(x + w, y + h, pic_bordbr);
}

//==========================================================================
//
//  DrawViewBorder
//
//==========================================================================

void DrawViewBorder(int x, int y, int w, int h)
{
	R_FillRectWithFlat(0, 0, 640, 480/* - 39*/, 'f_022');
	DrawBorder(x, y, w, h);
}

//==========================================================================
//
//  ParseGunShot
//
//==========================================================================

void ParseGunShot()
{
	TVec org;
	int i, j;
	particle_t *p;
	int c;

	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	for (i = 0; i < 20; i++)
	{
		p = NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(c, c, c);
		p->die = time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = pt_static;
	}
}

//==========================================================================
//
//  ParseBlood
//
//==========================================================================

void ParseBlood()
{
	TVec org;
	int damage;
	int i, j;
	particle_t *p;
	int c;

	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());
	damage = MSG_ReadByte();

	for (i = 0; i < damage; i++)
	{
		p = NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(32 + c, c >> 4, c >> 4);
		p->die = time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = pt_static;
	}
}

//==========================================================================
//
//  ParseExplosion
//
//==========================================================================

void ParseExplosion()
{
// FROM HEXEN
/*	TVec org;
	dlight_t *dl;
	int etype;

	etype = MSG_ReadByte();
	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	dl = AllocDlight(0);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = ecolours[etype];
	dl->die = cl.time + 0.5;
	dl->decay = 300.0;*/

// ORIGINAL FROM DOOM
	TVec org;
	int i;
	particle_t *p;
	dlight_t *dl;

	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	for (i = 0; i < 1024; i++)
	{
		p = NewParticle();
		if (!p)
			return;
		p->die = time + 5.0;
		p->colour = ramp1[0];
		p->ramp = Random() * 4.0;
		if (i & 1)
		{
			p->type = pt_explode;
		}
		else
		{
			p->type = pt_explode2;
		}
		p->org.x = org.x + ((Random() * 32.0) - 16.0);
		p->org.y = org.y + ((Random() * 32.0) - 16.0);
		p->org.z = org.z + ((Random() * 32.0) - 16.0);
		p->vel.x = (Random() * 512.0) - 256.0;
		p->vel.y = (Random() * 512.0) - 256.0;
		p->vel.z = (Random() * 512.0) - 256.0;
	}

	dl = AllocDlight(0);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = 0xffffffff;
	dl->die = time + 0.5;
	dl->decay = 300.0;

}

//==========================================================================
//
//  ParseExplosion2
//
//==========================================================================

void ParseExplosion2()
{
	TVec org;
	dlight_t *dl;
	int etype;

	etype = MSG_ReadByte();
	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	dl = AllocDlight(0);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = ecolours[etype];
	dl->die = time + 0.5;
	dl->decay = 300.0;
}

//==========================================================================
//
//  ParseIceChunks
//
//==========================================================================

void ParseIceChunks()
{
	TVec org;
	int i, c;
	particle_t *p;

	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	for (i = 0; i < 1024; i++)
	{
		p = NewParticle();
		if (!p)
			return;
		p->die = time + 5.0;
		c = P_Random() >> 1; 
		p->colour = RGB(c, c, c);
		p->ramp = Random() * 4.0;
		if (i & 1)
		{
			p->type = pt_chunks;
		}
		else
		{
			p->type = pt_chunks2;
		}
		p->org.x = org.x + ((Random() * 16.0) - 8.0);
		p->org.y = org.y + ((Random() * 16.0) - 8.0);
		p->org.z = org.z + ((Random() * 16.0) - 8.0);
		p->vel.x = (Random() * 256.0) - 128.0;
		p->vel.y = (Random() * 256.0) - 128.0;
		p->vel.z = (Random() * 256.0) - 128.0;
	}
}

//==========================================================================
//
//  ParseTeamSelect
//
//==========================================================================

void ParseTeamSelect()
{
	int i;
	bool created;
	created = false;

	if (TeamSelectScreen == none)
	{
		TeamSelectScreen = MenuScreenTeamSelect(SpawnMenu(MenuScreenTeamSelect));
		created = true;
	}
	TeamSelectScreen.maxTeams = MSG_ReadByte();
	TeamSelectScreen.activeTeams = MSG_ReadByte();

	for (i = 0; i < TeamSelectScreen.activeTeams; i++)
	{
		TeamSelectScreen.Teams[i] = MSG_ReadByte();
	}

	if (created)
	{
		TeamSelectScreen.curSelection = MSG_ReadByte();
		TeamSelectScreen.Ready = true;
		TeamSelectScreen.CreateChoices();
		PushMenuScreen(TeamSelectScreen);
	}
	else
		TeamSelectScreen.ReInit();
}

//==========================================================================
//
//  ParseLocalSound
//
//==========================================================================

void ParseLocalSound()
{
	bool put_in_queue;
	name sndName;

	switch(MSG_ReadByte())
	{
	case HexenDefs::lsid_kth_an_awaiting: sndName = 'KTH_AwaitingForHeresiarch'; break;
	case HexenDefs::lsid_kth_an_ishere: sndName = 'KTH_HeresiarchIsHere'; break;
	case HexenDefs::lsid_kth_an_hasreborn: sndName = 'KTH_HeresiarchIsReborn'; break;
	case HexenDefs::lsid_kth_an_isdown: sndName = 'KTH_HeresiarchIsDown'; break;
	case HexenDefs::lsid_kth_mus_death: sndName = 'KTH_HeresiarchMusDeath'; break;
	case HexenDefs::lsid_kth_mus_birth: sndName = 'KTH_HeresiarchMusBirth'; break;

	default: sndName = 'misc/chat'; break;
	}

	put_in_queue = MSG_ReadByte();

	if (put_in_queue && Announcer)
		Announcer.QueueSound(sndName, itof(MSG_ReadShort()) / 100.0);
	else
	{
		MSG_ReadShort();
		LocalSound(sndName);
	}
}

//==========================================================================
//
//  ParseDynamicLightEx
//
//	[CW] Full data dlight parsing
//
//==========================================================================

void ParseDynamicLightEx()
{
	dlight_t *dl;

	dl = AllocDlight(0);

	dl->colour = MSG_ReadLong();
	dl->origin.x = itof(MSG_ReadShort());
	dl->origin.y = itof(MSG_ReadShort());
	dl->origin.z = itof(MSG_ReadShort());
	dl->radius = itof(MSG_ReadShort());
	dl->die = time + itof(MSG_ReadShort()) * 0.01;
	dl->decay = itof(MSG_ReadShort()) * 0.01;
}

//==========================================================================
//
//  ParseServerCommand
//
//==========================================================================

bool ParseServerCommand(int cmd_type)
{
	switch (cmd_type)
	{
	case HexenDefs::svc_gun_shot:
		ParseGunShot();
		break;

	case HexenDefs::svc_blood:
		ParseBlood();
		break;

	case HexenDefs::svc_explosion:
		ParseExplosion();
		break;

// [Korax]
	case HexenDefs::svc_explosion2:
		ParseExplosion2();
		break;

	case HexenDefs::svc_chunks:
		ParseIceChunks();
		break;

	case HexenDefs::svc_fov:
		SetCvar('FOV', MSG_ReadByte());
		break;

	case HexenDefs::svc_teamselect:
		ParseTeamSelect();
		break;

	case HexenDefs::svc_scoreboard:
		if (MSG_ReadByte())
			GScBoard.Show();
		else
			GScBoard.Hide();
		break;

	case HexenDefs::svc_localsound:
		ParseLocalSound();
		break;

	case HexenDefs::svc_dlight:			// currently unused
		break;

	case HexenDefs::svc_dlightex:
		ParseDynamicLightEx();
		break;

	default:
		return false;
	}
	return true;
}

//==========================================================================
//
//  UpdateParticle
//
//==========================================================================

void UpdateParticle(particle_t * p, float DeltaTime)
{
	float time1, time2, time3;
	float dvel;
	float grav;
	int c;
	
	time1 = DeltaTime * 5.0;
	time2 = DeltaTime * 10.0;	// 15;
	time3 = DeltaTime * 15.0;

	dvel = DeltaTime * 4.0;
	grav = DeltaTime * 1.0 * 0.05; //frametime * sv_gravity.value * 0.05;

	switch (p->type)
	{
	case pt_static:
		break;

	case pt_explode:
		p->ramp += time2;
		if (p->ramp >= 16.0)
			p->die = -1.0;
		else
			p->colour = ramp1[ftoi(p->ramp)];
		p->vel.x += p->vel.x * dvel;
		p->vel.y += p->vel.y * dvel;
		p->vel.z += p->vel.z * dvel;
		p->vel.z -= grav;
		break;

	case pt_explode2:
		p->ramp += time3;
		if (p->ramp >= 16.0)
			p->die = -1.0;
		else
			p->colour = ramp2[ftoi(p->ramp)];
		p->vel.x -= p->vel.x * DeltaTime;
		p->vel.y -= p->vel.y * DeltaTime;
		p->vel.z -= p->vel.z * DeltaTime;
		p->vel.z -= grav;
		break;

	case pt_chunks:
		p->ramp += time1;
		if (p->ramp >= 16.0)
			p->die = -1.0;
		else
		c = P_Random() >> 1; 
		p->colour = RGB(c, c, c);
		p->vel.x += p->vel.x * dvel / 2.0;
		p->vel.y += p->vel.y * dvel / 2.0;
		p->vel.z += p->vel.z * dvel / 2.0;
		p->vel.z -= grav;
		break;

	case pt_chunks2:
		p->ramp += time2;
		if (p->ramp >= 16.0)
			p->die = -1.0;
		else
		c = P_Random() >> 1;
		p->colour = RGB(c, c, c);
		p->vel.x -= p->vel.x * DeltaTime / 2.0;
		p->vel.y -= p->vel.y * DeltaTime / 2.0;
		p->vel.z -= p->vel.z * DeltaTime / 2.0;
		p->vel.z -= grav;
		break;
	}
}

//==========================================================================
//
//  UpdateMobj
//
//==========================================================================

void UpdateMobj(Entity mobj, int key, float DeltaTime)
{
	dlight_t *dl;
	TVec fv;
	int i, c, count;
	particle_t *p;

	switch (mobj.Effects & HexenDefs::EF_DLIGHT_MASK)
	{
	case HexenDefs::EF_DL_NONE:
		break;

	case HexenDefs::EF_DL_WHITE:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(255, 255, 255);
		dl->die = time + 0.1;
		break;

	case HexenDefs::EF_DL_RED:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(255, 0, 0);
		dl->die = time + 0.1;
		break;

	case HexenDefs::EF_DL_GREEN:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(0, 255, 0);
		dl->die = time + 0.1;
		break;

	case HexenDefs::EF_DL_BLUE:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(0, 0, 255);
		dl->die = time + 0.1;
		break;

	case HexenDefs::EF_DL_DARK:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(1, 1, 1);
		dl->die = time + 0.1;
		break;

	case HexenDefs::EF_DL_ROCKET:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->radius = 200.0;
		dl->colour = RGB(255, 255, 255);
		dl->die = time + 0.1;

		count = ftoi(DeltaTime * 256.0);
		for (i = 0; i < count; i++)
		{
			p = NewParticle();
			if (!p)
				return;

			p->org = mobj.Origin;
			c = P_Random() >> 1;
			p->colour = RGB(c, c, c);
			p->die = time + 0.5 * Random();
			p->type = pt_static;
			p->vel.x = 16.0 * (Random() - 0.5);
			p->vel.y = 16.0 * (Random() - 0.5);
			p->vel.z = 16.0 * (Random() - 0.5);
		}
		break;

	case HexenDefs::EF_DL_MUZZLEFLASH:
		dl = AllocDlight(key);
		dl->origin = mobj.Origin;
		dl->origin.z += 48.0;
		AngleVector(&mobj.Angles, &fv);

		dl->origin = dl->origin + 18.0 * fv;
		dl->radius = 200.0 + Random() * 31.0;
		dl->colour = 0xffffffff;
		dl->minlight = 32.0;
		dl->die = time + 0.1;
	}
}

defaultproperties
{
	sb_height = 39;
	ramp1[0] = 0xfffff31b; ramp1[1] = 0xefdfab27; ramp1[2] = 0xdfbf772f; ramp1[3] = 0xcf9f4f33;
	ramp1[4] = 0xbf7f3b2b; ramp1[5] = 0xaf632f1f; ramp1[6] = 0x9f4b2313; ramp1[7] = 0x8f2f170b;
	ramp1[8] = 0x7f7f3b2b; ramp1[9] = 0x6f632f1f; ramp1[10] = 0x5f4b2313; ramp1[11] = 0x4f2f170b;
	ramp1[12] = 0x3f7f3b2b; ramp1[13] = 0x2f632f1f; ramp1[14] = 0x1f4b2313; ramp1[15] = 0x0f2f170b;
	ramp2[0] = 0xfffff31b; ramp2[1] = 0xefefcb1f; ramp2[2] = 0xdfdfab27; ramp2[3] = 0xcfcf8f2b;
	ramp2[4] = 0xbfbf772f; ramp2[5] = 0xafaf632f; ramp2[6] = 0x9f8f4333; ramp2[7] = 0x8f733723;
	ramp2[8] = 0x7fbf772f; ramp2[9] = 0x6faf632f; ramp2[10] = 0x5f8f4333; ramp2[11] = 0x4f733723;
	ramp2[12] = 0x3fbf772f; ramp2[13] = 0x2faf632f; ramp2[14] = 0x1f8f4333; ramp2[15] = 0x0f733723;
	ecolours[0] = 0;	// EF_DL_NONE
	ecolours[1] = RGB(255, 255, 255);	// EF_DL_WHITE
	ecolours[2] = RGB(255, 32, 32);	// EF_DL_RED
	ecolours[3] = RGB(32, 255, 32);	// EF_DL_GREEN
	ecolours[4] = RGB(32, 32, 255);	// EF_DL_BLUE
	ecolours[5] = RGB(1, 1, 1);    	// EF_DL_DARK
	ecolours[6] = RGB(255, 255, 255);	// EF_DL_ROCKET
	ecolours[7] = 0;			// EF_DL_MUZZLEFLASH
}
