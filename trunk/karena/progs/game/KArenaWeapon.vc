//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**	        ##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class KArenaWeapon : Weapon
	abstract;

//	Third ammo ... I mean mana.
class<Ammo>		AmmoType3;
int				AmmoGive3;
int				AmmoUse3;
Ammo			Ammo3;

bool			dex_mod;
bool			str_mod;
bool			int_mod;
bool			wis_mod;

bool			bInstantRanged;	// [CW] Can be used for BotAI too
TVec			ShotPosition;	// [CW]

// --------------------
// -- Bot AI stuff --
// --------------------

// Bot AI flags
bool			bBotProjectileMedium;	// [CW] renamed from bBotChooseMedium to correspond to other 2 flag names
name			DamageTypeHint;			// [CW]

										// TODO!!
float			BotSafeDist;			// [CW] Weapon.BotCombatDist determines effective range
										//      while BotSafeDist stands for minimal safe range

// Fire & Refire tics
float			fire_tics;
float			refire_tics;

//	We need this for weapon pickups, leave for other weapons for now.
int				Slot;

//	Amount the damage will be modified by balance factor
int				factor;
//-------------------------------------------------------------------------------------------------------
//					BALANCING MATRIX DATA
//	      These flags are all informative, just to know which feature a weapon has
//-------------------------------------------------------------------------------------------------------

int				Shots;					//	Purely informative; number of projectiles/shots/strikes
										//	(this should be used as a constant)
int				damage;					//	Purely informative; damage amount this weapon does
										//	(this should be used as a constant, verified by projectiles shot)
bool			bExplosive;				//	This weapon is explosive
bool			bDelayedExplosion;		//	This weapon produces delayed explosions
bool			bPersistent;			//	This weapon drops persistent projectiles (poison clouds)
bool			bMeleeLong;				//	This weapon has long melee range
bool			bProjectileShort;		//	It has shor projectile range (flamethrower)
bool			bProjectileSlow;		//	Shots slow projectiles (darkness)
bool			bProjectileFast;		//	Shots fast projectiles (wands, blasters)
bool			bProjectileSeeker;		//	Shots seeker missiles
bool			bProjectileRipper;		//	Shots ripper projectiles
bool			bHitScan;				//	Hitscan weapon
bool			bHighPrecision;			//	Shots precise hitscans
bool			bLowPrecision;			//	Shots unprecise hitscans
bool			bSpread;				//	This weapons spreads projectiles
										//	(or shots more projectiles when fired missile explodes)
bool			bMassSpread;			//	This weapons massively spreads projectiles
bool			bIncrementShots;		//	Weapon has small increment in shots with player's leveling
bool			bBigIncrementShots;		//	Weapon has big increment in shots with player's leveling

//-------------------------------------------------------------------------------------------------------
//					BALANCING MATRIX METHODS
//-------------------------------------------------------------------------------------------------------

//==========================================================================
//
//	WeaponLevel
//
//==========================================================================

int WeaponLevel(int slot)
{
	return	slot * 20;
}

//==========================================================================
//
//	FireRate
//
//==========================================================================

int FireRate()
{
	if (refire_tics <= 5.0)
	{
		// Repeating
		return 5;
	}
	else if (refire_tics > 5.0 && refire_tics <= 15.0)
	{
		// Fast
		return 3;
	}
	else if (refire_tics > 15.0 && refire_tics <= 30.0)
	{
		// Slow
		return 2;
	}
	else if (refire_tics > 30.0)
	{
		// Charging Power (or something :P)
		return 1;
	}
}

//==========================================================================
//
//	NumberOfShots
//
//==========================================================================

int NumberOfShots(int number)
{
	if (Shots <= 1)
	{
		return 1;
	}
	else if (Shots == 2)
	{
		return 2;
	}
	else if (Shots == 3)
	{
		return 5;
	}
	else if (Shots == 4 || bIncrementShots)
	{
		return 4;
	}
	else if (Shots >= 5 || bBigIncrementShots)
	{
		return 8;
	}
}

//==========================================================================
//
//	WeaponType
//
//==========================================================================

int WeaponType()
{
	int sum;

	if (bBotMelee)
	{
		// Weapon is melee type, check which specific type we have
		if (bMeleeLong)
		{
			sum += 2;
		}
		sum += 1;
	}
	if (bBotProjectileMedium || bBotProjectile)
	{
		if (bProjectileShort || bProjectileSlow)
		{
			sum += 3;
		}
		if (bProjectileSeeker || bProjectileRipper)
		{
			sum += 8;
		}
		if (bProjectileFast)
		{
			sum += 9;
		}
		sum += 6;
	}
	if (bHitScan)
	{
		// OK, this weapon is hitscan, now check which type of hitscan weapon we have
		if (bHighPrecision)
		{
			sum += 12;
		}
		if (bLowPrecision)
		{
			sum += 5;
		}
		sum += 10;
	}
	
	return sum;
}

//==========================================================================
//
//	SpreadingType
//
//==========================================================================

int SpreadingType()
{
	if (bSpread)
	{
		return 2;
	}
	else if (bMassSpread)
	{
		return 3;
	}

	// No spreading
	return 1;
}

//==========================================================================
//
//	ExplosiveType
//
//==========================================================================

int ExplosiveType()
{
	if (bExplosive)
	{
		return 3;
	}
	if (bDelayedExplosion)
	{
		return 6;
	}
	if (bPersistent)
	{
		return 10;
	}
	
	return 0;
}

//==========================================================================
//
//	DamageType
//
//==========================================================================

int DamageType()
{
	if (DamageTypeHint == 'Fire')
	{
		return 2;
	}
	else if (DamageTypeHint == 'Poison')
	{
		return 4;
	}
	else if (DamageTypeHint == 'Darkness')
	{
		return 6;
	}
	
	return 1;
}

//==========================================================================
//
//	CalculateFactors
//
//==========================================================================

void CalculateFactors()
{
	// First, check weapon level (slot)
	factor += WeaponLevel(Slot);
	// Add factor given from weapon's fire rate
	factor += FireRate();
	// Calculate factor from number of shots done by this weapon
	factor += NumberOfShots(Shots);
	// Calculate factor from weapon type
	factor += WeaponType();
	// Calculate factor from spreading type
	factor += SpreadingType();
	// Calculate factor from explosion type
	factor += ExplosiveType();
	// Calculate factor from damage type
	factor += DamageType();
	
	print("weapon's balance factor: %d", factor);
}

//==========================================================================
//
//	HandlePickup
//
//	In Korax Arena weapons pickups and actual weapon classes are different,
// so we are using a different pickup handling.
//
//==========================================================================

bool HandlePickup(Inventory Item)
{
	if (Item.Class == Class)
	{
		//	Actual weapon classes are used only internally to give weapons,
		// so just ignore it.
		return true;
	}

	if (WeaponPickup(Item) && KArenaPlayerPawn(Owner) &&
		KArenaPlayerPawn(Owner).WeaponClasses[WeaponPickup(Item).Slot - 1] == Class)
	{
		//	It's a weapon pickup that will give this weapon, so just pick it
		// up for more mana.
		if (WeaponPickup(Item).PickupForMana(EntityEx(Owner)))
		{
			Item.bPickupGood = true;
		}
		return true;
	}

	if (Inventory)
	{
		return Inventory.HandlePickup(Item);
	}
	return false;
}

//==========================================================================
//
//	AttachToOwner
//
//==========================================================================

void AttachToOwner(EntityEx NewOwner)
{
	::AttachToOwner(NewOwner);

	Ammo3 = AddAmmo(NewOwner, AmmoType3, AmmoGive3);
	CalculateFactors();
}

//==========================================================================
//
//  GetManaUse
//
//==========================================================================

final int GetManaUse(int manause)
{
	if (wis_mod)
	{
		manause = manause * 10 / Player(Owner.Player).wisdom;
	}

	// affect manause by balance factor
	manause = manause - (manause * (factor / manause ? manause : 1) / 80);

	// [FB] Always use mana, no matter what
	if(manause < 1)
	{
		manause = 1;
	}

	return manause;
}

//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough mana to shoot.  If not, selects the
// next weapon to use.
//
//==========================================================================

bool CheckAmmo(byte Mode, bool AutoSwitch, optional bool RequireAmmo)
{
	if (!RequireAmmo && bAmmoOptional)
	{
		return true;
	}
	bool Good = true;
	if (Ammo1 && Ammo1.Amount < GetManaUse(AmmoUse1))
	{
		Good = false;
	}
	if (Ammo2 && Ammo2.Amount < GetManaUse(AmmoUse2))
	{
		Good = false;
	}
	if (Ammo3 && Ammo3.Amount < GetManaUse(AmmoUse3))
	{
		Good = false;
	}
	if (Good)
	{
		return true;
	}
	if (!AutoSwitch)
	{
		return false;
	}
	Player(Owner.Player).centreprint("No mana");

	// out of mana, pick a weapon to change to
	Weapon Best = Player(Owner.Player).BestWeapon();

	Player(Owner.Player).PendingWeapon = Best;
	Player(Owner.Player).SetViewState(ps_weapon, GetDownState());
	return false;
}

//==========================================================================
//
//  UseWeaponMana
//
//==========================================================================

final void UseWeaponMana()
{
	if (Ammo1)
	{
		Ammo1.Amount -= GetManaUse(AmmoUse1);
		if (Ammo1.Amount < 0)
		{
			Ammo1.Amount = 0;
		}
	}
	if (Ammo2)
	{
		Ammo2.Amount -= GetManaUse(AmmoUse2);
		if (Ammo2.Amount < 0)
		{
			Ammo2.Amount = 0;
		}
	}
	if (Ammo3)
	{
		Ammo3.Amount -= GetManaUse(AmmoUse3);
		if (Ammo3.Amount < 0)
		{
			Ammo3.Amount = 0;
		}
	}
}

//===========================================================================
//
//  GunShot1 (Used for the pistol)
//
//===========================================================================

final void GunShot1(EntityEx A, int accurate, TVec dir)
{
	int damage;

	//  Make a copy of angles because inacurate shoot will modify them
	if (!accurate)
	{
		TAVec angles;

		VectorAngles(&dir, &angles);
		angles.yaw = AngleMod360(angles.yaw +
			(Random() - Random()) * 45.0 / 8.0);
		AngleVector(&angles, &dir);
	}

	// [CW] CHECKME!! -- this method is used by 2 Marine weapons
	// - Pistol and Shotgun; is this damage value should be
	// determined as a local constant here?
	damage = 13; //5 * (P_Random() % 3 + 1);
	ApplyDamageModifiers(damage);

	ShotPosition = Player(Owner.Player).MO.Origin;	// [CW]
	A.LineAttack(dir, MISSILERANGE, damage, BulletPuff);
}

//===========================================================================
//
//  GunShot2 (Used for the chaingun)
//
//===========================================================================

final void GunShot2(EntityEx A, int accurate, TVec dir)
{
	int damage;

	//  Make a copy of angles because inacurate shoot will modify them
	if (!accurate)
	{
		TAVec angles;

		VectorAngles(&dir, &angles);
		angles.yaw = AngleMod360(angles.yaw +
			(Random() - Random()) * 45.0 / 8.0);
		AngleVector(&angles, &dir);
	}

	damage = 17; //7 * (P_Random() % 3 + 1);

	ApplyDamageModifiers(damage);

	ShotPosition = Player(Owner.Player).MO.Origin;	// [CW]
	A.LineAttack(dir, MISSILERANGE, damage, BulletPuff);
}

//===========================================================================
//
//  ApplyDamageModifiers
//
//===========================================================================

final void ApplyDamageModifiers(out int damage)
{
	if (str_mod)
		damage = damage * Player(Owner.Player).strength / 10;
	if (int_mod)
		damage = damage * Player(Owner.Player).intelligence / 10;

	// affect damage by balance factor
	damage = damage - (damage * (factor / damage ? damage : 1) / 80);
	
	if (damage < 1)
	{
		damage = 1;
	}

	// PI Damage
	if(Player(Owner.Player).pi_damage)
		damage *= ftoi(3.1415);
}

defaultproperties
{
	bBloodSplatter = true;
	Kickback = 150.0;
}
