//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id: GameCTF.vc 508 2006-07-10 19:02:43Z firebrand_kh $
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class GameCTF : KArenaGameModel;

const int	MAXBANNERS	= 2;
const int	MINREQUIREDBANNERS = 2;	// ...how formal...

enum
{
	state_preparegame,
	state_gameplay,		// no actual game states here
	state_finished
};

/*enum
{
	event_colourtranslation		= GameModel::event_custom,
	num_ctfevents	= event_colourtranslation - GameModel::event_custom + 1
};*/

// acsnotifier_t	AcsNotifiersCTF[num_ctfevents];

array<CTFBanner>	Banners;
array<CTFBase>		spot_banners;	// Bases are all created during initialization


//==========================================================================
//
//	AllowedTeams
//
//==========================================================================

int AllowedTeams()
{
	return 2;
}

//==========================================================================
//
//	BeginState
//
//	ACS callable
//
//==========================================================================

void BeginState(int newstate)
{
	int i, count1, count2;

	dprint("BeginState: %i", newstate);

	switch(newstate)
	{
	case state_preparegame:
		if (!PrepareGame())
		{
			dprint("GameCTF : failed to prepare game, delay start");
			State = -1;
			return;
		}

		dprint("GameCTF : starting game!");
		
		HandleEvent(event_playablestatebegins, none, 0, 0, 0, 0);
		
		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (Game.Teams[0] && Game.Teams[0].Players[i])
			   Game.Teams[0].Players[i].UpdateScore(HexenDefs::scoreevent_PlayerReset);
			if (Game.Teams[1] && Game.Teams[1].Players[i])
			   Game.Teams[1].Players[i].UpdateScore(HexenDefs::scoreevent_PlayerReset);
		}
		if (Game.Teams[0])
			Game.Teams[0].UpdateScore(HexenDefs::scoreevent_TeamReset);
		if (Game.Teams[1])
			Game.Teams[1].UpdateScore(HexenDefs::scoreevent_TeamReset);
		EndCurrentState(state_gameplay);
		return;

	case state_gameplay:
		if (!Game.Teams[0] || !Game.Teams[1])
		{
			dprint("GameCTF::BeginState : state_gameplay : not enough teams, ending game");
			EndCurrentState(state_finished);
			return;
		}
		count1 = 0;
		for (i = 0; i < spot_banners.Num; i++)
		{
			if (spot_banners[i])
				count1++;
		}
		count2 = 0;
		for (i = 0; i < Banners.Num; i++)
		{
			if (Banners[i])
				count2++;
		}
		
		if (count1 < MINREQUIREDBANNERS || count2 < MINREQUIREDBANNERS)
		{
			dprint("GameCTF::BeginState : state_gameplay : not enough banners and/or spot_banners, ending game");
			EndCurrentState(state_finished);
			return;
		}

		for (i = 0; i < Banners.Num; i++)
		{
			if (Banners[i])
				Banners[i].ReturnToBase(none, true);
		}
		Game.bcentreprint("go for it!");
		break;

	case state_finished:
		//
		// do something here?
		//
		break;
	};

	State = newstate;
}

//==========================================================================
//
//	IsInPlayableState
//
//==========================================================================

bool IsInPlayableState()
{
	return (State >= 0 && State == state_gameplay);
}

//==========================================================================
//
//	OnUpdateTeams
//
//	TODO: handle delayed banners?? <-- checkme not applicable anymore
//
//==========================================================================

void OnUpdateTeams()
{
	dprint("GameCTF::NotifyUpdateTeams : State = %i, Teams[0] = %i, Teams[1] = %i", State, Game.Teams[0], Game.Teams[1]);
	if (!IsInPlayableState() && (Game.Teams[0] && Game.Teams[1]))
		BeginState(state_preparegame);
	else if (IsInPlayableState() && (!Game.Teams[0] || !Game.Teams[1]))
		EndCurrentState(state_finished);
}

//==========================================================================
//
//	OnPlayerDeath
//
//==========================================================================

void OnPlayerDeath(BasePlayer pl, EntityEx source)
{
	int i;
	for (i = 0; i < Banners.Num; i++)
	{
		if (Banners[i] && Banners[i].Capturer && Banners[i].Capturer == pl)
		{
			Banners[i].WasLost();
			break;
		}
	}
}

//==========================================================================
//
//	EndCurrentState
//
//	ACS callable
//
//==========================================================================

void EndCurrentState(int nextstate)
{
	if (nextstate == state_finished)
	{
		int i;
		for (i = 0; i < Banners.Num; i++)
		{
			if (Banners[i] && Banners[i].Capturer)
			{
				Banners[i].WasLost();
				break;
			}
		}
		Cleanup();
	}

	BeginState(nextstate);
}

/* //==========================================================================
//
//	HandleEvent
//
//	ACS callable
//
//==========================================================================

void HandleEvent(int Event, EntityEx A, int arg1, int arg2, int arg3, int arg4)
{
	int notifyACSArgs[3];

	dprint("GameCTF::HandleEvent : %i", Event);

	switch (Event)
	{
	case event_colortranslation:
		notifyACSArgs[0] = arg1;	// should be team index
		notifyACSArgs[1] = arg2;	// should be object tag
		break;
	default:
		::HandleEvent(Event, A, arg1, arg2, arg3, arg4);
		return;
	}

	NotifyACS(Event, notifyACSArgs[0], notifyACSArgs[1], notifyACSArgs[2]);
} */

/* //==========================================================================
//
//	SetACSNotifier
//
//	ACS callable
//
//==========================================================================

void SetACSNotifier(int event, int script)
{
	if (event >= GameModel::event_custom && event < event_colortranslation + num_ctfevents)
	{
		event -= GameModel::event_custom;
		AcsNotifiersCTF[event].enabled = true;
		AcsNotifiersCTF[event].script = script;
	}
	else
	{
		::SetACSNotifier(event, script);
	}
} */

/* //==========================================================================
//
//	NotifyACS
//
//==========================================================================

void NotifyACS(int Event, int Arg1, int Arg2, int Arg3)
{
	dprint("GameCTF::NotifyACS : Event = %i", Event);
	if (Event >= GameModel::event_custom && Event < event_colortranslation + num_ctfevents)
	{
		Event -= GameModel::event_custom;
		if (AcsNotifiersCTF[Event].enabled)
		{
			dprint("calling script = %i; %i,%i,%i", AcsNotifiersCTF[Event].script, Arg1, Arg2, Arg3);
			Level.ExecuteActionSpecial(LNSPEC_ACSExecuteAlways,
					AcsNotifiersCTF[Event].script,
					0,
					Arg1,
					Arg2,
					Arg3,
					NULL, 0, none);
		}
	}
	else
		::NotifyACS(Event, Arg1, Arg2, Arg3);
} */

//==========================================================================
//
//	Cleanup
//
//==========================================================================

void Cleanup()
{
	int i;

	for (i = 0; i < spot_banners.Num; i++)
	{
		spot_banners[i].Destroy();
		spot_banners[i] = none;
	}
}

//==========================================================================
//
//	PrepareGame
//
//==========================================================================

bool PrepareGame()
{
	int i, teams, teams_ready;
	int pairs;

	if (!Game.Teams[0] || !Game.Teams[1])
	{
		dprint("GameCTF : not enough teams");
		return false;
	}

	Cleanup();

	teams = AllowedTeams();
	teams_ready = Game.GetTeamCount();

	dprint("GameCTF : prepare game; allowed %i teams, present %i teams", teams, teams_ready);

	pairs = 0;

	CTFBanner banner;
	CTFBase base;

	foreach Level.AllThinkers(CTFBanner, banner)
	{
		if (banner.Args[0] >= teams)
		{
			dprint("GameCTF : initial banner (%i/%i) belongs to unsupported Team and will be disabled",
				banner.Args[0], banner.Args[1]);
			banner.Deactivate(none);
			continue;
		}

		Banners.Num = Banners.Num + 1;
		Banners[Banners.Num - 1] = banner;
		if (Banners.Num == MAXBANNERS)
			break;
	}

	dprint("GameCTF : found %i active banners", Banners.Num);

	for (i = 0; i < Banners.Num; i++)
	{
		spot_banners.Num = spot_banners.Num + 1;
		spot_banners[i] = HexenLevelInfo(Level).Spawn(CTFBase, Banners[i].Origin);
		if (spot_banners[i])
		{
			dprint("GameCTF : successfully spawned CTFBase at initial banner (%i/%i) position", 
				Banners[i].Args[0], Banners[i].Args[1]);
			pairs++;
			Banners[i].BaseOrigin = spot_banners[i];
			spot_banners[i].Banner = Banners[i];
			spot_banners[i].Args[0] = Banners[i].Args[0];
			spot_banners[i].Args[1] = Banners[i].Args[1];
			if (Banners[i].Args[0] < teams_ready)
			{
				Banners[i].Team = Game.Teams[Banners[i].Args[0]];
				spot_banners[i].Team = Game.Teams[spot_banners[i].Args[0]];
				Banners[i].SetTranslation();
				// [FB] This is important for bots AI
				Banners[i].Status = CTFBanner::BANNER_STATUS__BASE;
			}
			else
			{
				dprint("GameCTF : banner and base (%i/%i) belong to inactive Team and will be disabled until Team %i enters to the game",
					Banners[i].Args[0], Banners[i].Args[1], Banners[i].Args[0]);
				Banners[i].Deactivate(none);
				spot_banners[i].Deactivate(none);
			}
		}
		else
		{
			dprint("GameCTF : failed to spawn CTFBase at initial banner (%i/%i) position; banner (%i/%i) will be disabled",
				Banners[i].Args[0], Banners[i].Args[1], Banners[i].Args[0], Banners[i].Args[1]);
			Banners[i].Deactivate(none);
		}
	}

	if (pairs < MINREQUIREDBANNERS)
	{
		dprint("GameCTF : not enough banners and/or bases, need %i pairs, got %i pairs", MINREQUIREDBANNERS, pairs);
		return false;
	}

	dprint("GameCTF : game prepared with %i active banner/base pairs", pairs);

	PrepareColoredDecorations();

	return true;
}

//==========================================================================
//
//	CommitCapture
//
//	[CW] Called for banner that was captured and brought to opponent's base.
//
//==========================================================================

void CommitCapture(CTFBanner Banner)
{
	// TADA!!
	Game.BroadcastLocalSound((P_Random() > 127 ? (P_Random() > 127 ?
			HexenDefs::lsid_ctf_fanfare1 : HexenDefs::lsid_ctf_fanfare3) : 
			(P_Random() > 127 ? HexenDefs::lsid_ctf_fanfare4 : 
			(P_Random() > 127 ? HexenDefs::lsid_ctf_fanfare6 : 
			(P_Random() > 127 ? HexenDefs::lsid_ctf_fanfare8 :
			HexenDefs::lsid_ctf_fanfare11)))),	false, 0.0);
	Game.bcentreprint(va("%s flag captured by %s! %s team scores!",
						Banner.Team.Name,
						Banner.Capturer.PlayerName,
						Banner.Capturer.Team.Name));
	Banner.Capturer.UpdateScore(HexenDefs::scoreevent_CtfCapture);
	Banner.ReturnToBase(none, true);
}

defaultproperties
{
}
