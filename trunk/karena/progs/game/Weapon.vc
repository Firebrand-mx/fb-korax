//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Weapon : ViewEntity
	abstract;

const float MELEERANGE			= 64.0;
const float MISSILERANGE		= (32.0 * 64.0);

const float LOWERSPEED			= 6.0;
const float RAISESPEED			= 6.0;

const float WEAPONBOTTOM		= 128.0;
const float WEAPONTOP			= 32.0;

int Mana;
int manause;
bool needsnomana;
int UpState;
int DownState;
int ReadyState;
int AttackState;
int HoldAttackState;
float PSpriteSY;		// Y-adjustment value for full screen
bool dex_mod;
bool str_mod;
bool int_mod;
bool wis_mod;
bool active_damage; 	// [FB] TRUE if PI Damage is active
string PickUpMessage;

int GetUpState()
{
	return UpState;
}

int GetDownState()
{
	return DownState;
}

int GetReadyState()
{
	return ReadyState;
}

//==========================================================================
//
//	GetAttackState
//
//==========================================================================

int GetAttackState()
{
	return Player(Player).Refire ? HoldAttackState : AttackState;
}

void GotMana(int InMana)
{
}

// FIXME
void SnoutSniff()
{
}

//==========================================================================
//
//  IsWeaponManaUsable
//
//==========================================================================

bool IsWeaponManaUsable()
{
	if (Mana == HexenDefs::NUMMANA) 
	{
		    return Player(Player).Mana[HexenDefs::MANA_1] >= Player(Player).GetWeaponManaUse(self)
			&& Player(Player).Mana[HexenDefs::MANA_2] >= Player(Player).GetWeaponManaUse(self)
			&& Player(Player).Mana[HexenDefs::MANA_3] >= Player(Player).GetWeaponManaUse(self);
	}
	else 
	{
		return Player(Player).Mana[Mana] >= Player(Player).GetWeaponManaUse(self);
	}
}

//==========================================================================
//
//  UseWeaponMana
//
//==========================================================================

void UseWeaponMana()
{
	if (Mana == HexenDefs::NUMMANA)
	{
		Player(Player).Mana[HexenDefs::MANA_1] -= Player(Player).GetWeaponManaUse(self);
		Player(Player).Mana[HexenDefs::MANA_2] -= Player(Player).GetWeaponManaUse(self);
		Player(Player).Mana[HexenDefs::MANA_3] -= Player(Player).GetWeaponManaUse(self);
	}
	else 
	{
		Player(Player).Mana[Mana] -= Player(Player).GetWeaponManaUse(self);
	}
	if (Player(Player).Mana[Mana] < 0)
	{
		Player(Player).Mana[Mana] = 0;
	}	
}

//==========================================================================
//
//  A_Light0
//
//==========================================================================

void A_Light0()
{
	Player.ExtraLight = 0;
	Player.MO.Effects &= ~HexenDefs::EF_DLIGHT_MASK;
}

//==========================================================================
//
//  A_WeaponReady
//
//  The player can fire the weapon or change to another weapon at this time.
//
//==========================================================================

void A_WeaponReady()
{
	float angle;

	// Change player from attack state
	if (Player.MO.StateNum >= Actor(Player.MO).MissileState &&
		Player.MO.StateNum <= PlayerPawn(Player.MO).AttackEndState)
	{
		Player.MO.SetState(Actor(Player.MO).IdleState);
	}
	// Put the weapon away if the player has a pending weapon or has died.
	if (Player(Player).PendingWeapon != HexenDefs::wp_nochange || !Player.Health)
	{
		Player(Player).SetPsprite(ps_weapon, GetDownState());
		return;
	}

	// Check for fire. 
	if (Player.Buttons & BT_ATTACK)
	{
		Player.bAttackDown = true;
		Player(Player).FireWeapon();
		return;
	}
	else
	{
		Player.bAttackDown = false;
	}

	if (!Player(Player).MorphTime)
	{
		// Bob the weapon based on movement speed.
		angle = AngleMod360(180.0 * Player.Level.Game.level->time);
		SX = 1.0 + Player(Player).Bob * cos(angle);
		if (angle >= 180.0)
		{
			angle -= 180.0;
		}
		SY = WEAPONTOP + Player(Player).Bob * sin(angle);
	}
}

//==========================================================================
//
//  A_Lower
//
//==========================================================================

void A_Lower()
{
	if (Player(Player).MorphTime)
	{
		SY = WEAPONBOTTOM;
	}
	else
	{
		//	FIXME!!
		if (Player.Level.Game.frametime < 1.0 / 35.0)
			SY += LOWERSPEED;
		else
			SY += LOWERSPEED * 35.0 * Player.Level.Game.frametime;
	}
	if (SY < WEAPONBOTTOM)
	{
		// Not lowered all the way yet
		return;
	}
	if (Player.PlayerState == PST_DEAD)
	{
		// Player is dead, so don't bring up a pending weapon
		SY = WEAPONBOTTOM;
		return;
	}
	if (!Player.Health)
	{
		// Player is dead, so keep the weapon off screen
		Player(Player).SetPsprite(ps_weapon, S_NULL);
		return;
	}
	Player(Player).SetWeapon(Player(Player).PendingWeapon);
	Player(Player).BringUpWeapon();
}

//==========================================================================
//
//  A_Raise
//
//==========================================================================

void A_Raise()
{
	//	FIXME!!
	if (Player.Level.Game.frametime < 1.0 / 35.0)
		SY -= RAISESPEED;
	else
		SY -= RAISESPEED * 35.0 * Player.Level.Game.frametime;
	if (SY > WEAPONTOP)
	{
		// Not raised all the way yet
		return;
	}
	SY = WEAPONTOP;
	Player(Player).SetPsprite(ps_weapon, GetReadyState());
}

//==========================================================================
//
//  A_ReFire
//
//  The player can re fire the weapon without lowering it entirely.
//
//==========================================================================

void A_ReFire()
{
	if ((Player.Buttons & BT_ATTACK) &&
		Player(Player).PendingWeapon == HexenDefs::wp_nochange && Player.Health)
	{
		Player(Player).Refire++;
		Player(Player).FireWeapon();
	}
	else
	{
		Player(Player).Refire = 0;
		Player(Player).CheckMana();
	}
}

//==========================================================================
//
//  SetFOV
//
//==========================================================================

void SetFOV(int FOV)
{
	MSG_SelectClientMsg(MSG_SV_CLIENT, Player);
	MSG_WriteByte(HexenDefs::svc_fov);
	MSG_WriteByte(FOV);
}

// UNUSED
__states__
{
	S_LIGHTDONE('FPCH', 0, 0.0 / 35.0, S_NULL) { A_Light0(); }
}

defaultproperties
{
}

//**************************************************************************
//
//  $Log$
//  Revision 1.11  2006/03/02 18:40:23  dj_jl
//  Don't use game specific classes in common.
//
//  Revision 1.10  2006/02/28 22:49:17  dj_jl
//  Replaced defines with constants.
//
//  Revision 1.9  2006/02/17 19:17:52  dj_jl
//  Moved common defs to classes.
//
//  Revision 1.8  2006/02/15 23:37:46  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.7  2006/02/13 18:44:39  dj_jl
//  Moved all server progs global functions to classes.
//
//  Revision 1.6  2005/09/30 15:47:23  firebrand_kh
//  Added a quick fix to avoid negative mana counts
//
//  Revision 1.5  2005/08/12 23:04:25  firebrand_kh
//  Fixed weapons to work with the PI Damage artifact
//
//  Revision 1.4  2005/02/03 01:05:06  firebrand_kh
//  Fixed Doom Guy weapon bug with the new effects
//
//  Revision 1.3  2005/01/18 13:56:53  dj_jl
//  FOV control.
//
//  Revision 1.2  2004/12/09 00:03:57  firebrand_kh
//  Added stuff for the Doom Guy weapon effects
//
//  Revision 1.1  2004/11/16 13:33:22  dj_jl
//  Initial import.
//
//  Revision 1.2  2003/11/12 16:50:47  dj_jl
//  Changed player structure into a class
//
//  Revision 1.1  2002/10/26 16:32:27  dj_jl
//  New style of defining classes.
//
//**************************************************************************
