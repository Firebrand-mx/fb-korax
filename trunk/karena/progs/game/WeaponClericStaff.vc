//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WeaponClericStaff : Weapon;

int BlinkCount;
state BlinkState;
state Attack2State;

//============================================================================
//
//  A_CStaffInitBlink
//
//============================================================================

void A_CStaffInitBlink()
{
	BlinkCount = (P_Random() >> 1) + 20;
}

//============================================================================
//
//  A_CStaffCheckBlink
//
//============================================================================

void A_CStaffCheckBlink()
{
	if (!--BlinkCount)
	{
		SetState(BlinkState);
		BlinkCount = (P_Random() + 50) >> 2;
	}
}

//============================================================================
//
//  A_CStaffCheck
//
//============================================================================

void A_CStaffCheck()
{
	Actor pmo;
	int damage;
	int newLife;
	int i;
	TAVec angles;
	TVec vforward;

	pmo = Actor(Player.MO);
	damage = 27; //20 + (P_Random() & 15);

	if (int_mod)
	{
		if(!active_damage)
		{
			damage = damage * Player(Player).intelligence / 10;
		}
		else // [FB] PI Damage
		{
			damage = (damage * Player(Player).intelligence / 10) * ftoi(3.1415);
		}
	}
	for (i = 0; i < 3; i++)
	{
		angles = pmo.Angles;
		angles.yaw = AngleMod360(angles.yaw + itof(i) * (45.0 / 16.0));
		pmo.AimLineAttack(&angles, 3.0 * MELEERANGE / 2.0);
		if (pmo.linetarget)
		{
			AngleVector(&angles, &vforward);
			pmo.LineAttack(vforward, 1.5 * MELEERANGE, damage, ClericStaffPuff);
			pmo.Angles.yaw = atan2(pmo.linetarget.Origin.y - pmo.Origin.y,
				pmo.linetarget.Origin.x - pmo.Origin.x);
			if ((pmo.linetarget.bIsPlayer || pmo.linetarget.bCountKill) &&
				!pmo.linetarget.bDormant && !pmo.linetarget.bInvulnerable)
			{
				newLife = Player.Health + (damage >> 3);
				newLife = newLife > 100 ? 100 : newLife;
				pmo.Health = newLife;
				Player.Health = newLife;
				SetState(Attack2State);
			}
			UseWeaponMana();
			break;
		}
		angles = pmo.Angles;
		angles.yaw = AngleMod360(angles.yaw - itof(i) * (45.0 / 16.0));
		Actor(Player.MO).AimLineAttack(&angles, 3.0 * MELEERANGE / 2.0);
		if (Actor(Player.MO).linetarget)
		{
			AngleVector(&angles, &vforward);
			pmo.LineAttack(vforward, 1.5 * MELEERANGE, damage, ClericStaffPuff);
			pmo.Angles.yaw = atan2(Actor(Player.MO).linetarget.Origin.y - pmo.Origin.y,
				Actor(Player.MO).linetarget.Origin.x - pmo.Origin.x);
			if (Actor(Player.MO).linetarget.bIsPlayer || Actor(Player.MO).linetarget.bCountKill)
			{
				newLife = Player.Health + (damage >> 4);
				newLife = newLife > 100 ? 100 : newLife;
				pmo.Health = newLife;
				Player.Health = pmo.Health;
				SetState(Attack2State);
			}
			UseWeaponMana();
			break;
		}
	}
}

//============================================================================
//
//  A_CStaffAttack
//
//============================================================================

void A_CStaffAttack()
{
	Actor mo;
	PlayerPawn playPawn;


	UseWeaponMana();
	playPawn = PlayerPawn(Player.MO);
	mo = Player(Player).SpawnPlayerMissileAngle(ClericStaffMissile,
		playPawn.Angles.yaw - (45.0 / 15.0));
	if (mo)
	{
		ClericStaffMissile(mo).WeaveXY = 32;
	}
	mo = Player(Player).SpawnPlayerMissileAngle(ClericStaffMissile,
		playPawn.Angles.yaw + (45.0 / 15.0));
	if (mo)
	{
		ClericStaffMissile(mo).WeaveXY = 0;
		if (int_mod)
		{
			if(!active_damage)
			{
				mo.MissileDamage = mo.MissileDamage * Player(Player).intelligence / 10;
			}
			else // [FB] PI Damage
			{
				mo.MissileDamage = (mo.MissileDamage * Player(Player).intelligence / 10) * ftoi(3.1415);
			}
		}
	}
	playPawn.PlaySound('ClericCStaffFire', CHAN_WEAPON);
}

__states__
{
	S_CSTAFFREADY('CSSF', 2, 4.0 / 35.0, S_CSTAFFREADY1) { }
	S_CSTAFFREADY1('CSSF', 1, 3.0 / 35.0, S_CSTAFFREADY2) { A_CStaffInitBlink(); }
	S_CSTAFFREADY2('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY3) { A_WeaponReady(); }
	S_CSTAFFREADY3('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY4) { A_WeaponReady(); }
	S_CSTAFFREADY4('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY5) { A_WeaponReady(); }
	S_CSTAFFREADY5('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY6) { A_WeaponReady(); }
	S_CSTAFFREADY6('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY7) { A_WeaponReady(); }
	S_CSTAFFREADY7('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY8) { A_WeaponReady(); }
	S_CSTAFFREADY8('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY9) { A_WeaponReady(); }
	S_CSTAFFREADY9('CSSF', 0, 1.0 / 35.0, S_CSTAFFREADY2) { A_CStaffCheckBlink(); }
	S_CSTAFFBLINK1('CSSF', 1, 1.0 / 35.0, S_CSTAFFBLINK2) { A_WeaponReady(); }
	S_CSTAFFBLINK2('CSSF', 1, 1.0 / 35.0, S_CSTAFFBLINK3) { A_WeaponReady(); }
	S_CSTAFFBLINK3('CSSF', 1, 1.0 / 35.0, S_CSTAFFBLINK4) { A_WeaponReady(); }
	S_CSTAFFBLINK4('CSSF', 2, 1.0 / 35.0, S_CSTAFFBLINK5) { A_WeaponReady(); }
	S_CSTAFFBLINK5('CSSF', 2, 1.0 / 35.0, S_CSTAFFBLINK6) { A_WeaponReady(); }
	S_CSTAFFBLINK6('CSSF', 2, 1.0 / 35.0, S_CSTAFFBLINK7) { A_WeaponReady(); }
	S_CSTAFFBLINK7('CSSF', 2, 1.0 / 35.0, S_CSTAFFBLINK8) { A_WeaponReady(); }
	S_CSTAFFBLINK8('CSSF', 2, 1.0 / 35.0, S_CSTAFFBLINK9) { A_WeaponReady(); }
	S_CSTAFFBLINK9('CSSF', 1, 1.0 / 35.0, S_CSTAFFBLINK10) { A_WeaponReady(); }
	S_CSTAFFBLINK10('CSSF', 1, 1.0 / 35.0, S_CSTAFFBLINK11) { A_WeaponReady(); }
	S_CSTAFFBLINK11('CSSF', 1, 1.0 / 35.0, S_CSTAFFREADY2) { A_WeaponReady(); }
	S_CSTAFFDOWN('CSSF', 1, 3.0 / 35.0, S_CSTAFFDOWN2) { }
	S_CSTAFFDOWN2('CSSF', 2, 4.0 / 35.0, S_CSTAFFDOWN3) { }
	S_CSTAFFDOWN3('CSSF', 2, 1.0 / 35.0, S_CSTAFFDOWN3) { A_Lower(); }
	S_CSTAFFUP('CSSF', 2, 1.0 / 35.0, S_CSTAFFUP) { A_Raise(); }
	S_CSTAFFATK_1('CSSF', 0, 1.0 / 35.0, S_CSTAFFATK_2) { SY = 45.0; A_CStaffCheck(); }
	S_CSTAFFATK_2('CSSF', 9, 1.0 / 35.0, S_CSTAFFATK_3) { SY = 50.0; A_CStaffAttack(); }
	S_CSTAFFATK_3('CSSF', 9, 2.0 / 35.0, S_CSTAFFATK_4) { SY = 50.0; }
	S_CSTAFFATK_4('CSSF', 9, 2.0 / 35.0, S_CSTAFFATK_5) { SY = 45.0; }
	S_CSTAFFATK_5('CSSF', 0, 2.0 / 35.0, S_CSTAFFATK_6) { SY = 40.0; }
	S_CSTAFFATK_6('CSSF', 0, 2.0 / 35.0, S_CSTAFFREADY2) { SY = 36.0; }
	S_CSTAFFATK2_1('CSSF', 10, 10.0 / 35.0, S_CSTAFFREADY2) { SY = 36.0; }
}

/*SECOND*/
defaultproperties
{
	Mana = HexenDefs::MANA_1;
	manause = 2;
	UpState = S_CSTAFFUP;
	DownState = S_CSTAFFDOWN;
	ReadyState = S_CSTAFFREADY;
	BlinkState = S_CSTAFFBLINK1;
	AttackState = S_CSTAFFATK_1;
	HoldAttackState = S_CSTAFFATK_1;
	Attack2State = S_CSTAFFATK2_1;
	PSpriteSY = 10.0;
	wis_mod = true;
	int_mod = true;
	dex_mod = false;
	str_mod = false;
	PickUpMessage = "SERPENT STAFF";
}
