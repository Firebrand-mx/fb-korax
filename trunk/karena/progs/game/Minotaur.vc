//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Minotaur : Pawn
	__mobjinfo__(9);

//
// Minotaur variables
//

bool bFloorFireRefired;		//  internal to minotaur AI
//  Args[0]         minotaur start time
//  Args[4]         charge duration countdown

int LookState;
int SeeStateNF;
int RoamState;
int FloorFireState;
int FloorFireRefireState;
int ChargeState;

float MinotaurLookDist;
float ChargeSpeed;

//==========================================================================
//
//  Died
//
//==========================================================================

void Died(Actor source)
{
	::Died(source);
	if (Instigator && Instigator.Health > 0)
	{
		if (!Instigator.ActiveMinotaur())
		{
			Instigator.Player.Powers[pw_minotaur] = 0.0;
		}
	}
}

//==========================================================================
//
//  MorphMonster
//
//==========================================================================

bool MorphMonster(void)
{
	if (::MorphMonster())
	{
		// check for turning off minotaur power for active icon
		if (Instigator && Instigator.Health > 0)
		{
			if (!Instigator.ActiveMinotaur())
			{
				Instigator.Player.Powers[pw_minotaur] = 0.0;
			}
		}
		return true;
	}
	return false;
}

//===========================================================================
//
//  RoughCheckThing
//
//===========================================================================

bool RoughCheckThing(Entity Other)
{
	Actor link = Actor(Other);
	
	if (!link.bCountKill && (!link.bIsPlayer || link == Instigator))
	{
		return false;
	}
	if (!link.bShootable)
	{
		return false;
	}
	if (link.bDormant)
	{
		return false;
	}
	if (Minotaur(link) && link.Instigator == Instigator)
	{
		return false;
	}
	if (netgame && !deathmatch && link.bIsPlayer)
	{
		return false;
	}
	if (!CanSee(link))
	{
		return false;
	}
	return true;
}

//============================================================================
//
//  A_MinotaurFade0
//
//============================================================================

void A_MinotaurFade0(void)
{
	Translucency = 66;
}

//============================================================================
//
//  A_MinotaurFade1
//
//============================================================================

void A_MinotaurFade1(void)
{
	// Second level of transparency
	Translucency = 33;
}

//============================================================================
//
//  A_MinotaurFade2
//
//============================================================================

void A_MinotaurFade2(void)
{
	// Make fully visible
	Translucency = 0;
}

//============================================================================
//
//  A_MinotaurLook
//
//  Look for enemy of player
//
//============================================================================

void A_MinotaurLook(void)
{
	Actor mo;
	float dist;
	int i;

	Enemy = none;

	if (deathmatch)	// Quick search for players
	{
		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (!svvars->Players[i] || !svvars->Players[i].bSpawned)
				continue;
			mo = svvars->Players[i].MO;
			if (mo == Instigator)
				continue;
			if (mo.Health <= 0)
				continue;
			dist = MobjDist(self, mo);
			if (dist > MinotaurLookDist)
				continue;
			Enemy = mo;
			break;
		}
	}

	if (!Enemy)	// Near player monster search
	{
		if (Instigator && (Instigator.Health > 0) && Instigator.bIsPlayer)
			Enemy = Actor(Instigator.RoughMonsterSearch(20));
		else
			Enemy = Actor(RoughMonsterSearch(20));
	}

	if (!Enemy)	// Normal monster search
	{
		FOREACH(Actor, mo)
		{
			if (!mo.bCountKill)
				continue;
			if (mo.Health <= 0)
				continue;
			if (!mo.bShootable)
				continue;
			dist = MobjDist(self, mo);
			if (dist > MinotaurLookDist)
				continue;
			if ((mo == Instigator) || (mo == self))
				continue;
			if (mo.Class == Minotaur && mo.Instigator == Instigator)
 				continue;
			Enemy = mo;
			break;	// Found mobj to attack
		}
	}

	if (Enemy)
	{
		SetState(SeeStateNF);
	}
	else
	{
		SetState(RoamState);
	}
}

//============================================================================
//
//  A_MinotaurChase
//
//============================================================================

void A_MinotaurChase(void)
{
	int starttime = Args[0];

	Translucency = 0;	// In case pain caused him to skip his fade in.

	if ((level->tictime - starttime) >= ftoi(MAULATORTIME * 35.0))
	{
		Damage(none, none, 10000);
		return;
	}

	if (P_Random() < 30)
		A_MinotaurLook();	// adjust to closest target

	if (!Enemy || (Enemy.Health <= 0) || !Enemy.bShootable)
	{
		// look for a new enemy
		SetState(LookState);
		return;
	}

	FaceMovementDirection();
	ReactionCount = 0;

	// Melee attack
	if (MeleeState && CheckMeleeRange())
	{
		if (AttackSound)
		{
			PlaySound(AttackSound, CHAN_WEAPON);
		}
		SetState(MeleeState);
		return;
	}

	// Missile attack
	if (MissileState && CheckMissileRange())
	{
		SetState(MissileState);
		return;
	}

	// chase towards target
	if (!StepMove())
	{
		NewChaseDir();
	}

	// Active sound
	if (ActiveSound && P_Random() < 6)
	{
		PlaySound(ActiveSound, CHAN_VOICE);
	}

}

//============================================================================
//
//  A_MinotaurRoam
//
//============================================================================

void A_MinotaurRoam(void)
{
	int starttime = Args[0];

	Translucency = 0;	// In case pain caused him to skip his fade in.

	if ((level->tictime - starttime) >= ftoi(MAULATORTIME * 35.0))
	{
		Damage(none, none, 10000);
		return;
	}

	if (P_Random() < 30)
		A_MinotaurLook();	// adjust to closest target

	if (P_Random() < 6)
	{
		//Choose new direction
		MoveDir = P_Random() % 8;
		FaceMovementDirection();
	}
	if (!StepMove())
	{
		// Turn
		if (P_Random() & 1)
			MoveDir = (++MoveDir) % 8;
		else
			MoveDir = (MoveDir + 7) % 8;
		FaceMovementDirection();
	}
}

//============================================================================
//
//  A_MinotaurAtk1
//
//  Melee attack.
//
//============================================================================

void A_MinotaurAtk1(void)
{
	if (!Enemy)
	{
		return;
	}

	PlaySound('MaulatorHamSwing', CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(4));
	}
}

//============================================================================
//
//  A_MinotaurDecide
//
//  Choose a missile attack.
//
//============================================================================

void A_MinotaurDecide(void)
{
	float dist;

	if (!Enemy)
		return;
	dist = MobjDist(self, Enemy);

	if (Enemy.Origin.z + Enemy.Height > Origin.z &&
		Enemy.Origin.z + Enemy.Height < Origin.z + Height &&
		dist < 16.0 * 64.0 && dist > 1.0 * 64.0 && P_Random() < 150)
	{
		// Charge attack
		// Don't call the state function right away
		SetState(ChargeState);
		bSkullFly = true;
		A_FaceTarget();
		Velocity = Normalise(Enemy.Origin - Origin) * ChargeSpeed;
		Args[4] = 35 / 2;	// Charge duration
	}
	else if (Enemy.Origin.z == Enemy.FloorZ &&
		dist < 9.0 * 64.0 && P_Random() < 100)
	{
		// Floor fire attack
		SetState(FloorFireState);
		bFloorFireRefired = false;
	}
	else
	{
		// Swing attack
		A_FaceTarget();
		// Don't need to call SetState because the current state
		// falls through to the swing attack
	}
}

//============================================================================
//
//  A_MinotaurAtk2
//
//  Swing attack.
//
//============================================================================

void A_MinotaurAtk2(void)
{
	Actor mo;
	float angle;
	float momz;

	if (!Enemy)
		return;

	PlaySound('MaulatorHamSwing', CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(3));
		return;
	}
	mo = SpawnMissile(Enemy, MinotaurMissile);
	if (mo)
	{
		momz = mo.Velocity.z;
		angle = mo.Angles.yaw;
		SpawnMissileAngle(MinotaurMissile,
			AngleMod360(angle - (45.0 / 8.0)), momz);
		SpawnMissileAngle(MinotaurMissile,
			AngleMod360(angle + (45.0 / 8.0)), momz);
		SpawnMissileAngle(MinotaurMissile,
			AngleMod360(angle - (45.0 / 16.0)), momz);
		SpawnMissileAngle(MinotaurMissile,
			AngleMod360(angle + (45.0 / 16.0)), momz);
	}
}

//============================================================================
//
//  A_MinotaurAtk3
//
//  Floor fire attack.
//
//============================================================================

void A_MinotaurAtk3(void)
{
	Actor mo;

	if (!Enemy)
	{
		return;
	}
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(3));
		if (Enemy.bIsPlayer)
		{
			// Squish the player
			Enemy.Player.DeltaViewHeight = -512.0;
		}
	}
	else
	{
		mo = SpawnMissile(Enemy, MinotaurFloorFire1);
		if (mo)
		{
			mo.PlaySound('MaulatorHamHit', CHAN_VOICE);
		}
	}
	if (P_Random() < 192 && !bFloorFireRefired)
	{
		SetState(FloorFireRefireState);
		bFloorFireRefired = true;
	}
}

//============================================================================
//
//  A_MinotaurCharge
//
//============================================================================

void A_MinotaurCharge(void)
{
	Actor puff;

	if (!Enemy)
		return;

	if (Args[4] > 0)
	{
		puff = Spawn(PunchPuff, Origin);
		puff.Velocity.z = 2.0 * 35.0;
		Args[4]--;
	}
	else
	{
		bSkullFly = false;
		SetState(SeeState);
	}
}

//============================================================================
//
//  A_SmokePuffExit
//
//============================================================================

void A_SmokePuffExit(void)
{
	Spawn(MinotaurSmokeExit, Origin);
}

__states__
{
	S_MNTR_SPAWN1('MNTR', 0, 'monsters/minotaur/base.md2', 0, 15.0 / 35.0, S_MNTR_SPAWN2) { }
	S_MNTR_SPAWN2('MNTR', 0, 'monsters/minotaur/base.md2', 0, 15.0 / 35.0, S_MNTR_SPAWN3) { A_MinotaurFade1(); }
	S_MNTR_SPAWN3('MNTR', 0, 'monsters/minotaur/base.md2', 0, 3.0 / 35.0, S_MNTR_LOOK1) { A_MinotaurFade2(); }
	S_MNTR_LOOK1('MNTR', 0, 'monsters/minotaur/base.md2', 0, 2.5 / 35.0, S_MNTR_LOOK2) { A_MinotaurLook(); }
	S_MNTR_LOOK2('MNTR', 0, 'monsters/minotaur/base.md2', 1, 2.5 / 35.0, S_MNTR_LOOK3) { }
	S_MNTR_LOOK3('MNTR', 0, 'monsters/minotaur/base.md2', 2, 2.5 / 35.0, S_MNTR_LOOK4) { }
	S_MNTR_LOOK4('MNTR', 0, 'monsters/minotaur/base.md2', 3, 2.5 / 35.0, S_MNTR_LOOK5) { }
	S_MNTR_LOOK5('MNTR', 1, 'monsters/minotaur/base.md2', 4, 2.5 / 35.0, S_MNTR_LOOK6) { A_MinotaurLook(); }
	S_MNTR_LOOK6('MNTR', 1, 'monsters/minotaur/base.md2', 5, 2.5 / 35.0, S_MNTR_LOOK7) { }
	S_MNTR_LOOK7('MNTR', 1, 'monsters/minotaur/base.md2', 6, 2.5 / 35.0, S_MNTR_LOOK8) { }
	S_MNTR_LOOK8('MNTR', 1, 'monsters/minotaur/base.md2', 7, 2.5 / 35.0, S_MNTR_LOOK1) { }
	S_MNTR_WALK1_NF('MNTR', 0, 'monsters/minotaur/base.md2', 8, 2.5 / 35.0, S_MNTR_WALK2) { }
	S_MNTR_WALK1('MNTR', 0, 'monsters/minotaur/base.md2', 8, 2.5 / 35.0, S_MNTR_WALK2) { A_MinotaurChase(); }
	S_MNTR_WALK2('MNTR', 0, 'monsters/minotaur/base.md2', 9, 2.5 / 35.0, S_MNTR_WALK3) { }
	S_MNTR_WALK3('MNTR', 1, 'monsters/minotaur/base.md2', 10, 2.5 / 35.0, S_MNTR_WALK4) { A_MinotaurChase(); }
	S_MNTR_WALK4('MNTR', 1, 'monsters/minotaur/base.md2', 11, 2.5 / 35.0, S_MNTR_WALK5) { }
	S_MNTR_WALK5('MNTR', 2, 'monsters/minotaur/base.md2', 12, 2.5 / 35.0, S_MNTR_WALK6) { A_MinotaurChase(); }
	S_MNTR_WALK6('MNTR', 2, 'monsters/minotaur/base.md2', 13, 2.5 / 35.0, S_MNTR_WALK7) { }
	S_MNTR_WALK7('MNTR', 3, 'monsters/minotaur/base.md2', 14, 2.5 / 35.0, S_MNTR_WALK8) { A_MinotaurChase(); }
	S_MNTR_WALK8('MNTR', 3, 'monsters/minotaur/base.md2', 15, 2.5 / 35.0, S_MNTR_WALK9) { }
	S_MNTR_WALK9('MNTR', 0, 'monsters/minotaur/base.md2', 16, 2.5 / 35.0, S_MNTR_WALK10) { A_MinotaurChase(); }
	S_MNTR_WALK10('MNTR', 0, 'monsters/minotaur/base.md2', 17, 2.5 / 35.0, S_MNTR_WALK11) { }
	S_MNTR_WALK11('MNTR', 1, 'monsters/minotaur/base.md2', 18, 2.5 / 35.0, S_MNTR_WALK12) { A_MinotaurChase(); }
	S_MNTR_WALK12('MNTR', 1, 'monsters/minotaur/base.md2', 19, 2.5 / 35.0, S_MNTR_WALK13) { }
	S_MNTR_WALK13('MNTR', 2, 'monsters/minotaur/base.md2', 20, 2.5 / 35.0, S_MNTR_WALK14) { A_MinotaurChase(); }
	S_MNTR_WALK14('MNTR', 2, 'monsters/minotaur/base.md2', 21, 2.5 / 35.0, S_MNTR_WALK15) { }
	S_MNTR_WALK15('MNTR', 3, 'monsters/minotaur/base.md2', 22, 2.5 / 35.0, S_MNTR_WALK16) { A_MinotaurChase(); }
	S_MNTR_WALK16('MNTR', 3, 'monsters/minotaur/base.md2', 23, 2.5 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ROAM1_NF('MNTR', 0, 'monsters/minotaur/base.md2', 8, 2.5 / 35.0, S_MNTR_ROAM2) { }
	S_MNTR_ROAM1('MNTR', 0, 'monsters/minotaur/base.md2', 8, 2.5 / 35.0, S_MNTR_ROAM2) { A_MinotaurRoam(); }
	S_MNTR_ROAM2('MNTR', 0, 'monsters/minotaur/base.md2', 9, 2.5 / 35.0, S_MNTR_ROAM3) { }
	S_MNTR_ROAM3('MNTR', 1, 'monsters/minotaur/base.md2', 10, 2.5 / 35.0, S_MNTR_ROAM4) { A_MinotaurRoam(); }
	S_MNTR_ROAM4('MNTR', 1, 'monsters/minotaur/base.md2', 11, 2.5 / 35.0, S_MNTR_ROAM5) { }
	S_MNTR_ROAM5('MNTR', 2, 'monsters/minotaur/base.md2', 12, 2.5 / 35.0, S_MNTR_ROAM6) { A_MinotaurRoam(); }
	S_MNTR_ROAM6('MNTR', 2, 'monsters/minotaur/base.md2', 13, 2.5 / 35.0, S_MNTR_ROAM7) { }
	S_MNTR_ROAM7('MNTR', 3, 'monsters/minotaur/base.md2', 14, 2.5 / 35.0, S_MNTR_ROAM8) { A_MinotaurRoam(); }
	S_MNTR_ROAM8('MNTR', 3, 'monsters/minotaur/base.md2', 15, 2.5 / 35.0, S_MNTR_ROAM9) { }
	S_MNTR_ROAM9('MNTR', 0, 'monsters/minotaur/base.md2', 16, 2.5 / 35.0, S_MNTR_ROAM10) { A_MinotaurRoam(); }
	S_MNTR_ROAM10('MNTR', 0, 'monsters/minotaur/base.md2', 17, 2.5 / 35.0, S_MNTR_ROAM11) { }
	S_MNTR_ROAM11('MNTR', 1, 'monsters/minotaur/base.md2', 18, 2.5 / 35.0, S_MNTR_ROAM12) { A_MinotaurRoam(); }
	S_MNTR_ROAM12('MNTR', 1, 'monsters/minotaur/base.md2', 19, 2.5 / 35.0, S_MNTR_ROAM13) { }
	S_MNTR_ROAM13('MNTR', 2, 'monsters/minotaur/base.md2', 20, 2.5 / 35.0, S_MNTR_ROAM14) { A_MinotaurRoam(); }
	S_MNTR_ROAM14('MNTR', 2, 'monsters/minotaur/base.md2', 21, 2.5 / 35.0, S_MNTR_ROAM15) { }
	S_MNTR_ROAM15('MNTR', 3, 'monsters/minotaur/base.md2', 22, 2.5 / 35.0, S_MNTR_ROAM16) { A_MinotaurRoam(); }
	S_MNTR_ROAM16('MNTR', 3, 'monsters/minotaur/base.md2', 23, 2.5 / 35.0, S_MNTR_ROAM1) { }
	S_MNTR_ATK1_1('MNTR', 6, 'monsters/minotaur/base.md2', 24, 3.5 / 35.0, S_MNTR_ATK1_2) { A_FaceTarget(); }
	S_MNTR_ATK1_2('MNTR', 6, 'monsters/minotaur/base.md2', 25, 3.5 / 35.0, S_MNTR_ATK1_3) { }
	S_MNTR_ATK1_3('MNTR', 6, 'monsters/minotaur/base.md2', 26, 3.0 / 35.0, S_MNTR_ATK1_4) { }
	S_MNTR_ATK1_4('MNTR', 7, 'monsters/minotaur/base.md2', 27, 3.5 / 35.0, S_MNTR_ATK1_5) { A_FaceTarget(); }
	S_MNTR_ATK1_5('MNTR', 7, 'monsters/minotaur/base.md2', 28, 3.5 / 35.0, S_MNTR_ATK1_6) { }
	S_MNTR_ATK1_6('MNTR', 8, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK1_7) { A_MinotaurAtk1(); }
	S_MNTR_ATK1_7('MNTR', 8, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK1_8) { }
	S_MNTR_ATK1_8('MNTR', 8, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK2_1('MNTR', 6, 'monsters/minotaur/base.md2', 32, 3.5 / 35.0, S_MNTR_ATK2_2) { A_MinotaurDecide(); }
	S_MNTR_ATK2_2('MNTR', 6, 'monsters/minotaur/base.md2', 33, 3.5 / 35.0, S_MNTR_ATK2_3) { }
	S_MNTR_ATK2_3('MNTR', 6, 'monsters/minotaur/base.md2', 34, 3.0 / 35.0, S_MNTR_ATK2_4) { }
	S_MNTR_ATK2_4('MNTR', 9, 'monsters/minotaur/base.md2', 35, 2.0 / 35.0, S_MNTR_ATK2_5) { A_FaceTarget(); }
	S_MNTR_ATK2_5('MNTR', 9, 'monsters/minotaur/base.md2', 36, 2.0 / 35.0, S_MNTR_ATK2_6) { }
	S_MNTR_ATK2_6('MNTR', 10, 'monsters/minotaur/base.md2', 37, 3.0 / 35.0, S_MNTR_ATK2_7) { A_MinotaurAtk2(); }
	S_MNTR_ATK2_7('MNTR', 10, 'monsters/minotaur/base.md2', 38, 3.0 / 35.0, S_MNTR_ATK2_8) { }
	S_MNTR_ATK2_8('MNTR', 10, 'monsters/minotaur/base.md2', 39, 3.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK3_1('MNTR', 6, 'monsters/minotaur/base.md2', 24, 3.5 / 35.0, S_MNTR_ATK3_2) { A_FaceTarget(); }
	S_MNTR_ATK3_2('MNTR', 6, 'monsters/minotaur/base.md2', 25, 3.5 / 35.0, S_MNTR_ATK3_3) { }
	S_MNTR_ATK3_3('MNTR', 6, 'monsters/minotaur/base.md2', 26, 3.0 / 35.0, S_MNTR_ATK3_4) { }
	S_MNTR_ATK3_4('MNTR', 7, 'monsters/minotaur/base.md2', 27, 3.5 / 35.0, S_MNTR_ATK3_5) { A_FaceTarget(); }
	S_MNTR_ATK3_5('MNTR', 7, 'monsters/minotaur/base.md2', 28, 3.5 / 35.0, S_MNTR_ATK3_6) { }
	S_MNTR_ATK3_6('MNTR', 8, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK3_7) { A_MinotaurAtk3(); }
	S_MNTR_ATK3_7('MNTR', 8, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK3_8) { }
	S_MNTR_ATK3_8('MNTR', 8, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK3_9('MNTR', 8, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK3_10) { }
	S_MNTR_ATK3_10('MNTR', 8, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK3_11) { }
	S_MNTR_ATK3_11('MNTR', 8, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_ATK3_1) { }
	S_MNTR_ATK4_1('MNTR', 5, 'monsters/minotaur/base.md2', 40, 2.0 / 35.0, S_MNTR_ATK4_2) { }
	S_MNTR_ATK4_2('MNTR', 5, 'monsters/minotaur/base.md2', 40, 2.0 / 35.0, S_MNTR_ATK4_2) { A_MinotaurCharge(); }
	S_MNTR_PAIN1('MNTR', 4, 'monsters/minotaur/base.md2', 41, 3.0 / 35.0, S_MNTR_PAIN2) { }
	S_MNTR_PAIN2('MNTR', 4, 'monsters/minotaur/base.md2', 41, 6.0 / 35.0, S_MNTR_WALK1) { A_Pain(); }
	S_MNTR_DIE1('MNTR', 4, 'monsters/minotaur/base.md2', 41, 6.0 / 35.0, S_MNTR_DIE2) { }
	S_MNTR_DIE2('MNTR', 4, 'monsters/minotaur/base.md2', 41, 2.0 / 35.0, S_MNTR_DIE3) { A_Scream(); }
	S_MNTR_DIE3('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE4) { A_SmokePuffExit(); }
	S_MNTR_DIE4('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE5) { }
	S_MNTR_DIE5('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE6) { A_NoBlocking(); }
	S_MNTR_DIE6('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE7) { }
	S_MNTR_DIE7('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE8) { A_MinotaurFade1(); }
	S_MNTR_DIE8('MNTR', 4, 'monsters/minotaur/base.md2', 41, 5.0 / 35.0, S_MNTR_DIE9) { A_MinotaurFade0(); }
	S_MNTR_DIE9('MNTR', 4, 'monsters/minotaur/base.md2', 41, 10.0 / 35.0, S_NULL) { }
}

defaultproperties
{
	Health = 2500;
	Radius = 28.0;
	Height = 100.0;
	Mass = 800.0;
	StepSpeed = 16.0;
	PainChance = 0.09765625;
	MissileDamage = 7;
	Translucency = 66;
	ChargeSpeed = 805.0;
	MinotaurLookDist = 16.0 * 54.0;
	IdleState = S_MNTR_SPAWN1;
	LookState = S_MNTR_LOOK1;
	SeeState = S_MNTR_WALK1;
	SeeStateNF = S_MNTR_WALK1_NF;
	RoamState = S_MNTR_ROAM1_NF;
	MeleeState = S_MNTR_ATK1_1;
	MissileState = S_MNTR_ATK2_1;
	FloorFireState = S_MNTR_ATK3_1;
	FloorFireRefireState = S_MNTR_ATK3_9;
	ChargeState = S_MNTR_ATK4_1;
	PainState = S_MNTR_PAIN1;
	DeathState = S_MNTR_DIE1;
	SightSound = 'MaulatorSight';
	ActiveSound = 'MaulatorActive';
	AttackSound = 'MaulatorHamSwing';
	PainSound = 'MaulatorPain';
	DeathSound = 'MaulatorDeath';
	bActivateMCross = false;
	bCheckNoMonsters = false;	// We want minoaturs also in deathmatch
}

//**************************************************************************
//
//  $Log$
//  Revision 1.3  2005/07/27 21:02:36  firebrand_kh
//  Cleaned up some code
//
//  Revision 1.2  2004/12/27 12:30:01  dj_jl
//  Vavoom version 1.16 changes.
//
//  Revision 1.1  2004/11/16 13:33:22  dj_jl
//  Initial import.
//
//  Revision 1.4  2003/11/12 16:50:47  dj_jl
//  Changed player structure into a class
//
//  Revision 1.3  2003/07/11 16:47:39  dj_jl
//  Made array of players with pointers
//
//  Revision 1.2  2003/03/08 12:41:43  dj_jl
//  Removing class pointers.
//
//  Revision 1.1  2002/10/26 16:32:26  dj_jl
//  New style of defining classes.
//
//**************************************************************************
