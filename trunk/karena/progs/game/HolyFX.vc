//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class HolyFX : Projectile;

int WeaveXY;
int WeaveZ;

//==========================================================================
//
//	Touch
//
//==========================================================================

bool Touch(Entity InOther)
{
	int damage;
	Actor Other;

	Other = Actor(InOther);
	if (!Other.bSolid && !Other.bSpecial && !Other.bShootable)
		return true;

	// Check for skulls slamming into things
	if (bSkullFly)
	{
		if (Other.bShootable && Other != Instigator)
		{
			if (Level.Game.netgame && !Level.Game.deathmatch && Other.bIsPlayer)
			{
				// don't attack other co-op players
				return true;
			}
			if (Other.bReflective && (Other.bIsPlayer || Other.bBoss))
			{
				Enemy = Instigator;
				Instigator = Other;
				return true;
			}
			if (Other.bCountKill || Other.bIsPlayer)
			{
				Enemy = Other;
			}
			if (P_Random() < 96)
			{
				damage = 12;
				if (Other.bIsPlayer || Other.bBoss)
				{
					damage = 3;
					// ghost burns out faster when attacking players/bosses
					Health -= 6;
				}
				if (Instigator && Instigator.bIsPlayer)
				{
					damage = damage * Player(Instigator.Player).intelligence/10;
				}
				Other.Damage(self, Instigator, damage);
				if (P_Random() < 128)
				{
					Spawn(HolyPuff, Origin);
					PlaySound('SpiritAttack', CHAN_VOICE);
					if (Other.bCountKill && P_Random() < 128
						&& !GetSoundPlayingInfo(Other,
							GetSoundID('PuppyBeat')))
					{
						if ((Other.Class == Centaur) ||
							(Other.Class == CentaurLeader) ||
							(Other.Class == Ettin))
						{
							Other.PlaySound('PuppyBeat', CHAN_VOICE);
						}
					}
				}
			}
			if (Other.Health <= 0)
			{
				Enemy = none;
			}
		}
		return true;
	}
	return ::Touch(InOther);
}

//===========================================================================
//
//  RoughCheckThing
//
//===========================================================================

bool RoughCheckThing(Entity Other)
{
	Actor link = Actor(Other);
	
	if ((!link.bCountKill && !link.bIsPlayer) || link.bDormant)
	{
		return false;
	}
	if (!link.bShootable)
	{
		return false;
	}
	if (Level.Game.netgame && !Level.Game.deathmatch && link.bIsPlayer)
	{
		return false;
	}
	if (link == Instigator)
	{
		return false;
	}
	if (CanSee(link))
	{
		return true;
	}
	return false;
}

//============================================================================
//
//  CHolyFindTarget
//
//============================================================================

void CHolyFindTarget()
{
	Actor targ;

	targ = Actor(RoughMonsterSearch(6));
	if (targ)
	{
		Enemy = targ;
		bColideWithWorld = false;
		bSkullFly = true;
		bMissile = false;
	}
}

//============================================================================
//
//  CHolySeekerMissile
//
//  Similar to SeekerMissile, but seeks to a random Z on the target
//
//============================================================================

void CHolySeekerMissile(float thresh, float turnMax)
{
	int dir;
	float dist;
	float delta;
	float angle;
	float newZ;
	float deltaZ;

	if (!Enemy)
	{
		return;
	}
	if (!Enemy.bShootable || (!Enemy.bCountKill && !Enemy.bIsPlayer))
	{
		// Target died/target isn't a player or creature
		Enemy = none;
		bColideWithWorld = true;
		bSkullFly = false;
		bMissile = true;
		CHolyFindTarget();
		return;
	}
	dir = FaceActor(Enemy, &delta);
	if (delta > thresh)
	{
		delta /= 2.0;
		if (delta > turnMax)
		{
			delta = turnMax;
		}
	}
	if (dir)
	{
		// Turn clockwise
		Angles.yaw += delta;
	}
	else
	{
		// Turn counter clockwise
		Angles.yaw -= delta;
	}
	Angles.yaw = AngleMod360(Angles.yaw);
	angle = Angles.yaw;
	Velocity.x = Speed * cos(angle);
	Velocity.y = Speed * sin(angle);
	if (!(Level.Game.level->tictime & 15) ||
		Origin.z > Enemy.Origin.z + Enemy.Height ||
		Origin.z + Height < Enemy.Origin.z)
	{
		newZ = Enemy.Origin.z + Random() * Enemy.Height;
		deltaZ = newZ - Origin.z;
		if (fabs(deltaZ) > 15.0)
		{
			if (deltaZ > 0.0)
			{
				deltaZ = 15.0;
			}
			else
			{
				deltaZ = -15.0;
			}
		}
		dist = DistTo2(Enemy);
		dist = dist / Speed;
		if (dist < 1.0)
		{
			dist = 1.0;
		}
		Velocity.z = deltaZ / dist;
	}
	return;
}

//============================================================================
//
//	CHolyWeave
//
//============================================================================

void CHolyWeave()
{
	float newX, newY;
	float angle;

	angle = AngleMod360(Angles.yaw + 90.0);
	newX = Origin.x - cos(angle) * Level.Game.FloatBobOffsets[WeaveXY] * 4.0;
	newY = Origin.y - sin(angle) * Level.Game.FloatBobOffsets[WeaveXY] * 4.0;
	WeaveXY = (WeaveXY + (P_Random() % 5)) & 63;
	newX += cos(angle) * Level.Game.FloatBobOffsets[WeaveXY] * 4.0;
	newY += sin(angle) * Level.Game.FloatBobOffsets[WeaveXY] * 4.0;
	TryMove(vector(newX, newY, Origin.z));
	Origin.z -= Level.Game.FloatBobOffsets[WeaveZ] * 2.0;
	WeaveZ = (WeaveZ + (P_Random() % 5)) & 63;
	Origin.z += Level.Game.FloatBobOffsets[WeaveZ] * 2.0;
}

//============================================================================
//
//	A_CHolySeek
//
//============================================================================

void A_CHolySeek()
{
	Health--;
	if (Health <= 0)
	{
		Velocity.x /= 4.0;
		Velocity.y /= 4.0;
		Velocity.z = 0.0;
		SetState(DeathState);
		StateTime -= Random() * 0.1;
		return;
	}
	if (Enemy)
	{
		CHolySeekerMissile(itof(Args[0]), itof(Args[0]) * 2.0);
		if (!((Level.Game.level->tictime + 7) & 15))
		{
			Args[0] = 5 + (P_Random() / 20);
		}
	}
	CHolyWeave();
}

//============================================================================
//
//	A_CHolyCheckScream
//
//============================================================================

void A_CHolyCheckScream()
{
	A_CHolySeek();
	if (P_Random() < 20)
	{
		PlaySound('SpiritActive', CHAN_VOICE);
	}
	if (!Enemy)
	{
		CHolyFindTarget();
	}
}

__states__
{
	S_HOLY_FX1('SPIR', 0, 2.0 / 35.0, S_HOLY_FX2) { A_CHolySeek(); }
	S_HOLY_FX2('SPIR', 0, 2.0 / 35.0, S_HOLY_FX3) { A_CHolySeek(); }
	S_HOLY_FX3('SPIR', 1, 2.0 / 35.0, S_HOLY_FX4) { A_CHolySeek(); }
	S_HOLY_FX4('SPIR', 1, 2.0 / 35.0, S_HOLY_FX1) { A_CHolyCheckScream(); }
	S_HOLY_FX_X1('SPIR', 3, 4.0 / 35.0, S_HOLY_FX_X2) { }
	S_HOLY_FX_X2('SPIR', 4, 4.0 / 35.0, S_HOLY_FX_X3) { A_Scream(); }
	S_HOLY_FX_X3('SPIR', 5, 4.0 / 35.0, S_HOLY_FX_X4) { }
	S_HOLY_FX_X4('SPIR', 6, 4.0 / 35.0, S_HOLY_FX_X5) { }
	S_HOLY_FX_X5('SPIR', 7, 4.0 / 35.0, S_HOLY_FX_X6) { }
	S_HOLY_FX_X6('SPIR', 8, 4.0 / 35.0, none) { }
}

defaultproperties
{
	Health = 105;
	Radius = 10.0;
	Height = 6.0;
	Speed = 420.0;
	MissileDamage = 3;
	Translucency = 33;
	bRip = true;
	bSeekerMissile = true;
	bDamageInvulnerable = true;
	IdleState = S_HOLY_FX1;
	DeathState = S_HOLY_FX_X1;
	DeathSound = 'SpiritDie';
}

//**************************************************************************
//
//  $Log$
//  Revision 1.6  2006/03/12 19:37:08  dj_jl
//  States as objects, added state variable type.
//
//  Revision 1.5  2006/03/02 18:40:23  dj_jl
//  Don't use game specific classes in common.
//
//  Revision 1.4  2006/02/15 23:37:45  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.3  2006/02/05 18:57:00  dj_jl
//  Moved common utils to level info class or built-in.
//
//  Revision 1.2  2005/11/30 23:53:37  dj_jl
//  Switch statement requires integer.
//
//  Revision 1.1  2004/11/16 13:33:21  dj_jl
//  Initial import.
//
//  Revision 1.1  2002/10/26 16:32:26  dj_jl
//  New style of defining classes.
//
//**************************************************************************
