//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**	        ##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : KAPlayerBase;

const float
	INVULNTIME2				= 5.0;

const float HEAL_RADIUS_DIST		= 255.0;

struct trophies_t
{
	int		frags_at_once;
	int		kills_at_once;
	float	fao_timer;
	float	kao_timer;
	int		frags_row;
	int		kills_row;
};

enum
{
	targetinfo_none,
	targetinfo_noshoot,
	targetinfo_shoot
};

struct targetinfo_t
{
	byte	type;
	string	sname;
	byte	level;
	byte	healthpc;	// [CW] health percentage
};

int		altWeapon;
int		altWeaponMode[HexenDefs::NUM_ALTFIRE];

// [FB] Flash for the Darkness Missile
int BlindCount;
float LastBlindTime;
Actor Blinder;
Player PlayerBlinder;
int count;

BotPlayer Bot;

// not needed, declared in PlayerEx
// Actor Rain1;	// active rain maker 1
// Actor Rain2;	// active rain maker 2

CTFBanner Banner;	// this reference is for information only
int BannerStatus;	// this var is used as a fastest way to tell client player must return his team's flag

bool inited;

int pcount; // [FB] counter

// [Korax]
// [FB] Scores for the different gameplay modes
int Score;	//[CW] personal player's score value (depends on game mode)
int TeamFrags; // [FB] Frags from each team	// [CW] you mean THIS PLAYER's team
int TeamKills; // [FB] How many enemies has each team killed	// [CW] you mean THIS PLAYER's team
int TeamPosition; // [CW] team position in global competition list
int PlayerPosition; // [CW] player position in team inner competition list

// [FB] Player atributes for leveling system
int experience;
int exp_level;
int next_level;
int old_level; // [FB] If we are going backwards we need this
int strength;
int wisdom;
int dexterity;
int constitution;

bool pi_damage;	// [CW] for faster PI Damage checkup

//
//  Other stats (for status bar)
//
int max_health;
int old_maxhealth;	// [FB] To decrease the health
int bak_health;		// [FB] To keep an older level health

int				teamscores[HexenDefs::MAXTEAMSINGAME];

int use_power;				// [CW] is needed for some advanced game modes (football);
							//	stores time USE button was held pressed
int use_power_rangefrac;

int		TeamSelection;	//[CW] team selection process state
int		curTeamIndex;	//[CW] Team to join when spawn

						//////// used to skip respawn if Team Selection screen was cancelled
						///////// while REselecting team
PlayerTeam	Team;

bool			targetinfo_enabled;
int				targetinfo_counter;
targetinfo_t	targetinfo;

bool bNoHUD;

// [CW] for trophy announcements
trophies_t	Trophies;

bool bShowScoreboard; // [CW] True if scoreboard should be visible

bool bUnControllable; //[CW] Ignore user/AI commands (move/attack etc)
bool bSpectator;
bool bRespawnAsSpectator;
bool bAllowEndSpectate;

float	respawn_counter; // [CW] Time player must wait after his death before he may respawn
bool	bRecalculateLevelOnRespawn; // [CW] Forces to recalc exp level on player respawn

float	level_ender_counter; // [CW] Time to wait before exit level

replication
{
	reliable if (!bIsClient)
		Score, TeamFrags, TeamKills, TeamPosition, PlayerPosition,
		experience, exp_level, next_level, old_level, strength,
		wisdom, dexterity, constitution, max_health, old_maxhealth,
		bak_health, use_power, use_power_rangefrac, altWeapon,
		teamscores,	altWeaponMode, targetinfo, Banner, BannerStatus;

	unreliable if (!bIsClient)
		ClientDynamicLightEx, ParticleEffectEx;

	reliable if (!bIsClient)
		SetFOV, ClientTeamSelect, ClientScoreboard, CallLocalSound,
		CallLocalSoundNamed;

	reliable if (bIsClient)
		ServerDisconnectBot, ServerTeamSelect, ServerJoinTeam, ServerSpectate;
}

//==========================================================================
//
//	PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	::PutClientIntoServer();
	TeamSelection = HexenDefs::teamselection_none;
}

//==========================================================================
//
//  CreateBot
//
//==========================================================================

void CreateBot()
{
	Bot = BotPlayer(SpawnObject(BotPlayer));
	Bot.player = self;
	Bot.OnBeginPlay();
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	Blinder = none;

	TeamPosition = -1;
	PlayerPosition = -1;
	bUnControllable = false;	// [CW] In case SpawnClient called for 2nd time
								// after player selected team

	//
	// [CW]
	if (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM &&
		TeamSelection == HexenDefs::teamselection_none)
	{
		SelectPlayerTeam();
	}

	if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM ||
		TeamSelection == HexenDefs::teamselection_done)
	{
		MainGameInfo(Level.Game).InsertPlayer(self, curTeamIndex);
	}

	::SpawnClient();
}

//==========================================================================
//
//  OnNetReborn
//
//==========================================================================

void OnNetReborn(EntityEx OldMO)
{
	Weapon bestWeapon;
	Inventory Item;

	// Restore keys and weapons when reborn in co-op
	bestWeapon = none;
	if (OldMO)
	{
		for (Item = OldMO.Inventory; Item; )
		{
			Inventory Next = Item.Inventory;
			if (Weapon(Item))
			{
				if (!bestWeapon || bestWeapon.SelectionOrder >
					Weapon(Item).SelectionOrder)
				{
					bestWeapon = Weapon(Item);
				}
				Item.AttachToOwner(EntityEx(MO));
			}
			else if (Key(Item))
			{
				Item.AttachToOwner(EntityEx(MO));
			}
			Item = Next;
		}
		OldMO.DestroyAllInventory();
	}
	GiveAmmo(Mana1, 0);
	GiveAmmo(Mana2, 0);
	GiveAmmo(ItemManaRed, 0);
	EntityEx(MO).FindInventory(Mana1).Amount = 25;
	EntityEx(MO).FindInventory(Mana2).Amount = 25;
	EntityEx(MO).FindInventory(ItemManaRed).Amount = 25;
	if (bestWeapon)
	{
		// Bring up the best weapon
		PendingWeapon = bestWeapon;
	}
}

//==========================================================================
//
//  DestroyBot
//
//==========================================================================

void DestroyBot()
{
	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
}

//==========================================================================
//
//  ResetPlayerOnSpawn
//
//==========================================================================

void ResetPlayerOnSpawn()
{
	::ResetPlayerOnSpawn();

	BlindCount = 0; // [FB] Reset the blind count too!
	pcount = 0; // [FB] Reset counter
	respawn_counter = 2.0;

//[Korax]
	if (!inited)
	{
		InitiateLevel();
		inited = true;
	}
}

//==========================================================================
//
//  BotOnSpawn
//
//==========================================================================

void BotOnSpawn()
{
	Bot.OnSpawn();
}

//==========================================================================
//
//  DoClearPlayer
//
//==========================================================================

void DoClearPlayer()
{
	// [CW] not needed since no ClearPlayer called? CHECKME
	//targetinfo_enabled = !bIsBot && GetCvar('TargetInfo');
}

//==========================================================================
//
//  GetRebornHealth
//
//==========================================================================

int GetRebornHealth()
{
	return max_health;
}

//==========================================================================
//
//	GetMaxHealth
//
//==========================================================================

int GetMaxHealth()
{
	return max_health;
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;
	int i;

	impulse = Impulse;
	Impulse = 0;

	if (bUnControllable)
	{
		// Player can do almost nothing when uncontrollable
		if (impulse != HexenDefs::IMPULSE__SUICIDE && impulse != HexenDefs::IMPULSE__SCOREBOARD)
			return;
	}

	if (impulse <= HexenDefs::NUMWEAPONSLOTS)
	{
		ChangeWeapon(impulse);
	}
	else if (impulse == HexenDefs::IMPULSE__WEAPON_PREV)
	{
		PrevWeapon();
	}
	else if (impulse == HexenDefs::IMPULSE__WEAPON_NEXT)
	{
		NextWeapon();
	}
	else if (impulse == HexenDefs::IMPULSE__SUICIDE)
	{
		Actor(MO).Damage(none, Actor(self.MO), 10000);
	}
	// [FB] We want to make the scoreboard accesible with a new impulse value
	else if (impulse == HexenDefs::IMPULSE__SCOREBOARD)
	{
		Scoreboard(!bShowScoreboard);
	}
	else if (impulse == HexenDefs::IMPULSE__ATTACK2)
	{
		AlternateAttack();
	}
	else if (impulse == HexenDefs::IMPULSE__TOGGLE_BLASTRAD)
	{
		ToggleAltAttack(HexenDefs::ALTFIRE_BLASTRADIUS);
	}
	else if (impulse == HexenDefs::IMPULSE__TOGGLE_POSIONBAG)
	{
		ToggleAltAttack(HexenDefs::ALTFIRE_POISONBAG);
	}
	else if (impulse == HexenDefs::IMPULSE__FIXME)
	{
		KArenaPlayerPawn(MO).MoveToFreePosition();
	}
	else if (impulse >= 200 && impulse <= 205)
	{
		LineSpecialLevelInfo(Level).ConChoiceImpulse(impulse - 200);
	}
}

//==========================================================================
//
//  BotTick
//
//==========================================================================

void BotTick(float deltaTime)
{
	if (!bUnControllable)
	{
		Bot.BotTick(deltaTime);
	}
}

//==========================================================================
//
//  SpawnSpeedEffect
//
//==========================================================================

void SpawnSpeedEffect()
{
	Actor speedA = KArenaLevelInfo(Level).Spawn(PlayerSpeed, MO.Origin);
	if (speedA)
	{
		speedA.Angles.yaw = MO.Angles.yaw;
		speedA.Translation = MO.Translation;
		speedA.Target = Actor(MO);
		speedA.Special1 = PClass;
		if (speedA.Special1 > 2)
		{
			speedA.Special1 = 0;
		}
		int Idx;
		switch (PClass)
		{
		case HexenDefs::PCLASS_FIGHTER:
			Idx = 0;
			break;
		case HexenDefs::PCLASS_CLERIC:
			Idx = 2;
			break;
		case HexenDefs::PCLASS_MAGE:
			Idx = 4;
			break;
		case HexenDefs::PCLASS_HERETIC:
			Idx = 6;
			break;
		case HexenDefs::PCLASS_MARINE:
			Idx = 8;
			break;
		case HexenDefs::PCLASS_WITCHAVEN:
			Idx = 10;
			break;
		case HexenDefs::PCLASS_PIG:
			Idx = 12;
			break;
		}
		speedA.SetState(GetStatePlus(speedA.IdleState, Idx, true));
		speedA.FloorClip = MO.FloorClip;
#ifdef FIXME
		if (self == GPlayers[consoleplayer])
		{
			speedA.bHidden = true;
		}
#endif
	}
}

//==========================================================================
//
//  Cheat_Gimme
//
//==========================================================================

void Cheat_Gimme()
{
	class<Inventory>	Cls;
	class<WeaponPickup>	WpnCls;

	if (Cmd_CheckParm("weapons"))
	{
		int i;

		BasicArmor Armor = BasicArmor(EntityEx(MO).FindInventory(
			BasicArmor));
		Armor.Amount = 200;
		foreach AllClasses(WeaponPickup, WpnCls)
		{
			WeaponPickup Wpn = Level.Spawn(WpnCls,,,, false);
			Wpn.AmmoGive1 = 0;
			Wpn.AmmoGive2 = 0;
			Wpn.AmmoGive3 = 0;
			if (!Wpn.TryPickup(EntityEx(MO)))
			{
				Wpn.Destroy();
			}
		}
		foreach AllClasses(Ammo, Cls)
		{
			//	Only direct descendants.
			if (GetClassParent(Cls) != Ammo)
			{
				continue;
			}
			Inventory AmmoItem = EntityEx(MO).FindInventory(Cls);
			if (!AmmoItem)
			{
				AmmoItem = Level.Spawn(Cls,,,, false);
				if (!AmmoItem.TryPickup(EntityEx(MO)))
				{
					AmmoItem.Destroy();
					AmmoItem = none;
				}
			}
			if (AmmoItem)
			{
				AmmoItem.Amount = AmmoItem.MaxAmount;
			}
		}
		cprint("ALL WEAPONS");
	}
	if (Cmd_CheckParm("health"))
	{
		MO.Health = max_health;
		Health = MO.Health;
		cprint("FULL HEALTH");
	}
	if (Cmd_CheckParm("keys"))
	{
		foreach AllClasses(Key, Cls)
		{
			if (!Cls.default.IdleState)
			{
				//	Abstract class
				continue;
			}
			EntityEx(MO).GiveInventoryType(Cls);
		}
		cprint("ALL KEYS");
	}
	if (Cmd_CheckParm("puzzles"))
	{
		foreach AllClasses(PuzzleItem, Cls)
		{
			if (!Cls.default.IdleState)
			{
				//	Abstract class
				continue;
			}
			EntityEx(MO).GiveInventoryType(Cls);
		}
		cprint("ALL ARTIFACTS");
	}
	if (Cmd_CheckParm("exp"))
	{
		GiveExperience(10000, true);
		cprint("LOTS OF EXP");
	}
}

//==========================================================================
//
//  Cheat_Morph
//
//==========================================================================

void Cheat_Morph()
{
	if (MorphTime)
	{
		UndoPlayerMorph(false, self);
	}
	else
	{
		Actor(MO).MorphPlayer(EggMissile.default.PlayerClass, 0.0,
			EggMissile.default.MorphStyle, none, none, Actor(MO));
	}
	cprint("SQUEAL!!");
}

//==========================================================================
//
//  Cheat_Class
//
//==========================================================================

void Cheat_Class()
{
	int i;
	int pclass;

	if (MorphTime || MO.Class == PlayerHeresiarch)
	{
		// don't change class if the player is morphed
		return;
	}
	if (Cmd_CheckParm("fighter"))
		pclass = HexenDefs::PCLASS_FIGHTER;
	else if (Cmd_CheckParm("cleric"))
		pclass = HexenDefs::PCLASS_CLERIC;
	else if (Cmd_CheckParm("mage"))
		pclass = HexenDefs::PCLASS_MAGE;
	else if (Cmd_CheckParm("heretic"))
		pclass = HexenDefs::PCLASS_HERETIC;
	else if (Cmd_CheckParm("marine"))
		pclass = HexenDefs::PCLASS_MARINE;
	else if (Cmd_CheckParm("witchaven"))
		pclass = HexenDefs::PCLASS_WITCHAVEN;
	else
	{
		cprint("INVALID PLAYER CLASS");
		return;
	}
	PClass = pclass;
	BasicArmor Armor = BasicArmor(EntityEx(MO).FindInventory(BasicArmor));
	Armor.Amount = 0;
	BaseClass = pclass;
//	PostMorphWeapon(HexenDefs::WP_FIRST);
	SetCvar('class', pclass);
	SB_Start();
}

//==========================================================================
//
//  IsWeaponAlwaysExtremeDeath
//
//==========================================================================

bool IsWeaponAlwaysExtremeDeath()
{
	// Always extreme death from eight weapon
	return ReadyWeapon.bExtremeDeath;
}

//==========================================================================
//
//	KilledActor
//
//==========================================================================

void KilledActor(EntityEx Victim)
{
	bool bKilledByAlly = Victim.IsTeammate(EntityEx(MO));

	// [CW] Process actor-specific event
	Actor(Victim).KilledByPlayer(PlayerPawn(MO));

	// [FB] Increase the player's KillCount
	if (Victim.bMonster)
	{
		//[CW] update player's score
		UpdateScore(HexenDefs::scoreevent_Kill);

		//[CW] update kill trophies
		UpdateQueuedTrophy(HexenDefs::trophycl_killsatonce, 1);
		UpdateQueuedTrophy(HexenDefs::trophycl_killsrow, 1);
		
		// [CW] CHECKME: think - maybe make a game model for CS/TCS and put all that there
		// [FB] Check how many monsters there are in castle sweep mode
		if (MainGameInfo(Level.Game).GameType == HexenDefs::game_sweep ||
		    MainGameInfo(Level.Game).GameType == HexenDefs::game_sweepteam)
		{
			if (Level.CurrentKills < Level.TotalKills && Level.TotalKills - Level.CurrentKills <= 10)
			{
				//MO.PlaySound('misc/chat', CHAN_AUTO);
				MainGameInfo(Level.Game).BroadcastLocalSoundNamed('misc/chat', false, 0.0);
				MainGameInfo(Level.Game).bcentreprint(va("%i monsters left!", Level.TotalKills - Level.CurrentKills));
			}
			
			// [FB] No more monsters, so end the game!
			if (Level.TotalKills == Level.CurrentKills)
			{
				// [FB] Exit map
				int i;
				for (i = 0; i < MAXPLAYERS; i++)
				{
					if (Level.Game.Players[i] && !Level.Game.Players[i].bIsBot)
					{
						Player(Level.Game.Players[i]).level_ender_counter = 0.5;
					}
				}
			}
		}
	}

	// Check for frag changes
	if (Victim.bIsPlayer)
	{
		if (bIsBot)
		{
			Bot.Killed(Victim);
		}

		if (Victim.Player.bIsBot)
		{
			Player(Victim.Player).Bot.Died(EntityEx(MO));
		}

		if (Victim == MO)
		{
			// Self-frag
			Frags--;

			UpdateScore(HexenDefs::scoreevent_SelfFrag);
		}
		else
		{
			int se = bKilledByAlly ? HexenDefs::scoreevent_TeamFrag : HexenDefs::scoreevent_Frag;

			if (bKilledByAlly)
			{
				Frags--;
			}
			else
			{
				Frags++;
				GiveExperience(500 * Player(Victim.Player).exp_level /* / exp_level */);
			}

			if (se == HexenDefs::scoreevent_Frag && MainGameInfo(Level.Game).GameModel)
			{
				se = KArenaGameModel(MainGameInfo(Level.Game).GameModel).GetFragScoreEvent(self, Player(Victim.Player));
			}

			UpdateScore(se);

			//[CW] update frag trophies
			if (!bKilledByAlly)
			{
				UpdateQueuedTrophy(HexenDefs::trophycl_fragsatonce, 1);
				UpdateQueuedTrophy(HexenDefs::trophycl_fragsrow, 1);
			}
		}
	}
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	::DisconnectClient();

	//[CW] set leaving player down the competition table
	// NOTE: for future, should leaving player really loose his/her score?
	if (Team)
		Team.DiscardPlayer(self);

	// [CW] get rid of the MO corpse
	MO.Destroy();
	MO = none;
}

//==========================================================================
//
//  ServerDisconnectBot
//
//==========================================================================

void ServerDisconnectBot(int pl)
{
	if (Level.Game.Players[pl])
	{
		if (Level.Game.Players[pl].bIsBot)
			Level.Game.Players[pl].DisconnectBot();
		else
			Error("Player::ServerDisconnectBot : called for non-bot player %i", pl);
	}
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	targetinfo_enabled = !bIsBot && GetCvar('TargetInfo');
	bNoHUD = !bIsBot && GetCvar('NoHUD');
}

//==========================================================================
//
//  SelectPlayerTeam
//
//==========================================================================

void SelectPlayerTeam()
{
	if (bIsBot || GetCvar('TeamAutoSelect') && TeamSelection != HexenDefs::teamselection_reselect)
	{
		AutoSelectTeam();
	}
	else
	{
		int i;
		int maxTeams;
		maxTeams = MainGameInfo(Level.Game).MaxTeamsInGame[MainGameInfo(Level.Game).GameType]
						> HexenDefs::MAXTEAMSINGAME ? HexenDefs::MAXTEAMSINGAME :
						MainGameInfo(Level.Game).MaxTeamsInGame[MainGameInfo(Level.Game).GameType];
		for (i = 0; i < HexenDefs::MAXTEAMSINGAME; i++)
		{
			if (MainGameInfo(Level.Game).Teams[i] == none)
				break;
		}
		
		if (TeamSelection != HexenDefs::teamselection_reselect)
		{
			TeamSelection = HexenDefs::teamselection_wait;
			// [CW] perhaps not needed, since player starts as a spectator
			//SetControllable(false);
		}
		
		ClientTeamSelect(maxTeams, MainGameInfo(Level.Game).TeamInfos.Num, i,
			Team ? Team.Index : -1,
			MainGameInfo(Level.Game).Teams[0] ? MainGameInfo(Level.Game).Teams[0].Index : -1,
			MainGameInfo(Level.Game).Teams[0] ? MainGameInfo(Level.Game).Teams[0].GetPlayerCount() : 0,
			MainGameInfo(Level.Game).Teams[1] ? MainGameInfo(Level.Game).Teams[1].Index : -1,
			MainGameInfo(Level.Game).Teams[1] ? MainGameInfo(Level.Game).Teams[1].GetPlayerCount() : 0);
	}
}

//==========================================================================
//
//  AutoSelectTeam
//
//==========================================================================

void AutoSelectTeam()
{
	int i;
	int new_index = -1;

	bool fixed_teams = GetCvar('FixedTeams');
	int fixed_team1 = GetCvar('FixedTeam1');
	int fixed_team2 = GetCvar('FixedTeam2');
	int auto_player_team = GetCvar('AutoPlayerTeam');

	MainGameInfo Game = MainGameInfo(Level.Game);

	bool bKeepEqual = GetCvar('KeepTeamsEqual');

	if (bKeepEqual)
	{
		int count;
		int count_limit = Game.Teams[0].GetPlayerCount();

		for (i = 0; i < Game.MaxTeamsInGame[Game.GameType]; i++)
		{
			count = Game.Teams[i].GetPlayerCount();
			if (count_limit > count)
				count_limit = count;
		}

		if (!bIsBot && auto_player_team != -1 &&
			Game.Teams[auto_player_team].GetPlayerCount() <= count_limit)
		{
			new_index = Game.Teams[auto_player_team].Index;
		}
		else
		{
			for (i = 0; i < Game.MaxTeamsInGame[Game.GameType]; i++)
			{
				// [CW] Choose one of the existing teams that is available for joining
				if (Game.Teams[i].GetPlayerCount() <= count_limit)
				{
					new_index = Game.Teams[i].Index;
					break;
				}
			}
		}
	}
	else
	{
		// [CW] Choose one of the existing teams
		if (!bIsBot && auto_player_team != -1)
		{
			new_index = Game.Teams[auto_player_team].Index;
		}
		else
		{
			new_index = 
				Game.Teams[P_Random() % (Game.MaxTeamsInGame[Game.GameType])].Index;
		}
	}

	curTeamIndex = new_index;
	TeamSelection = HexenDefs::teamselection_done;
}

//==========================================================================
//
//  ClientTeamSelect
//
//	[CW] TODO: pass array here as Cols if this is possible? << ?? why
//
//==========================================================================

void ClientTeamSelect(int maxTeams, int registeredTeams, int activeTeams, int curSelection,
	int Col1, int Count1, int Col2, int Count2)
{
	ClientGameBaseEx(ClGame).TeamSelect(maxTeams, registeredTeams, activeTeams, curSelection,
		Col1, Count1, Col2, Count2);
}

//==========================================================================
//
//  ServerTeamSelect
//
//==========================================================================

void ServerTeamSelect()
{
	TeamSelection = HexenDefs::teamselection_reselect;
	SelectPlayerTeam();
}

//==========================================================================
//
//  ServerJoinTeam
//
//==========================================================================

void ServerJoinTeam(int Index)
{
	if (!bSpawned)
	{
		return;
	}

	if (TeamSelection == HexenDefs::teamselection_spectate)
	{
		TeamSelection = TeamSelection == HexenDefs::teamselection_wait;
	}

	// [CW] respawn player if team is selected or re-selected
	if (TeamSelection == HexenDefs::teamselection_wait ||
		TeamSelection == HexenDefs::teamselection_reselect)
	{
		curTeamIndex = Index;

		if (!(TeamSelection == HexenDefs::teamselection_reselect && (Team ? Team.Index : -1) == Index))
		{
			if (/*PlayerState != PST_DEAD*/ TeamSelection == HexenDefs::teamselection_wait)
			{
				TeamSelection = HexenDefs::teamselection_done;
				MO.Destroy();
				MO = none;
				PlayerState = PST_REBORN;
				SpawnClient();
			}
			else
			{
				TeamSelection = HexenDefs::teamselection_done;
				if (PlayerState != PST_DEAD)
					Actor(MO).Damage(none, none, 10000);
				// [CW] For player's convenience
				Scoreboard(false);
				centreprint("you will join selected team after respawn");
			}
		}
		else
			TeamSelection = HexenDefs::teamselection_done;
	}

	//[CW] update all server clients' scoreboards
	MainGameInfo(Level.Game).ScoreboardsUpdate(none);
}

//==========================================================================
//
//  ServerSpectate
//
//==========================================================================

void ServerSpectate()
{
	TeamSelection = HexenDefs::teamselection_spectate;

	if (Team)
	{
		Team.DiscardPlayer(self);	
		MO.Destroy();
		MO = none;
		SpawnClient();
	}
}

//==========================================================================
//
//  NetGameReborn
//
//	Respawn at the start
//
//	[CW] In general - a partial copy of PlayerEx::NetGameReborn
//		 without part corresponding not-deathmatch play and added
//		 team appliance.
//
//==========================================================================

void NetGameReborn()
{
	int i;
	bool foundSpot;
	EntityEx OldMO = EntityEx(MO);

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	OldMO.DestroyAllInventory();

	if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM ||
		TeamSelection == HexenDefs::teamselection_done)
	{
		MainGameInfo(Level.Game).InsertPlayer(self, curTeamIndex);
	}

	DeathMatchSpawnPlayer();
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	// [CW] CTF and Football special rule
	if (Team /* must already have a Team, otherwise pointless */
		&& (MainGameInfo(Level.Game).GameType == HexenDefs::game_ctf
			|| MainGameInfo(Level.Game).GameType == HexenDefs::game_football))
	{
		bool enough = false;
		j = 0;
		while(true)
		{
			if (enough)
			{
				if (j == Level.DeathmatchStarts.Num)
					break; // ah! just go spawn where you can, oh unlucky one...
				i = j;
			}
			else
				i = P_Random() % Level.DeathmatchStarts.Num;

#ifdef DEBUG_PLAYER_SPAWN
			print("Level.DeathmatchStarts[i].type = %i", Level.DeathmatchStarts[i].type);
#endif
			if (Level.DeathmatchStarts[i].type != 11 /* common */ &&
				((Level.DeathmatchStarts[i].type == 15040 /*aka Blue*/ &&
					Team.T2 == PlayerTeam::TEAM2_INDEX_BLUE) ||
				(Level.DeathmatchStarts[i].type == 15041 /*aka Red*/ &&
					Team.T2 == PlayerTeam::TEAM2_INDEX_RED)))
			{
				if (CheckSpot(&Level.DeathmatchStarts[i]))
				{
#ifdef DEBUG_PLAYER_SPAWN
					print("spawning at team location");
#endif
					SpawnPlayer(&Level.DeathmatchStarts[i], false);
					return;
				}
			}

			j++;
			if (!enough && j > 20)
			{
				enough = true;
				j = 0;
			}
		};
	}
	
	for (j = 0; j < 20; j++)
	{
		i = P_Random() % Level.DeathmatchStarts.Num;
		if (CheckSpot(&Level.DeathmatchStarts[i]))
		{
#ifdef DEBUG_PLAYER_SPAWN
			print("spawning tested at random deathmatch location");
#endif
			SpawnPlayer(&Level.DeathmatchStarts[i], false);
			return;
		}
	}
	
	// no good spot, so the player will probably get stuck
#ifdef DEBUG_PLAYER_SPAWN
	print("spawning blind at random deathmatch location");
#endif
	SpawnPlayer(&Level.DeathmatchStarts[P_Random() % Level.DeathmatchStarts.Num], false);
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level. Most of the player
// structure stays unchanged between levels.
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing, bool Voodoo)
{
#ifdef DEBUG_PLAYER_SPAWN
	print("Player::SpawnPlayer :");
#endif

	// TODO: Checkme - what about Voodoo flag?
	if (bRespawnAsSpectator || 
		!bIsBot && MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM && !Team)
	{
#ifdef DEBUG_PLAYER_SPAWN
		print("spawn spectator : forced %i, allowend %i", bRespawnAsSpectator, bAllowEndSpectate);
#endif
		SpawnSpectator(mthing, bRespawnAsSpectator ? bAllowEndSpectate : true);
		bRespawnAsSpectator = false;
	}
	else
	{
#ifdef DEBUG_PLAYER_SPAWN
	print("spawn normal");
#endif
		bSpectator = false;
		::SpawnPlayer(mthing, Voodoo);
	}

#ifdef DEBUG_PLAYER_SPAWN
	print("SpawnPlayer : 2");
#endif

	//
	// [CW] if the trophy data should be stored, do it here
	//
	Trophies.kills_at_once = 0;
	Trophies.kao_timer = 0.0;
	Trophies.frags_at_once = 0;
	Trophies.fao_timer = 0.0;
	Trophies.frags_row = 0;
	Trophies.kills_row = 0;

#ifdef DEBUG_PLAYER_SPAWN
	print("SpawnPlayer : 3");
#endif

	// [FB] There's a penalty for dying; you lost some experience
	if(inited && PlayerState == PST_LIVE)
	{
		// [CW] recalc attributes if needed
		if (bRecalculateLevelOnRespawn)
		{
			int bak_level;

			bRecalculateLevelOnRespawn = false;
			bak_level = exp_level;
			InitiateLevel();
			ActivateLevel(bak_level, false);
		}

		experience -= (200 * exp_level);
		if(experience < 0)
		{
			experience = 0; // [FB] We don't want to have negative experience, right?
		}

		KArenaPlayerPawn(MO).experience = experience;

		// [FB] But what if the experience we lost has made us lost a level??
		//	Then we use the old_level value to compare our current experience,
		//	if it is lower to it, then we activate a level less than
		//	our current one
		while(experience < old_level && exp_level >= 1)
		{
			DeactivateLevel(exp_level - 1, true); // [FB] Update Stats accordingly
		}
	}

#ifdef DEBUG_PLAYER_SPAWN
	print("SpawnPlayer : 4");
#endif

	if (!bSpectator)
	{
		if (bUnControllable)
			EntityEx(MO).Deactivate(none);

		// [CW] if there is advanced game mode initiated, notify it of player spawn
		if (MainGameInfo(Level.Game).GameModel)
			MainGameInfo(Level.Game).GameModel.OnPlayerSpawn(self);

		// [FB] Destroy all things touching players
		Actor(MO).TeleportMove(MO.Origin);
	}
#ifdef DEBUG_PLAYER_SPAWN
	print("SpawnPlayer : 5");
#endif
}

//==========================================================================
//
//  SpawnSpectator
//
//	(Re)Spawn player as invisible spectator
//
//==========================================================================

void SpawnSpectator(mthing_t * mthing, bool AllowRespawn)
{
#ifdef DEBUG_PLAYER_SPAWN
	print("Player::SpawnSpectator :");
#endif
	float				x;
	float				y;
	float				z;
	PlayerPawn			PP;

	if (PlayerState == PST_REBORN)
	{
		PlayerReborn();
	}

	PClass = HexenDefs::PCLASS_SPECTATOR;

	x = mthing->x;
	y = mthing->y;
	z = Actor::ONFLOORZ;
	
	PP = Level.Spawn(class<PlayerPawn>(PlayerSpectator),
		vector(x, y, z),,, false);
	PP.Origin.z += mthing->height;
	PP.LinkToWorld();
	PP.FriendPlayer = GetPlayerNum() + 1;	//	Players are their own friends.

	PP.Angles.yaw = itof(45 * (mthing->angle / 45));
	PP.Player = self;
	PP.bIsPlayer = true;
	PP.Health = Health;
	MO = PP;
	Camera = PP;
	ViewHeight = PP.ViewHeight;
	ViewOrg = MO.Origin;
	ViewOrg.z += ViewHeight;
	ViewAngles = PP.Angles;
	bFixAngle = true;

	ResetPlayerOnSpawn();

	// remove all psprites
	int i;
	for (i = 0; i < NUMPSPRITES; i++)
	{
		SetViewState(i, none);
	}

	// wake up the status bar
	SB_Start();

	// ?? need this here?
	//SetClientModel();

	EntityEx(MO).GiveInventoryType(PowerSpectatorFlight);
	UpdateRevealedMap();

	bSpectator = true;
	bAllowEndSpectate = AllowRespawn;
}

//===========================================================================
//
//	AddDefaultInventory
//
//===========================================================================

void AddDefaultInventory()
{
	HexenArmor HArmor = Level.Spawn(HexenArmor,,,, false);
	HArmor.AttachToOwner(EntityEx(MO));
	HArmor.Slots[4] = PlayerPawn(MO).HexenArmor[0];
	HArmor.SlotsIncrement[0] = PlayerPawn(MO).HexenArmor[1];
	HArmor.SlotsIncrement[1] = PlayerPawn(MO).HexenArmor[2];
	HArmor.SlotsIncrement[2] = PlayerPawn(MO).HexenArmor[3];
	HArmor.SlotsIncrement[3] = PlayerPawn(MO).HexenArmor[4];

	BasicArmor BArmor = Level.Spawn(BasicArmor,,,, false);
	BArmor.Amount = 0;
	BArmor.AttachToOwner(EntityEx(MO));

	int i;
	for (i = 0; i < KArenaPlayerPawn(MO).DropItemList.Num; i++)
	{
		Inventory Item = Inventory(Level.Spawn(KArenaPlayerPawn(MO).DropItemList[i].Type,,,, false));
		Item.bIgnoreSkill = true;
		if (EntityEx(MO).DropItemList[i].Amount > 0)
		{
			Item.Amount = KArenaPlayerPawn(MO).DropItemList[i].Amount;
		}
		if (KArenaWeapon(Item))
		{
			//	For better control empty weapon's ammo.
			KArenaWeapon(Item).AmmoGive1 = 0;
			KArenaWeapon(Item).AmmoGive2 = 0;
			KArenaWeapon(Item).AmmoGive3 = 0;
		}
		if (!Item.TryPickup(EntityEx(MO)))
		{
			Item.Destroy();
		}
		else if (KArenaWeapon(Item) && KArenaWeapon(Item).CheckAmmo(Weapon::FIRE_Either, false))
		{
			SetWeapon(KArenaWeapon(Item));
		}
	}
	LevelChanged();
}

//==========================================================================
//
//  GiveDefaultDeathMatchInventory
//
//==========================================================================

void GiveDefaultDeathMatchInventory()
{
	::GiveDefaultDeathMatchInventory();

	// [FB] Give temporary invulnerability in DM only
	EntityEx(MO).GiveInventoryType(InitialInvulnerability);
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialised
//
//==========================================================================

void PlayerReborn()
{
	::PlayerReborn();

	Scoreboard(false); // [CW]
}

//==========================================================================
//
//  PlayerExitMap
//
//  Called when the player leaves a map.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	::PlayerExitMap(clusterChange);

	BlindCount = 0; // [FB] Reset the blind count too!
}

//==========================================================================
//
//  SetControllable
//
//  [CW] toggles UnControllable mode on/off
//
//==========================================================================

void SetControllable(bool Mode)
{
	if (Mode)
	{
		bUnControllable = false;
		if (MO)
		{
			EntityEx(MO).Activate(none);
			MO.LinkToWorld();	// [CW] reassurance...
		}
	}
	else
	{
		bUnControllable = true;
		if (MO)
			EntityEx(MO).Deactivate(none);
		PoisonCount = 0;
		BlindCount = 0;
	}
}

//==========================================================================
//
//	GotAmmo
//
//==========================================================================

void GotAmmo(Ammo NewAmmo)
{
	if (ReadyWeapon.bWimpyWeapon)
	{
		Weapon Best = BestWeapon(class<Ammo>(NewAmmo.Class));
		if (Best && Best.SelectionOrder < ReadyWeapon.SelectionOrder)
		{
			PendingWeapon = Best;
		}
	}
}

//[Korax]
//==========================================================================
//
//  GiveExperience
//
//==========================================================================

void GiveExperience(int exp, optional bool Absolute)
{
	if (!Absolute)
	{
		exp = exp / exp_level;
	}

	experience += exp;
	if(experience > 9999)
	{
		experience = 9999;
	}

	KArenaPlayerPawn(MO).experience = experience;

	while (experience >= next_level && exp_level < 11)
	{
		ActivateLevel(exp_level + 1, true);
	}
}

//============================================================================
//
//  ActivateLevel
//
//  [FB] Using the new leveling system the maximum level is 10
//
//============================================================================

void ActivateLevel(int to_lvl, bool message)
{
	int addhealth;
	int i;

	while (exp_level < 11 && exp_level < to_lvl) // [FB] MAX Level it's 10
	{
		old_level = 1000 * (exp_level); // [FB] We need to keep this to compare it
		exp_level++;
		next_level = 1000 * exp_level;

		strength += strength * 25 / 100;
		constitution += constitution * 25 / 100;
		dexterity += dexterity * 25 / 100;
		intelligence += intelligence * 25 / 100;
		wisdom += wisdom * 25 / 100;
		old_maxhealth = max_health;
		bak_health = old_maxhealth - old_maxhealth * 9 / 100; // Save a previous level's health

		addhealth = max_health * 9 / 100;
		max_health += addhealth;
		Health = ftoi(itof(Health) / itof(old_maxhealth) * itof(max_health));
		MO.Health = Health;
	}

	LevelChanged();

	if (message)
	{
		cprint("YOU REACHED LEVEL %d", exp_level);
		MO.PlaySound('misc/chat', CHAN_AUTO);
	}

	pcount = 0;	/// reset exps loosing counter
}

//============================================================================
//
//  DeactivateLevel
//
//  [FB] We lost a level, so we need to update the attributes then!
//
//============================================================================

void DeactivateLevel(int to_lvl, bool message)
{
	int addhealth;
	int i;

	while(exp_level > 1 && exp_level > to_lvl)
	{
		exp_level--; // [FB] Decrese experience level
		next_level = 1000 * exp_level; // [FB] Decrease the next to the old_level
		old_level = 1000 * (exp_level - 1); // [FB] Also decrease the old_level

		strength -= strength * 25 / 100;
		constitution -= constitution * 25 / 100;
		dexterity -= dexterity * 25 / 100;
		intelligence -= intelligence * 25 / 100;
		wisdom -= wisdom * 25 / 100;

		addhealth = old_maxhealth * 9 / 100;
		max_health -= addhealth;

		if (Health > max_health)
			Health = max_health;
		MO.Health = Health;
	}

	LevelChanged();

	if (message)
	{
		cprint("YOU LOWERED TO LEVEL %d", exp_level);
		MO.PlaySound('misc/chat', CHAN_AUTO);
	}

	pcount = 0;	/// reset exps loosing counter
}

//============================================================================
//
//  InitiateLevel
//
//============================================================================

void InitiateLevel()
{
	max_health = KArenaPlayerPawn(MO).default.Health;	//100;

	exp_level = 1;
	next_level =  1000 * exp_level;
	old_level = 1000 * (exp_level - 1);
	Health = max_health;
	MO.Health = Health;

	strength = KArenaPlayerPawn(MO).InitialStrength;
	wisdom = KArenaPlayerPawn(MO).InitialWisdom;
	dexterity = KArenaPlayerPawn(MO).InitialDexterity;
	constitution = KArenaPlayerPawn(MO).InitialConstitution;
	intelligence = KArenaPlayerPawn(MO).InitialIntelligence;

	LevelChanged();
}

//==========================================================================
//
//  LooseExperience
//
//  [FB] If the player camps much time he'll lost experience too
//	[CW] Now it works all the time..
//
//==========================================================================

void LooseExperience()
{
	// [FB] Let's see, if a tic is 1/35 of a second, a second should
	//	be multiplied by 35 to have an equality, every 3 secs. the
	//	player stays camping (even if he's shooting), we'll start
	//	decreasing his experience according his level, so...
	int limit;

	limit = 17; // [FB] 0.5 seconds

	if(inited && (pcount >= limit) && (pcount % (1 * 35) == 0) && PlayerState == PST_LIVE)
	{
		experience -= (1 * exp_level); // [FB] Depending the player level

		if(experience < 0)
		{
			experience = 0; // [FB] We don't want to have negative experience
		}

		KArenaPlayerPawn(MO).experience = experience;

		if(pcount == 525)
		{
			pcount = 105; // [FB] To avoid overflows
		}

		// [FB] But what if the experience we lost has made us lost a level??
		//	Then we use the old_level value to compare our current experience,
		//	if it is lower than it, then we activate a level less than
		//	our current one
		while(experience < old_level && exp_level >= 1)
		{
			DeactivateLevel(exp_level - 1, true); // [FB] Update Stats accordingly
		}
	}
}	   

//==========================================================================
//
//  Scoreboard
//
// [FB] Here we keep a track of everything needed to make KA's scoreboard
//	for every gameplay mode
//
//==========================================================================

void Scoreboard(bool show)
{
	// [CW] If show == true, update scoreboard data first.
	if (show)
		MainGameInfo(Level.Game).ScoreboardsUpdate(self);
	
	// [CW] Set bShowScoreboard flag
	bShowScoreboard = show;
	ClientScoreboard(bShowScoreboard);
}

//==========================================================================
//
//  ClientScoreboard
//
//==========================================================================

void ClientScoreboard(bool show)
{
	ClientGameBaseEx(ClGame).ShowScoreBoard(show);
}

//==========================================================================
//
//  ToggleAltAttack
//
//==========================================================================

void ToggleAltAttack(int weapon)
{
	if (altWeapon == weapon)
	{
		if (altWeaponMode[altWeapon] == MainGameInfo(Level.Game).AltWeaponModeCount[altWeapon] - 1)
			altWeaponMode[altWeapon] = 0;
		else
			altWeaponMode[altWeapon]++;
	}

	altWeapon = weapon;
}

//==========================================================================
//
//  AlternateAttack
//
//==========================================================================

void AlternateAttack()
{
	Ammo type;

	switch (altWeapon)
	{
	case HexenDefs::ALTFIRE_BLASTRADIUS:
		type = Ammo(EntityEx(MO).FindInventory(ArtifactBlastRadius));
		if (type && type.Amount > 0)
		{
			// [FB] Use correct ammo
			type.Amount -= 10;
			UseArtiBlastRadius();
		}
		break;
	case HexenDefs::ALTFIRE_POISONBAG:
		type = Ammo(EntityEx(MO).FindInventory(ArtifactFlechette));
		if (type && type.Amount > 0)
		{
			type.Amount -= 10;
			UseArtiPoisonBag();
		}
		break;
	}
}

//==========================================================================
//
//  PlayerUnmorphed
//
//==========================================================================

void PlayerUnmorphed()
{
	// [CW] recalc attributes
	int bak_level = exp_level;
	InitiateLevel();
	ActivateLevel(bak_level, false);
	bRecalculateLevelOnRespawn = false;
}

//==========================================================================
//
//  CheckForRespawn
//
//==========================================================================

bool CheckForRespawn(float deltaTime)
{
	if (respawn_counter > 0.0)
	{
		respawn_counter -= deltaTime;
	}

	return respawn_counter <= 0.0 &&
		(bIsBot || (Buttons & BT_USE || Buttons & BT_ATTACK || Buttons & BT_JUMP ));
}

//==========================================================================
//
//  UpdateTargetInfo
//
//==========================================================================

void UpdateTargetInfo()
{
	if (PlayerState != PST_DEAD && Team && targetinfo_enabled)
	{
		TAVec angles;
		TVec Dir;
		
		angles = MO.Angles;
		KArenaPlayerPawn(MO).bOverrideCoop = true;
		EntityEx AimTarget = Actor(MO).AimLineAttack(Dir, angles, 64.0 * 64.0);
		KArenaPlayerPawn(MO).bOverrideCoop = false;
		if (AimTarget)
		{
			if (AimTarget.Player)
			{
				int teamfactor = KArenaGameModel::teamfactor_any;
				if (MainGameInfo(Level.Game).GameModel)
					teamfactor = KArenaGameModel(MainGameInfo(Level.Game).GameModel).TeamFactor(Actor(MO), AimTarget);
				if (teamfactor == KArenaGameModel::teamfactor_any)
				{
					if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM &&
						!MainGameInfo(Level.Game).bIsCoopGame[MainGameInfo(Level.Game).GameType])
						targetinfo.type = targetinfo_shoot;
					else
						targetinfo.type =
							(MainGameInfo(Level.Game).bIsCoopGame[MainGameInfo(Level.Game).GameType] ||
							Team.Index == Player(AimTarget.Player).Team.Index)
								? targetinfo_noshoot : targetinfo_shoot;
				}
				else
					targetinfo.type = teamfactor == KArenaGameModel::teamfactor_coop ? targetinfo_noshoot : targetinfo_shoot;

				targetinfo.sname = AimTarget.Player.PlayerName;
				targetinfo.level = Player(AimTarget.Player).exp_level;
				if (Player(AimTarget.Player).max_health)	// [CW] paranoia
					targetinfo.healthpc = 100 * Player(AimTarget.Player).Health / Player(AimTarget.Player).max_health;
				else
					targetinfo.healthpc = 0;
			}
			else
			{
				targetinfo.type = targetinfo_none;
				targetinfo.sname = "";
			}
			targetinfo_counter = 30;	//[CW] this should use real time, i think
		}
		
	}
	if (targetinfo_counter > 0)
	{
		targetinfo_counter--;
		if (!targetinfo_counter)
		{
			targetinfo.type = targetinfo_none;
			targetinfo.sname = "";
		}
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	// FIXME this sucks
	if (bNoHUD)
		ViewStates[ps_weapon].SY += 1000.0;

	if (bSpectator && bAllowEndSpectate && (Buttons & BT_ATTACK))
	{
		if (Team)
		{
			print("from spectator -> respawn normal");
			PlayerState = PST_REBORN;
			NetGameReborn();
		}
		else
		{
			// This will also respawn player
			// CHECKME: move this to some method?
			if (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM)
			{
				TeamSelection = HexenDefs::teamselection_wait;
				SelectPlayerTeam();
			}

			if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM ||
				TeamSelection == HexenDefs::teamselection_done)
			{
				MainGameInfo(Level.Game).InsertPlayer(self, curTeamIndex);
			}
		}
	}

	if (bUnControllable)
	{
		Buttons = 0;
		ForwardMove = 0.0;
		SideMove = 0.0;
		ViewAngles = MO.Angles;
	}

	// [CW]
	if (level_ender_counter > 0.0)
	{
		level_ender_counter -= deltaTime;
		if (level_ender_counter <= 0.0)
		{
			level_ender_counter = 0.0;
			// [FB] Exit map
			Level.ExitLevel(0);
		}
	}

/*	if (!MO)
		Error("Player::PlayerTick : MO reference not set to an instance of an object");*/

	if (bIsBot && !bSpectator)
	{
		BotTick(deltaTime);
	}

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (Actor(MO).bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		Actor(MO).bJustAttacked = false;
	}

	//	You can only press use while totally frozen
	if (bTotallyFrozen)
	{
		Buttons &= BT_USE;
		Impulse = 0;
		ViewAngles = MO.Angles;
		ForwardMove = 0.0;
		SideMove = 0.0;
		FlyMove = 0.0;
	}
	else if (bFrozen)
	{
		ForwardMove = 0.0;
		SideMove = 0.0;
		FlyMove = 0.0;
	}

	WorldTimer += deltaTime;
	// [FB] We don't have any inventory in KA...
//	InventoryTick(deltaTime);

	if (!bIsBot)
		UpdateTargetInfo();

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		if (bNoHUD)
			ViewStates[ps_weapon].SY -= 1000.0;
		return;
	}

	// Copy view angles
	if (MO == Camera)
	{
		MO.Angles = ViewAngles;
	}

	//[CW] trophies timed update
	UpdateTimedTrophies(deltaTime);

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime <= 0.0)
		{
			JumpTime = 0.0;
		}
	}
	if (MorphTime)
	{
		PlayerPawn(MO).MorphPlayerThink();
	}

	if (bUnControllable)
	{
		ForwardMove = 0.0;
		SideMove = 0.0;
	}

	//	Move around.
	//	ReactionTime is used to prevent movement for a bit after a teleport.
	if (Actor(MO).ReactionTime)
	{
		Actor(MO).ReactionTime -= deltaTime;
		if (Actor(MO).ReactionTime <= 0.0)
			Actor(MO).ReactionTime = 0.0;
		if (Actor(MO).bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
		if (EntityEx(MO).FindInventory(PowerSpeed) &&
			!(Level.XLevel.TicTime & 1) && Length(MO.Velocity) > 12.0 * 35.0)
		{
			SpawnSpeedEffect();
		}
	}

	CalcHeight(deltaTime);
	
/*	if (!MO.Sector)
		Error("Player::PlayerTick : MO.Sector pointer is NULL");*/

	if (MO.Sector->special || MO.Sector->Damage)
	{
		PlayerInSpecialSector(deltaTime);
	}
	PlayerOnSpecialFlat(Actor(MO).GetFloorType());
	PlayerInContents(deltaTime);

	if (MO.Velocity.z <= -35.0 * 35.0 && MO.Velocity.z >= -40.0 * 35.0 &&
		!MorphTime && MO.WaterLevel == 0 &&
		!GetSoundPlayingInfo(MO, GetSoundID('*falling')))
	{
		MO.PlaySound('*falling', CHAN_VOICE);
	}

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			EntityEx(MO).UseLines(USERANGE, USETHINGRANGE, '*usefail');
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	if (!ReadyWeapon && PendingWeapon)
	{
		SetWeapon(PendingWeapon);
		BringUpWeapon();
	}

	// Morph counter
	if (MorphTime)
	{
		if (ChickenPeck)
		{
			// Chicken attack counter
			ChickenPeck -= 3;
		}
		MorphTime -= deltaTime;
		if (MorphTime <= 0.0)
		{
			// Attempt to undo the chicken/pig
			MorphTime = 0.0;
			UndoPlayerMorph(false, self);
		}
	}

	//	Cycle psprites
	if (!bSpectator)
	{
		MovePsprites(deltaTime);
	}

	//	Counters
	// [CW]
	if (PoisonCount && Actor(MO).bDormant)
		PoisonCount = 0;

	if (PoisonCount && Level.XLevel.Time - LastPoisonTime >= 0.5)
	{
		PoisonCount -= 5;
		if (PoisonCount < 0)
		{
			PoisonCount = 0;
		}
		LastPoisonTime = Level.XLevel.Time;
/*		if (PoisonerPlayer && !PoisonerPlayer.MO)
			Error("Player::PlayerTick : PoisonerPlayer.MO reference not set to an instance of an object");*/
		Actor(MO).PoisonDamage(Poisoner, Poisoner, 1, true);
	}

	// [FB] Darkness blinding effect
	if (BlindCount && Level.XLevel.Time - LastBlindTime >= 0.5)
	{
		BlindCount -= 5;
		count += 1;
		if (BlindCount < 0)
		{
			BlindCount = 0;
		}
		LastBlindTime = Level.XLevel.Time;
		if(count % 5 == 0) // [FB] Damage the blinded player after some time
		{
/*			if (Blinder && !Blinder.MO)
				Error("Player::PlayerTick : Blinder.MO reference not set to an instance of an object");*/
			KArenaPlayerPawn(MO).BlindDamage(Blinder, 1, true);
		}
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	if (HazardTime)
	{
		HazardTime -= deltaTime;
		if (HazardTime <= 0.0)
			HazardTime = 0.0;
		if (Level.XLevel.Time - LastHazardTime >= 32.0 / 35.0 &&
			HazardTime > 16.0)
		{
			LastHazardTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
	}

	if(!bSpectator && !bUnControllable)
	{
		// [CW] No need for this processing when player has lost all experience
		if (experience > 0)
		{
			pcount++;
			LooseExperience();
		}
	}
	else
	{
		pcount = 0;
	}

	if (bNoHUD)
		ViewStates[ps_weapon].SY -= 1000.0;
}

//==========================================================================
//
//  PaletteFlash
//
//  Sets the new palette colour shift based upon the current values of
// Player.DamageFlash and Player.BonusFlash, contents and other inventory
// items.
//
//==========================================================================

void PaletteFlash()
{
	::PaletteFlash();

	// [FB] Black palette for blinding effect
	if (BlindCount)
	{
		float r = 0.0;
		float g = 0.0;
		float b = 0.0;
		float a = 0.0;
		AddBlend(r, g, b, a, CShift);
		AddBlend(r, g, b, a, RGBA(0, 0, 0, BlindCount));
		CShift = RGBA(ftoi(r * 255.0), ftoi(g * 255.0),
			ftoi(b * 255.0), ftoi(a * 255.0));
	}
}

//==========================================================================
//
//  Blast
//
//==========================================================================

final void Blast(Actor victim, Actor source, float strength)
{
	float angle;
	float ang;
	Actor A;
	float x;
	float y;
	float z;

	angle = atan2(victim.Origin.y - source.Origin.y,
		victim.Origin.x - source.Origin.x);
	if (strength < Actor::BLAST_FULLSTRENGTH)
	{
		victim.Velocity.x = strength * cos(angle) * 35.0;
		victim.Velocity.y = strength * sin(angle) * 35.0;
		if (victim.bIsPlayer)
		{
			// Players handled automatically
		}
		else
		{
			victim.bSlide = true;
			victim.bBlasted = true;
		}
	}
	else	// full strength blast from artifact
	{
		if (victim.bMissile)
		{
			if (SorcBall(victim))
			{
				// don't blast sorcerer balls
				return;
			}
			else if (MageStaffFX2(victim) || InfernalHandFX(victim))
			{
				// Reflect to originator
				victim.Tracer = victim.Target;
				victim.Target = source;
			}
		}
		if (victim.Class == HolyFX)
		{
			if (victim.Tracer == source)
			{
				victim.Tracer = victim.Target;
				victim.Target = source;
			}
		}
		victim.Velocity.x = Actor::BLAST_SPEED * cos(angle) * 35.0;
		victim.Velocity.y = Actor::BLAST_SPEED * sin(angle) * 35.0;

		// Spawn blast puff
		ang = atan2(source.Origin.y - victim.Origin.y,
			source.Origin.x - victim.Origin.x);
		x = victim.Origin.x + (victim.Radius + 1.0) * cos(ang);
		y = victim.Origin.y + (victim.Radius + 1.0) * sin(ang);
		z = victim.Origin.z - victim.FloorClip + victim.Height / 2.0;
		A = Level.Spawn(BlastEffect, vector(x, y, z));
		if (A)
		{
			A.Velocity.x = victim.Velocity.x;
			A.Velocity.y = victim.Velocity.y;
		}

		if (victim.bMissile)
		{
			victim.Velocity.z = 8.0 * 35.0;
			A.Velocity.z = victim.Velocity.z;
		}
		else
		{
			victim.Velocity.z = 1000.0 / victim.Mass * 35.0;
		}
		if (victim.bIsPlayer)
		{
			// Players handled automatically
		}
		else
		{
			victim.bSlide = true;
			victim.bBlasted = true;
		}
	}
}

//==========================================================================
//
//  BlastRadius
//
//==========================================================================

final void BlastRadius(optional int Limit)
{
	Actor mo;
	float dist;
	int blasts = 0;
#ifdef ALTERNATE_SEARCH
	EntityEx ee;
#endif

	MO.PlaySound('BlastRadius', CHAN_ITEM);
	KArenaLevelInfo(Level).NoiseAlert(MO, MO);

#ifdef ALTERNATE_SEARCH
	#ifdef ALTERNATE_SEARCH2
	foreach MO.RoughBlockSearch(ee, 10)
	#else
	foreach MO.RadiusThings(ee, MO.Origin, Actor::BLAST_RADIUS_DIST)
	#endif
#else
	foreach Level.AllThinkers(Actor, mo)
#endif
	{
#ifdef ALTERNATE_SEARCH
		if (!ClassIsChildOf(ee.Class, Actor))
		{
			continue;
		}

		mo = Actor(ee);
#endif

		if ((mo == MO) || mo.bBoss)
		{
			// Not a valid monster
			continue;
		}
		if (mo.bCanBlast || mo.bIceCorpse)	// frozen corpse
		{
			// Let these special cases go
		}
		else if (mo.bMonster && (mo.Health <= 0))
		{
			continue;
		}
		else if (!mo.bMonster && !mo.bIsPlayer && !mo.bMissile)
		{
			// Must be monster, player, or missile
			continue;
		}
		if (mo.bDormant)
		{
			continue;	// no dormant creatures
		}
		if (mo.bDontBlast)
		{
			continue;
		}
		dist = MO.DistTo(mo);
		if (dist > Actor::BLAST_RADIUS_DIST)
		{
			// Out of range
			continue;
		}
		Blast(mo, Actor(MO), Actor::BLAST_FULLSTRENGTH);

		blasts++;
		if (specified_Limit && blasts >= Limit)
			break;
	}
}

//==========================================================================
//
//  UseArtiBlastRadius
//
//  Blast all mobj things away
//
//==========================================================================

void UseArtiBlastRadius()
{
	switch(altWeaponMode[HexenDefs::ALTFIRE_BLASTRADIUS])
	{
	case HexenDefs::ALTFIRE_BLASTRADIUS_INSTANT:
		BlastRadius();
		break;
	case HexenDefs::ALTFIRE_BLASTRADIUS_BOMB:
		KArenaPlayerPawn(MO).Spawn_2(none, BlastRadiusBomb, MO.Origin +
			vector(16.0 * cos(MO.Angles.yaw), 24.0 * sin(MO.Angles.yaw), 8.0 - MO.FloorClip));
		break;
	case HexenDefs::ALTFIRE_BLASTRADIUS_MISSILE:
		KArenaPlayerPawn(MO).SpawnPlayerMissile_2(none, BlastRadiusMissile);
		break;
	}
}

//==========================================================================
//
//  UseArtiPoisonBag
//
//==========================================================================

void UseArtiPoisonBag()
{
	float angle;
	Actor bomb;

	angle = MO.Angles.yaw;

	switch(altWeaponMode[HexenDefs::ALTFIRE_POISONBAG])
	{
	case HexenDefs::ALTFIRE_POISONBAG_GRENADE:
		bomb = KArenaPlayerPawn(MO).Spawn_2(none, ThrowingBomb, MO.Origin +
			vector(0.0, 0.0, 35.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Angles.yaw = AngleMod360(MO.Angles.yaw +
				(Random() * 2.0 - 1.0) * 45.0 / 8.0);
			bomb.Velocity.z =
				(4.0 + 173.0 * -tan(MO.Angles.pitch) / 16.0) * 35.0;
			bomb.Origin.z += 173.0 * -tan(MO.Angles.pitch) / 16.0;
			bomb.Velocity.x += bomb.Speed * cos(bomb.Angles.yaw);
			bomb.Velocity.y += bomb.Speed * sin(bomb.Angles.yaw);
			bomb.Velocity.x += MO.Velocity.x / 2.0;
			bomb.Velocity.y += MO.Velocity.y / 2.0;
			bomb.Obituary = "$ob_playergrenade";
			bomb.StateTime -= Random() * 0.1;
			bomb.CheckMissileSpawn();
		}
		break;
		
	case HexenDefs::ALTFIRE_POISONBAG_BOMB:
		bomb = KArenaPlayerPawn(MO).Spawn_2(none, FireBomb, MO.Origin +
			vector(16.0 * cos(angle), 24.0 * sin(angle), 8.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Obituary = "$ob_playertimebomb";
		}
		break;

	case HexenDefs::ALTFIRE_POISONBAG_CLOUD:
		bomb = KArenaPlayerPawn(MO).Spawn_2(none, PoisonBag, MO.Origin +
			vector(16.0 * cos(angle), 24.0 * sin(angle), 8.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Obituary = "$ob_poisonbreathcloud";
		}
		break;
	}
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine
//
//==========================================================================

void MovePsprites(float deltaTime)
{
//[Korax]
	if (KArenaWeapon(ReadyWeapon).dex_mod)
	{
		if (dexterity < 6)
			deltaTime *= 0.8;
		else if (dexterity > 34)
			deltaTime *= 3.5;
		else if (dexterity > 24)
			deltaTime *= 2.5;
		else if (dexterity > 14)
			deltaTime *= 1.5;
	}
	::MovePsprites(deltaTime);
}

//==========================================================================
//
//  ActivateHeresiarchWeapon
//
//==========================================================================

void ActivateHeresiarchWeapon()
{
	AddDefaultInventory();
	ViewStates[ps_weapon].SY = Weapon::WEAPONTOP;
	SetWeapon(KArenaWeapon(EntityEx(MO).FindInventory(WeaponHeresiarchPRay)));
	SetViewState(ps_weapon, ReadyWeapon.GetReadyState());
}

//==========================================================================
//
//  ParticleEffectEx
//
//==========================================================================

void ParticleEffectEx(int count, int type1, int type2, TVec origin, TVec ornd, /*float zcamera,*/ float zcameradome,
	float zoffset, TVec velocity, TVec vrnd, TVec acceleration, float grav, int colour, float duration,
	float ramp, int Pattern)
{
	KArenaLevelInfo(Level).ParticleEffectEx(count, type1, type2, origin, ornd, /*zcamera,*/Camera.Origin,
				zcameradome, zoffset, velocity, vrnd, acceleration, grav, colour, duration, ramp, Pattern);
}

//==========================================================================
//
//  SetFOV
//
//==========================================================================

void SetFOV(int FOV)
{
	SetCvar('FOV', FOV);
}

//==========================================================================
//
//  CallLocalSound
//
//	[CW]
//
//==========================================================================

void CallLocalSound(int sound, bool put_in_queue, float delay)
{
	ClientGameBaseEx(ClGame).PlayAnouncerSound(sound, put_in_queue, delay);
}

//==========================================================================
//
//  CallLocalSoundNamed
//
//	[CW] TODO: use only this for all local sounds?
//
//==========================================================================

void CallLocalSoundNamed(name sound, bool put_in_queue, float delay)
{
	ClientGameBaseEx(ClGame).PlayAnouncerSoundNamed(sound, put_in_queue, delay);
}

//==========================================================================
//
//  UpdateScore
//
//  [CW] Updates current player's score.
//	 Scoring event list is defined in 'HexenDefs.vc'.
//
//==========================================================================

void UpdateScore(int event)
{
	int i;
	int mod = 0;
	bool force_update = false;

	switch(event)
	{
		case HexenDefs::scoreevent_PlayerDiscard:
			i = Team.GetPlayerCount();
			if (i > 1)
				mod = Team.Players[--i].Score - Score - 1;
			else
				mod = -Score;
			force_update = true;
			break;
		
		case HexenDefs::scoreevent_PlayerReset:
			mod = -Score;
			force_update = true;
			break;	// this should always set Score to 0
		case HexenDefs::scoreevent_Frag:
		case HexenDefs::scoreevent_Kill:
		case HexenDefs::scoreevent_KthHeresiarchDeath:
			if (MainGameInfo(Level.Game).GameType != HexenDefs::game_xmas)
				mod = 1;
			break;
		case HexenDefs::scoreevent_SelfFrag:
		case HexenDefs::scoreevent_TeamFrag:
			mod = -1;
			break;
		case HexenDefs::scoreevent_KthHeresiarchFrag:
			// [CW] This is an interesting case: whoever this called Player is,
			//		frag should go to Heresiarch and no one else!
			if (MainGameInfo(Level.Game).GameModel && MainGameInfo(Level.Game).GameModel.IsA('GameKTH'))
			{
				Player plH = GameKTH(MainGameInfo(Level.Game).GameModel).plHeresiarch;
				if (self == plH)
					mod = 1;
				else
					plH.UpdateScore(HexenDefs::scoreevent_KthHeresiarchFrag);
			}
			break;
		case HexenDefs::scoreevent_Survived:
			// Survived the monster wave in Survival game mode
			mod = 5 * GameSurvival(MainGameInfo(Level.Game).GameModel).Wave;
			break;
		case HexenDefs::scoreevent_CtfCapture:
			mod = 5;
			break;
		case HexenDefs::scoreevent_CtfSaveFlag:
			mod = 2;
			break;
		case HexenDefs::scoreevent_DomSigilCapture:
			mod = 1;
			break;
		case HexenDefs::scoreevent_FtbGoal:
			mod = 5;
			break;
		case HexenDefs::scoreevent_FtbSelfGoal:
			mod = -5;
			break;
		case HexenDefs::scoreevent_XmasTree:
			mod = 1;
			break;
	}

	Score += mod;

	if (Level && Level.Game && (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM) && Team)
	{
		Team.UpdateScore(event);
		// update team stats for clientstate	
		TeamFrags = Team.Frags;
		TeamKills = Team.Kills;
	}

	if (mod || force_update)
	{
		UpdateCompetition();
		// No player-referred announces in team games
		if (Level && Level.Game && (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM) &&
				(MainGameInfo(Level.Game).GetPlayerCount() > 1))
			CompetitionAnnouncements(Score - mod);

		if (Level && Level.Game)	// in case score is somehow updated between levels
		{
			if (mod > 0)
				MainGameInfo(Level.Game).bCompetitionOpened = true;
			MainGameInfo(Level.Game).ScoreboardsUpdate(none);
		}
	}

	// [CW] Check score limit
	if (Level && Level.Game && (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM))
		MainGameInfo(Level.Game).UpdateHighscore(Score);
}

//==========================================================================
//
//  CompetitionAnnouncements
//
//  [CW]
//
//==========================================================================

void CompetitionAnnouncements(int oldScore)
{
	int i;
			
	if (Score > oldScore)
	{
		// Is this player a new and only leader?
		if (PlayerPosition == 0)
		{
			if (Team.Players[1] && Team.Players[1].Score >= oldScore)
			{
				// [FB] Announce new leader!
				CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
				Level.bprint("%s leads!", PlayerName);
				
				// Anyone lost the Lead?
				int oldLeaderScore = Team.Players[1].Score;
				for (i = 1; i < MAXPLAYERS; i++)
				{
					if (!Team.Players[i])
						break;
					if (Team.Players[i].Score == oldLeaderScore)
					{
					   Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_lost_the_lead, true, HexenDefs::STANDART_LSQ_DELAY);
					   //Team.Players[i].centreprint("You lost the lead!");
					}
					else
					   break;
				}
			}
			else if (Score > 0 && !MainGameInfo(Level.Game).bCompetitionOpened)
			{
				// [FB] Announce new leader!
				CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
				Level.bprint("%s leads!", PlayerName);
			}
		}
		// Is he tied for the lead then?
		else
		{
			if (Score == Team.Players[0].Score)
			{
				// [FB] Tied for the lead!
				CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
				//centreprint("You are tied for the lead!");
				
				// If there was a single leader, notify him that he is tied for the lead now
				// (elsewise there's already a tied group)
				if (PlayerPosition == 1)
				{
					Team.Players[0].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
					//Team.Players[0].centreprint("You are tied for the lead!");
				}
			}
		}
	}
	// Score may drop down in case of self-frag or similar events
	else if (Score < oldScore)
	{
		// Is he tied for the lead?
		if (PlayerPosition == 0)
		{
			if (Team.Players[1] && Team.Players[1].Score == Score)
			{
				CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
				//centreprint("You are tied for the lead!");
				
				// Who else is tied?
				for (i = 1; i < MAXPLAYERS; i++)
				{
					if (!Team.Players[i])
						break;
					if (Team.Players[i].Score == Score)
					{
					   Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
					   //Team.Players[i].centreprint("You are tied for the lead!");
					}
					else
					   break;
				}
			}
		}
		// Was he a single leader or tied?
		else if (Team.Players[0].Score <= oldScore)
		{
			// [FB] You've lost the lead!
			CallLocalSound(HexenDefs::lsid_gen_an_y_lost_the_lead, true, HexenDefs::STANDART_LSQ_DELAY);
			//centreprint("You lost the lead!");
			
			// If he was a single leader, some others have the lead now
			if (Team.Players[0].Score < oldScore)
			{
				int newLeaderScore = Team.Players[0].Score;
				if (PlayerPosition != 1 && Team.Players[1].Score == newLeaderScore)
				{
					for (i = 0; i < PlayerPosition; i++)
					{
						if (Team.Players[i].Score < newLeaderScore)
							break;
						Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
						//Team.Players[i].centreprint("You are tied for the lead!");
					}
				}
				else
				{
					// [FB] Announce new leader!
					Team.Players[0].CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
					//Team.Players[0].centreprint("You lead!");
					Level.bprint("%s leads!", Team.Players[0].PlayerName);
				}
			}
		}
	}
}

//==========================================================================
//
//  UpdateCompetition
//
//  [CW] Updates player's position in inner team table.
//
//==========================================================================

void UpdateCompetition()
{
	bool bPositionShifted;
	int i, j;
	Player pl2;

	if (!Team)
	{
		FatalError("Player::UpdateCompetition : Team reference is not set to real object");
		return;
	}

	for (i = 0; i < MAXPLAYERS; i++)
	{
		if(Team.Players[i] == self)
		   break;
	}
	
	PlayerPosition = i;

	// first see if previous players has lower score now
	if (i != 0)
	{
		j = i-1;
		while (j >= 0)
		{
			pl2 = Team.Players[j];
			if (!pl2)
				Error("Player::UpdateCompetition : previous player slot in team has no reference to object");
			
			if (pl2.Score < Score)
			{
				bPositionShifted = true;
				Team.Players[j] = self;
				Team.Players[i] = pl2;
				PlayerPosition = j;
				pl2.PlayerPosition = i;
				i = j;
			}
			else
				break;
			j--;
		};
	}
	// secondly (in other case) see if next players has higher score (in case score gone down)
	if (!bPositionShifted && i != MAXPLAYERS - 1)
	{
		j = i+1;
		while (Team.Players[j] && j < MAXPLAYERS)
		{
			pl2 = Team.Players[j];

			if (pl2.Score > Score)
			{
				Team.Players[j] = self;
				Team.Players[i] = pl2;
				PlayerPosition = j;
				pl2.PlayerPosition = i;
				i = j;
			}
			else
				break;
			j++;
		};
	}
}

//==========================================================================
//
//  DisplayObituary
//
//  [CW] Displays message notificating that player was killed.
//
//
//	1) if damage type is 'Falling' or 'Telefrag', corresponding Obituaries
//	are used with 1 parameter: died player's name.
//
//	2) if both source & inflictor are null, it was pure enviroment:
//	slime, lava and such. Use generic messages like "was in wrong place" etc,
//	with 1 parameter: this player's name.
//
//	3) if inflictor has some value and source is null, it should be some trap
//	or similar thing; then use inflictor's Obituary with 1 parameter
//	as in pp.1,2.
//
//	4) if both inflictor and source has some values, but they are not
//	equal, that means that either a pawn or player have shot current
//	player with a missile (or killed by other spawned object);
//	use inflictor's Obituary with 2 parameters:	current player name and
//	either pawn name or killer's PlayerName.
//
//	5) if inflictor and source have equal values (not null), then it might
//	be melee attack OR instant ranged attack (like gunshot) by pawn or player;
//	use either pawn's Obituary or active player Weapon's Obituary, with 2
//	parameters as in p.4.
//
//	6) if there's source but no inflictor, it could be delayed kill like
//	poisoning or darkness power; use corresponding Obituary with 2 parameters
//	as in p.4.
//
//	7) in any case, if there is a source and source is equal to died player,
//	that means that player have killed himself either by his weapon, or using
//	'suicide' command; use corresponding Obituary with 1 parameter as in p.1.
//
//==========================================================================

void DisplayObituary(EntityEx inflictor, EntityEx source, name DmgType)
{
	string		Msg;

	if (DmgType == 'Falling')
	{
		//[CW] falling damage
		Msg = va("$ob_falling%d", (P_Random() % 3) + 1);
	}
	else if (DmgType == 'Telefrag')
	{
		// telefrag
		if (inflictor.bIsPlayer)
		{
			Msg = va("$ob_telefrag%d", (P_Random() % 3) + 1);
		}
		// telefrag
		else
		{
			Msg = va("$ob_monsttelefrag%d", (P_Random() % 3) + 1);
		}
	}
	else if (source)
	{
		//[CW] p.4: melee or instant ranged attack
		if (source == inflictor)
		{
			if (source.bIsPlayer)
			{
				KArenaWeapon kaw = KArenaWeapon(Player(inflictor.Player).ReadyWeapon);
				if (kaw.HitObituary)
					Msg = kaw.HitObituary;
				else
					Msg = kaw.Obituary;
			}
			else
			{
				if (source.HitObituary)
					Msg = source.HitObituary;
				else
					Msg = source.Obituary;
			}
		}
		//[CW] p.3: missile attack
		else if (inflictor)
		{
			if(source.bIsPlayer && source.Player == self)
			{
				Msg = va("$ob_selffrag%d", (P_Random() % 3) + 1);
			}
			else
			{
				Msg = Actor(inflictor).Obituary;
			}
		}
		else if (source.bIsPlayer)
		{
			// [CW] killed by slow poison
			if (DmgType == 'Poison')
			{
				if (Player(source.Player) == self)
					Msg = va("$ob_selfpoison");
				else
					Msg = va("$ob_poisoned");
			}
			// [CW] killed by darkness energy
			else if (DmgType == 'Blind')
			{
				if (Player(source.Player) == self)
					Msg = va("$ob_selfblind");//???
				else
					Msg = va("$ob_blinded");//???
			}
			//[CW] suicide
			else if (Player(source.Player) == self)
				Msg = va("$ob_suicide%d", (P_Random() % 3) + 1);
		}
	}
	//[CW] p.2: fragged by enviromental thing or trap
	else if (inflictor)
	{
		Msg = Actor(inflictor).Obituary;
	}

	if (!Msg)
	{
		//[CW] p.1: fragged by pure enviroment
		Msg = va("$ob_generic%d", (P_Random() % 3) + 1);
	}

	//	Look up string in language lump is necesary.
	if (StrStartsWith(Msg, "$"))
	{
		Msg = GetLangString(StrToName(substr(Msg, 1, strlen(Msg) - 1)));
	}

	//	Do replacements.
	Msg = StrReplace(Msg, "%o", PlayerName);
	if (source)
	{
		if (source.Player)
			Msg = StrReplace(Msg, "%k", source.Player.PlayerName);
		else if (source.bMonster)
			Msg = StrReplace(Msg, "%k", GetLangString('pw_monster'));
	}

	Level.bprint("%s", Msg);
}

//==========================================================================
//
//  ConfirmTrophy
//
//	[CW]
//
//==========================================================================

void ConfirmTrophy(int type)
{
	int a, announcement;
	int value;

	if (type >= HexenDefs::NUMTROPHIES)
		return;

	switch (type)
	{
	case HexenDefs::trophycl_killsatonce:
		a = -1;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_HUNT) a = 0;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_MASSACRE) a = 1;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_DEVASTATION) a = 2;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_ELIMINATION) a = 3;
		if (a < 0)
		{
			//Trophies.kills_at_once = 0;
			//Trophies.kao_timer = 0.0;
			return;
		}
		announcement = HexenDefs::trophy_hunt + a;
		value = Trophies.kills_at_once;
		break;
	case HexenDefs::trophycl_fragsatonce:
		if (Trophies.frags_at_once < 2)
		{
			//Trophies.frags_at_once = 0;
			//Trophies.fao_timer = 0.0;
			return;
		}
		announcement = HexenDefs::trophy_doublekill + Trophies.frags_at_once - 2;
		if (announcement > HexenDefs::trophy_LAST_fao)
			announcement = HexenDefs::trophy_LAST_fao;
		value = Trophies.frags_at_once;
		break;
	case HexenDefs::trophycl_fragsrow:
		/* if (Trophies.frags_row < HexenDefs::TROPHY_FRAGSROW_LIMIT)
		{
			Trophies.frags_row = 0;
			return;
		}*/
		if (Trophies.frags_row % HexenDefs::TROPHY_FRAGSROW_LIMIT != 0)
			return;
		announcement = HexenDefs::trophy_frags_row;
		value = Trophies.frags_row;
		break;
	case HexenDefs::trophycl_killsrow:
		/* if (Trophies.kills_row < HexenDefs::TROPHY_KILLSROW_LIMIT)
		{
			Trophies.kills_row = 0;
			return;
		}*/
		if (Trophies.kills_row % HexenDefs::TROPHY_KILLSROW_LIMIT != 0)
			return;
		announcement = HexenDefs::trophy_kills_row;
		value = Trophies.kills_row;
		break;
	case HexenDefs::trophycl_accuracy:
		announcement = HexenDefs::trophy_accuracy; break;
	default:
		return;
	}
	centreprint(va(MainGameInfo(Level.Game).TrophyText[announcement], value));
	CallLocalSound(HexenDefs::lsid_PLAYERTROPHY + announcement,	true, HexenDefs::STANDART_LSQ_DELAY);
}

//==========================================================================
//
//  UpdateQueuedTrophy
//
//	[CW] Queued trophies are trophies that depend on number
//		 of specific events occured in a sequence (there's a
//		 split for timed and non-timed).
//
//==========================================================================

void UpdateQueuedTrophy(int type, int value)
{
	if (type >= HexenDefs::NUMTROPHIES)
		return;

	switch (type)
	{
	case HexenDefs::trophycl_killsatonce:
		Trophies.kills_at_once += value;
		Trophies.kao_timer = HexenDefs::TROPHY_KAO_TIME;
		ConfirmTrophy(HexenDefs::trophycl_killsatonce);
		break;
	case HexenDefs::trophycl_fragsatonce:
		Trophies.frags_at_once += value;
		Trophies.fao_timer = HexenDefs::TROPHY_FAO_TIME;
		ConfirmTrophy(HexenDefs::trophycl_fragsatonce);
		break;
	case HexenDefs::trophycl_fragsrow:
		Trophies.frags_row += value;
		ConfirmTrophy(HexenDefs::trophycl_fragsrow);
		break;
	case HexenDefs::trophycl_killsrow:
		Trophies.kills_row += value;
		ConfirmTrophy(HexenDefs::trophycl_killsrow);
		break;
	}
}

//==========================================================================
//
//  UpdateTimedTrophies
//
//	[CW]
//
//==========================================================================

void UpdateTimedTrophies(float deltaTime)
{
	if (Trophies.kao_timer > 0.0)
	{
		Trophies.kao_timer -= deltaTime;
		if (Trophies.kao_timer <= 0.0)
		{
			Trophies.kills_at_once = 0;
			//ConfirmTrophy(HexenDefs::trophycl_killsatonce);
		}
	}
	if (Trophies.fao_timer > 0.0)
	{
		Trophies.fao_timer -= deltaTime;
		if (Trophies.fao_timer <= 0.0)
		{
			Trophies.frags_at_once = 0;
			//ConfirmTrophy(HexenDefs::trophycl_fragsatonce);
		}
	}
}

//==========================================================================
//
//  ClientDynamicLightEx
//
//	[CW] Full data dlight parsing
//
//==========================================================================

void ClientDynamicLightEx(int colour, TVec org, int radius, int lifetime, int decay)
{
	dlight_t *dl;

	dl = Level.AllocDlight(none);

	dl->colour = colour;
	dl->origin = org;
	dl->radius = itof(radius);
	dl->die = Level.XLevel.Time + itof(lifetime) * 0.01;
	dl->decay = itof(decay) * 0.01;
}

//==========================================================================
//
//  CheckFriendlyFire
//
//==========================================================================

bool CheckFriendlyFire(EntityEx source, int damage)
{
	if (!GetCvar('FriendlyFire'))
	{
		// [FB] Don't do damage to teammates in team game modes
		// [CW] ... and allies as well...
		if (source != MO && source && 
			(source.bIsPlayer ||
			!source.bMonster && source.Target && source.Target.bIsPlayer) &&
			damage < 1000)
		{
			if (MainGameInfo(Level.Game).GameModel)
			{
				int teamfactor = KArenaGameModel(MainGameInfo(Level.Game).GameModel).TeamFactor(EntityEx(MO),
							source.bIsPlayer ? source : source.Target);
				if (teamfactor == KArenaGameModel::teamfactor_coop)
					return true;
			}

			if (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM ||
				MainGameInfo(Level.Game).bIsCoopGame[MainGameInfo(Level.Game).GameType])
			{
				PlayerTeam col = (source.bIsPlayer ? Player(source.Player).Team : Player(source.Target.Player).Team);
				if (col == Team && damage < 1000)
					return true;
			}
		}
	}

	return false;
}

//==========================================================================
//
//	LevelChanged
//
//==========================================================================

final void LevelChanged()
{
	//	Update armor save percentages.
	BasicArmor Armor = BasicArmor(EntityEx(MO).FindInventory(BasicArmor));
	if (Armor && Armor.Amount)
	{
		Armor.SavePercent = 0.333 + itof(constitution) * 0.4;
		if (Armor.SavePercent > 0.75)
		{
			Armor.SavePercent = 0.75;
		}
	}

	//	Update player speed.
	float Value;
	// [FB] We make every class walk at the same speed now
	Value = 0.9 + itof(dexterity) / 100.0;
	KArenaPlayerPawn(MO).ForwardMove1 = Value;
	KArenaPlayerPawn(MO).ForwardMove2 = Value;
	KArenaPlayerPawn(MO).SideMove1 = Value;
	KArenaPlayerPawn(MO).SideMove2 = Value;
}

//==========================================================================
//
// Damaged
//
//==========================================================================

void Damaged(EntityEx inflictor)
{
	if(bIsBot)
	{
		Bot.React(inflictor);
	}

	// [CW] Heresiarch takes less damage flash
	if (PClass == HexenDefs::PCLASS_HERESIARCH)
	{
		DamageFlash /= 2.0;
	}

	if (MainGameInfo(Level.Game).GameType == HexenDefs::game_football)
	{
		use_power--;
		if (use_power < 0)
			use_power = 0;
	}
}

//==========================================================================
//
// FireWeapon
//
// [FB] We need to override this method to make bots update their reaction timer
//
//==========================================================================

void FireWeapon()
{
	int i;

	// [FB] Not sure if this is the right way to do it, anyway....
	for (i = 0; i < MAXPLAYERS; i++)
	{
		// [FB] Set t_anticip for our bot enemies
		if (Level.Game.Players[i] && Level.Game.Players[i].MO &&
			Level.Game.Players[i].bIsBot &&
			Player(Level.Game.Players[i]).Bot.enemy && 
			Player(Level.Game.Players[i]).Bot.enemy == MO)
			Player(Level.Game.Players[i]).Bot.t_anticip = 
					KArenaWeapon(ReadyWeapon).refire_tics;
	}

	::FireWeapon();
}

//==========================================================================
//
//	BestWeaponOfDamageType
//
//	Returns best weapon to use of selected DamageType
//
//	[CW] a slightly modified version of PlayerEx::BestWeapon,
//		added DamageType check and disabled Powered check
//
//==========================================================================

Weapon BestWeaponOfDamageType(name DamageType, optional class<Ammo> AmmoType)
{
	//bool Powered = !!EntityEx(MO).FindInventory(PowerWeaponLevel2);
	Weapon Best = none;
	Inventory Item;
	for (Item = EntityEx(MO).Inventory; Item; Item = Item.Inventory)
	{
		//	Must be a KArena weapon
		KArenaWeapon Wpn = KArenaWeapon(Item);
		if (!Wpn)
		{
			continue;
		}

		if (Wpn.DamageTypeHint != DamageType)
			continue;

		//	Check if best one is better that this one.
		if (Best && Wpn.SelectionOrder > Best.SelectionOrder)
		{
			continue;
		}
		//	Possibly limit to specific ammo type.
		if (AmmoType && Wpn.AmmoType1 != AmmoType)
		{
			continue;
		}
		/* // Check if it's for the current tome of power state.
		if (Powered && Wpn.SisterWeapon && Wpn.SisterWeapon.bPoweredUp)
		{
			continue;
		}*/
		/* if (!Powered && Wpn.bPoweredUp)
		{
			continue;
		}*/
		//	Make sure it has enough ammo.
		if (!Wpn.CheckAmmo(Weapon::FIRE_Either, false))
		{
			continue;
		}
		//	Good one.
		Best = Wpn;
	}
	return Best;
}

//==========================================================================
//
// IsOutOfAmmo
//
// [CW] Returns TRUE if cannot attack with any weapon that requires ammo
//
//==========================================================================

bool IsOutOfAmmo(optional class<KArenaWeapon> WeaponClass, optional class<Ammo> AmmoType,
				 optional bool bAlternativeWeapons)
{
	Inventory Item;
	for (Item = EntityEx(MO).Inventory; Item; Item = Item.Inventory)
	{
		if (!WeaponClass && (
			((bAlternativeWeapons || AmmoType == ArtifactFlechette) &&
			Item.Class == ArtifactFlechette) ||
			((bAlternativeWeapons || AmmoType == ArtifactBlastRadius) &&
			Item.Class == ArtifactBlastRadius)
			))
		{
			if (Item.Amount > 0)
				return false;
		}
		else
		{
			//	Must be a weapon
			KArenaWeapon Wpn = KArenaWeapon(Item);
			if (!Wpn)
			{
				continue;
			}

			if (WeaponClass)
			{
				if (Wpn.Class != WeaponClass)
					continue;
			}

			if (AmmoType)
			{
				if (Wpn.AmmoType1 != AmmoType && Wpn.AmmoType2 != AmmoType && Wpn.AmmoType3 != AmmoType)
					continue;
			}

			if (Wpn.CheckAmmo(Weapon::FIRE_Either, false, true))
				return false;
		}
	}
	return true;
}

//==========================================================================
//
// IsAmmoBelowLevel
//
//==========================================================================

bool IsAmmoBelowLevel(int AmountLimit, optional class<KArenaWeapon> WeaponClass, optional class<Ammo> AmmoType,
					  optional bool bAlternativeWeapons)
{
	Inventory Item;
	for (Item = EntityEx(MO).Inventory; Item; Item = Item.Inventory)
	{
		//	Must be an Ammo
		Ammo ammo = Ammo(Item);
		if (!ammo)
		{
			continue;
		}

		if (WeaponClass)
		{
			if (WeaponClass.default.AmmoType1 != ammo.Class &&
					WeaponClass.default.AmmoType2 != ammo.Class &&
					WeaponClass.default.AmmoType3 != ammo.Class)
				continue;
		}

		if (AmmoType)
		{
			if (ammo.Class != AmmoType)
				continue;
		}

		if (bAlternativeWeapons)
		{
			if (ammo.Class != ArtifactFlechette &&
					ammo.Class != ArtifactBlastRadius)
				continue;
		}

		if (ammo.Amount >= AmountLimit)
			return false;
	}
	return true;
}

//==========================================================================
//
// IsOKToPickupItem
//
//	[CW] move to bot ai?
//
//==========================================================================

bool IsOKToPickupItem(Inventory Item)
{
	class<Inventory> realClass;
	name parent_class;

	if (Item.Class == RandomPowerup)
	{
		realClass = RandomPowerup(Item).PowerupClass;
	}
	else
	{
		realClass = class<Inventory>(Item.Class);
	}

#ifdef DEBUG_BOT_AI_ITEMS
	print("IsOKToPickupItem : %n?", GetClassName(Item.Class));
	print("Item.MaxAmount = : %i", Item.MaxAmount);
#endif

	//
	// [CW] Mana, health and armor classes are all stored as items of
	//      their corresponding parent class, hence we must seek
	//		for parent class, not for exactly the class of item we are
	//		checking. Plus, some items should be tested differently.
	//

	if (Item.IsA('Health') || realClass == KArenaSuperHeal)
	{
		// [CW] Health is tested differently
		if (EntityEx(MO).CheckInventory('health') < max_health)
		{
			return true;
		}
		return false;
	}
	else if (Item.IsA('Armor') || realClass == KArenaArmor5)
	{
		// [CW] Armor is tested differently
		BasicArmor Armor = BasicArmor(EntityEx(MO).FindInventory(BasicArmor));
		if (Armor.Amount < /* ArmorPickup(Item). */class<ArmorPickup>(realClass).default.MaxSaveAmount)
			return true;
		return false;
	}
	else if (Item.IsA('ItemManaBlue'))
	{
		parent_class = 'Mana1';
	}
	else if (Item.IsA('ItemManaGreen'))
	{
		parent_class = 'Mana2';
	}
	else if (Item.IsA('ItemManaRed'))
	{
		parent_class = 'ItemManaRed';
	}
	else if (Item.IsA('ItemManaCombined') || realClass == ArtifactBoostMana)
	{
		// [CW] Combined mana and BoostMana are tested differently
		if (EntityEx(MO).CheckInventory('Mana1') < ItemManaBlue.default.MaxAmount ||
			EntityEx(MO).CheckInventory('Mana2') < ItemManaGreen.default.MaxAmount ||
			EntityEx(MO).CheckInventory('ItemManaRed') < ItemManaRed.default.MaxAmount)
		{
			return true;
		}
		return false;
	}
	else if (Item.IsA('WeaponPickup'))
	{
		class RealWeaponClass = KArenaPlayerPawn(MO).WeaponClasses[WeaponPickup(Item).Slot - 1];
		KArenaWeapon RealWeaponItem = KArenaWeapon(EntityEx(MO).FindInventory(class<Inventory>(RealWeaponClass)));

#ifdef DEBUG_BOT_AI_ITEMS
		print("WeaponPickup = %n", GetClassName(Item.Class));
		print("RealWeaponClass = %n", GetClassName(RealWeaponClass));
		print("RealWeaponItem = %i", RealWeaponItem);
#endif
		// [CW] Weapon is tested differently
		if (!RealWeaponItem)
			return true;	// no such weapon yet

#ifdef DEBUG_BOT_AI_ITEMS
		print("has weapon");
#endif

		if (RealWeaponItem.AmmoType1)
		{
			if (ClassIsChildOf(RealWeaponItem.AmmoType1, ItemManaBlue) &&
				EntityEx(MO).CheckInventory('Mana1') < ItemManaBlue.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType1, ItemManaGreen) &&
				EntityEx(MO).CheckInventory('Mana2') < ItemManaGreen.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType1, ItemManaRed) &&
				EntityEx(MO).CheckInventory('ItemManaRed') < ItemManaRed.default.MaxAmount)
			{
				return true;
			}
		}
		if (RealWeaponItem.AmmoType2)
		{
			if (ClassIsChildOf(RealWeaponItem.AmmoType2, ItemManaBlue) &&
				EntityEx(MO).CheckInventory('Mana1') < ItemManaBlue.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType2, ItemManaGreen) &&
				EntityEx(MO).CheckInventory('Mana2') < ItemManaGreen.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType2, ItemManaRed) &&
				EntityEx(MO).CheckInventory('ItemManaRed') < ItemManaRed.default.MaxAmount)
			{
				return true;
			}
		}
		if (RealWeaponItem.AmmoType3)
		{
			if (ClassIsChildOf(RealWeaponItem.AmmoType3, ItemManaBlue) &&
				EntityEx(MO).CheckInventory('Mana1') < ItemManaBlue.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType3, ItemManaGreen) &&
				EntityEx(MO).CheckInventory('Mana2') < ItemManaGreen.default.MaxAmount ||
				ClassIsChildOf(RealWeaponItem.AmmoType3, ItemManaRed) &&
				EntityEx(MO).CheckInventory('ItemManaRed') < ItemManaRed.default.MaxAmount)
			{
				return true;
			}
		}
		return false;
	}
	else if (!Item.MaxAmount)
	{
		// [CW] May happen because of faster tick rate
		if (Item.Class == InitialInvulnerability)
		{
			return false;
		}

		// [CW] must be some instant use artifact (?)
		return true;
	}
	else
	{
		parent_class = GetClassName(Item.Class);
	}

	//if (EntityEx(MO).CheckInventory(parent_class) < Item.MaxAmount)
	int amount;
	Inventory RealItem;
	class<Inventory> RealItemClass = class<Inventory>(FindClass(parent_class));
	if (RealItemClass)
	{
		RealItem = EntityEx(MO).FindInventory(RealItemClass);
		amount = (RealItem ? RealItem.Amount : 0);
	}
	
	if (amount < (RealItem ? RealItem.MaxAmount : Item.MaxAmount))
	{
		return true;
	}

	return false;
}

//==========================================================================
//
//	Killed
//
//==========================================================================

void Killed(EntityEx source, EntityEx inflictor)
{
	::Killed(source, inflictor);

	use_power = 0;
	use_power_rangefrac = 0;

	//[CW] Show scoreboard for died player
	Scoreboard(true);

	if (MorphTime || MO.Class == PlayerHeresiarch)
		bRecalculateLevelOnRespawn = true;

	//[CW] notify advanced game mode
	if (MainGameInfo(Level.Game).GameModel)
		MainGameInfo(Level.Game).GameModel.OnPlayerDeath(self, source);

	SetFOV(HexenDefs::DEFAULT_FOV);
}

//==========================================================================
//
//	PlayerMorphed
//
//==========================================================================

void PlayerMorphed(EntityEx OldMO)
{
	PClass = HexenDefs::PCLASS_PIG;
	// [CW] Recalc attributes based on the new player's body
	int bak_level = exp_level;
	InitiateLevel();
	ActivateLevel(bak_level, false);
}

defaultproperties
{
}
