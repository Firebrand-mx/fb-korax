//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**	        ##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : PlayerEx;

const float BLINKTHRESHOLD	= 4.0;

const float
	INVULNTIME				= 30.0,
	INVULNTIME2				= 5.0,
	INFRATIME				= 120.0,
	FLIGHTTIME 				= 60.0,
	SPEEDTIME 				= 45.0,
	MORPHTIME 				= 40.0,
	MAULATORTIME 			= 30.0,
	INVISTIME				= 40.0,
	DAMAGETIME				= 40.0;

//const int MAXMORPHHEALTH	= 30;		//[CW] this may be obsolete
//const float VIEWHEIGHT 		= 48.0;

//
// Player internal flags, for cheats and debug.
//
const int
	CF_NOCLIP				= 1,	// No clipping, walk through barriers.
	CF_GODMODE				= 2;	// No damage, no health loss.

// 16 pixels of bob
const float MAXBOB			= 16.0;

const float HEAL_RADIUS_DIST		= 255.0;

const float USERANGE 		= 64.0;

const float
	BLAST_RADIUS_DIST		= 255.0,
	BLAST_FULLSTRENGTH		= 0.0038909912,
	BLAST_SPEED				= 20.0;

struct inventory_t
{
	int type;
	int count;
};

struct trophies_t
{
	int		frags_at_once;
	int		kills_at_once;
	float	fao_timer;
	float	kao_timer;
	int		frags_row;
	int		kills_row;
	TVec	shot_position;	// for accuracy trophy
};

struct morph_t
{
	bool bMorphed;
	bool bTimer;
	bool bUntilDeath;
	float MorphTime;
};

enum
{
	targethint_noshoot,
	targethint_shoot
};

int SBFlags;

int ArmorPoints;
// [KORAX]
/*float ArmorPoints[HexenDefs::NUMARMOR];
int ArmorPerc[HexenDefs::NUMARMOR];*/

int Mana[HexenDefs::NUMMANA];

int Keys;

// Power ups. invinc and invis are tic counters.
float Powers[NUMPOWERS];

float InventoryTime;

int ArtifactFlash;
int ReadyArtifact;
int InvPtr;
int InvCurPos;
int InventorySlotNum;
inventory_t Inventory[HexenDefs::NUMARTIFACTS];
int Arti;	// artitype_t to use

float FlyHeight;

int ArtifactCount;

// Bit flags, for cheats and debug.
// See cheat_t, above.
int Cheats;

// Refired shots are less accurate.
int Refire;

//[Korax]
//int Pieces;	// Fourth Weapon pieces

// Is wp_nochange if not changing.
int PendingWeapon;
int ReadyWeapon;

bool WeaponOwned[HexenDefs::NUMWEAPONS];

int		altWeapon;
int		altWeaponMode[HexenDefs::NUM_ALTFIRE];

// [FB] Flash for the Darkness Missile
int BlindCount;
float LastBlindTime;
Actor Blinder;		// none for non-player mobjs
int count;

int PoisonCount;	// screen flash for poison damage
float LastPoisonTime;
Actor Poisoner;		// none for non-player mobjs

int NewTorch;	// used in the torch flicker effect.
int NewTorchDelta;

int WeaponCShift;
//[KORAX]float MorphTime;	// player is a pig if > 0
morph_t	Morph;

// For cleric's invulnerability effect
bool bAltShadow;
bool bShadow;
bool bDontDraw;

BotPlayer Bot;

Actor Rain1;	// active rain maker 1
Actor Rain2;	// active rain maker 2

bool inited;

int pcount; // [FB] counter
int dam_am; // [FB] Just for the hero's shield

bool onground;

// [Korax]
// [FB] Scores for the different gameplay modes
int Score;	//[CW] personal player's score value (depends on game mode)
int TeamFrags; // [FB] Frags from each team	// [CW] you mean THIS PLAYER's team
int KillsCount; // [FB] How many enemies the player killed	// [CW] only monsters
int TeamKills; // [FB] How many enemies has each team killed	// [CW] you mean THIS PLAYER's team
int TotalKills; // [FB] How many enemies there are in a map
int Deaths;
int TeamPosition; // [CW] team position in global competition list
int PlayerPosition; // [CW] player position in team inner competition list

// [FB] Player atributes for leveling system
int experience;
int exp_level;
int next_level;
int old_level; // [FB] If we are going backwards we need this
int strength;
int intelligence;
int wisdom;
int dexterity;
int constitution;
//
//  Other stats (for status bar)
//
int HealthBig;		// [CW] to subst original BasePlayer::Health which does not allow health > 255
int max_health;
int old_maxhealth;	// [FB] To decrease the health
int bak_health;		// [FB] To keep an older level health

int				teamscores[HexenDefs::MAXPLAYERTEAMS];

int use_power;				// [CW] is needed for some advanced game modes (football);
							//	stores time USE button was held pressed
int	targetnamehint_type;
string		targetnamehint;

int		TeamSelection;	//[CW] team selection process state
int		oldColour;		// used to skip respawn if Team Selection screen was cancelled
						// while REselecting team
PlayerTeam	Team;	//[CW]

bool targetnamehint_enabled;
int targetnamehint_counter;	//[CW]

// [CW] for trophy announcements
trophies_t	Trophies;

bool bShowScoreboard; // [CW] True if scoreboard should be visible

bool bUnControllable; //[CW] Ignore user/AI commands (move/attack etc)

float	respawn_counter; // [CW] Time player must wait after his death before he may respawn
bool	bRecalculateLevelOnRespawn; // [CW] Forces to recalc exp level on player respawn

replication
{
	reliable if (!bIsClient)
		SBFlags, ArmorPoints, InventoryTime, ArtifactFlash, ReadyArtifact,
		InvPtr, InvCurPos, InventorySlotNum, Mana, Inventory,
		Score, TeamFrags, KillsCount, TeamKills, TotalKills,
		Deaths, TeamPosition, PlayerPosition,
		experience, exp_level, next_level, old_level, strength, intelligence,
		wisdom, dexterity, constitution, HealthBig, max_health, old_maxhealth,
		bak_health, use_power, targetnamehint_type, altWeapon, teamscores,
		altWeaponMode, targetnamehint;

	unreliable if (!bIsClient)
		ClientGunShot, ClientBlood, ClientExplosion, ClientExplosion2,
		ClientIceChunks, ClientDynamicLightEx;

	reliable if (!bIsClient)
		SetFOV, ClientTeamSelect, ClientScoreboard, CallLocalSound;
}

//==========================================================================
//
//	PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	if (bIsBot)
	{
		Bot = BotPlayer(SpawnObject(BotPlayer));
		Bot.player = self;
		Bot.OnBeginPlay();
	}
	TeamSelection = HexenDefs::teamselection_none;
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	int j;
	int oldKeys;
//	int oldPieces;
	bool oldWeaponowned[HexenDefs::NUMWEAPONS];
	bool playerWasReborn;
	int bestWeapon;

	Attacker = none;
	Poisoner = none;
	Blinder = none;

	bUnControllable = false;

	dprint("Player::SpawnClient : ");
	dprint("Player::SpawnClient : netgame = %i", Level.Game.netgame);
	dprint("Player::SpawnClient : deathmatch = %i", Level.Game.deathmatch);
	dprint("Player::SpawnClient : GameType = %i", MainGameInfo(Level.Game).GameType);
	dprint("Player::SpawnClient : MO = %i", MO);

	if (Level.Game.netgame && !Level.Game.deathmatch)
	{
		// Cooperative net-play, retain keys and weapons
		oldKeys = Keys;
//		oldPieces = Pieces;
		for (j = 0; j < HexenDefs::NUMWEAPONS; j++)
		{
			oldWeaponowned[j] = WeaponOwned[j];
		}
		playerWasReborn = (PlayerState == PST_REBORN);
	}

	//  Spawn player
	if (Level.Game.deathmatch)
	{
		dprint("Player::SpawnClient : goto DeathMatchSpawnPlayer");
		DeathMatchSpawnPlayer();
	}
	else
	{
		dprint("Player::SpawnClient : goto SpawnPlayer (probably a bug for now!)");
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
	}

	if (Level.Game.netgame && !Level.Game.deathmatch && playerWasReborn)
	{
		// Restore keys and weapons when reborn in co-op
		Keys = oldKeys;
//		player.Pieces = oldPieces;
		for (bestWeapon = 0, j = 0; j < HexenDefs::NUMWEAPONS; j++)
		{
			if (oldWeaponowned[j])
			{
				bestWeapon = j;
				WeaponOwned[j] = true;
			}
		}
		Mana[HexenDefs::MANA_1] = 25;
		Mana[HexenDefs::MANA_2] = 25;
		Mana[HexenDefs::MANA_3] = 25;
		if (bestWeapon)
		{
			// Bring up the best weapon
			PendingWeapon = bestWeapon;
		}
	}

	dprint("Player::SpawnClient : before TeleportMove");
	// Destroy all things touching players
	Actor(MO).TeleportMove(MO.Origin);
	dprint("Player::SpawnClient : after TeleportMove");

	//
	// [CW] if the trophy data should be stored, do it here
	//
	Trophies.kills_at_once = 0;
	Trophies.kao_timer = 0.0;
	Trophies.frags_at_once = 0;
	Trophies.fao_timer = 0.0;
	Trophies.frags_row = 0;
	Trophies.kills_row = 0;
	

	/*// [CW] check trophies
	if (Trophies.fao_timer > 0.0)
		ConfirmTrophy(HexenDefs::trophycl_fragsatonce);
	if (Trophies.kao_timer > 0.0)
		ConfirmTrophy(HexenDefs::trophycl_killsatonce);
	if (Trophies.frags_row > 0)
		ConfirmTrophy(HexenDefs::trophycl_fragsrow);
	if (Trophies.kills_row > 0)
		ConfirmTrophy(HexenDefs::trophycl_killsrow);*/

	dprint("Player::SpawnClient : OUT");
}

//==========================================================================
//
//  NetGameReborn
//
//==========================================================================

void NetGameReborn()
{
	int i;
	bool oldWeaponowned[HexenDefs::NUMWEAPONS];
	int oldKeys;
//	int oldPieces;
	bool foundSpot;
	int bestWeapon;

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	// spawn at random spot if in death match
	if (Level.Game.deathmatch)
	{
		DeathMatchSpawnPlayer();
		return;
	}

	// Cooperative net-play, retain keys and weapons
	oldKeys = Keys;
//	oldPieces = Pieces;
	for (i = 0; i < HexenDefs::NUMWEAPONS; i++)
	{
		oldWeaponowned[i] = WeaponOwned[i];
	}

	foundSpot = false;
	if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
		GetPlayerNum()]))
	{
		// Appropriate player start spot is open
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
		foundSpot = true;
	}
	else
	{
		// Try to spawn at one of the other player start spots
		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]))
			{
				// Found an open start spot
				SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]);
				foundSpot = true;
				break;
			}
		}
	}

	if (!foundSpot)
	{
		// Player's going to be inside something
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
	}

	// Restore keys and weapons
	Keys = oldKeys;
//	Pieces = oldPieces;
	for (bestWeapon = 0, i = 0; i < HexenDefs::NUMWEAPONS; i++)
	{
		if (oldWeaponowned[i])
		{
			bestWeapon = i;
			WeaponOwned[i] = true;
		}
	}
	Mana[HexenDefs::MANA_1] = 25;
	Mana[HexenDefs::MANA_2] = 25;
	Mana[HexenDefs::MANA_3] = 25;
	if (bestWeapon)
	{
		// Bring up the best weapon
		PendingWeapon = bestWeapon;
	}
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	int i;

	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
	if (MO)
	{
		MO.Player = none;
		MO.bIsPlayer = false;
		Actor(MO).Damage(none, none, 10000);
	}
	if (ViewEnt)
	{
		ViewEnt.Destroy();
		ViewEnt = none;
	}
	Level.bprint("%s left the game\n", PlayerName);
	MO.PlaySound('misc/chat', CHAN_AUTO, 1.0, ATTN_NONE);

	//[CW] set leaving player down the competition table
	// NOTE: for future, should leaving player really loose his/her score?
	Team.DiscardPlayer(self);
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	targetnamehint_enabled = GetCvar('TargetNameHint');

	if (!bSpawned)
	{
		return;
	}
	SetPlayerColour();

	dprint("Player::UserinfoChanged : after set colour");

	//[CW] update all server clients' scoreboards
	MainGameInfo(Level.Game).ScoreboardsUpdate(none);

	dprint("Player::UserinfoChanged : OUT (after ScoreboardsUpdate)");
}

//==========================================================================
//
//  SetPlayerColour
//
//==========================================================================

void SetPlayerColour()
{
	MO.Translation = 0;
	if (PClass == HexenDefs::PCLASS_WITCHAVEN)
	{
		// [FB] The first type should be blue, and the second should be the
		// Witchaven's original red colour
		if (Colour == 0)
			MO.Translation = 1 + PClass * (MAXPLAYERS - 1);
		else if (Colour != 1)
			MO.Translation = Colour + PClass * (MAXPLAYERS - 1);
	}
	else if (PClass == HexenDefs::PCLASS_FIGHTER)
	{
		// The first type should be blue, and the third should be the
		// Fighter's original gold colour
		if (Colour == 0)
		{
			MO.Translation = 2;
		}
		else if (Colour != 2)
		{
			MO.Translation = Colour;
		}
	}
	else if (PClass == HexenDefs::PCLASS_HERETIC)
	{
		if (Colour == 0)
			MO.Translation = 3 + PClass * (MAXPLAYERS - 1);
		else if (Colour != 3)
			MO.Translation = Colour + PClass * (MAXPLAYERS - 1);
	}
	else if (PClass == HexenDefs::PCLASS_MARINE)
	{
		if (Colour == 0)
			MO.Translation = 4 + PClass * (MAXPLAYERS - 1);
		else if (Colour != 4)
			MO.Translation = Colour + PClass * (MAXPLAYERS - 1);
	}
	else if (Colour)
	{
		// Set colour translation bits for player sprites
		MO.Translation = PClass * (MAXPLAYERS - 1) + Colour;
	}

	// [CW] respawn player if team is selected or re-selected
	if (TeamSelection == HexenDefs::teamselection_wait || TeamSelection == HexenDefs::teamselection_reselect)
	{
		dprint("Player::SetPlayerColour : was 'teamselection_wait/reselect', now set to 'teamselection_done'");
		
		if (!(TeamSelection == HexenDefs::teamselection_reselect && oldColour == Colour))
		{
			TeamSelection = HexenDefs::teamselection_done;	
			if (PlayerState != PST_DEAD)
			{
				MO.Destroy();
				MO = none;
				dprint("removed old MO");
				SpawnClient();
				dprint("respawned client");
			}
			else
			{
				// [CW] For player's convenience
				Scoreboard(false);
				centreprint("you will join selected team after respawn");
			}
		}
		else
			TeamSelection = HexenDefs::teamselection_done;

		oldColour = Colour;
	}
	// ------------ REMOVE ME LATER ------------------------
	// [CW] this code is obsolete now, since team reselection
	//      is called using 'impulse' command,
	//		not by that ugly "setColor(getGolor)" thing
	/*else if (TeamSelection == HexenDefs::teamselection_done)
	{
		TeamSelection = HexenDefs::teamselection_reselect;
		SelectPlayerTeam(false);
	}*/
	// ------------ REMOVE ME LATER ------------------------
}

//==========================================================================
//
//  SelectPlayerTeam
//
//==========================================================================

void SelectPlayerTeam()
{
	dprint("Player::SelectPlayerTeam : ");

	if (bIsBot)
	{
		TeamSelection = HexenDefs::teamselection_done;
	}
	else
	{
		int i;
		int maxTeams;
		if (MainGameInfo(Level.Game).GameMode)
			maxTeams = MainGameInfo(Level.Game).GameMode.AllowedTeams();
		else
			maxTeams = HexenDefs::MAXTEAMSINGAME;
		for (i = 0; i < HexenDefs::MAXPLAYERTEAMS; i++)
		{
			if (MainGameInfo(Level.Game).Teams[i] == none)
				break;
		}
		
		if (TeamSelection != HexenDefs::teamselection_reselect)
		{
			TeamSelection = HexenDefs::teamselection_wait;
			SetControllable(false);
		}
		
		ClientTeamSelect(maxTeams, i, Colour,
			MainGameInfo(Level.Game).Teams[0] ? MainGameInfo(Level.Game).Teams[0].Colour : -1,
			MainGameInfo(Level.Game).Teams[1] ? MainGameInfo(Level.Game).Teams[1].Colour : -1,
			MainGameInfo(Level.Game).Teams[2] ? MainGameInfo(Level.Game).Teams[2].Colour : -1,
			MainGameInfo(Level.Game).Teams[3] ? MainGameInfo(Level.Game).Teams[3].Colour : -1);
	}
	dprint("Player::SelectPlayerTeam : OUT");
}

//==========================================================================
//
//  ClientTeamSelect
//
//==========================================================================

void ClientTeamSelect(int maxTeams, int activeTeams, int curSelection,
	int Col1, int Col2, int Col3, int Col4)
{
	ClientGameBaseEx(ClGame).TeamSelect(maxTeams, activeTeams, curSelection,
		Col1, Col2, Col3, Col4);
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	dprint("Player::DeathMatchSpawnPlayer : ");
	dprint("Player::DeathMatchSpawnPlayer : seek for a good spot - ");
	dprint("Player::DeathMatchSpawnPlayer : NumDeathmatchStarts = %i", Level.NumDeathmatchStarts);

	for (j = 0; j < 20; j++)
	{
		dprint("Player::DeathMatchSpawnPlayer : try %i", j);
		i = P_Random() % Level.NumDeathmatchStarts;
		dprint("Player::DeathMatchSpawnPlayer : check spot %i", i);
		if (CheckSpot(&Level.DeathmatchStarts[i]))
		{
			dprint("Player::DeathMatchSpawnPlayer : successful, goto SpawnPlayer");
			SpawnPlayer(&Level.DeathmatchStarts[i]);
			return;
		}
		dprint("Player::DeathMatchSpawnPlayer : failed, next try");
	}

	// no good spot, so the player will probably get stuck
	dprint("Player::DeathMatchSpawnPlayer : TOTAL FAILURE!");
}

//==========================================================================
//
//  CheckSpot
//
//  Returns false if the player cannot be respawned at the given mthing_t
// spot because something is occupying it
//
//==========================================================================

bool CheckSpot(mthing_t * mthing)
{
	float x;
	float y;
	sector_t *sec;
	float an;
	Actor A;
	int i;

	if (!MO)
	{
		// first spawn of level, before corpses
		for (i = 0; i < MAXPLAYERS; i++)
			if (Level.Game.Players[i] && Level.Game.Players[i].MO &&
				Level.Game.Players[i].MO.Origin.x == mthing->x &&
				Level.Game.Players[i].MO.Origin.y == mthing->y)
				return false;
		return true;
	}

	x = mthing->x;
	y = mthing->y;

	MO.bNoPassMobj = true;
	if (!MO.CheckPosition(vector(x, y, Actor::ONFLOORZ)))
	{
		MO.bNoPassMobj = false;
		return false;
	}
	MO.bNoPassMobj = false;

	// spawn a teleport fog 
	sec = Level.XLevel.PointInSector(vector(x, y, 0.0));
	an = itof(45 * (mthing->angle / 45));

	A = HexenLevelInfo(Level).Spawn(TeleportFog, vector(x + 20.0 * cos(an), y + 20.0 * sin(an),
		sec->floorheight + Actor::TELEFOGHEIGHT));
	A.PlaySound('Teleport', CHAN_VOICE);

	return true;
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level.  Most of the player
// structure stays unchanged between levels.
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing)
{
	int i;
	float x, y, z;
	PlayerPawn pp;
	bool ResetInventory = false;

	if (PlayerState == PST_REBORN)
	{
		PlayerReborn();
		ResetInventory = true;
	}
	if (MainGameInfo(Level.Game).randomclass && Level.Game.deathmatch)
	{
		PClass = P_Random() % 6;
		if (PClass == BaseClass)
		{
			PClass = (PClass + 1) % 6;
		}
		BaseClass = PClass;
		SB_Start();
	}
	else
	{
		PClass = BaseClass;
	}

	if (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM &&
		TeamSelection == HexenDefs::teamselection_none)
	{
		dprint("Player::SpawnPlayer : goto SelectPlayerTeam");
		SelectPlayerTeam();
	}

	x = mthing->x;
	y = mthing->y;
	z = Actor::ONFLOORZ;
	switch (PClass)
	{
	case HexenDefs::PCLASS_FIGHTER:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerFighter, vector(x, y, z)));
		break;
	case HexenDefs::PCLASS_CLERIC:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerCleric, vector(x, y, z)));
		break;
	case HexenDefs::PCLASS_MAGE:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerMage, vector(x, y, z)));
		break;
	case HexenDefs::PCLASS_HERETIC:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerHeretic, vector(x, y, z)));
		break;
	case HexenDefs::PCLASS_MARINE:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerMarine, vector(x, y, z)));
		break;
	case HexenDefs::PCLASS_WITCHAVEN:
		pp = PlayerPawn(HexenLevelInfo(Level).Spawn(PlayerWitchaven, vector(x, y, z)));
		break;
	default:
		Error("P_SpawnPlayer: Unknown class type");
		break;
	}
	pp.Origin.z += mthing->height;
	pp.LinkToWorld();

	pp.Angles.yaw = itof(45 * (mthing->angle / 45));
	pp.Player = self;
	pp.bIsPlayer = true;
	pp.Health = /*Health*/HealthBig;
	MO = pp;
	PlayerState = PST_LIVE;
	Refire = 0;
	DamageFlash = 0.0;
	BonusFlash = 0.0;
	PoisonCount = 0;
	BlindCount = 0; // [FB] Reset the blind count too!
	//MorphTime = 0.0;
	Morph.bMorphed = false;
	Morph.bTimer = false;
	Morph.bUntilDeath = false;
	Morph.MorphTime = 0.0;
	ExtraLight = 0;
	FixedColourmap = 0;
	LastSectorDamageTime = 0.0;
	LastHazardTime = 0.0;
	ViewHeight = pp.ViewHeight;	//VIEWHEIGHT;
	ViewAngles = pp.Angles;
	bFixAngle = true;
	pcount = 0; // [FB] Reset counter
	respawn_counter = 2.0;

//[Korax]
	if (!inited)
	{
		InitiateLevel();
		// [FB] Start TotalKills counter with all monster count, but just once!
		if (!Level.Game.deathmatch || MainGameInfo(Level.Game).GameType == HexenDefs::game_sweepteam)
			TotalKills = Level.TotalKills;
		inited = true;
	}

	// [FB] Wear out any power we had
	for (i = 0; i < NUMPOWERS; i++)
		Powers[i] = 0.0;

	if (ResetInventory)
	{
		SetWeapon(HexenDefs::WP_SECOND);
	}
	else
	{
		ViewEnt.Owner = MO;
	}
	SetupPsprites();
	if (Level.Game.deathmatch)
	{
		// Give all keys in death match mode
		Keys = 2047;
		// [FB] Give temporary invulnerability in DM only
		GivePower(pw_invulnerability2);
	}

	// Set translation table data
	if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM)
	{
		dprint("Player::SpawnPlayer : goto setplayercolour");
		SetPlayerColour();
	}
	if (bIsBot)
	{
		Bot.OnSpawn();
	}

	// [FB] There's a penalty for dying; you lost some experience
	if(inited && PlayerState == PST_LIVE)
	{
		// [CW] recalc attributes if needed
		if (bRecalculateLevelOnRespawn)
		{
			int bak_level;

			bRecalculateLevelOnRespawn = false;
			bak_level = exp_level;
			InitiateLevel();
			ActivateLevel(bak_level, false);
		}

		experience -= (200 * exp_level);
		Actor(MO).experience = experience;

		if(experience < 0)
		{
			experience = 0; // [FB] We don't want to have negative experience, right?
		}

		Actor(MO).experience = experience;

		// [FB] But what if the experience we lost has made us lost a level??
		//	Then we use the old_level value to compare our current experience,
		//	if it is lower to it, then we activate a level less than
		//	our current one
		while(experience < old_level && exp_level >= 1/* && (cl < PCLASS_PIG)*/)
		{
			/*exp_level--; // [FB] Decrese experience level
			next_level = 1000 * exp_level; // [FB] Decrease the next to the old_level
			old_level = 1000 * (exp_level - 1); // [FB] Also decrease the old_level*/
			DeactivateLevel(exp_level - 1, true); // [FB] Update Stats accordingly
		}
	}

	if (bUnControllable)
		MO.Deactivate();

	//[CW] put player into corresponding team
	if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM ||
		TeamSelection == HexenDefs::teamselection_done)
	{
		dprint("Player::SpawnPlayer : InsertPlayer to team");
		MainGameInfo(Level.Game).InsertPlayer(self);
	}

	// [CW] if there is advanced game mode initiated, notify it of player spawn
	if (MainGameInfo(Level.Game).GameMode)
		MainGameInfo(Level.Game).GameMode.NotifyPlayerSpawn(self);

	// [FB] Destroy all things touching players
	Actor(MO).TeleportMove(MO.Origin);
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialised
//
//==========================================================================

void PlayerReborn()
{
	BotPlayer B;

	B = Bot;
	if (!inited && Level.Game.deathmatch)
	{
	}
	else if (Level.Game.deathmatch)
	{
		WeaponOwned[HexenDefs::WP_THIRD] = false;
		WeaponOwned[HexenDefs::WP_FOURTH] = false;
		WeaponOwned[HexenDefs::WP_FIFTH] = false;
		WeaponOwned[HexenDefs::WP_SIXTH] = false;
		WeaponOwned[HexenDefs::WP_SEVENTH] = false;
		WeaponOwned[HexenDefs::WP_EIGHTH] = false;
		Mana[HexenDefs::MANA_4] = 0;
		Mana[HexenDefs::MANA_5] = 0;
		ArmorPoints = 0;
	}

	Bot = B;

	PClass = BaseClass;

	bUseDown = true;	// don't do anything immediately
	bAttackDown = true;
	PlayerState = PST_LIVE;
	HealthBig = max_health;
	Health = HealthBig > 0xff ? 0xff : HealthBig;
	WeaponOwned[HexenDefs::WP_FIRST] = true;
	WeaponOwned[HexenDefs::WP_SECOND] = true;
	Mana[HexenDefs::MANA_1] = 25;
	Mana[HexenDefs::MANA_2] = 25;
	Mana[HexenDefs::MANA_3] = 25;

	LocalQuakeHappening = 0;

	Scoreboard(false); // [CW]
}

//==========================================================================
//
//  PlayerExitMap
//
//  Called when the player leaves a map.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	int i;
	float flightPower;

	// Strip all current powers (retain flight)
	flightPower = Powers[pw_flight];
	for (i = 0; i < NUMPOWERS; i++)
		Powers[i] = 0.0;
//  memset(Powers, 0, sizeof(Powers));
	Powers[pw_flight] = flightPower;

	if (Level.Game.deathmatch)
	{
		Powers[pw_flight] = 0.0;
	}
	else
	{
		if (clusterChange)
		{	// Entering new cluster
			// Strip all keys
			Keys = 0;

			// Strip flight artifact
			for (i = 0; i < 25; i++)
			{
				Powers[pw_flight] = 0.0;
				PlayerUseArtifact(HexenDefs::arti_fly);
			}
			Powers[pw_flight] = 0.0;
		}
	}

	if (Morph.bMorphed/*MorphTime*/)
	{
		SetWeapon(Actor(MO)._Special1);	// Restore weapon
		Morph.bMorphed = false;
		Morph.bTimer = false;
		Morph.bUntilDeath = false;
		Morph.MorphTime = 0.0;
	}
	MO.Angles.pitch = 0.0;
	MO.Alpha = 1.0;	// Remove invisibility
	ExtraLight = 0;	// Remove weapon flashes
	FixedColourmap = 0;	// Remove torch
	DamageFlash = 0.0;	// No palette changes
	BonusFlash = 0.0;
	PoisonCount = 0;
	BlindCount = 0; // [FB] Reset the blind count too!
}

//==========================================================================
//
//  SetControllable
//
//  [CW] toggles UnControllable mode on/off
//
//==========================================================================

void SetControllable(bool Mode)
{
	if (Mode)
	{
		bUnControllable = false;
		if (MO)
		{
			MO.Activate();
			MO.LinkToWorld();	// [CW] reassurance...
		}
	}
	else
	{
		bUnControllable = true;
		if (MO)
			MO.Deactivate();
		PoisonCount = 0;
		BlindCount = 0;
	}
}

//==========================================================================
//
//  GiveMana
//
//  Returns true if the player accepted the mana, false if it was
// refused (player has MAX_MANA).
//
//==========================================================================

bool GiveMana(int mana, int count)
{
	int prevMana;

	if (mana == HexenDefs::MANA_NONE)
	{
		return false;
	}
	if (mana < 0 || mana >= HexenDefs::NUMMANA)
	{
		Error("Actor::GiveMana: bad type %i", mana);
	}
	if (Mana[mana] == HexenDefs::MAX_MANA)
	{
		return false;
	}
	if (Level.Game.gameskill == sk_baby || Level.Game.gameskill == sk_nightmare)
	{
		// extra mana in baby mode and nightmare mode
		count += count >> 1;
	}
	prevMana = Mana[mana];

	Mana[mana] += count;
	if (Mana[mana] > HexenDefs::MAX_MANA)
	{
		Mana[mana] = HexenDefs::MAX_MANA;
	}
	if (prevMana <= 0)
	{
		Weapon(ViewEnt).GotMana(mana);
	}
	if (Bot)
	{
		Bot.GaveMana(mana);
	}
	return true;
}

//==========================================================================
//
//  GiveBody
//
//  Returns false if the body isn't needed at all.
//
//==========================================================================

bool GiveBody(int num)
{
	int max;

	max = max_health;
	/*if (PClass == HexenDefs::PCLASS_PIG)
	{
		max = MAXMORPHHEALTH;
	}*/
	if (/*Health*/HealthBig >= max)
	{
		return false;
	}
	HealthBig += num;
	Health = HealthBig > 0xff ? 0xff : HealthBig;
	if (/*Health*/HealthBig > max)
	{
		HealthBig = max;
		Health = HealthBig > 0xff ? 0xff : HealthBig;
	}
	MO.Health = HealthBig;
	return true;
}

//==========================================================================
//
//  GiveArmor
//
//  Works like in Doom and Heretic now
//
//==========================================================================

bool GiveArmor(int armortype, int amount)
{
	float hits;
	float totalArmor;

	if (amount == -1)
	{
/*		hits = ArmorIncrement[Player.PClass * NUMARMOR + armortype];
		if (ArmorPoints[armortype] >= hits)
		{
			return false;
		}
		else
		{
			ArmorPoints[armortype] = hits;
		}
*/
		hits = MainGameInfo(Level.Game).ArmorIncrement[PClass * HexenDefs::NUMARMOR + armortype] * 2.0;
	}
	else
	{
/*		hits = itof(amount) * 5.0;
		totalArmor = ArmorPoints[ARMOR_ARMOR]
			+ ArmorPoints[ARMOR_SHIELD]
			+ ArmorPoints[ARMOR_HELMET]
			+ ArmorPoints[ARMOR_AMULET]
			+ AutoArmorSave[PClass];
		if (totalArmor < ArmorMax[PClass] * 5.0)
		{
			ArmorPoints[armortype] += hits;
		}
		else
		{
			return false;
		}
*/
		hits = itof(amount) * 10.0;
	}
	if (ArmorPoints < 200)
	{
		ArmorPoints += (ftoi(hits) < 200 - ArmorPoints ? ftoi(hits) : 200 - ArmorPoints);
		return true;
	}
	return false;
}
//==========================================================================
//
//  GiveKey
//
//==========================================================================

bool GiveKey(int key)
{
	if (Keys & (1 << key))
	{
		return false;
	}
	BonusFlash += Actor::BONUSADD;
	Keys |= 1 << key;
	return true;
}

//==========================================================================
//
//  GivePower
//
//  Returns true if power accepted.
//
//==========================================================================

bool GivePower(int power)
{
	if (power == pw_invulnerability)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INVULNTIME;
		Actor(MO).bInvulnerable = true;
		// [KORAX]
		/*if (PClass == PCLASS_MAGE)
		{
			MO.bReflective = true;
		}*/
		// [KORAX]
		return true;
	}
	if (power == pw_invulnerability2)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INVULNTIME2;
		Actor(MO).bInvulnerable = true;
		return true;
	}
	if (power == pw_flight)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = FLIGHTTIME;
		MO.bFly = true;
		MO.bNoGravity = true;
		if (MO.Origin.z <= MO.FloorZ)
		{
			FlyHeight = 10.0;	// thrust the player in the air a bit
		}
		return (true);
	}
	if (power == pw_infrared)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INFRATIME;
		return true;
	}
	if (power == pw_speed)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = SPEEDTIME;
		return true;
	}
	if (power == pw_minotaur)
	{
		// Doesn't matter if already have power, renew ticker
		Powers[power] = MAULATORTIME;
		return true;
	}
	if (power == pw_invisibility)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INVISTIME;
		MO.Alpha = 0.666;
		return true;
	}
	if (power == pw_damage)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = DAMAGETIME;
		Weapon(ViewEnt).active_damage = 1; // [FB] Adjust weapon damage
		return true;
	}
	if (Powers[power])
	{
		return false;	// already got it
	}
	Powers[power] = 1.0;
	return true;
}

//==========================================================================
//
//  GiveArtifact
//
//  Returns true if artifact accepted.
//
//==========================================================================

bool GiveArtifact(int arti)
{
//[Korax]
	if (arti == HexenDefs::arti_invulnerability)
	{
		return GivePower(pw_invulnerability);
	}
	else if (arti == HexenDefs::arti_speed)
	{
		return GivePower(pw_speed);
	}
	else if (arti == HexenDefs::arti_boostarmor)
	{
		return GiveArmor(0, 40);
	}
	else if (arti == HexenDefs::arti_summon)
	{
//		return GivePower(pw_minotaur);
		UseArtiSummon();
		return true;
	}
	else if (arti == HexenDefs::arti_torch)
	{
		return GivePower(pw_infrared);
	}
	else if (arti == HexenDefs::arti_invisibility)
	{
		return GivePower(pw_invisibility);
	}
	else if (arti == HexenDefs::arti_damage)
	{
		return GivePower(pw_damage);
	}
	else if (arti == HexenDefs::arti_egg)
	{
		UseArtiPig();
		return true;
	}
	else
	{
		return false;
	}
	int i;
	int j;
	bool slidePointer;

	slidePointer = false;
	i = 0;
	while (Inventory[i].type != arti && i < InventorySlotNum)
	{
		i++;
	}
	if (i == InventorySlotNum)
	{
		if (arti < HexenDefs::arti_firstpuzzitem)
		{
			i = 0;
			while (Inventory[i].type < HexenDefs::arti_firstpuzzitem
				&& i < InventorySlotNum)
			{
				i++;
			}
			if (i != InventorySlotNum)
			{
				for (j = InventorySlotNum; j > i; j--)
				{
					Inventory[j].count = Inventory[j - 1].count;
					Inventory[j].type = Inventory[j - 1].type;
					slidePointer = true;
				}
			}
		}
		Inventory[i].count = 1;
		Inventory[i].type = arti;
		InventorySlotNum++;
	}
	else
	{
		if (arti >= HexenDefs::arti_firstpuzzitem && Level.Game.netgame && !Level.Game.deathmatch)
		{
			// Can't carry more than 1 puzzle item in coop netplay
			return false;
		}
		if (Inventory[i].count >= 25)
		{
			// Player already has 25 of this item
			return false;
		}
		Inventory[i].count++;
	}
	if (!ArtifactCount)
	{
		ReadyArtifact = arti;
	}
	else if (slidePointer && i <= InvPtr)
	{
		InvPtr++;
		InvCurPos++;
		if (InvCurPos > 6)
		{
			InvCurPos = 6;
		}
	}
	ArtifactCount++;
	return true;
}

//[Korax]
//============================================================================
//
//  GiveExperience
//
//============================================================================

void GiveExperience(int exp)
{
	experience += exp;
	Actor(MO).experience = experience;

	if(experience > 9999)
	{
		experience = 9999;
	}

	Actor(MO).experience = experience;

	while (experience >= next_level && exp_level < 11/* && (cl < PCLASS_PIG)*/)
	{
		/*old_level = 1000 * (exp_level); // [FB] We need to keep this to compare it
		exp_level++;
		next_level = 1000 * exp_level;*/
		ActivateLevel(exp_level + 1, true);
	}
}

//============================================================================
//
//  ActivateLevel
//
//  [FB] Using the new leveling system the maximum level is 10
//
//============================================================================

void ActivateLevel(int to_lvl, bool message)
{
	int addhealth;
	int i;

	while (exp_level < 11 && exp_level < to_lvl) // [FB] MAX Level it's 10
	{
		old_level = 1000 * (exp_level); // [FB] We need to keep this to compare it
		exp_level++;
		next_level = 1000 * exp_level;

		strength += strength * 25 / 100;
		constitution += constitution * 25 / 100;
		dexterity += dexterity * 25 / 100;
		intelligence += intelligence * 25 / 100;
		wisdom += wisdom * 25 / 100;
		old_maxhealth = max_health;
		bak_health = old_maxhealth - old_maxhealth * 9 / 100; // Save a previous level's health

		/*if (old_maxhealth == 212)
		{
			//	Don't do anything
			addhealth = 0;
		}
		else
		{*/
		/*	if ((max_health + max_health * 9 / 100) >= 212)
			{
				addhealth = 212 - max_health;
			}
			else
			{*/
				addhealth = max_health * 9 / 100;
			//}
		//}

		//if (addhealth > 0)
		//{
			max_health += addhealth;
			HealthBig = ftoi(itof(HealthBig) / itof(old_maxhealth) * itof(max_health));
			Health = HealthBig > 0xff ? 0xff : HealthBig;
			MO.Health = HealthBig;
		//}

	}

	if (message)
	{
		cprint("YOU REACHED LEVEL %d", exp_level);
		MO.PlaySound('misc/chat', CHAN_AUTO);
	}
}

//============================================================================
//
//  DeactivateLevel
//
//  [FB] We lost a level, so we need to update the attributes then!
//
//============================================================================

void DeactivateLevel(int to_lvl, bool message)
{
	int addhealth;
	int i;

	while(exp_level > 1 && exp_level > to_lvl)
	{
		exp_level--; // [FB] Decrese experience level
		next_level = 1000 * exp_level; // [FB] Decrease the next to the old_level
		old_level = 1000 * (exp_level - 1); // [FB] Also decrease the old_level

		strength -= strength * 25 / 100;
		constitution -= constitution * 25 / 100;
		dexterity -= dexterity * 25 / 100;
		intelligence -= intelligence * 25 / 100;
		wisdom -= wisdom * 25 / 100;

		// [FB] We already calculated the bak_health value
		/*if (old_maxhealth < 100)
		{
			// Don't do anything
			addhealth = 0;
		}
		else
		{*/
			addhealth = old_maxhealth * 9 / 100;
		//}

		//if (addhealth > 0)
		//{
			max_health -= addhealth;
			HealthBig = max_health; // [FB] Just make it the same as the max_health value
			Health = HealthBig > 0xff ? 0xff : HealthBig;
			MO.Health = HealthBig;
		//}
	}

	if (message)
	{
		cprint("YOU LOWERED TO LEVEL %d", exp_level);
		MO.PlaySound('misc/chat', CHAN_AUTO);
	}
}

//============================================================================
//
//  InitiateLevel
//
//============================================================================

void InitiateLevel()
{
	max_health = PlayerPawn(MO).default.Health;	//100;

	exp_level = 1;
	next_level =  1000 * exp_level;
	old_level = 1000 * (exp_level - 1);
	Actor(MO).experience = 0;
	HealthBig = max_health;
	Health = HealthBig > 0xff ? 0xff : HealthBig;
	MO.Health = HealthBig;

	strength = PlayerPawn(MO).InitialStrength;
	wisdom = PlayerPawn(MO).InitialWisdom;
	dexterity = PlayerPawn(MO).InitialDexterity;
	constitution = PlayerPawn(MO).InitialConstitution;
	intelligence = PlayerPawn(MO).InitialIntelligence;
}

//==========================================================================
//
//  PlayerCamping
//
//  [FB] If the player camps much time he'll lost experience too
//
//==========================================================================

void PlayerCamping()
{
	// [FB] Let's see, if every a tic is 1/35 of a second, a second should
	//	be multiplied by 35 to have an equality, every 3 secs. the
	//	player stays camping (even if he's shooting), we'll start
	//	decreasing his experience according his level, so...
	int limit;

	limit = 17; // [FB] 0.5 seconds

	if(inited && (pcount >= limit) && (pcount % (1 * 35) == 0) && PlayerState == PST_LIVE)
	{
		experience -= (1 * exp_level); // [FB] Depending the player level

		if(experience < 0)
		{
			experience = 0; // [FB] We don't want to have negative experience
		}

		Actor(MO).experience = experience;

		if(pcount == 525)
		{
			pcount = 105; // [FB] To avoid overflows
		}

		// [FB] But what if the experience we lost has made us lost a level??
		//	Then we use the old_level value to compare our current experience,
		//	if it is lower than it, then we activate a level less than
		//	our current one
		while(experience < old_level && exp_level >= 1/* && (cl < PCLASS_PIG)*/)
		{
			/*exp_level--; // [FB] Decrese experience level
			next_level = next_level - 1000; // [FB] Decrease the next to the old_level
			old_level = old_level - 1000; // [FB] Also decrease the old_level*/
			DeactivateLevel(exp_level - 1, true); // [FB] Update Stats accordingly
		}
	}
}	   

//==========================================================================
//
//  Scoreboard
//
// [FB] Here we keep a track of everything needed to make KA's scoreboard
//	for every gameplay mode
//
//==========================================================================

void Scoreboard(bool show)
{
	// [CW] If show == true, update scoreboard data first.
	if (show)
		MainGameInfo(Level.Game).ScoreboardsUpdate(self);
	
	// [CW] Set bShowScoreboard flag
	bShowScoreboard = show;
	ClientScoreboard(bShowScoreboard);
}

//==========================================================================
//
//  ClientScoreboard
//
//==========================================================================

void ClientScoreboard(bool show)
{
	ClientGameBaseEx(ClGame).ShowScoreBoard(show);
}

//==========================================================================
//
//  InventoryLeft
//
//==========================================================================

void InventoryLeft()
{
	if (!InventoryTime)
	{
		InventoryTime = 5.0;
		return;
	}
	InventoryTime = 5.0;
	InvPtr--;
	if (InvPtr < 0)
	{
		InvPtr = 0;
	}
	else
	{
		InvCurPos--;
		if (InvCurPos < 0)
		{
			InvCurPos = 0;
		}
	}
}

//==========================================================================
//
//  InventoryRight
//
//==========================================================================

void InventoryRight()
{
	if (!InventoryTime)
	{
		InventoryTime = 5.0;
		return;
	}
	InventoryTime = 5.0;
	InvPtr++;
	if (InvPtr >= InventorySlotNum)
	{
		InvPtr--;
		if (InvPtr < 0)
			InvPtr = 0;
	}
	else
	{
		InvCurPos++;
		if (InvCurPos > 6)
		{
			InvCurPos = 6;
		}
	}
}

//==========================================================================
//
//  InventoryUse
//
//==========================================================================

void InventoryUse()
{
	ReadyArtifact = Inventory[InvPtr].type;
	// flag to denote that it's okay to use an artifact
	if (InventoryTime)
	{
		InventoryTime = 0.0;
	}
	else
	{
		Arti = ReadyArtifact;
	}
}

//==========================================================================
//
//  InventoryTick
//
//==========================================================================

void InventoryTick(float deltaTime)
{
	// turn inventory off after a certain amount of time
	if (InventoryTime)
	{
		InventoryTime -= deltaTime;
		if (InventoryTime <= 0.0)
		{
			InventoryTime = 0.0;
			ReadyArtifact = Inventory[InvPtr].type;
		}
	}
	if (ArtifactFlash)
	{
		ArtifactFlash--;
	}
}

//==========================================================================
//
//  MovePlayer
//
//==========================================================================

void MovePlayer(float deltaTime)
{
	float fly;
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	if (bUnControllable)
	{
		ForwardMove = 0.0;
		SideMove = 0.0;
	}

	forward = ForwardMove;
	side = SideMove;

	switch (PClass)
	{
	// [FB] We make every class walk at the same speed now
	case HexenDefs::PCLASS_FIGHTER:
	case HexenDefs::PCLASS_CLERIC:
	case HexenDefs::PCLASS_MAGE:
	case HexenDefs::PCLASS_HERETIC:
	case HexenDefs::PCLASS_MARINE:
	case HexenDefs::PCLASS_WITCHAVEN:
	case HexenDefs::PCLASS_HERESIARCH:
		forward *= 0.9 + itof(dexterity) / 100.0;
		side *= 0.9 + itof(dexterity) / 100.0;
		break;

	case HexenDefs::PCLASS_PIG:
		forward *= 0.97 + itof(dexterity) / 100.0;
		side *= 0.97 + itof(dexterity) / 100.0;
		break;
	}

	if (Powers[pw_speed] && PClass != HexenDefs::PCLASS_PIG/*!MorphTime*/)
	{
		// Adjust for a player with a speed artifact
		forward *= 1.5;
		side *= 1.5;
	}
/*	if ((!Powers[pw_flight] || MO.Origin.z <= MO.FloorZ) &&
		(Actor(MO).GetFloorType()->Friction < Actor::FRICTION_NORMAL ||
		(MO.Sector->special & SECSPEC_BASE_MASK) == SECSPEC_FrictionLow))
	{
		forward *= 0.5;
		side *= 0.5;
	}*/

	// Do not let the player control movement
	//  if not onground.
	onground = (MO.Origin.z <= MO.FloorZ) || Actor(MO).bOnMobj;
	if (!(onground || MO.bFly))
	{
		//  not on ground, so little effect on velocity
		forward *= MainGameInfo(Level.Game).air_control;
		side *= MainGameInfo(Level.Game).air_control;
	}

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	wishvel = forward * vforward + side * vright;
	if (onground && !MO.bFly)
	{
		wishvel.z = 0.0;
	}

	MO.Velocity += 5.0 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	fly = FlyMove / 16.0;
	if (fly && Powers[pw_flight])
	{
		if (FlyMove != TOCENTRE)
		{
			FlyHeight = fly * 2.0;
			if (!MO.bFly)
			{
				MO.bFly = true;
				MO.bNoGravity = true;
				if (MO.Velocity.z <= -39.0 * 35.0)
				{
					// stop falling scream
					MO.StopSound(CHAN_AUTO);
				}
			}
		}
		else
		{
			MO.bFly = false;
			MO.bNoGravity = false;
		}
	}
	else if (fly > 0.0)
	{
		PlayerUseArtifact(HexenDefs::arti_fly);
	}
	if (MO.bFly)
	{
		MO.Velocity.z = FlyHeight * 35.0 + wishvel.z;
		if (FlyHeight)
		{
			FlyHeight /= 2.0;
		}
	}
	if ((Buttons & BT_JUMP) && onground && !JumpTime)
	{
		if (PClass == HexenDefs::PCLASS_PIG/*!MorphTime*/)
		{
			MO.Velocity.z = 6.5 * 35.0;
		}
		else
		{
			MO.Velocity.z = 9.5 * 35.0;
		}
		Actor(MO).bOnMobj = false;
		JumpTime = 0.5;
	}
}

//==========================================================================
//
//  CheckWaterJump
//
//==========================================================================

void CheckWaterJump()
{
	TVec start;
	TVec end;
	TVec vforward;
	TVec HitPoint;
	TVec HitNormal;

	// check for a jump-out-of-water
	AngleVector(&MO.Angles, &vforward);
	start = MO.Origin;
	start.z += MO.Height * 0.5 + 8.0;
	vforward.z = 0.0;
	vforward = Normalise(vforward);
	end = start + vforward * 24.0;
	if (!Level.XLevel.TraceLine(start, end, HitPoint, HitNormal))
	{
		// solid at waist
		start.z = MO.Origin.z + MO.Height;
		end = start + vforward * 24.0;
		MoveDir = HitNormal * -50.0;
		if (Level.XLevel.TraceLine(start, end, HitPoint, HitNormal))
		{
			// open at eye level
			Actor(MO).bWaterJump = true;
			MO.Velocity.z = 350.0;
			Actor(MO).ReactionTime = 2.0;	// safety net
		}
	}
}

//==========================================================================
//
//  WaterMove
//
//==========================================================================

void WaterMove(float deltaTime)
{
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	// Do not let the player control movement
	//  if not onground.
	onground = (MO.Origin.z <= MO.FloorZ) || Actor(MO).bOnMobj;

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	forward = ForwardMove;
	side = SideMove;

	//  Adjust speed depending on class. Originaly this is done in input
	// code, but to simplify it, we do it here.
	//  Fighter moves faster, cleric at original speed, mage - slower,
	// pig - slower, but faster than mage
	switch (PClass)
	{
	case HexenDefs::PCLASS_FIGHTER:
	case HexenDefs::PCLASS_CLERIC:
	case HexenDefs::PCLASS_MAGE:
	case HexenDefs::PCLASS_HERETIC:
	case HexenDefs::PCLASS_MARINE:
	case HexenDefs::PCLASS_WITCHAVEN:
	case HexenDefs::PCLASS_HERESIARCH:
		forward *= 0.9 + itof(dexterity) / 100.0;
		side *= 0.9 + itof(dexterity) / 100.0;
		break;

	case HexenDefs::PCLASS_PIG:
		forward *= 0.97 + itof(dexterity) / 100.0;
		side *= 0.97 + itof(dexterity) / 100.0;
		break;
	}

	if (Powers[pw_speed] && PClass != HexenDefs::PCLASS_PIG/*!MorphTime*/)
	{
		// Adjust for a player with a speed artifact
		forward *= 1.5;
		side *= 1.5;
	}

	wishvel = vforward * forward + vright * side;
	if (!forward && !side /* && !cmd.upmove */ )
		wishvel.z -= 60.0;	// drift towards bottom
//  else
//      wishvel.z += cmd.upmove;

	MO.Velocity += 3.5 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if (Buttons & BT_JUMP)
	{
		if (MO.WaterType == CONTENTS_WATER)
			MO.Velocity.z = 100.0;
		else if (MO.WaterType == CONTENTS_SLUDGE)
			MO.Velocity.z = 80.0;
		else
			MO.Velocity.z = 50.0;
	}
	CheckWaterJump();
}

//==========================================================================
//
//  WaterJump
//
//==========================================================================

void WaterJump()
{
	if (!Actor(MO).ReactionTime || !MO.WaterLevel)
	{
		Actor(MO).bWaterJump = false;
		Actor(MO).ReactionTime = 0.0;
	}
	MO.Velocity.x = MoveDir.x;
	MO.Velocity.y = MoveDir.y;
}

//==========================================================================
//
//  ThrustPlayer
//
//  Moves the given origin along a given angle.
//
//==========================================================================

void ThrustPlayer(float angle, float move, float deltaTime)
{
	if (!(Powers[pw_flight] && !(MO.Origin.z <= MO.FloorZ)) &&
		(Actor(MO).GetFloorType()->Friction < Actor::FRICTION_NORMAL ||
		(MO.Sector->special & SECSPEC_BASE_MASK) == SECSPEC_FrictionLow))
	{
		move *= 0.5;
	}
	MO.Velocity.x += move * cos(angle) * deltaTime;
	MO.Velocity.y += move * sin(angle) * deltaTime;
}

//==========================================================================
//
//  CalcHeight
//
//  Calculate the walking / running height adjustment
//
//==========================================================================

void CalcHeight(float deltaTime)
{
	float angle;
	float bob;

	// Regular movement bobbing
	// (needs to be calculated for gun swing
	// even if not on ground)
	// OPTIMIZE: tablify angle
	// Note: a LUT allows for effects
	//  like a ramp with low health.
	Bob = MO.Velocity.x * MO.Velocity.x + MO.Velocity.y * MO.Velocity.y;
	Bob /= 4.0 * 35.0 * 35.0;
	if (Bob > MAXBOB)
		Bob = MAXBOB;

	if (MO.bFly && !onground)
	{
		Bob = 0.5;
	}

	angle = 180.0 * 35.0 / 10.0 * Level.XLevel.Time;
	bob = Bob / 2.0 * sin(angle);

	// move viewheight
	if (PlayerState == PST_LIVE)
	{
		ViewHeight += DeltaViewHeight * deltaTime;

		if (ViewHeight > PlayerPawn(MO).ViewHeight /*VIEWHEIGHT*/)
		{
			ViewHeight = PlayerPawn(MO).ViewHeight;	//VIEWHEIGHT;
			DeltaViewHeight = 0.0;
		}

		if (ViewHeight < PlayerPawn(MO).ViewHeight /*VIEWHEIGHT*/ / 2.0)
		{
			ViewHeight = PlayerPawn(MO).ViewHeight /*VIEWHEIGHT*/ / 2.0;
			if (DeltaViewHeight <= 0.0)
				DeltaViewHeight = 0.00001;
		}

		if (DeltaViewHeight)
		{
			DeltaViewHeight += 256.0 * deltaTime;
			if (!DeltaViewHeight)
				DeltaViewHeight = 0.00001;
		}
	}
	if (PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
	{
		ViewOrg.z = MO.Origin.z + ViewHeight - 20.0;
	}
	else
	{
		ViewOrg.z = MO.Origin.z + ViewHeight + bob;
	}
	if (MO.FloorClip && PlayerState != PST_DEAD &&
		MO.Origin.z <= MO.FloorZ)
	{
		ViewOrg.z -= MO.FloorClip;
	}
	if (ViewOrg.z < MO.FloorZ + 4.0)
	{
		ViewOrg.z = MO.FloorZ + 4.0;
	}

	if (ViewOrg.z > MO.CeilingZ - 4.0)
		ViewOrg.z = MO.CeilingZ - 4.0;
}

//==========================================================================
//
//  PlayerInSpecialSector
//
//  Called every tic frame that the player origin is in a special sector.
//
//==========================================================================

void PlayerInSpecialSector(float deltaTime)
{
	float speed;
	float finean;

	if (MO.Origin.z != GetPlanePointZ(&MO.Sector->floor, MO.Origin))
	{
		// Player is not touching the floor
		return;
	}

	if (MO.Sector->special & SECSPEC_SECRET_MASK)
	{
		//	Secret area.
		SecretCount++;
		Level.CurrentSecret++;
		MO.Sector->special &= ~SECSPEC_SECRET_MASK;
		centreprint("You found a secret area");
	}

	if (MO.Sector->special <= SECSPEC_LightFlicker &&
		MO.Sector->special >= 255)
	{
		switch (MO.Sector->special)
		{
		case SECSPEC_DamageHellslime:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case SECSPEC_DamageSludge:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 4);
			}
			break;
		case SECSPEC_DamageNukage:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case SECSPEC_LightStrobeFastDamage:
		case SECSPEC_DamageSuperHellslime:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		case SECSPEC_DamageSuperHellslimeExit:
			// EXIT SUPER DAMAGE! (for E1M8 finale)
			Cheats &= ~CF_GODMODE;
	
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
	
			if (HealthBig <= 10)
			{
				Level.ExitLevel(0);
			}
			break;
		case SECSPEC_DamageLavaWimpy:
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(HexenLevelInfo(Level).GLavaInflictor, none, 5);
				Actor(MO).HitFloorType();
				MO.PlaySound('LavaSizzle', CHAN_BODY);
			}
			break;
		case SECSPEC_DamageLavaHefty:
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(HexenLevelInfo(Level).GLavaInflictor, none, 8);
				Actor(MO).HitFloorType();
				MO.PlaySound('LavaSizzle', CHAN_BODY);
			}
			break;
		case SECSPEC_ScrollEastLavaDamage:
			ThrustPlayer(0.0, 1024.0, deltaTime);
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(HexenLevelInfo(Level).GLavaInflictor, none, 5);
				Actor(MO).HitFloorType();
				MO.PlaySound('LavaSizzle', CHAN_BODY);
			}
			break;
		case SECSPEC_DamageHazard:
			HazardTime += 2.0 * deltaTime;
			break;
		case SECSPEC_DamageInstantDeath:
			Actor(MO).Damage(none, none, 999);
			break;
		case SECSPEC_DamageSuperHazard:
			HazardTime += 4.0 * deltaTime;
			break;
		}
	}
	else
	{
		//	Extended sector damage type.
		switch (MO.Sector->special & SECSPEC_DAMAGE_MASK)
		{
		case 0x0100:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case 0x0200:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case 0x0300:
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		}
	}

	//	Apply any customizable damage
	if (MO.Sector->Damage)
	{
		if (MO.Sector->Damage < 20)
		{
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (MO.Sector->Damage < 50)
		{
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (Level.XLevel.Time - LastSectorDamageTime >= 1.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, MO.Sector->Damage);
		}
	}

	switch (MO.Sector->special)
	{
	case SECSPEC_ScrollCurrent:
		speed = itof((MO.Sector->tag - 100) % 10) / 16.0 * 35.0;
		finean = itof((MO.Sector->tag - 100) / 10) * 45.0;
		MO.Velocity.x += speed * cos(finean);
		MO.Velocity.y += speed * sin(finean);
		break;
	case SECSPEC_ScrollNorthSlow:
	case SECSPEC_ScrollNorthMedium:
	case SECSPEC_ScrollNorthFast:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthSlow], deltaTime);
		break;
	case SECSPEC_ScrollEastSlow:
	case SECSPEC_ScrollEastMedium:
	case SECSPEC_ScrollEastFast:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthSlow:
	case SECSPEC_ScrollSouthMedium:
	case SECSPEC_ScrollSouthFast:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthSlow], deltaTime);
		break;
	case SECSPEC_ScrollWestSlow:
	case SECSPEC_ScrollWestMedium:
	case SECSPEC_ScrollWestFast:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthWestSlow:
	case SECSPEC_ScrollNorthWestMedium:
	case SECSPEC_ScrollNorthWestFast:
		ThrustPlayer(135.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthEastSlow:
	case SECSPEC_ScrollNorthEastMedium:
	case SECSPEC_ScrollNorthEastFast:
		ThrustPlayer(45.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthEastSlow:
	case SECSPEC_ScrollSouthEastMedium:
	case SECSPEC_ScrollSouthEastFast:
		ThrustPlayer(315.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthWestSlow:
	case SECSPEC_ScrollSouthWestMedium:
	case SECSPEC_ScrollSouthWestFast:
		ThrustPlayer(225.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollEast5:
	case SECSPEC_ScrollEast10:
	case SECSPEC_ScrollEast25:
	case SECSPEC_ScrollEast30:
	case SECSPEC_ScrollEast35:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEast5], deltaTime);
		break;
	case SECSPEC_ScrollNorth5:
	case SECSPEC_ScrollNorth10:
	case SECSPEC_ScrollNorth25:
	case SECSPEC_ScrollNorth30:
	case SECSPEC_ScrollNorth35:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorth5], deltaTime);
		break;
	case SECSPEC_ScrollSouth5:
	case SECSPEC_ScrollSouth10:
	case SECSPEC_ScrollSouth25:
	case SECSPEC_ScrollSouth30:
	case SECSPEC_ScrollSouth35:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouth5], deltaTime);
		break;
	case SECSPEC_ScrollWest5:
	case SECSPEC_ScrollWest10:
	case SECSPEC_ScrollWest25:
	case SECSPEC_ScrollWest30:
	case SECSPEC_ScrollWest35:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWest5], deltaTime);
		break;
	}
}

//============================================================================
//
//  PlayerOnSpecialFlat
//
//============================================================================

void PlayerOnSpecialFlat(VTerrainInfo* floorType)
{
	if (MO.Origin.z != MO.FloorZ)
	{
		// Player is not touching the floor
		return;
	}
	if (floorType->DamageAmount && Level.XLevel.Time - LastSectorDamageTime >=
		itof(floorType->DamageTimeMask + 1) / 35.0)
	{
		LastSectorDamageTime = Level.XLevel.Time;
		Actor(MO).Damage(HexenLevelInfo(Level).GLavaInflictor, none, 10);
		MO.PlaySound('LavaSizzle', CHAN_BODY);
	}
}

//==========================================================================
//
//  PlayerInContents
//
//==========================================================================

void PlayerInContents(float deltaTime)
{
	if (!MO.WaterLevel)
	{
		return;
	}
	switch (MO.WaterType)
	{
	case CONTENTS_LAVA:
		if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(HexenLevelInfo(Level).GLavaInflictor, none, 10);
			MO.PlaySound('LavaSizzle', CHAN_BODY);
		}
		break;

	case CONTENTS_NUKAGE:
		if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
		break;

	case CONTENTS_SLIME:
		if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 10);
		}
		break;

	case CONTENTS_HELLSLIME:
		if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 20);
		}
		break;

	case CONTENTS_SLUDGE:
		if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 4);
		}
		break;

	case CONTENTS_HAZARD:
		HazardTime += 2.0 * deltaTime;
		break;
	}
}

//==========================================================================
//
//  PrevWeapon
//
//==========================================================================

void PrevWeapon()
{
	int			i;
	int			NewWeap;

	if (PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
		return;
	for (i = HexenDefs::NUMWEAPONS - 1; i > 0; i--)
	{
		NewWeap = (ReadyWeapon + i) % HexenDefs::NUMWEAPONS;
		if (WeaponOwned[NewWeap])
		{
			PendingWeapon = NewWeap;
			return;
		}
	}
}

//==========================================================================
//
//  NextWeapon
//
//==========================================================================

void NextWeapon()
{
	int			i;
	int			NewWeap;

	if (PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
		return;
	for (i = 1; i < HexenDefs::NUMWEAPONS; i++)
	{
		NewWeap = (ReadyWeapon + i) % HexenDefs::NUMWEAPONS;
		if (WeaponOwned[NewWeap])
		{
			PendingWeapon = NewWeap;
			return;
		}
	}
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;
	int i;

	impulse = Impulse;
	Impulse = 0;
	Arti = 0;

	if (bUnControllable)
	{
		// Player can do almost nothing when uncontrollable
		if (impulse != HexenDefs::IMPULSE__SUICIDE && impulse != HexenDefs::IMPULSE__SCOREBOARD)
			return;
	}

	if (impulse <= HexenDefs::NUMWEAPONS)
	{
		// The actual changing of the weapon is done when the weapon
		// psprite can do it (A_WeaponReady), so it doesn't happen in
		// the middle of an attack.
		if (WeaponOwned[impulse - 1] &&
			impulse - 1 != ReadyWeapon && PClass != HexenDefs::PCLASS_PIG/*!MorphTime*/)
		{
			PendingWeapon = impulse - 1;
		}
	}
	else if (impulse == HexenDefs::IMPULSE__WEAPON_PREV && PClass != HexenDefs::PCLASS_PIG/*!MorphTime*/)
	{
		int newWeapon;
		newWeapon = ReadyWeapon;
		do 
		{
			newWeapon = newWeapon -1;
			if (newWeapon == -1)
				newWeapon = HexenDefs::NUMWEAPONS -1;
		}
		while (!WeaponOwned[newWeapon]);
		if (newWeapon != ReadyWeapon)
		{
			PendingWeapon = newWeapon;
		}
	}
	else if (impulse == HexenDefs::IMPULSE__WEAPON_NEXT && PClass != HexenDefs::PCLASS_PIG/*!MorphTime*/)
	{
		int newWeapon;
		newWeapon = ReadyWeapon;
		do 
		{
			newWeapon = newWeapon + 1;
			if (newWeapon == HexenDefs::NUMWEAPONS)
				newWeapon = 0;
		}
		while (!WeaponOwned[newWeapon]);
		if (newWeapon != ReadyWeapon)
		{
			PendingWeapon = newWeapon;
		}		
	}
//[Korax]
/*	else if (impulse == 13)
	{
		InventoryLeft();
	}
	else if (impulse == 14)
	{
		InventoryRight();
	}
	else if (impulse == 15)
	{
		InventoryUse();
	}*/
	else if (impulse == HexenDefs::IMPULSE__SUICIDE)
	{
		Actor(MO).Damage(none, Actor(self.MO), 10000);
	}
	else if (impulse == 17)
	{
		PrevWeapon();
	}
	else if (impulse == 18)
	{
		NextWeapon();
	}
	// [FB] We want to make the scoreboard accesible with a new impulse value
	else if (impulse == HexenDefs::IMPULSE__SCOREBOARD)
	{
		// [FB] The player is calling the scoreboard!!
		Scoreboard(!bShowScoreboard);
	}
	else if (impulse == HexenDefs::IMPULSE__ATTACK2)
	{
		AlternateAttack();
	}
	else if (impulse == HexenDefs::IMPULSE__TOGGLE_BLASTRAD)
	{
		ToggleAltAttack(HexenDefs::ALTFIRE_BLASTRADIUS);
		//PlayerUseArtifact(HexenDefs::arti_blastradius);
	}
	else if (impulse == HexenDefs::IMPULSE__TOGGLE_POSIONBAG)
	{
		ToggleAltAttack(HexenDefs::ALTFIRE_POISONBAG);
		//PlayerUseArtifact(HexenDefs::arti_poisonbag);
	}
	else if (impulse == HexenDefs::IMPULSE__SELECTTEAM)
	{
		TeamSelection = HexenDefs::teamselection_reselect;
		SelectPlayerTeam();
	}

	// Hexen artifact hot keys
///	if (!Arti)
///	{
/*		if (impulse == 11)
		{
			Arti = HexenDefs::NUMARTIFACTS;
		}
		else if (impulse == 12 && (HealthBig < max_health))
		{
			Arti = HexenDefs::arti_health;
		}
		else */
		
		/*
		else if (impulse == 8)
		{
			Arti = HexenDefs::arti_teleport;
		}
		else if (impulse == 7)
		{
			Arti = HexenDefs::arti_teleportother;
		}
		else if (impulse == 6)
		{
			Arti = HexenDefs::arti_egg;
		}
		else if (impulse == 5 && !Powers[pw_invulnerability])
		{
			Arti = HexenDefs::arti_invulnerability;
		}*/
//	}
/*
	if (Arti)
	{
		// Use an artifact
		if (Arti == HexenDefs::NUMARTIFACTS)
		{
			// use one of each artifact (except puzzle artifacts)
			for (i = 1; i < HexenDefs::arti_firstpuzzitem; i++)
			{
				PlayerUseArtifact(i);
			}
		}
		else
		{
			PlayerUseArtifact(Arti);
		}
	}
*/
//[Korax]
}

//==========================================================================
//
//  ToggleAltAttack
//
//==========================================================================

void ToggleAltAttack(int weapon)
{
	dprint("TAA weapon = %i", weapon);
	if (altWeapon == weapon)
	{
		if (altWeaponMode[altWeapon] == MainGameInfo(Level.Game).AltWeaponModeCount[altWeapon] - 1)
			altWeaponMode[altWeapon] = 0;
		else
			altWeaponMode[altWeapon]++;
	}

	altWeapon = weapon;
}

//==========================================================================
//
//  AlternateAttack
//
//==========================================================================

void AlternateAttack()
{
	switch (altWeapon)
	{
	case HexenDefs::ALTFIRE_BLASTRADIUS:
		PlayerUseArtifact(HexenDefs::arti_blastradius);
		break;
	case HexenDefs::ALTFIRE_POISONBAG:
		PlayerUseArtifact(HexenDefs::arti_poisonbag);
		break;
	}
}



//==========================================================================
//
//  MorphPlayerThink
//
//==========================================================================

void MorphPlayerThink()
{
	//FIXME this sucks
	if (ftoi(32.0 * Morph.MorphTime) & 15)
	{
		return;
	}
	if (!(MO.Velocity.x + MO.Velocity.y) && P_Random() < 64)
	{
		// Snout sniff
		Weapon(ViewEnt).SnoutSniff();
		MO.PlaySound('PigActive1', CHAN_VOICE);	// snort
		return;
	}
	if (P_Random() < 48)
	{
		if (P_Random() < 128)
		{
			MO.PlaySound('PigActive1', CHAN_VOICE);
		}
		else
		{
			MO.PlaySound('PigActive2', CHAN_VOICE);
		}
	}
}

//==========================================================================
//
//  UndoPlayerMorph
//
//==========================================================================

bool UndoPlayerMorph()
{
	Actor fog;
	Actor A;
	int bak_level;

	MO.UnlinkFromWorld();
	switch (BaseClass)
	{
	case HexenDefs::PCLASS_FIGHTER:
		A = HexenLevelInfo(Level).Spawn(PlayerFighter, MO.Origin);
		break;
	case HexenDefs::PCLASS_CLERIC:
		A = HexenLevelInfo(Level).Spawn(PlayerCleric, MO.Origin);
		break;
	case HexenDefs::PCLASS_MAGE:
		A = HexenLevelInfo(Level).Spawn(PlayerMage, MO.Origin);
		break;
	case HexenDefs::PCLASS_HERETIC:
		A = HexenLevelInfo(Level).Spawn(PlayerHeretic, MO.Origin);
		break;
	case HexenDefs::PCLASS_MARINE:
		A = HexenLevelInfo(Level).Spawn(PlayerMarine, MO.Origin);
		break;
	case HexenDefs::PCLASS_WITCHAVEN:
		A = HexenLevelInfo(Level).Spawn(PlayerWitchaven, MO.Origin);
		break;
	default:
		Error("UndoPlayerMorph: Unknown player class %d", BaseClass);
	}
	if (!A.TestLocation())
	{
		// Didn't fit
		A.Destroy();
		MO.LinkToWorld();
		Morph.MorphTime = 2.0;
		return false;
	}
	MO.LinkToWorld();
	A.Angles = MO.Angles;
	A.Player = self;
	A.bIsPlayer = true;
	A.ReactionTime = 0.5;
	if (MO.bFly)
	{
		A.bFly = true;
		A.bNoGravity = true;
	}
	Morph.MorphTime = 0.0;
	A.Health = max_health;
	HealthBig = A.Health;
	Health = HealthBig > 0xff ? 0xff : HealthBig;
	PClass = BaseClass;
	SetPlayerColour();
	PostMorphWeapon(Actor(MO)._Special1);

	fog = HexenLevelInfo(Level).Spawn(TeleportFog, MO.Origin + vector(20.0 * cos(MO.Angles.yaw),
		20.0 * sin(MO.Angles.yaw), Actor::TELEFOGHEIGHT));
	fog.PlaySound('Teleport', CHAN_VOICE);

	MO.SetState(Actor::S_FREETARGMOBJ);
	MO = A;

	// [CW] recalc attributes
	bak_level = exp_level;
	InitiateLevel();
	ActivateLevel(bak_level, false);
	bRecalculateLevelOnRespawn = false;

	return true;
}

//==========================================================================
//
//  SetPlayerRunState
//
//==========================================================================

void SetPlayerRunState()
{
	if (MO.State == Actor(MO).IdleState)
	{
		MO.SetState(Actor(MO).SeeState);
	}
}

//==========================================================================
//
//  DeathPlayerTick
//
//  Fall on your face when dying. Decrease POV height to floor height.
//
//==========================================================================

void DeathPlayerTick(float deltaTime)
{
	int dir;
	float delta;
	int lookDelta;

	if (respawn_counter > 0.0)
		respawn_counter -= deltaTime;

	MovePsprites(deltaTime);
	if (MO.WaterLevel > 1)
	{
		MO.Velocity.z -= 60.0;	// drift towards bottom
	}

	onground = (MO.Origin.z <= MO.FloorZ);

	if (MO.Class == BloodySkull || MO.Class == IceChunk)
	{
		// Flying bloody skull or flying ice chunk
		ViewHeight = 6.0;
		DeltaViewHeight = 0.0;
		//damagecount = 20;
		if (onground)
		{
#ifdef FIXME
			if (lookdir < 60)
			{
				lookDelta = (60 - lookdir) / 8;
				if (lookDelta < 1 && (Level.Game.level->tictime & 1))
				{
					lookDelta = 1;
				}
				else if (lookDelta > 6)
				{
					lookDelta = 6;
				}
				lookdir += lookDelta;
			}
#endif
		}
	}
	else if (!Actor(MO).bIceDamage)
	{
		// Fall to ground (if not frozen)
		DeltaViewHeight = 0.0;
		if (ViewHeight > 6.0)
		{
			ViewHeight -= 35.0 * deltaTime;
		}
		if (ViewHeight < 6.0)
		{
			ViewHeight = 6.0;
		}
#ifdef FIXME
		if (lookdir > 0)
		{
			lookdir -= 6;
		}
		else if (lookdir < 0)
		{
			lookdir += 6;
		}
		if (abs(lookdir) < 6)
		{
			lookdir = 0;
		}
#endif
	}
	CalcHeight(deltaTime);

	if (Attacker && Attacker != MO)
	{
		// Watch killer
		dir = Actor(MO).FaceActor(Actor(Attacker), &delta);
		if (delta < 10.0)
		{
			// Looking at killer, so fade damage and poison counters
			if (DamageFlash)
			{
				DamageFlash -= deltaTime;
				if (DamageFlash <= 0.0)
					DamageFlash = 0.0;
			}
			if (PoisonCount)
			{
				PoisonCount--;
			}
		}
		delta = delta / 8.0;
		if (delta > 5.0)
		{
			delta = 5.0;
		}
		if (dir)
		{
			// Turn clockwise
			MO.Angles.yaw += delta;
		}
		else
		{	
			// Turn counter clockwise
			MO.Angles.yaw -= delta;
		}
	}
	else if (DamageFlash || PoisonCount)
	{
		if (DamageFlash)
		{
			DamageFlash -= deltaTime;
			if (DamageFlash <= 0.0)
				DamageFlash = 0.0;
		}
		else
		{
			PoisonCount--;
		}
	}

	if (respawn_counter <= 0.0 &&
		(bIsBot || (Buttons & BT_USE || Buttons & BT_ATTACK || Buttons & BT_JUMP )))
	{
		WeaponCShift = 0;
		InvPtr = 0;
		InvCurPos = 0;
		NewTorch = 0;
		NewTorchDelta = 0;
		PlayerState = PST_REBORN;
		Actor(MO)._Special1 = PClass;
		if (Actor(MO)._Special1 > 2)
		{
			Actor(MO)._Special1 = 0;
		}
		// Let the mobj know the player has entered the reborn state.  Some
		// mobjs need to know when it's ok to remove themselves.
		Actor(MO)._Special2 = 666;
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	int newweapon;
	Actor speedA;

	// Deny to do anything while immobile
	if (bUnControllable)
	{
		Buttons = 0;
		ViewOrg.z = MO.Origin.z + PlayerPawn(MO).ViewHeight;
		return;
	}

	if (bIsBot)
	{
		Bot.BotTick(deltaTime);
	}

//[Korax]
	experience = Actor(MO).experience;

	// Copy view angles
	MO.Angles = ViewAngles;

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (Actor(MO).bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		Actor(MO).bJustAttacked = false;
	}

	WorldTimer += deltaTime;
	InventoryTick(deltaTime);

	//
	//[CW] "Targeted enemy hint" feature
	//
	if (PlayerState != PST_DEAD && targetnamehint_enabled)
	{
		TAVec angles;
		angles = MO.Angles;
		Actor(MO).AimLineAttack(angles, 64.0 * 64.0);
		if (Actor(MO).linetarget)
		{
			if (Actor(MO).linetarget.Player)
			{
				int teamfactor = Game::teamfactor_any;
				targetnamehint = Actor(MO).linetarget.Player.PlayerName;
				if (MainGameInfo(Level.Game).GameMode)
					teamfactor = MainGameInfo(Level.Game).GameMode.TeamFactor(Actor(MO), Actor(MO).linetarget);
				if (teamfactor == Game::teamfactor_any)
				{
					if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM)
						targetnamehint_type = targethint_shoot;
					else
						targetnamehint_type = (Colour == Actor(MO).linetarget.Player.Colour) ? targethint_noshoot : targethint_shoot;
				}
				else
					targetnamehint_type = teamfactor == Game::teamfactor_coop ? targethint_noshoot : targethint_shoot;
			}
			else
				targetnamehint = "";
			targetnamehint_counter = 30;	//[CW] this should use real time, i think
		}
		
	}
	if (targetnamehint_counter > 0)
	{
		targetnamehint_counter--;
		if (!targetnamehint_counter)
			targetnamehint = "";
	}


	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	//[CW] trophies timed update
	UpdateTimedTrophies(deltaTime);

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime <= 0.0)
			JumpTime = 0.0;
	}
	if (Morph.bTimer && Morph.MorphTime)
	{
		MorphPlayerThink();
	}

	//  Move around.
	//  ReactionTime is used to prevent movement for a bit after a teleport.
	if (Actor(MO).ReactionTime)
	{
		Actor(MO).ReactionTime -= deltaTime;
		if (Actor(MO).ReactionTime <= 0.0)
			Actor(MO).ReactionTime = 0.0;
		if (Actor(MO).bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel >= 2)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
		if (Powers[pw_speed] && !(Level.XLevel.TicTime & 1) &&
			Length(MO.Velocity) > 12.0 * 35.0)
		{
			speedA = HexenLevelInfo(Level).Spawn(PlayerSpeed, MO.Origin);
			if (speedA)
			{
				speedA.Angles.yaw = MO.Angles.yaw;
				speedA.Translation = MO.Translation;
				speedA.Target = Actor(MO);
				speedA._Special1 = PClass;
				if (speedA._Special1 > 2)
				{
					speedA._Special1 = 0;
				}
				switch (PClass)
				{
				case HexenDefs::PCLASS_FIGHTER:
					speedA.SetState(PlayerSpeed::S_PLAYER_F_SPEED1);
					break;
				case HexenDefs::PCLASS_CLERIC:
					speedA.SetState(PlayerSpeed::S_PLAYER_C_SPEED1);
					break;
				case HexenDefs::PCLASS_MAGE:
					speedA.SetState(PlayerSpeed::S_PLAYER_M_SPEED1);
					break;
				case HexenDefs::PCLASS_HERETIC:
					speedA.SetState(PlayerSpeed::S_PLAYER_H_SPEED1);
					break;
				case HexenDefs::PCLASS_MARINE:
					speedA.SetState(PlayerSpeed::S_PLAYER_D_SPEED1);
					break;
				case HexenDefs::PCLASS_WITCHAVEN:
					speedA.SetState(PlayerSpeed::S_PLAYER_W_SPEED1);
					break;
				case HexenDefs::PCLASS_PIG:
					speedA.SetState(PlayerSpeed::S_PLAYER_P_SPEED1);
					break;
				}
				speedA.FloorClip = MO.FloorClip;
#ifdef FIXME
				if (self == GPlayers[consoleplayer])
				{
					speedA.bHidden = true;
				}
#endif
			}
		}
	}

	CalcHeight(deltaTime);
	
	if (!MO.Sector)
		dprint("Player::PlayerTick : BAD MO.Sector REFERENCE!!");

	if (MO.Sector->special)
	{
		PlayerInSpecialSector(deltaTime);
	}
	PlayerOnSpecialFlat(Actor(MO).GetFloorType());
	PlayerInContents(deltaTime);

	if (MO.Velocity.z <= -35.0 * 35.0 && MO.Velocity.z >= -40.0 * 35.0 &&
		/*!MorphTime*/ PClass != HexenDefs::PCLASS_PIG && !GetSoundPlayingInfo(MO, GetSoundID('*falling')))
	{
		MO.PlaySound('*falling', CHAN_VOICE);
	}

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			P_UseLines();
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	// Morph counter
	if (Morph.bTimer && Morph.MorphTime)
	{
		Morph.MorphTime -= deltaTime;
		if (Morph.MorphTime <= 0.0)
		{
			// Attempt to undo the pig
			Morph.MorphTime = 0.0;
			UndoPlayerMorph();
		}
	}

	// cycle psprites
	MovePsprites(deltaTime);

	// Counters, time dependend power ups.
	if (Powers[pw_invulnerability])
	{
		//[KORAX]
		/*if (PClass == PCLASS_CLERIC)
		{
			if (!(Level.Game.level->tictime & 7) && bShadow && !bDontDraw)
			{
				bShadow = false;
				if (!bAltShadow)
				{
					bDontDraw = true;
 					Actor(MO).bNonShootable = true;
				}
			}
			if (!(Level.Game.level->tictime & 31))
			{
				if (bDontDraw)
				{
					if (!bShadow)
					{
						bShadow = true;
 						bAltShadow = true;
					}
					else
					{
						bDontDraw = false;
 						Actor(MO).bNonShootable = false;
					}
				}
				else
				{
					bShadow = true;
					bAltShadow = false;
				}
			}
			if (bDontDraw)
			{
				MO.Alpha = 0.0;
			}
			else if (bShadow)
			{
				MO.Alpha = 0.333;
			}
			else if (bAltShadow)
			{
				MO.Alpha = 0.666;
			}
			else
			{
				MO.Alpha = 1.0;
			}
		}*/
		//[KORAX]
		Powers[pw_invulnerability] -= deltaTime;
		if (Powers[pw_invulnerability] <= 0.0)
		{
			Powers[pw_invulnerability] = 0.0;
			Actor(MO).bInvulnerable = false;
			//[KORAX]
			/*MO.bReflective = false;
			if (PClass == PCLASS_CLERIC)
			{
				Actor(MO).bNonShootable = false;
				MO.Alpha = 1.0;
			}*/
			//[KORAX]
		}
	}
	if (Powers[pw_invulnerability2])
	{
		if (!(Level.XLevel.TicTime & 1) && bShadow && !bDontDraw)
		{
			bShadow = false;
			if (!bAltShadow)
			{
				bDontDraw = true;
			}
		}
		if (!(Level.XLevel.TicTime & 2))
		{
			if (bDontDraw)
			{
				if (!bShadow)
				{
					bShadow = true;
					bAltShadow = true;
				}
				else
				{
					bDontDraw = false;
				}
			}
			else
			{
				bShadow = true;
				bAltShadow = false;
			}
		}
		if (!(Level.XLevel.TicTime & 3) && bShadow && !bDontDraw)
		{
			bShadow = false;
			if (!bAltShadow)
			{
				bDontDraw = true;
			}
		}
		if (!(Level.XLevel.TicTime & 4))
		{
			if (bDontDraw)
			{
				if (!bShadow)
				{
					bShadow = true;
					bAltShadow = true;
				}
				else
				{
					bDontDraw = false;
				}
			}
			else
			{
				bShadow = true;
				bAltShadow = false;
			}
		}
		if (bDontDraw)
		{
			Actor(MO).Alpha = 0.0;
		}
		else if (bShadow)
		{
			Actor(MO).Alpha = 0.333;
		}
		else if (bAltShadow)
		{
			Actor(MO).Alpha = 0.666;
		}
		else
		{
			Actor(MO).Alpha = 1.0;
		}
		Powers[pw_invulnerability2] -= deltaTime;
		if (Powers[pw_invulnerability2] <= 0.0)
		{
			Powers[pw_invulnerability2] = 0.0;
			Actor(MO).bInvulnerable = false;
			MO.Alpha = 1.0;
		}
	}

	if (Powers[pw_minotaur])
	{
		Powers[pw_minotaur] -= deltaTime;
		if (Powers[pw_minotaur] <= 0.0)
			Powers[pw_minotaur] = 0.0;
	}

	if (Powers[pw_infrared])
	{
		Powers[pw_infrared] -= deltaTime;
		if (Powers[pw_infrared] <= 0.0)
			Powers[pw_infrared] = 0.0;
	}

	if (Powers[pw_speed])
	{
		Powers[pw_speed] -= deltaTime;
		if (Powers[pw_speed] <= 0.0)
			Powers[pw_speed] = 0.0;
	}

	if (Powers[pw_flight] && Level.Game.netgame)
	{
		Powers[pw_flight] -= deltaTime;
		if (Powers[pw_flight] <= 0.0)
		{
			Powers[pw_flight] = 0.0;
			if (MO.Origin.z != MO.FloorZ)
			{
//FIXME             centreing = true;
			}
			MO.bFly = false;
			MO.bNoGravity = false;
		}
	}
	if (Powers[pw_invisibility])
	{
		Powers[pw_invisibility] -= deltaTime;
		if (Powers[pw_invisibility] <= 0.0)
		{
			Powers[pw_invisibility] = 0.0;
			Actor(MO).Alpha = 1.0;
		}
	}
	if (Powers[pw_damage])
	{
		Powers[pw_damage] -= deltaTime;
		if (Powers[pw_damage] <= 0.0)
		{
			Powers[pw_damage] = 0.0;
			Weapon(ViewEnt).active_damage = 0; // [FB] Return to using normal damage
		}
	}

	// [CW]
	if (PoisonCount && Actor(MO).bDormant)
		PoisonCount = 0;

	if (PoisonCount && Level.XLevel.Time - LastPoisonTime >= 0.5)
	{
		PoisonCount -= 5;
		if (PoisonCount < 0)
		{
			PoisonCount = 0;
		}
		LastPoisonTime = Level.XLevel.Time;
		Actor(MO).PoisonDamage(Poisoner, 1, true);
	}

	// [FB] Darkness blinding effect
	if (BlindCount && Level.XLevel.Time - LastBlindTime >= 0.5)
	{
		BlindCount -= 5;
		count += 1;
		if (BlindCount < 0)
		{
			BlindCount = 0;
		}
		LastBlindTime = Level.XLevel.Time;
		if(count % 5 == 0) // [FB] Damage the blinded player after some time
		{
			Actor(MO).BlindDamage(Blinder, 1, true);
		}
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	if (HazardTime)
	{
		HazardTime -= deltaTime;
		if (HazardTime <= 0.0)
			HazardTime = 0.0;
		if (Level.XLevel.Time - LastHazardTime >= 32.0 / 35.0 &&
			HazardTime > 16.0)
		{
			LastHazardTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
	}

	// Handling colourmaps.
	if (Powers[pw_infrared])
	{
		if (Powers[pw_infrared] <= BLINKTHRESHOLD)
		{
			if (ftoi(Powers[pw_infrared] * 35.0) & 8)
			{
				FixedColourmap = 0;
			}
			else
			{
				FixedColourmap = 1;
			}
		}
		else if (!(Level.XLevel.TicTime & 16))
		{
			if (NewTorch)
			{
				if (FixedColourmap + NewTorchDelta > 7 ||
					FixedColourmap + NewTorchDelta < 1 ||
					FixedColourmap == NewTorch)
				{
					NewTorch = 0;
				}
				else
				{
					FixedColourmap += NewTorchDelta;
				}
			}
			else
			{
				NewTorch = (P_Random() & 7) + 1;
				NewTorchDelta = (NewTorch == FixedColourmap) ? 0 :
					((NewTorch > FixedColourmap) ? 1 : -1);
			}
		}
	}
	else
		FixedColourmap = 0;

	if(!bUnControllable && MO.Velocity.x == 0.0 && MO.Velocity.y == 0.0) // [FB] Check for the player camping
	{
		// [CW] No need for camping processing when player has lost all experience
		if (experience > 0)
		{
			pcount++;
			PlayerCamping();
		}
	}
	else
	{
		pcount = 0;
	}
}

//==========================================================================
//
//  PaletteFlash
//
//  Sets the new palette based upon the current values of
// Player.DamageFlash and Player.BonusFlash.
//
//==========================================================================

void PaletteFlash()
{
	CShifts[CSHIFT_Contents] = 0;
	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			CShifts[CSHIFT_Contents] = RGBA(130, 80, 50, 128);
			break;

		case CONTENTS_LAVA:
			CShifts[CSHIFT_Contents] = RGBA(255, 80, 0, 150);
			break;

		case CONTENTS_NUKAGE:
			CShifts[CSHIFT_Contents] = RGBA(50, 255, 50, 150);
			break;

		case CONTENTS_SLIME:
			CShifts[CSHIFT_Contents] = RGBA(0, 25, 5, 150);
			break;

		case CONTENTS_HELLSLIME:
			CShifts[CSHIFT_Contents] = RGBA(255, 80, 0, 150);
			break;

		case CONTENTS_BLOOD:
			CShifts[CSHIFT_Contents] = RGBA(160, 16, 16, 150);
			break;

		case CONTENTS_SLUDGE:
			CShifts[CSHIFT_Contents] = RGBA(128, 160, 128, 150);
			break;

		case CONTENTS_HAZARD:
			CShifts[CSHIFT_Contents] = RGBA(128, 160, 128, 128);
			break;
		}
	}

	// [FB] Black palette for blinding effect
	CShifts[CSHIFT_Blind] = 0;
	if (BlindCount)
	{
		CShifts[CSHIFT_Blind] = RGBA(0, 0, 0, BlindCount);
	}

	CShifts[CSHIFT_Damage] = 0;
	if (DamageFlash)
	{
		int Amount = ftoi(114.0 * DamageFlash);
		if (Amount >= 228)
			Amount = 228;
		CShifts[CSHIFT_Damage] = RGBA(255, 0, 0, Amount);
	}

	CShifts[CSHIFT_Bonus] = 0;
	if (BonusFlash)
	{
		int Amount = ftoi(256.0 * BonusFlash);
		if (Amount >= 128)
			Amount = 128;
		CShifts[CSHIFT_Bonus] = RGBA(215, 186, 68, Amount);
	}

	CShifts[CSHIFT_Poison] = 0;
	if (PoisonCount)
	{
		int Amount = PoisonCount * 160 / 32;
		if (Amount >= 160)
		{
			Amount = 160;
		}
		CShifts[CSHIFT_Poison] = RGBA(56, 118, 46, Amount);
	}

	if (Actor(MO).bIceDamage)
	{
		// Frozen player
		CShifts[CSHIFT_Damage] = RGBA(2, 2, 255, 113);
		CShifts[CSHIFT_Bonus] = 0;
		CShifts[CSHIFT_Poison] = 0;
	}

	CShifts[CSHIFT_Weapon] = WeaponCShift;
}

//==========================================================================
//
//  SetViewPos
//
//==========================================================================

void SetViewPos()
{
	int		i;

	ViewOrg.x = MO.Origin.x;
	ViewOrg.y = MO.Origin.y;

	if (LocalQuakeHappening)
	{
		float intensity;

		intensity = itof(LocalQuakeHappening);
		ViewOrg.x += (Random() - 0.5) * (intensity * 4.0);
		ViewOrg.y += (Random() - 0.5) * (intensity * 4.0);
	}

	ViewAngles = MO.Angles;

	PaletteFlash();

	SBFlags = Keys;
	SBFlags |= ReadyWeapon << HexenDefs::SBF_WPN_SHIFT;
//[Korax]
//	SBFlags |= Pieces << HexenDefs::SBF_PIECES_SHIFT;
	if (Powers[pw_flight])
	{
		if (Powers[pw_flight] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_flight] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_FLIGHT;
		}
	}
	if (Powers[pw_speed])
	{
		if (Powers[pw_speed] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_speed] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_SPEED;
		}
	}
	if (Powers[pw_invulnerability])
	{
		if (Powers[pw_invulnerability] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_invulnerability] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_INVULN;
		}
	}
	if (Powers[pw_minotaur])
	{
		if (Powers[pw_minotaur] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_minotaur] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_MINOTAUR;
		}
	}
	if (Powers[pw_invisibility])
	{
		if (Powers[pw_invisibility] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_invisibility] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_INVISIBILITY;
		}
	}
	if (Powers[pw_damage])
	{
		if (Powers[pw_damage] > BLINKTHRESHOLD ||
			!(ftoi(Powers[pw_damage] * 35.0) & 16))
		{
			SBFlags |= HexenDefs::SBF_POWER_DAMAGE;
		}
	}
	if (MO.bFly)
	{
		SBFlags |= HexenDefs::SBF_IN_FLIGHT;
	}

	ClientSetViewOrg(ViewOrg.x, ViewOrg.y, ViewOrg.z);
}

//==========================================================================
//
//  PlayerUseArtifact
//
//==========================================================================

void PlayerUseArtifact(int arti)
{
//[Korax]
	if (arti == HexenDefs::arti_poisonbag)
	{
		int manause = 10;
		manause = manause * 10 / wisdom;
		if (Mana[HexenDefs::MANA_5] >= manause)
		{
			UseArtiPoisonBag();
			Mana[HexenDefs::MANA_5] -= manause;
		}
		return;
	}
	else if (arti == HexenDefs::arti_blastradius)
	{
		int manause = 10;
		manause = manause * 10 / wisdom;
		if (Mana[HexenDefs::MANA_4] >= manause)
		{
			UseArtiBlastRadius();
			Mana[HexenDefs::MANA_4] -= manause;
		}
		return;
	}
//[Korax]
	int i;

	for (i = 0; i < InventorySlotNum; i++)
	{
		if (Inventory[i].type == arti)
		{
			// Found match - try to use
			if (UseArtifact(arti))
			{
				// Artifact was used - remove it from inventory
				PlayerRemoveArtifact(i);
				if (arti < HexenDefs::arti_firstpuzzitem)
				{
					MO.PlaySound('UseArtifact', CHAN_ITEM);
				}
				else
				{
					MO.PlaySound('PuzzleSuccess', CHAN_ITEM);
				}
				ArtifactFlash = 4;
			}
			else if (arti < HexenDefs::arti_firstpuzzitem)
			{
				// Unable to use artifact, advance pointer
				PlayerNextArtifact();
			}
			break;
		}
	}
}

//==========================================================================
//
//  PlayerNextArtifact
//
//==========================================================================

void PlayerNextArtifact()
{
	InvPtr--;
	if (InvPtr < 6)
	{
		InvCurPos--;
		if (InvCurPos < 0)
		{
			InvCurPos = 0;
		}
	}
	if (InvPtr < 0)
	{
		InvPtr = InventorySlotNum - 1;
		if (InvPtr < 6)
		{
			InvCurPos = InvPtr;
		}
		else
		{
			InvCurPos = 6;
		}
	}
	ReadyArtifact = Inventory[InvPtr].type;
}

//==========================================================================
//
//  PlayerRemoveArtifact
//
//==========================================================================

void PlayerRemoveArtifact(int slot)
{
	int i;

	ArtifactCount--;
	if (!(--Inventory[slot].count))
	{
		// Used last of a type - compact the artifact list
		ReadyArtifact = HexenDefs::arti_none;
		Inventory[slot].type = HexenDefs::arti_none;
		for (i = slot + 1; i < InventorySlotNum; i++)
		{
			Inventory[i - 1].type = Inventory[i].type;
			Inventory[i - 1].count = Inventory[i].count;
		}
		InventorySlotNum--;
		// Set position markers and get next ReadyArtifact
		InvPtr--;
		if (InvPtr < 6)
		{
			InvCurPos--;
			if (InvCurPos < 0)
			{
				InvCurPos = 0;
			}
		}
		if (InvPtr >= InventorySlotNum)
		{
			InvPtr = InventorySlotNum - 1;
		}
		if (InvPtr < 0)
		{
			InvPtr = 0;
		}
		ReadyArtifact = Inventory[InvPtr].type;
	}
}

//==========================================================================
//
//  UseArtifact
//
//  Returns true if the artifact was used.
//
//==========================================================================

bool UseArtifact(int arti)
{
	int i;
	int count;

	switch (arti)
	{
	case HexenDefs::arti_health:
		if (!GiveBody(25))
		{
			return false;
		}
		break;
	case HexenDefs::arti_superhealth:
		if (!GiveBody(100))
		{
			return false;
		}
		break;
	case HexenDefs::arti_invulnerability:
		if (!GivePower(pw_invulnerability))
		{
			return false;
		}
		break;
	case HexenDefs::arti_torch:
		if (!GivePower(pw_infrared))
		{
			return false;
		}
		break;
	case HexenDefs::arti_fly:
		if (!GivePower(pw_flight))
		{
			return false;
		}
		if (MO.Velocity.z <= -35.0 * 35.0)
		{
			// stop falling scream
			MO.StopSound(CHAN_AUTO);
		}
		break;
	case HexenDefs::arti_teleport:
		UseArtiTele();
		break;
	case HexenDefs::arti_egg:
		UseArtiPig();
		break;
	case HexenDefs::arti_healingradius:
		if (!UseArtiHealRadius())
		{
			return false;
		}
		break;
	case HexenDefs::arti_summon:
		UseArtiSummon();
		break;
	case HexenDefs::arti_blastradius:
		UseArtiBlastRadius();
		break;
	case HexenDefs::arti_poisonbag:
		UseArtiPoisonBag();
		break;
	case HexenDefs::arti_teleportother:
		UseArtiTeleportOther();
		break;
	case HexenDefs::arti_speed:
		if (!GivePower(pw_speed))
		{
			return false;
		}
		break;
	case HexenDefs::arti_boostmana:
		if (!GiveMana(HexenDefs::MANA_3, HexenDefs::MAX_MANA))
		{
			return false;
		}
		break;
	case HexenDefs::arti_boostarmor:
		count = 0;

		for (i = 0; i < HexenDefs::NUMARMOR; i++)
		{
			count += GiveArmor(i, 1);	// 1 point per armor type
		}
		if (!count)
		{
			return false;
		}
		break;
	case HexenDefs::arti_invisibility:
		if (!GivePower(pw_invisibility))
		{
			return false;
		}
		break;
	case HexenDefs::arti_damage:
		if (!GivePower(pw_damage))
		{
			return false;
		}
		break;

	case HexenDefs::arti_puzzskull:
	case HexenDefs::arti_puzzgembig:
	case HexenDefs::arti_puzzgemred:
	case HexenDefs::arti_puzzgemgreen1:
	case HexenDefs::arti_puzzgemgreen2:
	case HexenDefs::arti_puzzgemblue1:
	case HexenDefs::arti_puzzgemblue2:
	case HexenDefs::arti_puzzbook1:
	case HexenDefs::arti_puzzbook2:
	case HexenDefs::arti_puzzskull2:
	case HexenDefs::arti_puzzfweapon:
	case HexenDefs::arti_puzzcweapon:
	case HexenDefs::arti_puzzmweapon:
	case HexenDefs::arti_puzzgear1:
	case HexenDefs::arti_puzzgear2:
	case HexenDefs::arti_puzzgear3:
	case HexenDefs::arti_puzzgear4:
		if (UsePuzzleItem(arti - HexenDefs::arti_firstpuzzitem))
		{
			return true;
		}
		else
		{
			centreprint("$txt_usepuzzlefailed");
			return false;
		}
		break;
	default:
		return false;
	}
	return true;
}

//==========================================================================
//
//  UseArtiTele
//
//==========================================================================

void UseArtiTele()
{
	int i;
	float destX;
	float destY;
	float destAngle;

	if (Level.Game.deathmatch)
	{
		i = P_Random() % Level.NumDeathmatchStarts;
		destX = Level.DeathmatchStarts[i].x;
		destY = Level.DeathmatchStarts[i].y;
		destAngle = itof(45 * (Level.DeathmatchStarts[i].angle / 45));
	}
	else
	{
		destX = Level.PlayerStarts[0].x;
		destY = Level.PlayerStarts[0].y;
		destAngle = itof(45 * (Level.PlayerStarts[0].angle / 45));
	}
	Actor(MO).Teleport(vector(destX, destY, Actor::ONFLOORZ), destAngle, true, true,
		false);
	if (Morph.bMorphed && PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
	{
		// Teleporting away will undo any morph effects (pig)
		UndoPlayerMorph();
	}
}

//==========================================================================
//
//  UseArtiPig
//
//==========================================================================

void UseArtiPig()
{
	int i;

	for(i = 0; i < 49; i++)
	{
		SpawnPlayerMissileAngle(EggMissile, AngleMod360(MO.Angles.yaw - 7.5 * itof(i)));
		SpawnPlayerMissileAngle(EggMissile, AngleMod360(MO.Angles.yaw + 7.5 * itof(i)));
	}
}

//==========================================================================
//
//  UseArtiHealRadius
//
//  Do class specific effect for everyone in radius
//
//==========================================================================

bool UseArtiHealRadius()
{
	PlayerPawn P;
	float dist;
	int effective = false;
	int amount;

	foreach MO.AllThinkers(PlayerPawn, P)
	{
		if (!P.bIsPlayer)
			continue;
		if (P.Health <= 0)
			continue;
		dist = MO.DistTo(P);
		if (dist > HEAL_RADIUS_DIST)
		{
			// Out of range
			continue;
		}

		switch (PClass)
		{
		case HexenDefs::PCLASS_FIGHTER:	// Radius armor boost
			if ((Player(P.Player).GiveArmor(HexenDefs::ARMOR_ARMOR, 1)) ||
				(Player(P.Player).GiveArmor(HexenDefs::ARMOR_SHIELD, 1)) ||
				(Player(P.Player).GiveArmor(HexenDefs::ARMOR_HELMET, 1)) ||
				(Player(P.Player).GiveArmor(HexenDefs::ARMOR_AMULET, 1)))
			{
				effective = true;
				P.PlaySound('MysticIncant', CHAN_ITEM);
			}
			break;
		case HexenDefs::PCLASS_CLERIC:	// Radius heal
			amount = 50 + (P_Random() % 50);
			if (Player(P.Player).GiveBody(amount))
			{
				effective = true;
				P.PlaySound('MysticIncant', CHAN_ITEM);
			}
			break;
		case HexenDefs::PCLASS_MAGE:	// Radius mana boost
			amount = 50 + (P_Random() % 50);
			if ((Player(P.Player).GiveMana(HexenDefs::MANA_1, amount)) ||
				(Player(P.Player).GiveMana(HexenDefs::MANA_2, amount)) ||
				(Player(P.Player).GiveMana(HexenDefs::MANA_3, amount)))
			{
				effective = true;
				P.PlaySound('MysticIncant', CHAN_ITEM);
			}
			break;
		case HexenDefs::PCLASS_PIG:
		default:
			break;
		}
	}
	return effective;
}

//==========================================================================
//
//  UseArtiSummon
//
//  [FB] Original function
//
//==========================================================================

void UseArtiSummon()
{
/*	Actor mo;

	mo = SpawnPlayerMissile(SummonMissile);
	if (mo)
	{
		mo.Instigator = MO;
		mo.Velocity.z = 5.0 * 35.0;
	}*/
//                          ^
// [FB] Old behaviour above |
	int i;
	float x;
	float y;
	float z;
	TVec Orig;
	TVec dir;
	Actor fog, newmobj;

	if (!HexenLevelInfo(Level).MinotaurSpotCount)
	{
		// No spots
		return;
	}
	i = P_Random();

	i++;
	x = HexenLevelInfo(Level).MinotaurSpot[i % HexenLevelInfo(Level).MinotaurSpotCount].x;
	y = HexenLevelInfo(Level).MinotaurSpot[i % HexenLevelInfo(Level).MinotaurSpotCount].y;
	z = HexenLevelInfo(Level).MinotaurSpot[i % HexenLevelInfo(Level).MinotaurSpotCount].z;
	dir = HexenLevelInfo(Level).MinotaurSpot[i % HexenLevelInfo(Level).MinotaurSpotCount] /*-Origin*/;
	Orig = vector(x, y, z);

	fog = HexenLevelInfo(Level).Spawn(TeleportFog, Orig);
	fog.PlaySound('Teleport', CHAN_VOICE);
	newmobj = HexenLevelInfo(Level).Spawn(SummonMissile, Orig);
	newmobj.Origin = Orig;
	newmobj.Angles.yaw = HexenLevelInfo(Level).MinotaurSpot_angle[i % HexenLevelInfo(Level).MinotaurSpotCount];
	newmobj.Velocity.z = 5.0 * 35.0;
	newmobj.Target = Actor(MO);
	newmobj.bTelestomp = true;
}

//==========================================================================
//
//  UseArtiBlastRadius
//
//  Blast all mobj things away
//
//==========================================================================

void UseArtiBlastRadius()
{
	Actor mo;
	float dist;

	MO.PlaySound('BlastRadius', CHAN_ITEM);
	HexenLevelInfo(Level).NoiseAlert(Actor(MO), Actor(MO));

	switch(altWeaponMode[HexenDefs::ALTFIRE_BLASTRADIUS])
	{
	case HexenDefs::ALTFIRE_BLASTRADIUS_DEFAULT:

		foreach MO.AllThinkers(Actor, mo)
		{
			if ((mo == MO) || mo.bBoss)
			{
				// Not a valid monster
				continue;
			}
			if ((mo.Class == PoisonCloud) ||	// poison cloud
				(mo.Class == HolyFX) ||		// holy fx
				mo.bIceCorpse)			// frozen corpse
			{
				// Let these special cases go
			}
			else if (mo.bCountKill && (mo.Health <= 0))
			{
				continue;
			}
			else if (!mo.bCountKill && !mo.bIsPlayer && !mo.bMissile)
			{
				// Must be monster, player, or missile
				continue;
			}
			if (mo.bDormant)
			{
				continue;	// no dormant creatures
			}
			if ((mo.Class == WraithBuried) && mo.bHidden)
			{
				continue;	// no underground wraiths
			}
			if ((mo.Class == WaterSplashBase) || (mo.Class == WaterSplash))
			{
				continue;
			}
			if (mo.Class == Serpent || mo.Class == SerpentLeader)
			{
				continue;
			}
			dist = MO.DistTo(mo);
			if (dist > BLAST_RADIUS_DIST)
			{
				// Out of range
				continue;
			}
			mo.Blast(Actor(MO), BLAST_FULLSTRENGTH);
		}
		break;
	}
}

//==========================================================================
//
//  UseArtiPoisonBag
//
//==========================================================================

void UseArtiPoisonBag()
{
	float angle;
	Actor bomb;

	angle = MO.Angles.yaw;

	switch(altWeaponMode[HexenDefs::ALTFIRE_POISONBAG])
	{
	case HexenDefs::ALTFIRE_POISONBAG_GRENADE:		
	
	/*if (PClass == HexenDefs::PCLASS_CLERIC || PClass == HexenDefs::PCLASS_WITCHAVEN)
	{*/
		bomb = HexenLevelInfo(Level).Spawn(PoisonBag, MO.Origin +
			vector(16.0 * cos(angle), 24.0 * sin(angle), 8.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Target = Actor(MO);
		}
		break;
		
	case HexenDefs::ALTFIRE_POISONBAG_BOMB:
	
	/*}
	else if (PClass == HexenDefs::PCLASS_MAGE || PClass == HexenDefs::PCLASS_HERETIC)
	{*/
		bomb = HexenLevelInfo(Level).Spawn(FireBomb, MO.Origin +
			vector(16.0 * cos(angle), 24.0 * sin(angle), 8.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Target = Actor(MO);
		}
		break;

	case HexenDefs::ALTFIRE_POISONBAG_CLOUD:	
	/*}
	else	// PCLASS_FIGHTER && PCLASS_MARINE, obviously (also pig, not so obviously)
	{*/
		bomb = HexenLevelInfo(Level).Spawn(ThrowingBomb, MO.Origin +
			vector(0.0, 0.0, 35.0 - MO.FloorClip));
		if (bomb)
		{
			bomb.Angles.yaw = AngleMod360(MO.Angles.yaw +
				(Random() * 2.0 - 1.0) * 45.0 / 8.0);
			bomb.Velocity.z =
				(4.0 + 173.0 * -tan(MO.Angles.pitch) / 16.0) * 35.0;
			bomb.Origin.z += 173.0 * -tan(MO.Angles.pitch) / 16.0;
			bomb.Velocity.x += bomb.Speed * cos(bomb.Angles.yaw);
			bomb.Velocity.y += bomb.Speed * sin(bomb.Angles.yaw);
			bomb.Velocity.x += MO.Velocity.x / 2.0;
			bomb.Velocity.y += MO.Velocity.y / 2.0;
			bomb.Target = Actor(MO);
			bomb.StateTime -= Random() * 0.1;
			bomb.CheckMissileSpawn();
		}
		break;
	}
}

//==========================================================================
//
//  UseArtiTeleportOther
//
//==========================================================================

void UseArtiTeleportOther()
{
	Actor mo;

	mo = SpawnPlayerMissile(TeleportOtherFX1);
	if (mo)
	{
		mo.Target = Actor(MO);
	}
}

//==========================================================================
//
//  SpawnPlayerMissile
//
//  Tries to aim at a nearby monster
//
//==========================================================================

Actor SpawnPlayerMissile(class<Actor> type)
{
	return SpawnPlayerMissile2(type, true);
}

//==========================================================================
//
//  SpawnPlayerMissile2
//
//  Tries to aim at a nearby monster
//
//==========================================================================

Actor SpawnPlayerMissile2(class<Actor> type, bool explode)
{
	Actor MissileMobj;
	TAVec angles;
	TVec vforward;
	TVec org;

	// Try to find a target
	vforward = Actor(MO).Aim(16.0 * 64.0);
	VectorAngles(&vforward, &angles);
	org = MO.Origin;
	if (type == LightningFloor)
	{
		org.z = Actor::ONFLOORZ;
		angles.pitch = 0.0;
		vforward.z = 0.0;
		vforward = Normalise(vforward);
	}
	else if (type == LightningCeiling)
	{
		org.z = Actor::ONCEILINGZ;
		angles.pitch = 0.0;
		vforward.z = 0.0;
		vforward = Normalise(vforward);
	}
	else
	{
		org.z = MO.Origin.z + 4.0 * 8.0 - tan(MO.Angles.pitch);
		org.z -= MO.FloorClip;
	}
	MissileMobj = HexenLevelInfo(Level).Spawn(type, org);
	MissileMobj.Target = Actor(MO);
	MissileMobj.Angles = angles;
	MissileMobj.Velocity = vforward * MissileMobj.Speed;
	if (MissileMobj.Class == MageWandMissile || MissileMobj.Class == MageWandRedMissile ||
		 MissileMobj.Class == MageWandGreyMissile || MissileMobj.Class == ClericFlameMissile)
	{
		// Ultra-fast ripper spawning missile
		MissileMobj.Origin += MissileMobj.Velocity * Level.Game.frametime / 8.0;
	}
	else
	{
		// Normal missile
		MissileMobj.Origin += MissileMobj.Velocity * Level.Game.frametime / 2.0;
	}
	if (explode)
	{
		if (!MissileMobj.TryMove(MissileMobj.Origin))
		{
			// Exploded immediately
			MissileMobj.ExplodeMissile();
			return none;
		}
	}
	return MissileMobj;
}

//===========================================================================
//
//  SpawnPlayerMissileAngle
//
//===========================================================================

Actor SpawnPlayerMissileAngle(class<Actor> type, float angle)
{
	TAVec angles;
	TVec vforward;
	Actor th;
	TVec org;

	//
	// see which target is to be aimed at
	//
	vforward = Actor(MO).Aim(16.0 * 64.0, angle);
	VectorAngles(&vforward, &angles);
	org = MO.Origin;
	org.z = MO.Origin.z + 4.0 * 8.0 - tan(MO.Angles.pitch);
	org.z -= MO.FloorClip;
	th = HexenLevelInfo(Level).Spawn(type, org);
	th.Target = Actor(MO);
	th.Angles = angles;
	th.Velocity = vforward * th.Speed;
	return th.CheckMissileSpawn() ? th : none;
}

//===========================================================================
//
//  SpawnPlayerMissileAngleXYZ
//
//===========================================================================

Actor SpawnPlayerMissileAngleXYZ(TVec org, class<Actor> type, float angle)
{
	Actor th;
	TAVec angles;
	TVec vforward;

	//
	// see which target is to be aimed at
	//
	vforward = Actor(MO).Aim(16.0 * 64.0, angle);
	VectorAngles(&vforward, &angles);
	org.z += 4.0 * 8.0 - tan(MO.Angles.pitch);
	org.z -= MO.FloorClip;
	th = HexenLevelInfo(Level).Spawn(type, org);
	th.Target = Actor(MO);
	th.Angles = angles;
	th.Velocity = vforward * th.Speed;
	return th.CheckMissileSpawn() ? th : none;
}

//============================================================================
//
//  AdjustPlayerAngle
//
//============================================================================

void AdjustPlayerAngle()
{
	float angle;
	float difference;

	angle = atan2(Actor(MO).linetarget.Origin.y - MO.Origin.y,
		Actor(MO).linetarget.Origin.x - MO.Origin.x);
	difference = AngleMod180(angle - MO.Angles.yaw);
	if (fabs(difference) > 5.0)
	{
		MO.Angles.yaw += difference > 0.0 ? 5.0 : -5.0;
	}
	else
	{
		MO.Angles.yaw = angle;
	}
	bFixAngle = true;
}

//****************************************************************************
//
//  WEAPON UTILITES
//
//****************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(int NewWeapon)
{
	ReadyWeapon = NewWeapon;
	PendingWeapon = HexenDefs::wp_nochange;

	if (ViewEnt)
	{
		ViewEnt.Destroy();
	}
	ViewEnt = Weapon(HexenLevelInfo(Level).Spawn(
		MainGameInfo(Level.Game).WeaponClasses[
		NewWeapon * HexenDefs::NUMCLASSES + PClass], vector(0.0, 0.0, 0.0)));
	ViewEnt.Owner = MO;
	PSpriteSY = Weapon(ViewEnt).PSpriteSY;
}

//==========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
//
//==========================================================================

void BringUpWeapon()
{
	PendingWeapon = HexenDefs::wp_nochange;
	ViewStates[ps_weapon].SY = Weapon::WEAPONBOTTOM;
	SetViewState(ps_weapon, Weapon(ViewEnt).GetUpState());
}

//==========================================================================
//
//  DropWeapon
//
//  The player died, so put the weapon away.
//
//==========================================================================

void DropWeapon()
{
	SetViewState(ps_weapon, Weapon(ViewEnt).GetDownState());
}

//==========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player
//
//==========================================================================

void SetupPsprites()
{
	int i;

	// Remove all psprites
	for (i = 0; i < NUMPSPRITES; i++)
	{
		SetViewState(i, none);
	}
	// Spawn the ready weapon
	BringUpWeapon();
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine
//
//==========================================================================

void MovePsprites(float deltaTime)
{
//[Korax]
	if (Weapon(ViewEnt).dex_mod)
	{
		if (dexterity < 6)
			deltaTime *= 0.8;
		else if (dexterity > 34)
			deltaTime *= 3.5;
		else if (dexterity > 24)
			deltaTime *= 2.5;
		else if (dexterity > 14)
			deltaTime *= 1.5;
	}
	AdvanceViewStates(deltaTime);
	ViewStates[ps_flash].SX = ViewStates[ps_weapon].SX;
	ViewStates[ps_flash].SY = ViewStates[ps_weapon].SY;
}

//==========================================================================
//
//  CheckMana
//
//  Returns true if there is enough mana to shoot.  If not, selects the
// next weapon to use.
//
//==========================================================================

bool CheckMana()
{
	/*Weapon cur_weap;

	cur_weap = Weapon(player.ViewEnt);
	if (cur_weap.Mana == MANA_NONE || cur_weap.needsnomana 
		|| IsWeaponManaUsable(player, cur_weap))*/
	if (GetWeaponMUse(ReadyWeapon))
	{
		return true;
	}
	centreprint("No mana");
	// out of mana, pick a weapon to change to
	do
	{
		if (WeaponOwned[HexenDefs::WP_EIGHTH] && GetWeaponMUse(HexenDefs::WP_EIGHTH))
		{
			PendingWeapon = HexenDefs::WP_EIGHTH;
		}
		else if (WeaponOwned[HexenDefs::WP_SEVENTH] && GetWeaponMUse(HexenDefs::WP_SEVENTH))
		{
			PendingWeapon = HexenDefs::WP_SEVENTH;
		}
		else if (WeaponOwned[HexenDefs::WP_SIXTH] && GetWeaponMUse(HexenDefs::WP_SIXTH))
		{
			PendingWeapon = HexenDefs::WP_SIXTH;
		}
		else if (WeaponOwned[HexenDefs::WP_FIFTH] && GetWeaponMUse(HexenDefs::WP_FIFTH))
		{
			PendingWeapon = HexenDefs::WP_FIFTH;
		}
		else if (WeaponOwned[HexenDefs::WP_FOURTH] && GetWeaponMUse(HexenDefs::WP_FOURTH))
		{
			PendingWeapon = HexenDefs::WP_FOURTH;
		}
		else if (WeaponOwned[HexenDefs::WP_THIRD] && GetWeaponMUse(HexenDefs::WP_THIRD))
		{
			PendingWeapon = HexenDefs::WP_THIRD;
		}
		else if (WeaponOwned[HexenDefs::WP_SECOND] && GetWeaponMUse(HexenDefs::WP_SECOND))
		{
			PendingWeapon = HexenDefs::WP_SECOND;
		}
		else 
		{
			PendingWeapon = HexenDefs::WP_FIRST;
		}
	}
	while (PendingWeapon == HexenDefs::wp_nochange);
	SetViewState(ps_weapon, Weapon(ViewEnt).GetDownState());
	return false;
}

//==========================================================================
//
//  FireWeapon
//
//==========================================================================

void FireWeapon()
{
	if (!CheckMana())
	{
		return;
	}

	//[CW] store attack position for possible accuracy trophy
	Trophies.shot_position = MO.GetCentre();

	MO.SetState(Actor(MO).MissileState);
	SetViewState(ps_weapon, Weapon(ViewEnt).GetAttackState());
	HexenLevelInfo(Level).NoiseAlert(Actor(MO), Actor(MO));
}

//==========================================================================
//
//  ActivateMorphWeapon
//
//==========================================================================

void ActivateMorphWeapon()
{
	switch (PClass)
	{
	case HexenDefs::PCLASS_HERESIARCH:
		WeaponOwned[HexenDefs::WP_FIRST] = true;
		WeaponOwned[HexenDefs::WP_SECOND] = true;
		WeaponOwned[HexenDefs::WP_THIRD] = true;
		WeaponOwned[HexenDefs::WP_FOURTH] = true;
		WeaponOwned[HexenDefs::WP_FIFTH] = true;
		WeaponOwned[HexenDefs::WP_SIXTH] = true;
		WeaponOwned[HexenDefs::WP_SEVENTH] = true;
		WeaponOwned[HexenDefs::WP_EIGHTH] = true;
		Mana[HexenDefs::MANA_1] = 100;
		Mana[HexenDefs::MANA_2] = 100;
		Mana[HexenDefs::MANA_3] = 100;
		Mana[HexenDefs::MANA_4] = 30;
		Mana[HexenDefs::MANA_5] = 30;
		break;
	case HexenDefs::PCLASS_PIG:
		break;
	}

	ViewStates[ps_weapon].SY = Weapon::WEAPONTOP;
	SetWeapon(HexenDefs::WP_FIRST);	// Snout is the first weapon
	SetViewState(ps_weapon, Weapon(ViewEnt).ReadyState);
}

//==========================================================================
//
//  PostMorphWeapon
//
//==========================================================================

void PostMorphWeapon(int weapon)
{
	SetWeapon(weapon);
	ViewStates[ps_weapon].SY = Weapon::WEAPONBOTTOM;
	SetViewState(ps_weapon, Weapon(ViewEnt).UpState);
}

//==========================================================================
//
//  GetWeaponManaUse
//
//==========================================================================

int GetWeaponManaUse(Weapon weapon)
{
	int manause = weapon.manause;
	if (weapon.wis_mod)
	{
		manause = manause * 10 / wisdom;
	}
	// [FB] Always use mana, no matter what
	if(manause < 1)
	{
		manause = 1;
	}
	return manause;
}

//==========================================================================
//
//  GetWeaponClassManaUse
//
//==========================================================================

int GetWeaponClassManaUse(class<Weapon> weapon)
{
	int manause = weapon.default.manause;
	if (weapon.default.wis_mod)
	{
		manause = manause * 10 / wisdom;
	}
	// [FB] Always use mana, no matter what
	if(manause < 1)
	{
		manause = 1;
	}
	return manause;
}

//==========================================================================
//
//  GetWeaponMUse
//
//==========================================================================

bool GetWeaponMUse(int weapon)
{
	class<Weapon>	w;

	w = GetWea(weapon);
	return w.default.Mana == HexenDefs::MANA_NONE || w.default.needsnomana ||
		(w.default.Mana == HexenDefs::NUMMANA ?
			Mana[HexenDefs::MANA_1] >= GetWeaponClassManaUse(w) &&
			Mana[HexenDefs::MANA_2] >= GetWeaponClassManaUse(w) &&
			Mana[HexenDefs::MANA_3] >= GetWeaponClassManaUse(w) :
			Mana[w.default.Mana] >= GetWeaponClassManaUse(w));
}

//==========================================================================
//
//  GetWea
//
//==========================================================================

class<Weapon> GetWea(int weapon)
{
	return MainGameInfo(Level.Game).WeaponClasses[weapon * HexenDefs::NUMCLASSES + PClass];
}

//==========================================================================
//
//  GetWeaponPickupText
//
//==========================================================================

string GetWeaponPickupText(int weapon)
{
	return GetWea(weapon).default.PickUpMessage;
}

//==========================================================================
//
//  P_UseLines
//
//  Looks for special lines in front of the player to activate.
//
//==========================================================================

void P_UseLines()
{
	float x1;
	float y1;
	float x2;
	float y2;
	TVec useDir;
	intercept_t* in;

	AngleVector(&MO.Angles, &useDir);
	x1 = MO.Origin.x;
	y1 = MO.Origin.y;
	x2 = x1 + USERANGE * useDir.x;
	y2 = y1 + USERANGE * useDir.y;
	foreach MO.PathTraverse(in, x1, y1, x2, y2, PT_ADDLINES)
	{
		line_t* line;
		float pheight;
		TVec hit_point;
		opening_t *open;

		line = in->line;
		if (!line->special || (Level.GET_SPAC(line->flags) != SPAC_USE &&
			Level.GET_SPAC(line->flags) != SPAC_USETHROUGH))
		{
			hit_point = MO.Origin + (USERANGE * in->frac) * useDir;
			open = LineOpenings(line, hit_point);
			open = FindOpening(open, hit_point.z + MO.Height / 2.0,
				hit_point.z + MO.Height);
			if (!open)
			{
				if (PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
				{
					MO.PlaySound('PigActive1', CHAN_VOICE);
				}
				else
				{
					MO.PlaySound('*usefail', CHAN_VOICE);
				}
				break;	// can't use through a wall
			}
			pheight = MO.Origin.z + MO.Height / 2.0;
			if ((open->top < pheight) || (open->bottom > pheight))
			{
				if (PClass == HexenDefs::PCLASS_PIG/*MorphTime*/)
				{
					MO.PlaySound('PigActive1', CHAN_VOICE);
				}
				else
				{
					MO.PlaySound('*usefail', CHAN_VOICE);
				}
			}
			continue;	// not a special line, but keep checking
		}

		if (PointOnPlaneSide(MO.Origin, line) == 1)
		{
			//	Don't use back side unless it's a two-sided line.
			if (line->backsector != NULL)
				continue;
			break;
		}

		HexenLevelInfo(Level).ActivateLine(line, Actor(MO), 0, SPAC_USE);

		//WAS can't use more than one special line in a row
		//jff 3/21/98 NOW multiple use allowed with enabling line flag
		if (Level.GET_SPAC(line->flags) != SPAC_USETHROUGH)
		{
			break;
		}
	}
}

//==========================================================================
//
//  UsePuzzleItem
//
//  USING A PUZZLE ITEM
//
//  Returns true if the puzzle item was used on a line or a thing.
//
//==========================================================================

bool UsePuzzleItem(int itemType)
{
	float x1, y1, x2, y2;
	int PuzzleItemType;
	bool PuzzleActivated;
	TVec PuzzleUseDir;
	intercept_t* in;

	PuzzleItemType = itemType;
	PuzzleActivated = false;
	AngleVector(&MO.Angles, &PuzzleUseDir);
	x1 = MO.Origin.x;
	y1 = MO.Origin.y;
	x2 = x1 + USERANGE * PuzzleUseDir.x;
	y2 = y1 + USERANGE * PuzzleUseDir.y;
	foreach MO.PathTraverse(in, x1, y1, x2, y2, PT_ADDLINES | PT_ADDTHINGS)
	{
		Actor mobj;
		TVec hit_point;
		opening_t *open;

		if (in->bIsALine)
		{
			// Check line
			hit_point = MO.Origin + (USERANGE * in->frac) * PuzzleUseDir;
			if (in->line->special != LNSPEC_UsePuzzleItem)
			{
				open = LineOpenings(in->line, hit_point);
				if (!open || open->range <= 0.0)
				{
					if (MO.bIsPlayer)
					{
						MO.PlaySound('*puzzfail', CHAN_VOICE);
					}
					break;	// can't use through a wall
				}
				continue;	// Continue searching
			}
			if (PointOnPlaneSide(MO.Origin, in->line) == 1)
			{
				// Don't use back sides
				break;
			}
			if (PuzzleItemType != in->line->arg1)
			{
				// Item type doesn't match
				break;
			}
			MO.XLevel.StartACS(in->line->arg2, 0, in->line->arg3, in->line->arg4,
				in->line->arg5, MO, in->line, 0, false, false);
			in->line->special = 0;
			PuzzleActivated = true;
			break;	// Stop searching
		}
		// Check thing
		mobj = Actor(in->Thing);
		if (mobj.Special != LNSPEC_UsePuzzleItem)
		{
			// Wrong special
			continue;
		}
		if (PuzzleItemType != mobj.Args[0])
		{
			// Item type doesn't match
			continue;
		}
		MO.XLevel.StartACS(mobj.Args[1], 0, mobj.Args[2], mobj.Args[3], mobj.Args[4],
			MO, NULL, 0, false, false);
		mobj.Special = 0;
		PuzzleActivated = true;
		break;	// Stop searching
	}
	return PuzzleActivated;
}

//==========================================================================
//
//  Cheat_God
//
//==========================================================================

void Cheat_God()
{
	Cheats ^= CF_GODMODE;
	if (Cheats & CF_GODMODE)
	{
		cprint("GOD MODE ON");
	}
	else
	{
		cprint("GOD MODE OFF");
	}
}

//==========================================================================
//
//  Cheat_NoClip
//
//==========================================================================

void Cheat_NoClip()
{
	Cheats ^= CF_NOCLIP;
	if (Cheats & CF_NOCLIP)
	{
		cprint("NO CLIPPING ON");
	}
	else
	{
		cprint("NO CLIPPING OFF");
	}
}

//==========================================================================
//
//  Cheat_Gimme
//
//==========================================================================

void Cheat_Gimme()
{
	if (Cmd_CheckParm("weapons"))
	{
		int i;

/*		for (i = 0; i < NUMARMOR; i++)
		{
			ArmorPoints[i] = ArmorIncrement[PClass * NUMARMOR + i];
		}
*/
		ArmorPoints = 200;
		for (i = 0; i < HexenDefs::NUMWEAPONS; i++)
		{
			WeaponOwned[i] = true;
		}
		for (i = 0; i < HexenDefs::NUMMANA; i++)
		{
			Mana[i] = HexenDefs::MAX_MANA;
		}
//		Pieces = 7;
		cprint("ALL WEAPONS");
	}
	if (Cmd_CheckParm("health"))
	{
		/*if (PClass == HexenDefs::PCLASS_PIG)
		{
			MO.Health = MAXMORPHHEALTH;
			Health = MO.Health;
		}
		else
		{*/
			MO.Health = max_health;
			HealthBig = MO.Health;
			Health = HealthBig > 0xff ? 0xff : HealthBig;
		//}
		cprint("FULL HEALTH");
	}
	if (Cmd_CheckParm("keys"))
	{
		Keys = 2047;
		cprint("ALL KEYS");
	}
	if (Cmd_CheckParm("artifacts"))
	{
		int i;
		int j;

		for (i = HexenDefs::arti_none + 1; i < HexenDefs::arti_firstpuzzitem; i++)
		{
			for (j = 0; j < 25; j++)
			{
				GiveArtifact(i);
			}
		}
		cprint("ALL ARTIFACTS");
	}
	if (Cmd_CheckParm("puzzles"))
	{
		int i;

		for (i = HexenDefs::arti_firstpuzzitem; i < HexenDefs::NUMARTIFACTS; i++)
		{
			GiveArtifact(i);
		}
		cprint("ALL ARTIFACTS");
	}
	if (Cmd_CheckParm("exp"))
	{
		GiveExperience(10000);
		cprint("LOTS OF EXP");
	}
}

//==========================================================================
//
//  P_Massacre
//
//  Kills all monsters.
//
//==========================================================================

int P_Massacre()
{
	int count;
	Actor mo;

	count = 0;
	foreach MO.AllThinkers(Actor, mo)
	{
		if (mo.bCountKill && mo.Health > 0)
		{
			mo.bNonShootable = false;
			mo.bInvulnerable = false;
			mo.bShootable = true;
			mo.Damage(none, none, 10000);
			count++;
		}
	}
	return count;
}

//==========================================================================
//
//  Cheat_KillAll
//
//==========================================================================

void Cheat_KillAll()
{
	cprint("%d MONSTERS KILLED", P_Massacre());
}

//==========================================================================
//
//  Cheat_Morph
//
//==========================================================================

void Cheat_Morph()
{
	if (Morph.bMorphed/*MorphTime*/)
	{
		UndoPlayerMorph();
	}
	else
	{
		Actor(MO).MorphPlayer(HexenDefs::PCLASS_PIG, PlayerPig, false, false);
	}
	cprint("SQUEAL!!");
}

//==========================================================================
//
//  Cheat_NoWeapons
//
//==========================================================================

void Cheat_NoWeapons()
{
	int i;

	if (Morph.bMorphed/*MorphTime*/)
	{
		return;
	}
	for (i = 1; i < 4; i++)
	{
		WeaponOwned[i] = false;
	}
	PendingWeapon = HexenDefs::WP_FIRST;
//	Pieces = 0;
	cprint("CHEATER - YOU DON'T DESERVE WEAPONS");
}

//==========================================================================
//
//  Cheat_Class
//
//==========================================================================

void Cheat_Class()
{
	int i;
	int pclass;

	if (Morph.bMorphed/*MorphTime*/)
	{
		// don't change class if the player is morphed
		return;
	}
	if (Cmd_CheckParm("fighter"))
		pclass = HexenDefs::PCLASS_FIGHTER;
	else if (Cmd_CheckParm("cleric"))
		pclass = HexenDefs::PCLASS_CLERIC;
	else if (Cmd_CheckParm("mage"))
		pclass = HexenDefs::PCLASS_MAGE;
	else if (Cmd_CheckParm("heretic"))
		pclass = HexenDefs::PCLASS_HERETIC;
	else if (Cmd_CheckParm("marine"))
		pclass = HexenDefs::PCLASS_MARINE;
	else if (Cmd_CheckParm("witchaven"))
		pclass = HexenDefs::PCLASS_WITCHAVEN;
	else
	{
		cprint("INVALID PLAYER CLASS");
		return;
	}
	PClass = pclass;
/*	for (i = 0; i < NUMARMOR; i++)
	{
		ArmorPoints[i] = 0.0;
	}
*/
	ArmorPoints = 0;
	BaseClass = pclass;
	PostMorphWeapon(HexenDefs::WP_FIRST);
	SetCvar('class', pclass);
	SB_Start();
}

//==========================================================================
//
//  SetFOV
//
//==========================================================================

void SetFOV(int FOV)
{
	SetCvar('FOV', FOV);
}

//==========================================================================
//
//  CallLocalSound
//
//	[CW]
//
//==========================================================================

void CallLocalSound(int sound, bool put_in_queue, float delay)
{
	ClientGameBaseEx(ClGame).PlayAnouncerSound(sound, put_in_queue, delay);
}

//==========================================================================
//
//  UpdateScore
//
//  [CW] Updates current player's score.
//	 Scoring event list is defined in 'hexndefs.vc'.
//
//==========================================================================

void UpdateScore(int event)
{
	int mod = 0;

	switch(event)
	{
		case HexenDefs::scoreevent_PlayerDiscard:
		
			for (mod = 0; mod < MAXPLAYERS; mod++)
			{
				if (!Team.Players[mod] || mod == MAXPLAYERS - 1)
				{
					if (mod)
					   mod = Team.Players[mod - 1].Score - Score - 1;
					else
					   mod = 0;
					break;
				}
			}
			break;
		
		case HexenDefs::scoreevent_PlayerReset:
			mod = -Score;
			break;	// this should always set Score to 0
		case HexenDefs::scoreevent_Frag:
		case HexenDefs::scoreevent_Kill:
		case HexenDefs::scoreevent_KthHeresiarchDeath:
			mod = 1;
			break;
		case HexenDefs::scoreevent_FtbGoal:
			mod = 5;
			break;
		case HexenDefs::scoreevent_FtbSelfGoal:
			mod = -5;
			break;
		case HexenDefs::scoreevent_SelfFrag:
		case HexenDefs::scoreevent_TeamFrag:
			mod = -1;
			break;
		case HexenDefs::scoreevent_KthHeresiarchFrag:
			// [CW] This is an interesting case: whoever this called Player is,
			//		frag should go to Heresiarch and no one else!
			if (MainGameInfo(Level.Game).GameMode && MainGameInfo(Level.Game).GameMode.IsA('GameKTH'))
			{
				Player plH = GameKTH(MainGameInfo(Level.Game).GameMode).plHeresiarch;
				if (self == plH)
					mod = 1;
				else
					plH.UpdateScore(HexenDefs::scoreevent_KthHeresiarchFrag);
			}
			break;
		case HexenDefs::scoreevent_CtfCapture:
			mod = 5;
			break;
		case HexenDefs::scoreevent_CtfSaveFlag:
			mod = 2;
			break;
	}

	Score += mod;

	if (Level && MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM && Team)
	{
		Team.UpdateScore(event);
		// update team stats for clientstate	
		TeamFrags = Team.Frags;
		TeamKills = Team.Kills;
	}

	UpdateCompetition();
	// No player-referred announces in team games
	if (MainGameInfo(Level.Game).GameType < HexenDefs::NUMGAMESNOTEAM)
		CompetitionAnnouncements(Score - mod);
	
	if (mod > 0)
		MainGameInfo(Level.Game).bCompetitionOpened = true;

	if (Level)	// in case score is somehow updated between levels
		MainGameInfo(Level.Game).ScoreboardsUpdate(none);
}

//==========================================================================
//
//  CompetitionAnnouncements
//
//  [CW]
//
//==========================================================================

void CompetitionAnnouncements(int oldScore)
{
	int i;
			
	if (Score > oldScore)
	{
		// Is this player a new and only leader?
		if (PlayerPosition == 0)
		{
			if (Team.Players[1] && Team.Players[1].Score >= oldScore)
			{
				// [FB] Announce new leader!
				CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
				centreprint("You lead!");
				
				// Anyone lost the Lead?
				int oldLeaderScore = Team.Players[1].Score;
				for (i = 1; i < MAXPLAYERS; i++)
				{
					if (!Team.Players[i])
						break;
					if (Team.Players[i].Score == oldLeaderScore)
					{
					   Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_lost_the_lead, true, HexenDefs::STANDART_LSQ_DELAY);
					   Team.Players[i].centreprint("You lost the lead!");
					}
					else
					   break;
				}
			}
			else if (Score > 0 && !MainGameInfo(Level.Game).bCompetitionOpened)
			{
				// [FB] Announce new leader!
				CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
				centreprint("You lead!");
			}
		}
		// Is he tied for the lead then?
		else
		{
			if (Score == Team.Players[0].Score)
			{
				// [FB] Tied for the lead!
				CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
				centreprint("You are tied for the lead!");
				
				// If there was a single leader, notify him that he is tied for the lead now
				// (elsewise there's already a tied group)
				if (PlayerPosition == 1)
				{
					Team.Players[0].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
					Team.Players[0].centreprint("You are tied for the lead!");
				}
			}
		}
	}
	// Score may drop down in case of self-frag or similar events
	else if (Score < oldScore)
	{
		// Is he tied for the lead?
		if (PlayerPosition == 0)
		{
			if (Team.Players[1] && Team.Players[1].Score == Score)
			{
				CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
				centreprint("You are tied for the lead!");
				
				// Who else is tied?
				for (i = 1; i < MAXPLAYERS; i++)
				{
					if (!Team.Players[i])
						break;
					if (Team.Players[i].Score == Score)
					{
					   Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
					   Team.Players[i].centreprint("You are tied for the lead!");
					}
					else
					   break;
				}
			}
		}
		// Was he a single leader or tied?
		else if (Team.Players[0].Score <= oldScore)
		{
			// [FB] You've lost the lead!
			CallLocalSound(HexenDefs::lsid_gen_an_y_lost_the_lead, true, HexenDefs::STANDART_LSQ_DELAY);
			centreprint("You lost the lead!");
			
			// If he was a single leader, some others have the lead now
			if (Team.Players[0].Score < oldScore)
			{
				int newLeaderScore = Team.Players[0].Score;
				if (PlayerPosition != 1 && Team.Players[1].Score == newLeaderScore)
				{
					for (i = 0; i < PlayerPosition; i++)
					{
						if (Team.Players[i].Score < newLeaderScore)
							break;
						Team.Players[i].CallLocalSound(HexenDefs::lsid_gen_an_y_tied_ftl, true, HexenDefs::STANDART_LSQ_DELAY);
						Team.Players[i].centreprint("You are tied for the lead!");
					}
				}
				else
				{
					// [FB] Announce new leader!
					Team.Players[0].CallLocalSound(HexenDefs::lsid_gen_an_y_lead, true, HexenDefs::STANDART_LSQ_DELAY);
					Team.Players[0].centreprint("You lead!");
				}
			}
		}
	}
}

//==========================================================================
//
//  UpdateCompetition
//
//  [CW] Updates player's position in inner team table.
//
//==========================================================================

void UpdateCompetition()
{
	bool bPositionShifted;
	int i, j;
	Player pl2;

	for (i = 0; i < MAXPLAYERS; i++)
	{
		if(Team.Players[i] == self)
		   break;
	}
	
	PlayerPosition = i;

	// first see if previous players has lower score now
	if (i != 0)
	{
		j = i-1;
		while (j >= 0)
		{
			pl2 = Team.Players[j];
			
			if (pl2.Score < Score)
			{
				bPositionShifted = true;
				Team.Players[j] = self;
				Team.Players[i] = pl2;
				PlayerPosition = j;
				pl2.PlayerPosition = i;
				i = j;
			}
			else
				break;
			j--;
		};
	}
	// secondly (in other case) see if next players has higher score (in case score gone down)
	if (!bPositionShifted && i != MAXPLAYERS - 1)
	{
		j = i+1;
		while (Team.Players[j] && j < MAXPLAYERS)
		{
			pl2 = Team.Players[j];

			if (pl2.Score > Score)
			{
				Team.Players[j] = self;
				Team.Players[i] = pl2;
				PlayerPosition = j;
				pl2.PlayerPosition = i;
				i = j;
			}
			else
				break;
			j++;
		};
	}
}

//==========================================================================
//
//  DisplayDeathMessage
//
//  [CW] Displays message notificating that player was killed.
//
//	Usage:
//	All of the message strings are members of MainGameInfo.
//

//	1) if both source & inflictor are null, it was pure enviroment:
//	slime, lava and such. Use generic messages like "was in wrong place" etc,
//	with 1 parameter: this player's name.
//	2) if inflictor has some value and source is null, it should be some trap
//	or similar thing; then use inflictor's DeathMessage index with 1 parameter as in p.1.
//	3) if both inflictor and source has some values, but they are not
//	equal, that means that either a pawn or player have shot current
//	player with a missile; use inflictor's DeathMessage index with 2 parameters:
//	current player name and either pawn name or player's PlayerName.
//	4) if inflictor and source have equal values (not null), then it might
//	be melee attack OR instant ranged attack (like gunshot) by pawn or player;
//	use either pawn's DeathMessage index or	active player Weapon's DeathMessage index,
//	with 2 parameters as in p.3.

//==========================================================================

void DisplayDeathMessage(EntityEx inflictor, EntityEx source)
{
	if(source)
	{
		//[CW] p.4: melee or instant ranged attack
		if(source == inflictor)
		{
			if(source.bIsPlayer)
			{
				if (Weapon(source.Player.ViewEnt).bInstantRanged)
					Level.bprint(MainGameInfo(Level.Game).DeathMessageRanged[Weapon(inflictor.Player.ViewEnt).deathmsgi],
						PlayerName, source.Player.PlayerName);
				else
					Level.bprint(MainGameInfo(Level.Game).DeathMessageMelee[Weapon(inflictor.Player.ViewEnt).deathmsgi],
						PlayerName, source.Player.PlayerName);
			}
			else if(source.IsA('Pawn'))
			{
				if (Pawn(source).bInstantRanged)
					Level.bprint(MainGameInfo(Level.Game).DeathMessageRanged[Actor(inflictor).deathmsgi],
						PlayerName, MainGameInfo(Level.Game).PawnName[Pawn(source).pawnname]);
				else
					Level.bprint(MainGameInfo(Level.Game).DeathMessageMelee[Actor(inflictor).deathmsgi],
						PlayerName, MainGameInfo(Level.Game).PawnName[Pawn(source).pawnname]);
			}
		}
		//[CW] p.3: missile attack
		else if(inflictor)
		{
			if(source.bIsPlayer)
			{
				if(Player(source.Player) == self)
				{
					Level.bprint(MainGameInfo(Level.Game).SelfFragDeathMessage[P_Random() % 3],
						PlayerName);
				}
				else
					Level.bprint(MainGameInfo(Level.Game).DeathMessageRanged[Actor(inflictor).deathmsgi],
						PlayerName, source.Player.PlayerName);
			}
			else if(source.IsA('Pawn'))
			{
				Level.bprint(MainGameInfo(Level.Game).DeathMessageRanged[Actor(inflictor).deathmsgi],
						PlayerName, MainGameInfo(Level.Game).PawnName[Pawn(source).pawnname]);
			}
		}
		//[CW] suicide
		else if (source.bIsPlayer && Player(source.Player) == self)
		{
			Level.bprint(MainGameInfo(Level.Game).SuicideDeathMessage[P_Random() % 3], PlayerName);
		}
		//[CW] falling damage
		else
		{
			Level.bprint(MainGameInfo(Level.Game).FallingDeathMessage[P_Random() % 3], PlayerName);
		}
	}
	//[CW] p.2: fragged by enviromental thing or trap, or telefragged
	else if(inflictor)
	{
		// telefrag
		if (inflictor.bIsPlayer)
			Level.bprint(MainGameInfo(Level.Game).DeathMessageTelefrag[P_Random() % 3], PlayerName, inflictor.Player.PlayerName);
		// telefrag
		else if (inflictor.IsA('Pawn'))
			Level.bprint(MainGameInfo(Level.Game).DeathMessageTelefrag[P_Random() % 3], PlayerName,
				MainGameInfo(Level.Game).PawnName[Pawn(inflictor).pawnname]);
		else
			Level.bprint(MainGameInfo(Level.Game).DeathMessageMisc[Actor(inflictor).deathmsgi], PlayerName);
	}
	//[CW] p.1: fragged by pure enviroment
	else
	{
		Level.bprint(MainGameInfo(Level.Game).GenericDeathMessage[P_Random() % 3], PlayerName);
	}
}

//==========================================================================
//
//  ConfirmTrophy
//
//	[CW]
//
//==========================================================================

void ConfirmTrophy(int type)
{
	int a, announcement;
	int value;

	if (type >= HexenDefs::NUMTROPHIES)
		return;

	switch (type)
	{
	case HexenDefs::trophycl_killsatonce:
		a = -1;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_HUNT) a = 0;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_MASSACRE) a = 1;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_DEVASTATION) a = 2;
		if (Trophies.kills_at_once == HexenDefs::TROPHY_KAO_ELIMINATION) a = 3;
		if (a < 0)
		{
			//Trophies.kills_at_once = 0;
			//Trophies.kao_timer = 0.0;
			return;
		}
		announcement = HexenDefs::trophy_hunt + a;
		value = Trophies.kills_at_once;
		break;
	case HexenDefs::trophycl_fragsatonce:
		if (Trophies.frags_at_once < 2)
		{
			//Trophies.frags_at_once = 0;
			//Trophies.fao_timer = 0.0;
			return;
		}
		announcement = HexenDefs::trophy_doublekill + Trophies.frags_at_once - 2;
		if (announcement > HexenDefs::trophy_LAST_fao)
			announcement = HexenDefs::trophy_LAST_fao;
		value = Trophies.frags_at_once;
		break;
	case HexenDefs::trophycl_fragsrow:
		/*if (Trophies.frags_row < HexenDefs::TROPHY_FRAGSROW_LIMIT)
		{
			Trophies.frags_row = 0;
			return;
		}*/
		if (Trophies.frags_row % HexenDefs::TROPHY_FRAGSROW_LIMIT != 0)
			return;
		announcement = HexenDefs::trophy_frags_row;
		value = Trophies.frags_row;
		break;
	case HexenDefs::trophycl_killsrow:
		/*if (Trophies.kills_row < HexenDefs::TROPHY_KILLSROW_LIMIT)
		{
			Trophies.kills_row = 0;
			return;
		}*/
		if (Trophies.kills_row % HexenDefs::TROPHY_KILLSROW_LIMIT != 0)
			return;
		announcement = HexenDefs::trophy_kills_row;
		value = Trophies.kills_row;
		break;
	case HexenDefs::trophycl_accuracy:
		announcement = HexenDefs::trophy_accuracy; break;
	default:
		return;
	}
	centreprint(va(MainGameInfo(Level.Game).TrophyText[announcement], value));
	CallLocalSound(HexenDefs::lsid_PLAYERTROPHY + announcement,	true, HexenDefs::STANDART_LSQ_DELAY);


/*	// [CW] if the trophy data should be stored, do it here
	
	switch (type)
	{
	case HexenDefs::trophycl_killsatonce:
		Trophies.kills_at_once = 0;
		Trophies.kao_timer = 0.0;
		break;
	case HexenDefs::trophycl_fragsatonce:
		Trophies.frags_at_once = 0;
		Trophies.fao_timer = 0.0;
		break;
	case HexenDefs::trophycl_fragsrow:
		Trophies.frags_row = 0; break;
	case HexenDefs::trophycl_killsrow:
		Trophies.kills_row = 0; break;
	}
*/
}

//==========================================================================
//
//  UpdateQueuedTrophy
//
//	[CW] Queued trophies are trophies that depend on number
//		 of specific events occured in a sequence (there's a
//		 split for timed and non-timed).
//
//==========================================================================

void UpdateQueuedTrophy(int type, int value)
{
	if (type >= HexenDefs::NUMTROPHIES)
		return;

	switch (type)
	{
	case HexenDefs::trophycl_killsatonce:
		Trophies.kills_at_once += value;
		Trophies.kao_timer = HexenDefs::TROPHY_KAO_TIME;
		ConfirmTrophy(HexenDefs::trophycl_killsatonce);
		break;
	case HexenDefs::trophycl_fragsatonce:
		Trophies.frags_at_once += value;
		Trophies.fao_timer = HexenDefs::TROPHY_FAO_TIME;
		ConfirmTrophy(HexenDefs::trophycl_fragsatonce);
		break;
	case HexenDefs::trophycl_fragsrow:
		Trophies.frags_row += value;
		ConfirmTrophy(HexenDefs::trophycl_fragsrow);
		break;
	case HexenDefs::trophycl_killsrow:
		Trophies.kills_row += value;
		ConfirmTrophy(HexenDefs::trophycl_killsrow);
		break;
	}
}

//==========================================================================
//
//  UpdateTimedTrophies
//
//	[CW]
//
//==========================================================================

void UpdateTimedTrophies(float deltaTime)
{
	if (Trophies.kao_timer > 0.0)
	{
		Trophies.kao_timer -= deltaTime;
		if (Trophies.kao_timer <= 0.0)
		{
			Trophies.kills_at_once = 0;
			//ConfirmTrophy(HexenDefs::trophycl_killsatonce);
		}
	}
	if (Trophies.fao_timer > 0.0)
	{
		Trophies.fao_timer -= deltaTime;
		if (Trophies.fao_timer <= 0.0)
		{
			Trophies.frags_at_once = 0;
			//ConfirmTrophy(HexenDefs::trophycl_fragsatonce);
		}
	}
}

//==========================================================================
//
//  ClientGunShot
//
//==========================================================================

void ClientGunShot(TVec org)
{
	int i, j;
	particle_t *p;
	int c;

	for (i = 0; i < 20; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(c, c, c);
		p->die = Level.XLevel.Time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = HexenDefs::pt_static;
	}
}

//==========================================================================
//
//  ClientBlood
//
//==========================================================================

void ClientBlood(TVec org, int damage)
{
	int i, j;
	particle_t *p;
	int c;

	for (i = 0; i < damage; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(32 + c, c >> 4, c >> 4);
		p->die = Level.XLevel.Time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = HexenDefs::pt_static;
	}
}

//==========================================================================
//
//  ClientExplosion
//
//==========================================================================

void ClientExplosion(TVec org)
{
// FROM HEXEN
/*	TVec org;
	dlight_t *dl;
	int etype;

	etype = MSG_ReadByte();
	org.x = itof(MSG_ReadShort());
	org.y = itof(MSG_ReadShort());
	org.z = itof(MSG_ReadShort());

	dl = AllocDlight(0);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = ecolours[etype];
	dl->die = cl.time + 0.5;
	dl->decay = 300.0;*/

// ORIGINAL FROM DOOM
	int i;
	particle_t *p;
	dlight_t *dl;

	for (i = 0; i < 1024; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;
		p->die = Level.XLevel.Time + 5.0;
		p->colour = HexenDefs.default.ramp1[0];
		p->ramp = Random() * 4.0;
		if (i & 1)
		{
			p->type = HexenDefs::pt_explode;
		}
		else
		{
			p->type = HexenDefs::pt_explode2;
		}
		p->org.x = org.x + ((Random() * 32.0) - 16.0);
		p->org.y = org.y + ((Random() * 32.0) - 16.0);
		p->org.z = org.z + ((Random() * 32.0) - 16.0);
		p->vel.x = (Random() * 512.0) - 256.0;
		p->vel.y = (Random() * 512.0) - 256.0;
		p->vel.z = (Random() * 512.0) - 256.0;
	}

	dl = Level.AllocDlight(none);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = 0xffffffff;
	dl->die = Level.XLevel.Time + 0.5;
	dl->decay = 300.0;

}

//==========================================================================
//
//  ClientExplosion2
//
//==========================================================================

void ClientExplosion2(int etype, TVec org)
{
	dlight_t *dl;

	dl = Level.AllocDlight(none);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = HexenDefs.default.ecolours[etype];
	dl->die = Level.XLevel.Time + 0.5;
	dl->decay = 300.0;
}

//==========================================================================
//
//  ClientIceChunks
//
//==========================================================================

void ClientIceChunks(TVec org)
{
	int i, c;
	particle_t *p;

	for (i = 0; i < 1024; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;
		p->die = Level.XLevel.Time + 5.0;
		c = P_Random() >> 1; 
		p->colour = RGB(c, c, c);
		p->ramp = Random() * 4.0;
		if (i & 1)
		{
			p->type = HexenDefs::pt_chunks;
		}
		else
		{
			p->type = HexenDefs::pt_chunks2;
		}
		p->org.x = org.x + ((Random() * 16.0) - 8.0);
		p->org.y = org.y + ((Random() * 16.0) - 8.0);
		p->org.z = org.z + ((Random() * 16.0) - 8.0);
		p->vel.x = (Random() * 256.0) - 128.0;
		p->vel.y = (Random() * 256.0) - 128.0;
		p->vel.z = (Random() * 256.0) - 128.0;
	}
}

//==========================================================================
//
//  ClientDynamicLightEx
//
//	[CW] Full data dlight parsing
//
//==========================================================================

void ClientDynamicLightEx(int colour, TVec org, int radius, int lifetime, int decay)
{
	dlight_t *dl;

	dl = Level.AllocDlight(none);

	dl->colour = colour;
	dl->origin = org;
	dl->radius = itof(radius);
	dl->die = Level.XLevel.Time + itof(lifetime) * 0.01;
	dl->decay = itof(decay) * 0.01;
}

defaultproperties
{
}
