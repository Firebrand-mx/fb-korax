//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MagicMissile : Projectile;

//===========================================================================
//
//  RoughCheckThing
//
//===========================================================================

bool RoughCheckThing(Entity Other)
{
	float angle;
	Actor link = Actor(Other);
	
	if (!(link.bMonster || link.bIsPlayer) || link == Target ||
		link.bDormant)
	{
		return false;
	}
	if (!link.bShootable)
	{
		return false;
	}
	if (Level.Game.netgame && !Level.Game.deathmatch && link.bIsPlayer)
	{
		return false;
	}
	if (CanSee(link))
	{
		angle = AngleMod360(atan2(
			link.Origin.y - Target.Origin.y,
			link.Origin.x - Target.Origin.x) -
			Target.Angles.yaw);
		if (angle > 226.0 * 360.0 / 256.0 ||
			angle < 30.0 * 360.0 / 256.0)
		{
			return true;
		}
	}
	return false;
}

//==========================================================================
//
//  A_MMissileTrack
//
//==========================================================================

final void A_MMissileTrack()
{
	if (!Tracer && (P_Random() < 50))
	{
		Tracer = Actor(RoughMonsterSearch(2));
	}
	SeekerMissile(2.0, 10.0);
}

__states__
{
	S_WMMISSILE_FX_1('WENE', 1 | FF_FULLBRIGHT, 2.0 / 35.0, S_WMMISSILE_FX_2) = A_MMissileTrack
	S_WMMISSILE_FX_2('WENE', 0 | FF_FULLBRIGHT, 2.0 / 35.0, S_WMMISSILE_FX_2) = A_MMissileTrack
	S_WMMISSILE_FX_X1('WENE', 1 | FF_FULLBRIGHT, 4.0 / 35.0, S_WMMISSILE_FX_X2) = none
	S_WMMISSILE_FX_X2('WENE', 2 | FF_FULLBRIGHT, 5.0 / 35.0, S_WMMISSILE_FX_X3) = none
	S_WMMISSILE_FX_X3('WENE', 3 | FF_FULLBRIGHT, 5.0 / 35.0, S_WMMISSILE_FX_X4) = none
	S_WMMISSILE_FX_X4('WENE', 4 | FF_FULLBRIGHT, 5.0 / 35.0, S_WMMISSILE_FX_X5) = none
	S_WMMISSILE_FX_X5('WENE', 5 | FF_FULLBRIGHT, 5.0 / 35.0, S_WMMISSILE_FX_X6) = none
	S_WMMISSILE_FX_X6('WENE', 6 | FF_FULLBRIGHT, 4.0 / 35.0, none) = none
}

defaultproperties
{
	Radius = 8.0;
	Height = 8.0;
	Speed = 600.0;
	MissileDamage = 5;
	bSeekerMissile = true;
	IdleState = S_WMMISSILE_FX_1;
	DeathState = S_WMMISSILE_FX_X1;
	DeathSound = 'MageStaffExplode';
	Effects = HexenDefs::EF_DL_BLUE;
	Obituary = "$ob_magicmissile";
}
