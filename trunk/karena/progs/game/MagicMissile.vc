//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MagicMissile : Actor;

//===========================================================================
//
//  FindEnemy
//
//===========================================================================

final void FindEnemy()
{
	Entity Other;

	foreach RoughBlockSearch(Other, 2)
	{
		EntityEx link = EntityEx(Other);

		if (!(link.bMonster || link.bIsPlayer) || link == Target ||
			link.bDormant)
		{
			continue;
		}
		if (!link.bShootable)
		{
			continue;
		}
		if (Level.Game.netgame && !Level.Game.deathmatch && link.bIsPlayer)
		{
			continue;
		}
		if (MainGameInfo(Level.Game).GameModel && 
			MainGameInfo(Level.Game).GameModel.TeamFactor(Target, link))
		{
			continue;
		}
		if (MainGameInfo(Level.Game).GameType >= HexenDefs::NUMGAMESNOTEAM &&
			link.bIsPlayer && Player(Target.Player).Team == Player(link.Player).Team)
		{
			continue;
		}
		if (CanSee(link))
		{
			float angle = AngleMod360(atan2(
				link.Origin.y - Target.Origin.y,
				link.Origin.x - Target.Origin.x) -
				Target.Angles.yaw);
			if (angle > 226.0 * 360.0 / 256.0 ||
				angle < 30.0 * 360.0 / 256.0)
			{
				Tracer = link;
				return;
			}
		}
	}
}

//==========================================================================
//
//  A_MMissileTrack
//
//==========================================================================

final void A_MMissileTrack()
{
	if (!Tracer && (P_Random() < 50))
	{
		FindEnemy();
	}
	SeekerMissile(2.0, 10.0);
}

states
{
Spawn:
	WENE AB 2 Bright A_MMissileTrack
	Wait
Death:
	WENE B 4 Bright
	WENE CDEF 5 Bright
	WENE G 4 Bright
	Stop
}

defaultproperties
{
	Radius = 8.0;
	Height = 8.0;
	Speed = 600.0;
	MissileDamage = 5;
	bMissile = true;
	bNoBlockmap = true;
	bDropOff = true;
	bNoGravity = true;
	bNoTeleport = true;
	bActivateImpact = true;
	bActivatePCross = true;
	bBloodSplatter = true;
	bSeekerMissile = true;
	bThruGhost = true;
	DeathSound = 'MageStaffExplode';
	DLightColour = RGB(67, 197, 246);
	DLightRadius = 118.0;
//	Obituary = "$ob_magicmissile";
}
