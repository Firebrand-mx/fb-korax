//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**	        ##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id: GameSurvival.vc 508 2006-07-10 19:02:43Z firebrand_kh $
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class GameSurvival : KArenaGameModel;

enum
{
	state_preparegame,
	state_countdownsequence,
	state_wave
};

enum
{
	event_wavestart		= GameModel::event_custom,
	event_waveend,
	event_countdown,
	event_wavespawn,
	num_survivalevents	= event_wavespawn - GameModel::event_custom + 1
};

acsnotifier_t	AcsNotifiersSurvival[num_survivalevents];

// monster classes in the order of growing difficulty
array< name > MonsterClasses; 		// array of monster class names
array< int > MonsterCosts;			// monster cost to be spawned (in monster counts)
array< name > BossClasses;			// array of monster boss class names

array<mthing_t> SpawnStarts;

int Wave;
int LoLimit;						// lower index in monster array
int HiLimit;						// higher index in monster array
int BoostedLimit;					// highest index in monster array that allows spawning of boosted versions of monsters
int CountToSpawnMonsters;			// how many monsters to spawn
int CountToSpawnBosses;				// how many bosses to spawn
int CountMonstersAtOnce;			// how many monsters & bosses can be on the level at the same time
int PlaceCounter;					// counter to avoid spawning two monsters on the same spot

GameSequencer		countdownSequencer;
int					countDown;
GameSequencer		waveSequencer;

//==========================================================================
//
//	AllowedTeams
//
//==========================================================================

int AllowedTeams()
{
	return 1;
}

//==========================================================================
//
//	AllowAllies
//
//==========================================================================

int AllowAllies()
{
	return true;
}

//==========================================================================
//
//	BeginState
//
//	ACS callable
//
//==========================================================================

void BeginState(int newstate)
{
	dprint("GameSurvival::BeginState : %i", newstate);

	switch(newstate)
	{
	case state_preparegame:
		Level.bAllowMonsterTelefrags = false; // Safety issue
		RegisterSpawnStarts();
		Wave = GetCvar('StartingWave');
		EndCurrentState(state_countdownsequence);
		return;

	case state_countdownsequence:

		// Disable experience loss for players between waves
		SetPlayerExperienceLoss(false);

		//
		// Setup countdown sequence
		countDown = 10;
		countdownSequencer = Level.Spawn(GameSequencer);
		if (countdownSequencer)
		{
			countdownSequencer.time_limit = 1.0;
			countdownSequencer.event_id = event_countdown;
		}
		break;

	case state_wave:

		// Allow experience loss for players
		SetPlayerExperienceLoss(true);

		SetUpWave();
		waveSequencer = Level.Spawn(GameSequencer);
		if (waveSequencer)
		{
			waveSequencer.time_limit = 1.0;
			waveSequencer.event_id = event_wavespawn;
		}
		HandleEvent(event_wavestart, none, 0, 0, 0, 0);
		break;
	};

	State = newstate;
}

//==========================================================================
//
//	EndCurrentState
//
//	ACS callable
//
//==========================================================================

void EndCurrentState(int nextstate)
{
	int i;

	dprint("GameSurvival::EndCurrentState : from %i to %i", State, nextstate);

	switch (State)
	{
	case state_wave:
		waveSequencer.Destroy();
		waveSequencer = none;
		//
		// Kill all existing monsters, just in case
		Level.P_Massacre();
		//
		// Ressurect killed players
		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (Game.Players[i] && Game.Players[i].MO)
			{
				if (Game.Players[i].MO.Class == PlayerSpectator)
				{
					Game.Players[i].PlayerState = PST_REBORN;
					Game.Players[i].NetGameReborn();
				}
				else
				{
					Player(Game.Players[i]).UpdateScore(HexenDefs::scoreevent_Survived);
				}
			}
		}
		HandleEvent(event_waveend, none, 0, 0, 0, 0);
		break;

	case state_countdownsequence:
		countdownSequencer.Destroy();
		countdownSequencer = none;
		break;
	}

	::EndCurrentState(nextstate);
}

//==========================================================================
//
//	IsInPlayableState
//
//==========================================================================

bool IsInPlayableState()
{
	return State > state_preparegame;
}

//==========================================================================
//
//	TeamFactor
//
//==========================================================================

int TeamFactor(EntityEx a1, EntityEx a2)
{
	if (a1.bIsPlayer && a2.IsA('MinotaurFriend') ||
		a2.bIsPlayer && a1.IsA('MinotaurFriend') ||
		a1.bIsPlayer == a2.bIsPlayer)	// both players or both monsters
	{
		return teamfactor_coop;
	}
	else
	{
		return teamfactor_enemy;
	}
}

//==========================================================================
//
//	OnActorDeath
//
//==========================================================================

void OnActorDeath(Actor a, EntityEx source)
{
	// Friendly minotaur doesn't has a meaning here or the actor is invalid
	if (!a.bMonster || a.IsA('MinotaurFriend'))
	{
		return;
	}

	int mleft = (Level.TotalKills - Level.CurrentKills) + CountToSpawnMonsters + CountToSpawnBosses;
	if (mleft > 0 && mleft <= 10)
	{
		Game.BroadcastLocalSoundNamed('misc/chat', true, 0.0);
		Game.bcentreprint(va("%i monsters left!", (Level.TotalKills - Level.CurrentKills) + CountToSpawnMonsters + CountToSpawnBosses));
	}

	if (!mleft)
	{
		EndCurrentState(state_countdownsequence);
	}
}

//==========================================================================
//
//	OnPlayerSpawn
//
//==========================================================================

void OnPlayerSpawn(BasePlayer pl)
{
	// If the game hasn't been started yet and
	// if there're at least 1 player in game, begin gameplay
	if (State < 0)
	{
		BeginState(state_preparegame);
	}
}

//==========================================================================
//
//	OnPlayerDeath
//
//==========================================================================

void OnPlayerDeath(BasePlayer pl, EntityEx source)
{
	// When player is killed during the monster wave he becomes a
	// spectator until the end of the wave.
	if (State == state_wave)
	{
		Player(pl).bRespawnAsSpectator = true;
		Player(pl).bAllowEndSpectate = false;

		int i;
		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (Game.Players[i] && Game.Players[i].MO && Game.Players[i].MO.Class != PlayerSpectator && Game.Players[i].MO.Health > 0)
			{
				// There are still living players around, so game should continue
				//
				return;
			}
		}

		// Too bad, all players died, end the game
		End();
	}
}

//==========================================================================
//
//	HandleEvent
//
//	ACS callable
//
//==========================================================================

void HandleEvent(int Event, EntityEx A, int arg1, int arg2, int arg3, int arg4)
{
	dprint("GameSurvival::HandleEvent : %i", Event);

	switch (Event)
	{
	case event_wavestart:
		Game.BroadcastLocalSoundNamed('BellRing', false, 0.0);
		Game.bcentreprint(va("wave %i!", Wave));
		break;
	case event_waveend:
		Game.BroadcastLocalSoundNamed('Survival_WaveEnd', false, 0.0);
		break;
	case event_countdown:
		countDown--;
		if (!countDown)
		{
			EndCurrentState(state_wave);
		}
		else
		{
			Game.BroadcastLocalSoundNamed('ClockTick', false, 0.0);
			Game.bcentreprint(va("next wave in %i seconds", countDown));
		}
		break;
	case event_wavespawn:
		WaveSpawn();
		break;
	default:
		::HandleEvent(Event, A, arg1, arg2, arg3, arg4);
		return;
	}

	NotifyACS(Event, 0, 0, 0);
}

//==========================================================================
//
//	SetACSNotifier
//
//	ACS callable
//
//==========================================================================

void SetACSNotifier(int event, int script)
{
	if (event >= GameModel::event_custom && event < event_wavestart + num_survivalevents)
	{
		event -= GameModel::event_custom;
		AcsNotifiersSurvival[event].enabled = true;
		AcsNotifiersSurvival[event].script = script;
	}
	else
	{
		::SetACSNotifier(event, script);
	}
}

//==========================================================================
//
//	NotifyACS
//
//==========================================================================

void NotifyACS(int Event, int Arg1, int Arg2, int Arg3)
{
	dprint("GameSurvival::NotifyACS : Event = %i", Event);
	if (Event >= GameModel::event_custom && Event < event_wavestart + num_survivalevents)
	{
		Event -= GameModel::event_custom;
		if (AcsNotifiersSurvival[Event].enabled)
		{
			Level.ExecuteActionSpecial(LNSPEC_ACSExecuteAlways,
					AcsNotifiersSurvival[Event].script,
					0,
					Arg1,
					Arg2,
					Arg3,
					NULL, 0, none);
		}
	}
	else
		::NotifyACS(Event, Arg1, Arg2, Arg3);
}

//==========================================================================
//
//	RegisterSpawnStarts
//
//==========================================================================

void RegisterSpawnStarts()
{
	bool bSpawnSpotOnly;
	Actor SpawnSpot;

	SpawnStarts.Num = 0;

	foreach Level.AllThinkers(SurvivalSpawnSpot, SpawnSpot)
	{
		if (SpawnSpot.IsA('SurvivalSpawnSpotOnly'))
		{
			bSpawnSpotOnly = true;
		}

		SpawnStarts.Num = SpawnStarts.Num + 1;
		SpawnStarts[SpawnStarts.Num - 1].x = SpawnSpot.Origin.x;
		SpawnStarts[SpawnStarts.Num - 1].y = SpawnSpot.Origin.y;
		SpawnStarts[SpawnStarts.Num - 1].height = SpawnSpot.Origin.z - GetPlanePointZ(&SpawnSpot.Sector->floor, SpawnSpot.Origin);
		SpawnStarts[SpawnStarts.Num - 1].angle = SpawnSpot.AngleIncrements() * ftoi(SpawnSpot.Angles.yaw / itof(SpawnSpot.AngleIncrements()));
	}

	if (!bSpawnSpotOnly)
	{
		int i = 0;
		for (i = 0; i < Level.DeathmatchStarts.Num; i++)
		{
			SpawnStarts.Num = SpawnStarts.Num + 1;
			SpawnStarts[SpawnStarts.Num - 1].x = Level.DeathmatchStarts[i].x;
			SpawnStarts[SpawnStarts.Num - 1].y = Level.DeathmatchStarts[i].y;
			SpawnStarts[SpawnStarts.Num - 1].height = Level.DeathmatchStarts[i].height;
			SpawnStarts[SpawnStarts.Num - 1].angle = Level.DeathmatchStarts[i].angle;
		}
	}
}

//==========================================================================
//
//	SetUpWave
//
//==========================================================================

void SetUpWave()
{
	Wave++;

	int PlayerCount = Game.GetPlayerCount();
	int SpawnStarts = SpawnStarts.Num;

	// (8 + (0 to 7)) monsters per player + (1 to 4) monsters per player per wave index
	CountToSpawnMonsters = (8 + P_Random() % 8) * PlayerCount +
							(P_Random() % (PlayerCount << 2) + 1) * Wave;

	switch (GetCvar('MonsterAmount'))
	{
	case 0: CountToSpawnMonsters /= 4; break;
	case 1: CountToSpawnMonsters /= 3; break;
	case 2: CountToSpawnMonsters /= 2; break;
	case 3: break; // normal
	case 4: CountToSpawnMonsters = ftoi(itof(CountToSpawnMonsters) * 1.5); break;
	case 5: CountToSpawnMonsters *= 2; break;
	case 6: CountToSpawnMonsters = ftoi(itof(CountToSpawnMonsters) * 2.5); break;
	case 7: CountToSpawnMonsters *= 3; break;
	case 8: CountToSpawnMonsters *= 4; break;
	}

	CountToSpawnBosses = Wave / (16 - PlayerCount);

	// (1 to 2) per spawn start (i.e. at least 8 - 16)
	CountMonstersAtOnce = PlayerCount + SpawnStarts + P_Random() % (SpawnStarts + 1);

	HiLimit = 3 + Wave;
	if (HiLimit > MonsterClasses.Num)
	{
		HiLimit = MonsterClasses.Num;
	}
	LoLimit = HiLimit >= 8 ? HiLimit - 8 : 0;
	BoostedLimit = -2 + (Wave - 1) / 2; // Note, BoostedLimit may go above monster classes num
}

//==========================================================================
//
//	WaveSpawn
//
//==========================================================================

void WaveSpawn()
{
	int		mClass;
	class	<Actor> MClass;
	bool	bBoosted;
	Actor	monster;

	// If current number of monsters is equal or greater than this, no new monsters are spawned until some of older ones are killed
	if (CountToSpawnMonsters + CountToSpawnBosses > 0 && (Level.TotalKills - Level.CurrentKills) < CountMonstersAtOnce)
	{
		// If we are to spawn bosses, let's spawn them randomly
		if (CountToSpawnBosses > 0 && (!CountToSpawnMonsters || Random() < 0.35))
		{
			//
			// Spawn Boss monster
			//
			mClass = P_Random() % BossClasses.Num;
			MClass = class<Actor>(FindClass(BossClasses[mClass]));
			CountToSpawnBosses--;
		}
		else
		{
			if (BoostedLimit > 0 && Random() < 0.1)
			{
				//
				// Spawn boosted monster of weaker monster class
				//
				mClass = P_Random() % (BoostedLimit < MonsterClasses.Num ? BoostedLimit : MonsterClasses.Num);
				bBoosted = true;
			}
			else
			{
				//
				// Spawn normal monster
				//
				mClass = P_Random() % (HiLimit - LoLimit) + LoLimit;
			}

			MClass = class<Actor>(FindClass(MonsterClasses[mClass]));
			CountToSpawnMonsters--;
		}

		if (PlaceCounter >= SpawnStarts.Num)
			PlaceCounter = 0;

		monster = Level.Spawn(MClass,,, &SpawnStarts[PlaceCounter++]);

		if (monster)
		{
			if (bBoosted)
			{
				// Spawn a boosted monster with enhanced stats
				if ((P_Random() < (BoostedLimit - mClass)) && Random() < 0.25)
				{
					// Spawn really huge monster :P
					monster.Health *= ftoi(25.0 + Random() * itof(BoostedLimit - mClass) / 100.0);
					monster.Speed *= 2.5;
					monster.ScaleX *= 5.0;
					monster.ScaleY *= 5.0;
					monster.Height *= 3.0;
					monster.Radius *= 3.0;
					monster.bBoss = true;
					monster.MissileState = none;
				}
				else
				{
					monster.Health *= ftoi(1.5 + Random() * itof(BoostedLimit - mClass) / 100.0);
					monster.Speed *= 1.5 + Random() * itof(BoostedLimit - mClass) / 100.0;

					if (Random() < 0.15)
						monster.RenderStyle = Entity::STYLE_Fuzzy;
					else if (Random() < 0.75)
						monster.RenderStyle = Entity::STYLE_Add;
					else
					{
						monster.ScaleX *= 1.5;
						monster.ScaleY *= 1.5;
					}
				}
			}

			switch (GetCvar('MonsterHealth'))
			{
			case 0: monster.Health /= 4; break;
			case 1: monster.Health /= 3; break;
			case 2: monster.Health /= 2; break;
			case 3: break; // normal
			case 4: monster.Health *= 2; break;
			case 5: monster.Health *= 3; break;
			case 6: monster.Health *= 4; break;
			case 7: monster.Health *= 6; break;
			case 8: monster.Health *= 10; break;
			}

			// Recalculate experience based on new starting Health value
			monster.experience = monster.Health;

			// monster.TeleportMove(monster.Origin, false);
			Level.Spawn(TeleportFog, monster.GetCentre());
			Level.NoiseAlert(Game.Players[0].MO, monster);
		}
	}
}

defaultproperties
{
	MonsterClasses.Num = 26;
	MonsterClasses[0] = 'HereticMummy';
	MonsterClasses[1] = 'Ettin';
	MonsterClasses[2] = 'KArenaHereticImp';
	MonsterClasses[3] = 'HereticMummyGhost';
	MonsterClasses[4] = 'KArenaEttinGhost';	
	MonsterClasses[5] = 'KArenaClink';
	MonsterClasses[6] = 'KArenaCentaur';
	MonsterClasses[7] = 'KArenaCentaurGhost';
	MonsterClasses[8] = 'KArenaBeast';
	MonsterClasses[9] = 'KArenaDemon1';
	MonsterClasses[10] = 'KArenaKnight';
	MonsterClasses[11] = 'HereticMummyLeader';
	MonsterClasses[12] = 'KArenaSnake';
	MonsterClasses[13] = 'KArenaDemon2';
	MonsterClasses[14] = 'KArenaFireDemon';
	MonsterClasses[15] = 'HereticImpLeader';
	MonsterClasses[16] = 'KArenaBeastGhost';
	MonsterClasses[17] = 'KArenaDemon1Ghost';
	MonsterClasses[18] = 'KArenaKnightGhost';
	MonsterClasses[19] = 'HereticMummyLeaderGhost';
	MonsterClasses[20] = 'KArenaDemon2Ghost';
	MonsterClasses[21] = 'KArenaIceGuy';
	MonsterClasses[22] = 'KArenaCentaurLeader';
	MonsterClasses[23] = 'KArenaWizard';
	MonsterClasses[24] = 'KArenaBishop';
	MonsterClasses[25] = 'Wraith';

	BossClasses.Num = 4;
	BossClasses[0] = 'KArenaIronlich';
	BossClasses[1] = 'Minotaur';
	BossClasses[2] = 'Heresiarch';
	BossClasses[3] = 'KArenaDragon';
}
