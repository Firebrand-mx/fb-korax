//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WeaponStaff : Weapon;

//===========================================================================
//
//  A_StaffAttackPL1
//
//===========================================================================

void A_StaffAttackPL1()
{
	TAVec angles;
	int damage;
	TVec vforward;
	Actor Owner = Player.MO;

	damage = 12; //5 + (P_Random() & 15);
	if (str_mod)
	{
		if(!active_damage)
		{
			damage = damage * Player(Player).strength / 10;
		}
		else // [FB] PI Damage
		{
			damage = (damage * Player(Player).intelligence / 10) * ftoi(3.1415);
		}
	}
	angles = Owner.Angles;
	angles.yaw = AngleMod360(angles.yaw +
		(Random() - Random()) * 45.0 / 8.0);
	Owner.AimLineAttack(&angles, 1.5 * MELEERANGE);
	if (Owner.linetarget)
	{
		AngleVector(&angles, &vforward);
		Owner.LineAttack(vforward, 1.5 * MELEERANGE, damage, StaffPuff);
		if (Owner.linetarget.bCountKill || Owner.linetarget.bIsPlayer)
		{
			Owner.linetarget.Thrust(angles.yaw, 2.0);
		}
		Player(Player).AdjustPlayerAngle();
	}
	else
	{
		// didn't find any creatures, so try to strike any walls
		angles = Player.MO.Angles;
		Actor(Player.MO).AimLineAttack(&angles, MELEERANGE);
		AngleVector(&angles, &vforward);
		if (Actor(Player.MO).LineAttack(vforward, MELEERANGE, damage, StaffPuff))
		{
			Player.MO.PlaySound('StaffHit', CHAN_WEAPON);
		}
	}
}

__states__
{
	S_STAFFREADY('CSTF', 0, 1.0 / 35.0, S_STAFFREADY) { A_WeaponReady(); }
	S_STAFFDOWN('CSTF', 0, 1.0 / 35.0, S_STAFFDOWN) { A_Lower(); }
	S_STAFFUP('CSTF', 0, 1.0 / 35.0, S_STAFFUP) { A_Raise(); }
	S_STAFFATK1_1('CSTF', 1, 6.0 / 35.0, S_STAFFATK1_2) { }
	S_STAFFATK1_2('CSTF', 2, 8.0 / 35.0, S_STAFFATK1_3) { A_StaffAttackPL1(); }
	S_STAFFATK1_3('CSTF', 1, 8.0 / 35.0, S_STAFFREADY) { A_ReFire(); }
}

defaultproperties
{
	Mana = HexenDefs::MANA_NONE;
	UpState = S_STAFFUP;
	DownState = S_STAFFDOWN;
	ReadyState = S_STAFFREADY;
	AttackState = S_STAFFATK1_1;
	HoldAttackState = S_STAFFATK1_1;
	PSpriteSY = 0.0;
	wis_mod = false;
	int_mod = false;
	dex_mod = true;
	str_mod = true;
}

//**************************************************************************
//
//  $Log$
//  Revision 1.7  2006/03/02 18:40:23  dj_jl
//  Don't use game specific classes in common.
//
//  Revision 1.6  2006/02/17 19:17:52  dj_jl
//  Moved common defs to classes.
//
//  Revision 1.5  2006/02/15 23:37:46  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.4  2005/12/11 21:37:29  dj_jl
//  Made path traversal callbacks class members.
//
//  Revision 1.3  2005/08/12 23:04:25  firebrand_kh
//  Fixed weapons to work with the PI Damage artifact
//
//  Revision 1.2  2005/07/27 21:00:06  firebrand_kh
//  Balanced weapon damage rates & mana usage
//
//  Revision 1.1  2004/11/16 13:33:22  dj_jl
//  Initial import.
//
//  Revision 1.2  2003/11/12 16:49:44  dj_jl
//  Changed player structure into a class
//
//  Revision 1.1  2002/10/26 16:32:23  dj_jl
//  New style of defining classes.
//
//**************************************************************************
