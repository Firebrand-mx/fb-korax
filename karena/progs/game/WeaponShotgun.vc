//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WeaponShotgun : WeaponDoomBase;

//===========================================================================
//
//  A_FireShotgun
//
//===========================================================================

void A_FireShotgun(void)
{
	int i;
	TVec dir;
	TAVec angles;

	Player.MO.PlaySound('ShotgunFire', CHAN_WEAPON);
	Player.MO.SetState(Player.MO.MissileState);

	UseWeaponMana();

	Player.SetPsprite(ps_flash, FlashState);

	angles = Player.MO.Angles;
	Player.MO.AimLineAttack(&angles, MISSILERANGE);
	AngleVector(&angles, &dir);
//	dir = Aim(Player.MO, 16.0 * 64.0);
	for (i = 0; i < 10/*7*/; i++)
	{
		GunShot(Player.MO, false, dir);
	}
	Player.MO.Effects |= EF_DL_MUZZLEFLASH;
}

__states__
{
	S_SGUN('SHTG', 0, 1.0 / 35.0, S_SGUN) { A_WeaponReady(); }
	S_SGUNDOWN('SHTG', 0, 1.0 / 35.0, S_SGUNDOWN) { A_Lower(); }
	S_SGUNUP('SHTG', 0, 1.0 / 35.0, S_SGUNUP) { A_Raise(); }
	S_SGUN1('SHTG', 0, 3.0 / 35.0, S_SGUN2) { }
	S_SGUN2('SHTG', 0, 7.0 / 35.0, S_SGUN3) { A_FireShotgun(); }
	S_SGUN3('SHTG', 1, 5.0 / 35.0, S_SGUN4) { }
	S_SGUN4('SHTG', 2, 5.0 / 35.0, S_SGUN5) { }
	S_SGUN5('SHTG', 3, 4.0 / 35.0, S_SGUN6) { }
	S_SGUN6('SHTG', 2, 5.0 / 35.0, S_SGUN7) { }
	S_SGUN7('SHTG', 1, 5.0 / 35.0, S_SGUN8) { }
	S_SGUN8('SHTG', 0, 3.0 / 35.0, S_SGUN9) { }
	S_SGUN9('SHTG', 0, 7.0 / 35.0, S_SGUN) { A_ReFire(); }
	S_SGUNFLASH1('SHTF', 0 | FF_FULLBRIGHT, 4.0 / 35.0, S_SGUNFLASH2) { /*A_Light1();*/ }
	S_SGUNFLASH2('SHTF', 1 | FF_FULLBRIGHT, 3.0 / 35.0, S_LIGHTDONE) { /*A_Light2();*/ }
}

defaultproperties
{
	UpState = S_SGUNUP;
	DownState = S_SGUNDOWN;
	ReadyState = S_SGUN;
	AttackState = S_SGUN1;
	FlashState = S_SGUNFLASH1;
	HoldAttackState = S_SGUN1;
	Mana = MANA_2;
	manause = 10;
	wis_mod = true;
	int_mod = true;
	dex_mod = false;
	str_mod = false;
	PickUpMessage = "SHOTGUN";
}

//**************************************************************************
//
//  $Log$
//  Revision 1.6  2005/12/11 21:37:29  dj_jl
//  Made path traversal callbacks class members.
//
//  Revision 1.5  2005/08/08 18:16:51  firebrand_kh
//  Took out model definitions
//
//  Revision 1.4  2005/07/27 21:00:06  firebrand_kh
//  Balanced weapon damage rates & mana usage
//
//  Revision 1.3  2005/02/03 01:05:06  firebrand_kh
//  Fixed Doom Guy weapon bug with the new effects
//
//  Revision 1.2  2004/12/09 00:03:57  firebrand_kh
//  Added stuff for the Doom Guy weapon effects
//
//  Revision 1.1  2004/11/16 13:33:22  dj_jl
//  Initial import.
//
//  Revision 1.2  2003/11/12 16:48:33  dj_jl
//  Changed player structure into a class
//
//  Revision 1.1  2002/10/26 16:32:20  dj_jl
//  New style of defining classes.
//
//**************************************************************************
