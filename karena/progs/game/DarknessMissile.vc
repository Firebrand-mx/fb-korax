//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class DarknessMissile : Effect;

int LifeCount;

//==========================================================================
//
//  InflictorModifyDamage
//
//==========================================================================

int InflictorModifyDamage(Actor victim, Actor source, int damage)
{
	if (victim.bIsPlayer)
	{
		victim.BlindPlayer(source, 20);
	}
	return -1;
}

//==========================================================================
//
//	InitMe
//
//==========================================================================

void InitMe(Actor InstigatedBy)
{
	LifeCount = 36 + (P_Random() & 7);
	Instigator = InstigatedBy;
	Radius = 20.0;
	Height = 14.0;
	bColideWithThings = true;
	bColideWithWorld = true;
}

//==========================================================================
//
//  A_Attack
//
//==========================================================================

void A_Attack(void)
{
	P_RadiusAttack(4, 40.0, false);
}

//==========================================================================
//
//  A_ChkDeath
//
//==========================================================================

void A_ChkDeath(void)
{
	if (!--LifeCount)
	{
		SetState(DeathState);
	}
}

__states__
{
	S_DARK_MISSILE_1('DRKM', 0, 3.0 / 35.0, S_DARK_MISSILE_2) { A_Attack(); }
	S_DARK_MISSILE_2('DRKM', 1, 3.0 / 35.0, S_DARK_MISSILE_3) { A_Attack(); }
	S_DARK_MISSILE_3('DRKM', 2, 3.0 / 35.0, S_DARK_MISSILE_4) { A_Attack(); }
	S_DARK_MISSILE_4('DRKM', 3, 3.0 / 35.0, S_DARK_MISSILE_1) { A_ChkDeath(); }
	S_DARK_MISSILE_X1('DRKM', 4, 3.0 / 35.0, S_DARK_MISSILE_X2) { }
	S_DARK_MISSILE_X2('DRKM', 5, 3.0 / 35.0, S_DARK_MISSILE_X3) { }
	S_DARK_MISSILE_X3('DRKM', 6, 3.0 / 35.0, S_DARK_MISSILE_X4) { }
	S_DARK_MISSILE_X4('DRKM', 7, 3.0 / 35.0, S_NULL) { }
}

defaultproperties
{
	Radius = 1.0;
	Height = 1.0;
	Mass = 99999.0;
	bDropOff = true;
	bColideWithWorld = false;
	bColideWithThings = false;
	bNoDamageThrust = true;
	IdleState = S_DARK_MISSILE_1;
	DeathState = S_DARK_MISSILE_X1;
	DeathSound = 'FighterHammerExplode';
	Effects = EF_DL_DARK;	
}

//**************************************************************************
//
//  $Log$
//  Revision 1.8  2006/02/06 19:26:42  firebrand_kh
//  Made the missile don't damage the player that spawned it
//
//  Revision 1.7  2005/12/11 21:37:29  dj_jl
//  Made path traversal callbacks class members.
//
//  Revision 1.6  2005/11/30 23:53:37  dj_jl
//  Switch statement requires integer.
//
//  Revision 1.5  2005/10/16 22:44:03  firebrand_kh
//  Made darkness missile to damage enemies too
//
//  Revision 1.4  2005/08/03 00:13:23  firebrand_kh
//  Reduced height and radius
//
//  Revision 1.3  2005/07/27 21:00:06  firebrand_kh
//  Balanced weapon damage rates & mana usage
//
//  Revision 1.2  2005/05/09 22:49:29  firebrand_kh
//  Fixed Weapon of Darkness to blind the Player
//
//  Revision 1.1  2004/11/16 13:33:21  dj_jl
//  Initial import.
//
//  Revision 1.1  2002/10/26 16:32:26  dj_jl
//  New style of defining classes.
//
//**************************************************************************
