//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ######  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		##    ######    ##### ###   ##    ##
//**	      ##  ##  ##   ##  #####  ####  ##  ##  ##
//**	     ##    ## ##   ## ##      ##### ## ##    ##
//**	     ######## #####   #####   ## ##### ########
//**	     ##    ## ######  ##      ##  #### ##    ##
//**	     ##    ## ##  ###  #####  ##   ### ##    ##
//**
//**    $Id: MenuScreenTeamSelect.vc 508 2006-07-10 19:02:43Z firebrand_kh $
//**
//**    Copyright (C) 2004-2006 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MenuScreenTeamSelect : MenuScreen;

bool 			Ready;
bool			bIsSelected;
array<string>	TeamNames;//[HexenDefs::MAXPLAYERTEAMS];
int				Teams[HexenDefs::MAXTEAMSINGAME];
int				Counts[HexenDefs::MAXTEAMSINGAME];
int				maxTeams;
int				registeredTeams;
int				activeTeams;
int				curSelection;


//==========================================================================
//
//	CreateChoices
//
//==========================================================================

void CreateChoices()
{
	int i, y;
	MenuStaticText staticText;
	MenuChoice_Team newChoice;
	//bool teams_active[HexenDefs::MAXPLAYERTEAMS];
	array<bool> teams_active;

	teams_active.Num = registeredTeams;
	// CHECKME
	//for (i = 0; i < HexenDefs::MAXPLAYERTEAMS; i++)
	//	teams_active[i] = false;

	if (!Ready)
		return;

	ReadTeams();

	bool bKeepEqual = GetCvar('KeepTeamsEqual');
	int count_limit = activeTeams > 1 ? Counts[0] : 0;

	if (bKeepEqual)
	{
		for (i = 0; i < activeTeams && i < HexenDefs::MAXTEAMSINGAME; i++)
		{
			if (count_limit > Counts[i])
				count_limit = Counts[i];
		}
	}

	y = ChoicesStartY;
	if (activeTeams > 0)
	{
		staticText = MenuStaticText(NewChild(MenuStaticText));
		staticText.Text = "ACTIVE TEAMS";
		staticText.SetPos(ChoicesStartX, y);
		y += 24;
	
		// Active Teams
		for (i = 0; i < activeTeams && i < HexenDefs::MAXTEAMSINGAME; i++)
		{
			if (Teams[i] == -1)
				break;

			newChoice = MenuChoice_Team(NewChild(MenuChoice_Team));
			newChoice.SetOrigin(ChoicesStartX, y);
			newChoice.Text = TeamNames[Teams[i]];
			if (bKeepEqual)
			{
				newChoice.Text = va("%s (%i)", newChoice.Text, Counts[i]);
				if (curSelection == Teams[i])
				{
					newChoice.Text = va("%s (YOUR TEAM)", newChoice.Text);
					newChoice.bAllowed = true;
				}
				else if (Counts[i] < count_limit || curSelection == -1 && Counts[i] == count_limit)
				{
					newChoice.Text = va("%s (CAN JOIN)", newChoice.Text);
					newChoice.bAllowed = true;
				}
				else
				{
					newChoice.Text = va("%s (CANNOT JOIN)", newChoice.Text);
					newChoice.bAllowed = false;
				}
			}
			else
				newChoice.bAllowed = true;
			newChoice.Team = Teams[i];
			teams_active[Teams[i]] = true;
			y += 18;
		}
		y += 24;
	}

	// Available Teams
	if (activeTeams < maxTeams && activeTeams < HexenDefs::MAXTEAMSINGAME)
	{
		staticText = MenuStaticText(NewChild(MenuStaticText));
		staticText.Text = "AVAILABLE TEAMS";
		staticText.SetPos(ChoicesStartX, y);
		y += 24;
		for (i = 0; i < /*HexenDefs::MAXPLAYERTEAMS*/registeredTeams; i++)
		{
			if (teams_active[i])
			{
				continue;
			}
			newChoice = MenuChoice_Team(NewChild(MenuChoice_Team));
			newChoice.SetOrigin(ChoicesStartX, y);
			newChoice.Text = TeamNames[i];
			newChoice.Team = i;
			newChoice.bAllowed = true;
			y += 18;
		}
	}

	SetHeight(y);

	SetDefaultChoice();
}

//==========================================================================
//
//	ReInit
//
//==========================================================================

void ReInit()
{
	int i;
	for (i = 1; i < NumItems; i++)
	{
		Items[i].Destroy();
		Items[i] = none;
	}
	NumItems = 0;
	CreateChoices();

	// re-position selection
	SetDefaultChoice();
}

//==========================================================================
//
//	OnDestroy
//
//==========================================================================

void OnDestroy()
{
	if (!bIsSelected)
	{
		//SetCvar('Colour', curSelection);
		if (curSelection >= 0)
			Player(ClGame.cl).ServerJoinTeam(curSelection);
		else
		{
			int i;
			for (i = 0; i < NumItems; i++)
			{
				if (Items[i] && Items[i].IsA('MenuChoice_Team')
					&& MenuChoice_Team(Items[i]).bAllowed)
				{
					Player(ClGame.cl).ServerJoinTeam(MenuChoice_Team(Items[i]).Team);
					break;
				}
			}
		}
	}
}

//==========================================================================
//
//	SetDefaultChoice
//
//==========================================================================

void SetDefaultChoice()
{
	int i;
	int firstEnabled;

	CursorPos = -1;
	firstEnabled = -1;

	for (i = 0; i < NumItems; i++)
	{
		if (Items[i] && Items[i].Enabled)
		{
			if (firstEnabled == -1)
				firstEnabled = i;
			if (!Items[i].IsA('MenuChoice_Team'))
				continue;
			if (curSelection == -1 || MenuChoice_Team(Items[i]).Team == curSelection)
			{
				CursorPrev = -1;
				CursorPos = i;
				CursorMoved();
				break;
			}
		}
	}
	if (CursorPos == -1)
	{
		CursorPrev = -1;
		CursorPos = firstEnabled;
		CursorMoved();
	}
}

//==========================================================================
//
//	ReadTeams
//
//==========================================================================

void ReadTeams()
{
	TeamInfoParser::teaminfo_t tinfo;
	TeamInfoParser sc = TeamInfoParser(SpawnObject(TeamInfoParser));

	TeamNames.Num = 0;

	sc.OpenLumpName('teaminfo');
	if (sc.AtEnd())
	{
		sc.Destroy();
		Error("MenuScreenTeamSelect::ReadTeams : TEAMINFO lump missing or empty");
		return;
	}

	while(sc.ReadTeam(&tinfo))
	{
		TeamNames.Num = TeamNames.Num + 1;
		TeamNames[TeamNames.Num - 1] = tinfo.Name;
	}

	sc.Destroy();
}

defaultproperties
{
	Teams[0] = 0;
	Teams[1] = 1;

	maxTeams = HexenDefs::MAXTEAMSINGAME;
	curSelection = -1;

	ChoicesStartX = 120;
	ChoicesStartY = 44;
	SelectorType = MenuSelector_SmallRight;
	Title = "SELECT A TEAM TO JOIN:";
}
