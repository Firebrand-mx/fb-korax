//------------------------------------------------------------------------------
actor KRPGFireDemon : IntelligentActor replaces FireDemon
{
	Health 80
	Radius 20
	Height 68
	Mass 75
	Speed 13
	PainChance 1
	Damage 1
	Monster
	+FloorClip
	+Telestomp
	+NoGravity
	+DropOff
	+Float
	+Invulnerable
	SeeSound "FireDemonSpawn"
	ActiveSound "FireDemonActive"
	PainSound "FireDemonPain"
	DeathSound "FireDemonDeath"
	Obituary "$ob_firedemon"

	+EvadeMelee
	+RunOnEvade
	+FireDemonSpecialMove

	action native A_FiredRocks();
	action native A_FiredAttack();
	action native A_FiredSplotch();

	states
	{
	Spawn:
		FDMN X 5 Bright
		FDMN EFG 10 Bright A_LookAI
		Goto Spawn+1
	See:
		FDMN E 0 A_JumpIf(Health < default_health, "SeeDamaged")
		FDMN E 8 Bright
		FDMN F 6 Bright
		FDMN G 5 Bright
		FDMN F 8 Bright
		FDMN E 6 Bright
		FDMN F 7 Bright A_FiredRocks
		FDMN HI 5 Bright
		FDMN J 5 Bright A_UnSetInvulnerable
		FDMN ABC 5 Bright A_ChaseAI
		Goto See+10
	SeeDamaged:
		FDMD ABC 5 Bright A_ChaseAI
		Loop
	Pain:
		FDMN E 0 A_JumpIf(Health < default_health, "PainDamaged")
		FDMN D 6 Bright A_KRPGPain
		Goto See+10
	PainDamaged:
		FDMD D 6 Bright A_KRPGPain
		Goto SeeDamaged
	Missile:
		FDMN E 0 A_JumpIf(Health < default_health, "MissileDamaged")
		FDMN K 3 Bright A_FaceTarget
		FDMN KKK 5 Bright A_FiredAttack
		Goto See+10
	MissileDamaged:
		FDMD K 3 Bright A_FaceTarget
		FDMD KKK 5 Bright A_FiredAttack
		Goto SeeDamaged
	Death:
		FDMD D 4 Bright A_FaceTarget
		FDMN L 4 Bright A_Scream
		FDMN L 4 Bright A_NoBlocking
		FDMN L 200 Bright
		Stop
	XDeath:
	Crash:
		FDMN M 5 A_FaceTarget
		FDMN N 5 A_NoBlocking
		FDMN O 5 A_FiredSplotch
		Stop
	Ice:
		FDMN R 5 A_FreezeDeath
		FDMN R 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor FireDemonLeader : KRPGFireDemon 16038
{
	Health 160
	Radius 30
	Height 96
	PainChance 135
	
	-EvadeMelee
	-RunOnEvade
	
	Scale 1.5
	
	states
	{
	Pain:
		FDMN E 0 A_JumpIf(Health < default_health, "PainDamaged")
		FDMN D 6 Bright A_KRPGPain
		FDMN J 5 Bright
		FDMN IH 5 Bright
		FDMN F 7 Bright A_AISetInvulnerable
		FDMN EFGEFGEFGFE 6 Bright
		FDMN F 7 Bright A_FiredRocks
		FDMN HI 5 Bright
		FDMN J 5 Bright A_AIUnSetInvulnerable
		Goto See+10
	PainDamaged:
		FDMD D 6 Bright A_KRPGPain
		FDMD J 5 Bright
		FDMD IH 5 Bright
		FDMD F 7 Bright A_AISetInvulnerable
		FDMD EFGEFGEFGFE 6 Bright
		FDMD F 7 Bright A_FiredRocks
		FDMD HI 5 Bright
		FDMD J 5 Bright A_AIUnSetInvulnerable
		Goto SeeDamaged
	Missile:
		FDMN E 0 A_JumpIf(Health < default_health, "MissileDamaged")
		FDMN K 3 Bright A_FaceTarget
		FDMN KKKKK 5 Bright A_SimpleMissileAttack("FireDemonLeaderMissile", "FireDemonAttack")
		Goto See+10
	MissileDamaged:
		FDMD K 3 Bright A_FaceTarget
		// when damaged it fires only 3 times
		FDMD KKK 5 Bright A_SimpleMissileAttack("FireDemonLeaderMissile", "FireDemonAttack")
		Goto SeeDamaged
	}
}

//------------------------------------------------------------------------------
actor FireDemonLeaderMissile : FireDemonMissile
{
	Radius 15
	Height 9
	Mass 30
	Scale 1.5
}

//------------------------------------------------------------------------------
actor FireDemonMartyr : KRPGFireDemon 16039
{
	Health 60
	PainChance 0
	
	-EvadeMelee
	-RunOnEvade
	
	action native A_MartyrFlight();
	
	states
	{
	Melee:
		FDMN E 0 A_JumpIf(Health < default_health, "MeleeDamaged")
		FDMN D 6 Bright
		FDMN K 3 Bright A_FaceTarget
		FDMN K 2 Bright A_MartyrDetonate(Random(40, 125), 128, "FireDemonMartyrFX", Random(4, 12), 128, 32)
		Stop
	MeleeDamaged:
		FDMD D 6 Bright
		FDMD K 3 Bright A_FaceTarget
		FDMD K 2 Bright A_MartyrDetonate(Random(40, 125), 128, "FireDemonMartyrFX", Random(4, 12), 128, 32)
		Stop
	Missile:
		FDMN E 0 A_JumpIf(Health < default_health, "MissileDamaged")
		FDMN K 3 Bright A_FaceTarget
		FDMN J 5 Bright
		FDMN IH 5 Bright
		FDMN F 7 Bright
		FDMN E 4 Bright A_SkullAttack
		FDMN EFGEF 4 Bright A_MartyrFlight() //A_JumpIfCloser(128, "Explode")
		FDMN G 6 Bright A_FaceTarget
		Goto Missile+6
	MissileDamaged:
		FDMD E 0 A_JumpIf(Health < default_health, "MissileDamaged")
		FDMD K 3 Bright A_FaceTarget
		FDMD J 5 Bright
		FDMD IH 5 Bright
		FDMD F 7 Bright A_AISetInvulnerable
		FDMD E 4 Bright A_SkullAttack
		FDMD EFGEF 4 Bright A_MartyrFlight() //A_JumpIfCloser(128, "Explode")
		FDMD G 6 Bright A_FaceTarget
		Goto MissileDamaged+6
	Explode:
		FDMD G 2 Bright A_MartyrDetonate(Random(40, 125), 128, "FireDemonMartyrFX", Random(4, 12), 128, 32)
		Stop
	}	
}

//------------------------------------------------------------------------------
actor FireDemonMartyrFX
{
	RenderStyle Add
	Scale 2
	
	states
	{
	Spawn:
		FDMB B 5 Bright A_PlaySound("FireDemonMissileHit")
		FDMB CDE 8 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor FireDemonMartyrFX2
{
	Health 5
	RenderStyle Add
	+NoGravity
	
	states
	{
	Spawn:
		DMFX ABC 4 Bright A_FadeAway(0.2, 0.2, 1)
		DMFX C 0 A_JumpIf(Health < 0, "Death")
		Loop
	Death:
		Stop
	}
}
