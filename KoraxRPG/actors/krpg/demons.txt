//------------------------------------------------------------------------------
actor KRPGDemon1 : IntelligentActor replaces Demon1
{
	Health 250
	Radius 32
	Height 64
	Mass 220
	Speed 13
	PainChance 50
	Experience 200
	DropItem "GoldBar", 45, 1
	SeeSound "DemonSight"	
	ActiveSound "DemonActive"
	AttackSound "DemonAttack"
	PainSound "DemonPain"
	DeathSound "DemonDeath"	
	Obituary "$ob_demon1"
	Monster
	
	+FloorClip
	+Telestomp
	+Living
	
	+CanBackpedal
	+CanSidemove
	+CanBerserk
	
	states
	{
	Spawn:
		DEMN A 0 A_JumpIf(Health < default_health / 2, "SpawnDamaged")
		DEMN AA 10 A_LookAI
		Loop
	SpawnDamaged:
		DEMN R 0 A_JumpIf(Health > default_health / 2, "Spawn")
		DEMN RR 10 A_LookAI
		Loop
	See:
		DEMN A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		DEMN ABCD 4 A_ChaseAI
		Loop
	SeeDamaged:
		DEMN R 0 A_JumpIf(Health > default_health / 2, "See")
		DEMN RSTU 4 A_ChaseAI
		Loop
	Melee:
		DEMN E 0 A_JumpIf(Health < default_health / 2, "MeleeDamaged")
		DEMN E 6 A_FaceTarget
		DEMN F 8 A_FaceTarget
		DEMN G 6 A_CustomMeleeAttack(random(1, 8) * 2)
		Goto See
	MeleeDamaged:
		DEMN V 0 A_JumpIf(Health > default_health / 2, "Melee")
		DEMN V 6 A_FaceTarget
		DEMN W 8 A_FaceTarget
		DEMN X 6 A_CustomMeleeAttack(random(1, 8) * 2)
		Goto SeeDamaged
	Missile:
		DEMN E 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		DEMN E 5 A_FaceTarget
		DEMN F 6 A_FaceTarget
		//DEMN G 5 A_CustomMissile("Demon1FX1", 62, 0)	
		DEMN G 5 A_PredictingMissileAttack("Demon1FX1", 0.5, 25, 62, 0)
		Goto See
	MissileDamaged:
		DEMN V 0 A_JumpIf(Health > default_health / 2, "Missile")
		DEMN V 5 A_FaceTarget
		DEMN W 6 A_FaceTarget
		//DEMN X 5 A_CustomMissile("Demon1FX1", 62, 0)
		DEMN X 5 A_PredictingMissileAttack("Demon1FX1", 0.5, 25, 62, 0)
		Goto SeeDamaged
	Pain:
		DEMN E 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		DEMN E 4
		DEMN E 4 A_KRPGPain
		Goto See
	PainDamaged:
		DEMN V 0 A_JumpIf(Health > default_health / 2, "Pain")
		DEMN V 4
		DEMN V 4 A_KRPGPain
		Goto SeeDamaged
	Death:
		DEMN HI 6
		DEMN J 6 A_Scream
		DEMN K 6 A_NoBlocking
		DEMN L 6 A_QueueCorpse
		DEMN MNO 6
		DEMN P -1
		Stop
	XDeath:
		DEMN H 6
		DEMN I 0 A_SpawnItemEx("Demon1Chunk1", 0, 0, 45, random(0, 255) * 0.015625 + 1, random(0,255) * 0.015625 + 1, 8, 90, SXF_TRANSFERTRANSLATION)
		DEMN I 0 A_SpawnItemEx("Demon1Chunk2", 0, 0, 45, random(0, 255) * 0.015625 + 1, random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)
		DEMN I 0 A_SpawnItemEx("Demon1Chunk3", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)
		DEMN I 0 A_SpawnItemEx("Demon1Chunk4", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)
		DEMN I 6 A_SpawnItemEx("Demon1Chunk5", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)
		DEMN J 6 A_Scream
		DEMN K 6 A_NoBlocking
		DEMN L 6 A_QueueCorpse
		DEMN MNO 6
		DEMN P -1
		Stop
	Ice:		
		DEMN Y 5 A_FreezeDeath
		DEMN Y 1 A_FreezeDeathChunks
		Wait	
	}
}

//------------------------------------------------------------------------------
actor KRPGDemon2 : KRPGDemon1 replaces Demon2
{
	Experience 300
	Obituary "$ob_demon2"
	
	states
	{
	Spawn:
		DEM2 A 0 A_JumpIf(Health < default_health / 2, "SpawnDamaged")
		DEM2 AA 10 A_LookAI
		Loop
	SpawnDamaged:
		DEM2 Q 0 A_JumpIf(Health > default_health / 2, "Spawn")
		DEM2 QQ 10 A_LookAI
		Loop
	See:
		DEM2 A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		DEM2 ABCD 4 A_ChaseAI
		Loop
	SeeDamaged:
		DEM2 Q 0 A_JumpIf(Health > default_health / 2, "See")
		DEM2 QRST 4 A_ChaseAI
		Loop
	Melee:		
		DEM2 E 0 A_JumpIf(Health < default_health / 2, "MeleeDamaged")
		DEM2 E 6 A_FaceTarget
		DEM2 F 8 A_FaceTarget
		DEM2 G 6 A_CustomMeleeAttack(random(1, 8) * 2)
		Goto See
	MeleeDamaged:
		DEM2 U 0 A_JumpIf(Health > default_health / 2, "Melee")
		DEM2 U 6 A_FaceTarget
		DEM2 V 8 A_FaceTarget
		DEM2 W 6 A_CustomMeleeAttack(random(1, 8) * 2)
		Goto SeeDamaged
	Missile:
		DEM2 E 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		DEM2 E 5 A_FaceTarget
		DEM2 F 6 A_FaceTarget
		//DEM2 G 5 A_CustomMissile(KRPGDemon2FX1, 62, 0)
		DEM2 G 5 A_PredictingMissileAttack("KRPGDemon2FX1", 0.5, 25, 62, 0)
		Goto See
	MissileDamaged:
		DEM2 U 0 A_JumpIf(Health > default_health / 2, "Missile")
		DEM2 U 5 A_FaceTarget
		DEM2 V 6 A_FaceTarget
		//DEM2 W 5 A_CustomMissile(KRPGDemon2FX1, 62, 0)
		DEM2 W 5 A_PredictingMissileAttack("KRPGDemon2FX1", 0.5, 25, 62, 0)
		Goto SeeDamaged
	Pain:
		DEM2 E 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		DEM2 E 4
		DEM2 E 4 A_Pain
		Goto See
	PainDamaged:
		DEM2 U 0 A_JumpIf(Health > default_health / 2, "Pain")
		DEM2 U 4
		DEM2 U 4 A_Pain
		Goto SeeDamaged	
	Death:
		DEM2 HI 6
		DEM2 J 6 A_Scream
		DEM2 K 6 A_NoBlocking
		DEM2 L 6 A_QueueCorpse
		DEM2 MNO 6
		DEM2 P -1
		Stop
	XDeath:
		DEM2 H 6
		DEM2 I 0 A_SpawnItemEx("Demon2Chunk1", 0, 0, 45, random(0, 255) * 0.015625 + 1, random(0, 255) * 0.015625 + 1, 8, 90, SXF_TRANSFERTRANSLATION)
		DEM2 I 0 A_SpawnItemEx("Demon2Chunk2", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)	
		DEM2 I 0 A_SpawnItemEx("Demon2Chunk3", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)	
		DEM2 I 0 A_SpawnItemEx("Demon2Chunk4", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)
		DEM2 I 6 A_SpawnItemEx("Demon2Chunk5", 0, 0, 45, random(0, 255) * 0.015625 + 1,	random(0, 255) * 0.015625 + 1, 8, 270, SXF_TRANSFERTRANSLATION)	
		DEM2 J 6 A_Scream
		DEM2 K 6 A_NoBlocking
		DEM2 L 6 A_QueueCorpse
		DEM2 MNO 6
		DEM2 P -1
		Stop
	}
}

//------------------------------------------------------------------------------
actor KRPGDemon1Mash : KRPGDemon1 replaces Demon1Mash
{
	RenderStyle Translucent
	Alpha 0.4
	+NoBlood
	+Blasted
	-Telestomp
	+NoIceDeath
	
	states
	{
	Death:
	XDeath:	
	Ice:
		Stop
	}
}


//------------------------------------------------------------------------------
actor KRPGDemon2Mash : KRPGDemon2 replaces Demon2Mash
{
	RenderStyle Translucent	
	Alpha 0.4
	+NoBlood
	+Blasted
	-Telestomp
	+NoIceDeath
	states
	{
	Death:
	XDeath:
	Ice:
		Stop
	}
}
