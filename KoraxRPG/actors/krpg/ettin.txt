//------------------------------------------------------------------------------
actor KRPGEttin : IntelligentActor replaces Ettin
{
	Health 175
	Radius 25
	Height 68
	Mass 175
	Speed 20 //13
	Damage 3
	MeleeRange 72
	PainChance 60
	Experience 100
	DropItem "SilverBar", 40, 1
	SeeSound "EttinSight"
	ActiveSound "EttinActive"
	AttackSound "EttinAttack"
	PainSound "EttinPain"
	DeathSound "EttinDeath"
	HowlSound "PuppyBeat"
	Obituary "$ob_ettin"
	Monster

	+FloorClip
	+Telestomp
	+Living
	
	//+CanJump
	+CanBackpedal
	+CanSidemove
	+CanBerserk
	
	AttackRushChance 0.1
	AttackRushDistance 256

	states
	{
	Spawn:
		ETTN A 0 A_JumpIf(Health < default_health / 2, "SpawnDamaged")
		ETTN AA 10 A_LookAI
		Loop
	SpawnDamaged:
		ETTN S 0 A_JumpIf(Health > default_health / 2, "Spawn")
		ETTN SS 10 A_LookAI
		Loop
	See:
		ETTN A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		ETTN ABCD 5 A_ChaseAI
		Loop
	SeeDamaged:
		ETTN S 0 A_JumpIf(Health > default_health / 2, "See")
		ETTN STUV 5 A_ChaseAI
		Loop
	Pain:
		ETTN H 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		ETTN H 7 A_KRPGPain
		Goto See
	PainDamaged:
		ETTN Z 0 A_JumpIf(Health > default_health / 2, "Pain")
		ETTN Z 7 A_KRPGPain
		Goto SeeDamaged
	Melee:
		ETTN E 0 A_JumpIf(Health < default_health / 2, "MeleeDamaged")
		ETTN EF 6 A_FaceTarget
		ETTN G 8 A_CustomMeleeAttack(random(1, 8) * ((default_health - 175) / 175 + 2))
		Goto See
	MeleeDamaged:
		ETTN W 0 A_JumpIf(Health > default_health / 2, "Melee")
		ETTN WX 6 A_FaceTarget
		ETTN Y 8 A_CustomMeleeAttack(random(1, 8) * ((default_health - 175) / 175 + 2))
		Goto SeeDamaged
	Death:
		ETTN IJ 4
		ETTN K 4 A_Scream
		ETTN L 4 A_NoBlocking
		ETTN M 4 A_QueueCorpse
		ETTN NOP 4
		ETTN Q -1
		Stop
	XDeath:
		ETTB A 4
		ETTB B 4 A_NoBlocking
		ETTB C 4 A_SpawnItemEx(EttinMace, 0, 0, 34, random(-128, 127) * 0.03125,
			random(-128, 127) * 0.03125, 10 + random(0, 255) * 0.015625, 0,
			SXF_ABSOLUTEMOMENTUM)
		ETTB D 4 A_Scream
		ETTB E 4 A_QueueCorpse
		ETTB FGHIJK 4
		ETTB L -1
		Stop
	Ice:
		ETTN R 5 A_FreezeDeath
		ETTN R 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor KRPGEttinMash : KRPGEttin replaces EttinMash
{
	RenderStyle Translucent
	Alpha 0.4
	+NoBlood
	+Blasted
	+NoIceDeath
	-Telestomp

	states
	{
	Death:
	XDeath:
	Ice:
		Stop
	}
}

//------------------------------------------------------------------------------
actor EttinCommander : KRPGEttin 16002
{
	Health 700	// Ettin health x 4
	Experience 400
	DropItem "GoldBar", 45, 5
	DropItem "Mana2", 20, 15

	states
	{
	Missile:
		ETTN E 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		ETTN EF 6 A_FaceTarget
		//ETTN G 8 A_CustomMissile("EttinMissile", 32, 0)
		ETTN G 8 A_PredictingMissileAttack("EttinMissile", 0.5, 35, 32, 0)
		Goto Super::See
	MissileDamaged:
		ETTN W 0 A_JumpIf(Health > default_health / 2, "Missile")
		ETTN WX 6 A_FaceTarget
		//ETTN Y 8 A_CustomMissile("EttinMissile", 32, 0)
		ETTN Y 8 A_PredictingMissileAttack("EttinMissile", 0.5, 35, 32, 0)
		Goto Super::See
	}
}

//------------------------------------------------------------------------------
actor EttinMissile : HammerMissile
{
	SeeSound "EttinAttack"
}

//------------------------------------------------------------------------------
actor EttinSlinger : KRPGEttin 16001
{
	Experience 200
	DropItem "SilverBar", 45, 1
	DropItem "GoldBar", 25, 5

	states
	{
	Missile:
		ETTN E 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		ETTN EF 6 A_FaceTarget
		//ETTN G 8 A_CustomMissile("EttinArrow", 32, 0)
		ETTN G 8 A_PredictingMissileAttack("EttinArrow", 0.3, 35, 32, 0)
		Goto Super::See
	MissileDamaged:
		ETTN W 0 A_JumpIf(Health > default_health / 2, "Missile")
		ETTN WX 6 A_FaceTarget
		//ETTN Y 8 A_CustomMissile("EttinArrow", 32, 0)
		ETTN Y 8 A_PredictingMissileAttack("EttinArrow", 0.3, 35, 32, 0)
		Goto Super::See
	}
}

//------------------------------------------------------------------------------
actor EttinArrow
{
	Radius 8
	Height 4
	Speed 18
	Damage 2
	SeeSound "EttinAttack"
	Projectile

	states
	{
	Spawn:
		ARRW A -1
		Stop
	Death:
		ARRW A 1
		Stop
	}
}

//------------------------------------------------------------------------------
actor EttinFlechetter : KRPGEttin 16028
{
	Experience 300
	DropItem "SilverBar", 50, 1
	DropItem "ArtiPoisonBag", 30, 3

	+EvadeMelee
	-CanBerserk

	states
	{
	Melee:
		ETTN A 0 A_JumpIf(Health < default_health / 2, "MeleeDamaged")
		ETTN A 6 A_FaceTarget
		ETTN G 24 A_SpawnItem("FireBomb")
		Goto Super::See
	MeleeDamaged:
		ETTN S 0 A_JumpIf(Health > default_health / 2, "Melee")
		ETTN S 6 A_FaceTarget
		ETTN Y 24 A_SpawnItem("FireBomb")
		Goto Super::SeeDamaged
	Missile:
		ETTN A 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		ETTN A 6 A_AimGrenade("ThrowingBomb", Random(45,60), 24, "KRPGEttin::See")
		ETTN EF 12 A_FaceTarget
		ETTN G 24 A_ThrowAimedGrenade("ThrowingBomb", Random(45,60), 24, 68, Random(90, 99))
		Goto Super::See
	MissileDamaged:
		ETTN S 0 A_JumpIf(Health > default_health / 2, "Missile")
		ETTN S 6 A_AimGrenade("ThrowingBomb", Random(45,60), 24, "KRPGEttin::See")
		ETTN WX 12 A_FaceTarget
		ETTN Y 24 A_ThrowAimedGrenade("ThrowingBomb", Random(45,60), 24, 68, Random(90, 99))
		Goto Super::SeeDamaged
	}
}
