//------------------------------------------------------------------------------
actor KRPGEttin : IntelligentActor replaces Ettin
{
	Health 175
	Radius 25
	Height 68
	Mass 175
	Speed 13
	Damage 3
	PainChance 60
	SeeSound "EttinSight"
	ActiveSound "EttinActive"
	AttackSound "EttinAttack"
	PainSound "EttinPain"
	DeathSound "EttinDeath"
	HowlSound "PuppyBeat"
	Obituary "$ob_ettin"
	Monster
	+FloorClip
	+Telestomp
	+Living
	states
	{
	Spawn:
		ETTN AA 10 A_LookAI
		Loop
	See:
		ETTN ABCD 5 A_ChaseAI
		Loop
	Pain:
		ETTN H 7 A_Pain
		Goto See
	Melee:
		ETTN EF 6 A_FaceTarget
		ETTN G 8 A_CustomMeleeAttack(random(1, 8) * ((default_health - 175) / 175 + 2))
		Goto See
	Death:
		ETTN IJ 4
		ETTN K 4 A_Scream
		ETTN L 4 A_NoBlocking
		ETTN M 4 A_QueueCorpse
		ETTN NOP 4
		ETTN Q -1
		Stop
	XDeath:
		ETTB A 4
		ETTB B 4 A_NoBlocking
		ETTB C 4 A_SpawnItemEx(EttinMace, 0, 0, 34, random(-128, 127) * 0.03125,
			random(-128, 127) * 0.03125, 10 + random(0, 255) * 0.015625, 0,
			SXF_ABSOLUTEMOMENTUM)
		ETTB D 4 A_Scream
		ETTB E 4 A_QueueCorpse
		ETTB FGHIJK 4
		ETTB L -1
		Stop
	Ice:
		ETTN R 5 A_FreezeDeath
		ETTN R 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor KRPGEttinMash : KRPGEttin replaces EttinMash
{
	RenderStyle Translucent
	Alpha 0.4
	+NoBlood
	+Blasted
	+NoIceDeath
	-Telestomp
	states
	{
	Death:
	XDeath:
	Ice:
		Stop
	}
}

//------------------------------------------------------------------------------
actor EttinCommander : KRPGEttin 16002
{
	Health 700	// Ettin health x 4
	states
	{
	Missile:
		ETTN EF 6 A_FaceTarget
		ETTN G 8 A_CustomMissile("EttinMissile", 32, 0)
		Goto Super::See
	}
}

//------------------------------------------------------------------------------
actor EttinMissile : HammerMissile
{
	SeeSound "EttinAttack"
}

//------------------------------------------------------------------------------
actor EttinSlinger : KRPGEttin 16001
{
	states
	{
	Missile:
		ETTN EF 6 A_FaceTarget
		ETTN G 8 A_CustomMissile("EttinArrow", 32, 0)
		Goto Super::See
	}
}

//------------------------------------------------------------------------------
actor EttinArrow
{
	Radius 8
	Height 4
	Speed 18
	Damage 2
	SeeSound "EttinAttack"
	Projectile
	states
	{
	Spawn:
		ARRW A -1
		Stop
	Death:
		ARRW A 1
		Stop
	}
}
