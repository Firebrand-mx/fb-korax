//------------------------------------------------------------------------------
actor KRPGBishop : IntelligentActor replaces Bishop
{
	Health 130
	Radius 22
	Height 65
	Mass 100
	Speed 20
	PainChance 110
	Monster
	DropItem "GoldBar", 45, 20
	+Telestomp
	+NoGravity
	+Float
	+NoBlood
	-ActivateMCross
	+NoTargetSwitch
	+DontOverlap
	SeeSound "BishopSight"
	ActiveSound "BishopActiveSounds"
	AttackSound "BishopAttack"
	PainSound "BishopPain"
	DeathSound "BishopDeath"
	Obituary "$ob_bishop"

	+EvadeMelee
	+RunOnEvade

	action native A_BishopChase();
	action native A_BishopDecide();
	action native A_BishopDoBlur();
	action native A_BishopSpawnBlur();
	action native A_BishopAttack();
	action native A_BishopAttack2();
	action native A_BishopPainBlur();

	states
	{
	Spawn:
		BISH A 10 A_LookAI
		Loop
	See:
		BISH A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		BISH A 2 A_ChaseAI
		BISH A 2 A_BishopChase
		BISH A 2
		BISH B 2 A_BishopChase
		BISH B 2 A_ChaseAI
		BISH B 2 A_BishopChase
		BISH A 1 A_BishopDecide
		Loop
	SeeDamaged:
		BISD A 2 A_ChaseAI
		BISD A 2 A_BishopChase
		BISD A 2
		BISD B 2 A_BishopChase
		BISD B 2 A_ChaseAI
		BISD B 2 A_BishopChase
		BISD A 1 A_BishopDecide
		Loop
	Blur:
	Evade:
		BISH A 0 A_JumpIf(Health < default_health / 2, "BlurDamaged")
		BISH A 2 A_BishopDoBlur
		BISH A 4 A_BishopSpawnBlur
		Wait
	BlurDamaged:
	EvadeDamaged:
		BISD A 2 A_BishopDoBlur
		BISD A 4 A_BishopSpawnBlur
		Wait
	Missile:
		BISH A 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		BISH A 3 A_FaceTarget
		BISH DE 3 Bright A_FaceTarget
		BISH F 3 Bright A_BishopAttack
		BISH F 5 Bright A_BishopAttack2
		Wait
	MissileDamaged:
		BISD A 3 A_FaceTarget
		BISD DE 3 Bright A_FaceTarget
		BISD F 3 Bright A_BishopAttack
		BISD F 5 Bright A_BishopAttack2
		Wait
	Pain:
		BISH A 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		BISH C 6 A_KRPGPain
		BISH CCC 6 A_BishopPainBlur
		BISH C 0
		Goto See
	PainDamaged:
		BISD C 6 A_KRPGPain
		BISD CCC 6 A_BishopPainBlur
		BISD C 0
		Goto SeeDamaged
	Death:
		BISH G 6
		BISH H 6 Bright A_Scream
		BISH I 5 Bright A_NoBlocking
		BISH J 5 Bright A_Explode(Random(25, 40))
		BISH K 5 Bright
		BISH LM 4 Bright
		BISH N 4 A_SpawnItemEx("BishopPuff", 0, 0, 40, 0, 0, 0.5)
		BISH O 4 A_QueueCorpse
		BISH P -1
		Stop
	Ice:
		BISH X 5 A_FreezeDeath
		BISH X 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor BishopFXRed : BishopFX
{
	// from Heretic : HornRodFX1
	Radius 12
	Height 8
	Speed 10
	DeathSound "CardinalMissileExplode"
	-StrifeDamage
	states
	{
	Spawn:
		FX00 ABAB 1 Bright A_BishopMissileWeave
		FX00 B 0 Bright A_SeekerMissile(2, 7)
		Loop
	Death:
		FX00 HI 5 Bright
		FX00 JK 4 Bright
		FX00 LM 3 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopFXBlack : BishopFXRed
{
	Speed 14
	Damage 2
	DeathSound "AdvisorMissileExplode"
	states
	{
	Spawn:
		FX19 ABAB 1 Bright A_BishopMissileWeave
		FX19 B 0 Bright A_SeekerMissile(2, 15)
		Loop
	Death:
		FX19 HI 5 Bright
		FX19 JK 4 Bright
		FX19 LM 3 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopBlurRed : BishopBlur
{
	states
	{
	Spawn:
		BISR A 16
		BISR A 8 A_SetTranslucent(0.4)
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopBlurBlack : BishopBlur
{
	states
	{
	Spawn:
		BISB A 16
		BISB A 8 A_SetTranslucent(0.4)
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopPainBlurRed : BishopPainBlur
{
	states
	{
	Spawn:
		BISR C 8
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopPainBlurBlack : BishopPainBlur
{
	states
	{
	Spawn:
		BISB C 8
		Stop
	}
}
