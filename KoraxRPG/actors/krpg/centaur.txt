//------------------------------------------------------------------------------
actor KRPGCentaur : IntelligentActor replaces Centaur
{
	Health 200
	Radius 20
	Height 64
	Mass 120
	Speed 13
	PainChance 135
	SeeSound "CentaurSight"
	ActiveSound "CentaurActive"
	AttackSound "CentaurAttack"
	PainSound "CentaurPain"
	DeathSound "CentaurDeath"
	HowlSound "PuppyBeat"
	Obituary "$ob_centaur"
	Monster
	+FloorClip
	+Telestomp
	+ShieldReflect
	states
	{
	Spawn:
		CENT AB 10 A_LookAI
		Loop
	See:
		CENT ABCD 4 A_ChaseAI
		Loop
	Melee:
		CENT H 5 A_FaceTarget
		CENT I 4 A_FaceTarget
		CENT J 7 A_CustomMeleeAttack(random(3, 10))
		Goto See
	Pain:
		CENT G 6 A_Pain
		CENT G 6 A_AISetReflectiveInvulnerable
		CENT EEE 15 A_CentaurDefend
		CENT E 1 A_AIUnSetReflectiveInvulnerable
		Goto See
	Death:
		CENT K 4
		CENT L 4 A_Scream
		CENT MN 4
		CENT O 4 A_NoBlocking
		CENT PQ 4
		CENT R 4 A_QueueCorpse
		CENT S 4
		CENT T -1
		Stop
	XDeath:
		CTXD A 4
		CTXD B 4 A_NoBlocking
		CTXD C 0 A_SpawnItemEx("CentaurShield", 0, 0, 45, random(-128, 127) * 0.03125 + 1,
			random(-128, 127) * 0.03125 + 1, 8 + random(0, 255) * 0.015625, 90)
		CTXD C 4 A_SpawnItemEx("CentaurSword", 0, 0, 45, random(-128, 127) * 0.03125 + 1,
			random(-128, 127) * 0.03125 + 1, 8 + random(0, 255) * 0.015625, 270)
		CTXD D 3 A_Scream
		CTXD E 4 A_QueueCorpse
		CTXD F 3
		CTXD G 4
		CTXD H 3
		CTXD I 4
		CTXD J 3
		CTXD K -1
		Stop
	Ice:
		CENT U 5 A_FreezeDeath
		CENT U 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor KRPGCentaurLeader : KRPGCentaur replaces CentaurLeader
{
	Health 250
	Speed 10
	PainChance 96
	Obituary "$ob_slaughtaur"
	HitObituary "$ob_slaughtaurhit"
	states
	{
	Missile:
		CENT E 10 A_FaceTarget
		CENT F 8 Bright A_CustomMissile("CentaurFX", 45, 0, 0, CMF_AIMOFFSET)
		CENT E 10 A_FaceTarget
		CENT F 8 Bright A_CustomMissile("CentaurFX", 45, 0, 0, CMF_AIMOFFSET)
		Goto See
	}
}

//------------------------------------------------------------------------------
actor KRPGCentaurMash : KRPGCentaur replaces CentaurMash
{
	RenderStyle Translucent
	Alpha 0.4
	+NoBlood
	+Blasted
	+NoIceDeath
	-Telestomp
	states
	{
	Death:
	XDeath:
	Ice:
		Stop
	}
}
