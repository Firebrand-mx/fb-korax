//------------------------------------------------------------------------------
actor KRPGWraith : IntelligentActor replaces Wraith
{
	Health 150
	Radius 20
	Height 55
	Mass 75
	Speed 11
	PainChance 25
	Damage 10
	Monster
	+FloorClip
	+Telestomp
	+NoGravity
	+DropOff
	+Float
	+CanBerserk
	SeeSound "WraithSight"
	ActiveSound "WraithActive"
	AttackSound "WraithAttack"
	PainSound "WraithPain"
	DeathSound "WraithDeath"
	Obituary "$ob_wraith"
	HitObituary "$ob_wraithhit"

	action native A_WraithInit();
	action native A_WraithChaseAI();
	action native A_WraithFX3();
	action native A_WraithMelee();

	states
	{
	Spawn:
		WRTH A 10
		WRTH B 5 A_WraithInit
	Look:
		WRTH AB 15 A_LookAI
		Loop
	See:
		WRTH A 0 A_JumpIf(Health < default_health / 2, "SeeDamaged")
		WRTH ABCD 4 A_WraithChaseAI
		Loop
	SeeDamaged:
		WRTD ABCD 4 A_WraithChaseAI
		Loop
	Melee:
		WRTH A 0 A_JumpIf(Health < default_health / 2, "MeleeDamaged")
		WRTH E 6 A_FaceTarget
		WRTH F 6 A_WraithFX3
		WRTH G 6 A_WraithMelee
		Goto See
	MeleeDamaged:
		WRTD E 6 A_FaceTarget
		WRTD F 6 A_WraithFX3
		WRTD G 6 A_WraithMelee
		Goto SeeDamaged
	Missile:
		WRTH A 0 A_JumpIf(Health < default_health / 2, "MissileDamaged")
		WRTH E 6 A_FaceTarget
		WRTH F 6
		WRTH G 6 A_CustomMissile("WraithFX1", 32, 0)
		Goto See
	MissileDamaged:
		WRTD E 6 A_FaceTarget
		WRTD F 6
		WRTD G 6 A_CustomMissile("WraithFX1", 32, 0)
		Goto SeeDamaged
	Pain:
		WRTH A 0 A_JumpIf(Health < default_health / 2, "PainDamaged")
		WRTH A 2
		WRTH H 6 A_KRPGPain
		Goto See
	PainDamaged:
		WRTD A 2
		WRTD H 6 A_KRPGPain
		Goto SeeDamaged
	Death:
		WRTH I 4
		WRTH J 4 A_Scream
		WRTH KL 4
		WRTH M 4 A_NoBlocking
		WRTH N 4 A_QueueCorpse
		WRTH O 4
		WRTH PQ 5
		WRTH R -1
		Stop
	XDeath:
		WRT2 A 5
		WRT2 B 5 A_Scream
		WRT2 CD 5
		WRT2 E 5 A_NoBlocking
		WRT2 F 5 A_QueueCorpse
		WRT2 G 5
		WRT2 H -1
		Stop
	Ice:
		WRT2 I 5 A_FreezeDeath
		WRT2 I 1 A_FreezeDeathChunks
		Wait
	}
}

//------------------------------------------------------------------------------
actor KRPGWraithBuried : KRPGWraith replaces WraithBuried
{
	Height 68
	PainChance 0
	-Solid
	-Shootable
	+Invisible
	+DontMorph
	+DontBlast
	+StayMorphed

	action native A_WraithRaiseInit();
	action native A_WraithRaise();

	states
	{
	Spawn:
		Goto Super::Look
	See:
		WRTH A 2 A_WraithRaiseInit
		WRTH A 2 A_WraithRaise
		WRTH A 2 A_FaceTarget
		WRTH BB 2 A_WraithRaise
		Goto See+1
	Chase:
		Goto Super::See
	}
}
