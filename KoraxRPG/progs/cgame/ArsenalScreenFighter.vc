//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**		        ######  #####   ######
//**	            ##   ## ##  ## ##    ##
//**	     	    ##   ## ##  ## ##   
//**	            #####   #####  ##  ####
//**	            ## ###  ##     ###   ##
//**	     	    ##  ### ##      ######
//**
//**    $Id: ArsenalScreenFighter.vc 1638 2008-08-28 19:10:17Z firebrand_kh $
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ArsenalScreenFighter : ArsenalScreen;

MenuStaticText	StaticText;
MenuTextButton	SelectedEntry;

array<MenuSpriteAnim>   Anim;
array<anim_t>			anim_def;
name list[2];

//==========================================================================
//
//	CreateChoices
//
//==========================================================================

void CreateChoices()
{
	int i;
	int YPos = ChoicesStartY;

	if (cl)
	{
		anim_def.Num = 0;

		// [FB] Initialize the sprite defs here
		for (i = 0; i < 2; i++)
		{
			anim_def.Num = anim_def.Num + 1;
			anim_def[i].Name = list[i];
			anim_def[i].Index = i;
		}

		StaticText = MenuStaticText(NewChild(MenuStaticText));
		StaticText.SetOrigin(ChoicesStartX, YPos);
		// [CW] TODO: make methods for static to automatically adjust size
		StaticText.Text = va("Slots: %i - %i;\n Current Slot: %i", SlotFirst, SlotLast, SlotCurrent);
		StaticText.Height = StaticText.TextHeight(StaticText.Text);
		YPos += StaticText.Height << 1;

		Anim.Num = 0;
		CreateWeaponEntries(YPos);
	}
}

//==========================================================================
//
//	CreateWeaponEntries
//
//==========================================================================

void CreateWeaponEntries(int YPos)
{
	int i, j;
	MenuTaggedTextButton Btn, Btn2;
	Inventory Item = cl.WeaponFirst;

	while (Item)
	{
		if (HexenWeapon(Item).Slot == SlotCurrent)
		{
			for (j = 0; j < 2; j++)
			{
				// [FB] Scan sprite definitions here,
				//		also make sure not to duplicate sprite instances...
				if (HexenWeapon(Item).IconName == anim_def[j].Name)
				{
					if  (!anim_def[j].Installed)
					{
						R_InstallSprite(va("%n", HexenWeapon(Item).IconName), 500 + anim_def[j].Index);
						// [FB] Sprite is installed now
						anim_def[j].Installed = true;
					}
					Anim.Num = Anim.Num + 1;
					Anim[Anim.Num - 1] = MenuSpriteAnim(NewChild(MenuSpriteAnim));
					Anim[Anim.Num - 1].SprDef = 500 + anim_def[j].Index;
					Anim[Anim.Num - 1].NumFrames = 1;
					Anim[Anim.Num - 1].Speed = 1;
					Anim[Anim.Num - 1].SetPos(ChoicesStartX + 6, YPos + 4);
				}
			}


			Btn = MenuTaggedTextButton(NewChild(MenuTaggedTextButton));
			Btn.Tag = i++;
			Btn.SetOrigin(ChoicesStartX + 46, YPos + 2);
			if (StrStartsWith(Item.PickupMessage, "$"))
			{
				Btn.Text = GetLangString(StrToName(substr(Item.PickupMessage, 1, strlen(Item.PickupMessage) - 1)));
			}
			else
			{
				Btn.Text = Item.PickupMessage;
			}
			Btn.Width = 600;

			if (cl.WeaponsBelt[SlotCurrent] == Item)
			{
				SelectedEntry = Btn;
				Btn.TextColour = CR_YELLOW;
				Btn.TextColourFocus = CR_GOLD;
			}
			else
			{
				Btn.TextColour = CR_UNTRANSLATED;
				Btn.TextColourFocus = CR_WHITE;
			}
			Btn.Activated = WeaponSelect;
			YPos += Btn.Height + 25;
		}

		Item = Item.NextWpn();
	}
}

//==========================================================================
//
//	OnDraw
//
//==========================================================================

void OnDraw()
{
	DrawPic(0, 0, R_RegisterPic('figascrn'));
	ShadeRect(15, 255, 608, 202, GetCvarF('menu_darkening'));
}

//==========================================================================
//
//	OnKeyDown
//
//==========================================================================

bool OnKeyDown(int Key)
{
	switch (Key)
	{
	case K_LEFTARROW:
		PrevPage();
		LocalSound('menu/change');
		break;
	case K_RIGHTARROW:
		NextPage();
		LocalSound('menu/change');
		break;
	case K_DOWNARROW:
	case K_UPARROW:
		if (Selector)
		{
			return ::OnKeyDown(Key);
		}
		break;

	default:
		{
			if (Key >= 0x30 && Key <= 0x39)
			{
				SetPage(Key == 0x30 ? 10 : Key - 0x30);
				LocalSound('menu/change');
			}
			else
			{
				return ::OnKeyDown(Key);
			}
		}
	}
	
	return true;
}

//==========================================================================
//
//	NextPage
//
//==========================================================================

void NextPage()
{
	if (SlotCurrent == SlotLast)
	{
		SlotCurrent = SlotFirst;
	}
	else
	{
		SlotCurrent++;
	}

	UpdatePage();
}

//==========================================================================
//
//	PrevPage
//
//==========================================================================

void PrevPage()
{
	if (SlotCurrent == SlotFirst)
	{
		SlotCurrent = SlotLast;
	}
	else
	{
		SlotCurrent--;
	}

	UpdatePage();
}

//==========================================================================
//
//	SetPage
//
//==========================================================================

void SetPage(int page)
{
	if (page >= SlotFirst && page <= SlotLast)
	{
		SlotCurrent = page;
		UpdatePage();
	}
}

//==========================================================================
//
//	UpdatePage
//
//==========================================================================

void UpdatePage()
{
	int i, j;
	StaticText.Destroy();
	for (i = 0; i < Anim.Num; i++)
	{
		Anim[i].Destroy();
	}
	for (i = 0; i < NumItems; i++)
	{
		Items[i].Destroy();
		Items[i] = none;
	}
	NumItems = 0;
	CreateChoices();

	if (NumItems)
	{
		CreateSelector();
		SetDefaultChoice();
	}
	else
	{
		Selector.Destroy();
	}
}

//==========================================================================
//
//	WeaponSelect
//
//==========================================================================

void WeaponSelect(Object Sender)
{
	if (SelectedEntry != Sender)
	{
		Player(ClientGame(ClGame).cl).PutWeaponOnBelt(SlotCurrent, MenuTaggedTextButton(Sender).Tag);
		if (SelectedEntry)
		{
			SelectedEntry.TextColour = CR_UNTRANSLATED;
			SelectedEntry.TextColourFocus = CR_WHITE;
		}
		SelectedEntry = MenuTextButton(Sender);
		SelectedEntry.TextColour = CR_YELLOW;
		SelectedEntry.TextColourFocus = CR_GOLD;
	}
}

defaultproperties
{
	X = 0;
	Y = 0;
	Width = 640;
	Height = 480;
	TitleX = 320;
	ChoicesStartX = 140;
	ChoicesStartY = 260;
	list[0] = 'WFAXE';
	list[1] = 'WFHAMR';

	SelectorType = MenuSelector_SmallLeft;
	Title = "Arsenal";
}
