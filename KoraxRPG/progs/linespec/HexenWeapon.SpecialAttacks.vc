//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id: HexenWeapon.SpecialAttacks.vc 1638 2008-08-28 19:10:17Z firebrand_kh $
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//--------------------------------------------------------------------------
//
//
// Special attack methods for KRPG Weapons
//
//
//--------------------------------------------------------------------------

//
// [CW] CHECKME: Some of these could be converted to generic ones (later)
//

//==========================================================================
//
//  WA_CStaffCheck
//
//==========================================================================

final void WA_CStaffCheck() self(Actor)
{
	int			damage;
	int			newLife;
	int			i;
	TAVec		angles;
	TVec		vforward;
	EntityEx	AimTarget;

	if (!bIsPlayer)
	{
		return;
	}

	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	damage = (20 + (P_Random() & 15)) * KRPGPlayerBase(Player).intelligence / 10;
	AimTarget = AimEx(vforward, 3.0 * MELEERANGE / 2.0, 45.0 / 16.0, 3);
	if (AimTarget)
	{
		VectorAngles(&vforward, &angles);
		LineAttack(vforward, 1.5 * MELEERANGE, damage, CStaffPuff);
		Angles.yaw = atan2(AimTarget.Origin.y - Origin.y,
			AimTarget.Origin.x - Origin.x);
		if ((AimTarget.bIsPlayer || AimTarget.bMonster) &&
			!AimTarget.bDormant && !AimTarget.bInvulnerable)
		{
			newLife = Player.Health + (damage >> 3);
			newLife = newLife > 100 ? 100 : newLife;
			Health = newLife;
			Player.Health = newLife;
			Player.SetViewState(ps_weapon, Wpn.FindState('Fire2'));
		}
		HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode, false);
	}
}

//==========================================================================
//
//  WA_CStaffAttack
//
//==========================================================================

final void WA_CStaffAttack() self(Actor)
{
	EntityEx	mo;

	if (!bIsPlayer)
	{
		return;
	}

	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	mo = SpawnPlayerMissileAngle(CStaffMissile, Angles.yaw - (45.0 / 15.0));
	if (mo)
	{
		mo.Special2 = 32;
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
	}
	mo = SpawnPlayerMissileAngle(CStaffMissile, Angles.yaw + (45.0 / 15.0));
	if (mo)
	{
		mo.Special2 = 0;
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
	}
	PlaySound('ClericCStaffFire', CHAN_WEAPON);
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//============================================================================
//
//  WA_CFlameAttack
//
//============================================================================

final void WA_CFlameAttack() self(Actor)
{
	EntityEx mo;

	if (!bIsPlayer)
	{
		return;
	}

	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	mo = SpawnPlayerMissile(CFlameMissile);
	if (mo)
	{
		Actor(mo).Special1 = 2;
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
	}

	PlaySound('ClericFlameFire', CHAN_WEAPON);
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//============================================================================
//
//  WA_CHolyAttack
//
//============================================================================

final void WA_CHolyAttack() self(Actor)
{
	EntityEx	A;
	EntityEx	AimTarget;

	if (!bIsPlayer)
	{
		return;
	}

	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	A = SpawnPlayerMissileEx(HolyMissile, false, AimTarget);
	if (A)
	{
		A.Tracer = AimTarget;
		A.MissileDamage = A.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
	}
//	CWeapWraithverge(Wpn).CShift = RGBA(255, 255, 255, 66);
	PlaySound('HolySymbolFire', CHAN_WEAPON);
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//==========================================================================
//
//  WA_FAxeAttack
//
//==========================================================================

final void WA_FAxeAttack() self(Actor)
{
	float			power;
	int				damage;
	int				useMana;
	TAVec			angles;
	TVec			vforward;
	class<Actor>	PuffType;
	EntityEx		AimTarget;

	if (!bIsPlayer)
	{
		return;
	}

	HexenWeapon Wpn = HexenWeapon(PlayerEx(Player).ReadyWeapon);

	damage = (40 + (P_Random() & 15) + (P_Random() & 7)) * KRPGPlayerBase(Player).strength / 10;
	power = 0.0;
	if (FindInventory(Mana1).Amount > 0)
	{
		damage <<= 1;
		power = 6.0;
		PuffType = AxePuffGlow;
		useMana = 1;
	}
	else
	{
		PuffType = AxePuff;
		useMana = 0;
	}
	AimTarget = AimEx(vforward, HexenWeapon::AxeRange, 45.0 / 16.0, 16, MELEERANGE);
	if (AimTarget)
	{
		VectorAngles(&vforward, &angles);
		if (LineAttack(vforward, HexenWeapon::AxeRange, damage, PuffType))
		{
			PlaySound(PuffType.default.ActiveSound, CHAN_WEAPON);
		}
		if (AimTarget.bMonster || AimTarget.bIsPlayer)
		{
			AimTarget.Thrust(angles.yaw, power);
		}
		PlayerEx(Player).AdjustPlayerAngle(AimTarget);
		useMana++;
	}
	else
	{
		// didn't find any creatures, so try to strike any walls
		if (LineAttack(vforward, MELEERANGE, damage, PuffType))
		{
			PlaySound(PuffType.default.ActiveSound, CHAN_WEAPON);
		}
	}

	if (useMana == 2)
	{
		HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode, false);
		if (!Wpn.CheckAmmo(Wpn.FireMode, false, true))
		{
			Player.SetViewState(ps_weapon, Wpn.FindState('NoMoreMana'));
		}
	}
}

//============================================================================
//
//  WA_FHammerAttack
//
//============================================================================

final void WA_FHammerAttack() self(Actor)
{
	int			damage;
	int			r_damage;
	int			m_damage;
	float		power;
	TAVec		angles;
	TVec		vforward;
	EntityEx	AimTarget;

	if (!bIsPlayer)
	{
		return;
	}
	HexenWeapon Wpn = HexenWeapon(PlayerEx(Player).ReadyWeapon);

	damage = (60 + (P_Random() & 63)) * KRPGPlayerBase(Player).strength / 10;
	power = 10.0;
	AimTarget = AimEx(vforward, HexenWeapon::HammerRange, 45.0 / 32.0, 16, HexenWeapon::HammerRange);
	if (AimTarget)
	{
		VectorAngles(&vforward, &angles);
		if (LineAttack(vforward, HexenWeapon::HammerRange, damage, HammerPuff))
		{
			PlaySound('FighterHammerMiss', CHAN_WEAPON);
		}
		PlayerEx(Player).AdjustPlayerAngle(AimTarget);
		if (AimTarget.bMonster || AimTarget.bIsPlayer)
		{
			AimTarget.Thrust(angles.yaw, power);
		}
		Special1 = false;	// Don't throw a hammer
	}
	else
	{
		// didn't find any targets in meleerange, so set to throw out a hammer
		LineSpecialLevelInfo(Level).bPuffSpawned = false;
		if (LineAttack(vforward, HexenWeapon::HammerRange, damage, HammerPuff))
		{
			PlaySound('FighterHammerMiss', CHAN_WEAPON);
		}
		if (LineSpecialLevelInfo(Level).bPuffSpawned)
		{
			Special1 = false;
		}
		else
		{
			Special1 = true;
		}
	}

	if (!Wpn.CheckAmmo(Wpn.FireMode, false, true))
	{
		// Don't spawn a hammer if the player doesn't have enough mana
		Special1 = false;
	}
	return;
}

//============================================================================
//
//  WA_FHammerThrow
//
//============================================================================

final void WA_FHammerThrow() self(Actor)
{
	EntityEx mo;
	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	if (!Special1)
	{
		return;
	}
	
	// [FB] Dexterity modifies the damage done by ranged physical attacks
	mo = SpawnPlayerMissile(HammerMissile);
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode, false);
}

//============================================================================
//
//  WA_FSwordAttack
//
//============================================================================

final void WA_FSwordAttack() self(Actor)
{
	EntityEx mo;

	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	mo = SpawnPlayerMissileAngleXYZ(Origin + vector(0.0, 0.0, -10.0),
		FSwordMissile, AngleMod360(Angles.yaw + 45.0 / 4.0));
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	mo = SpawnPlayerMissileAngleXYZ(Origin + vector(0.0, 0.0, -5.0),
		FSwordMissile, AngleMod360(Angles.yaw + 45.0 / 8.0));
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	mo = SpawnPlayerMissileAngleXYZ(Origin, FSwordMissile,
		Angles.yaw);
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	mo = SpawnPlayerMissileAngleXYZ(Origin + vector(0.0, 0.0, 5.0),
		FSwordMissile, AngleMod360(Angles.yaw - 45.0 / 8.0));
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	mo = SpawnPlayerMissileAngleXYZ(Origin + vector(0.0, 0.0, 10.0),
		FSwordMissile, AngleMod360(Angles.yaw - 45.0 / 4.0));
	if (mo)
	{
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).dexterity / 10;
	}
	PlaySound('FighterSwordFire', CHAN_WEAPON);
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//==========================================================================
//
//  WA_FireConePL1
//
//==========================================================================

final void WA_FireConePL1() self(Actor)
{
	TAVec			ang;
	int				damage;
	int				i;
	FrostMissile	mo;
	int				conedone = false;
	TVec			Dir;
	EntityEx		AimTarget;

	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	PlaySound('MageShardsFire', CHAN_WEAPON);

	damage = (90 + (P_Random() & 15)) * KRPGPlayerBase(Player).intelligence / 10;
	for (i = 0; i < 16; i++)
	{
		ang = Angles;
		ang.yaw = AngleMod360(ang.yaw + itof(i) * (45.0 / 16.0));
		AimTarget = AimLineAttack(Dir, ang, MELEERANGE);
		if (AimTarget)
		{
			AimTarget.Damage(self, self, damage, 'Ice');
			conedone = true;
			break;
		}
	}

	// didn't find any creatures, so fire projectiles
	if (!conedone)
	{
		mo = FrostMissile(SpawnPlayerMissile(FrostMissile));
		if (mo)
		{
			mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
			mo.bShardSpawnLeft = true;
			mo.bShardSpawnRight = true;
			mo.bShardSpawnUp = true;
			mo.bShardSpawnDown = true;
			mo.Special2 = 3;	// Set sperm count (levels of reproductivity)
			mo.Target = self;
			mo.Args[0] = 3;	// Mark Initial shard as super damage
		}
	}

	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//==========================================================================
//
//  WA_MLightningAttack
//
//==========================================================================

final void WA_MLightningAttack() self(Actor)
{
	Lightning fmo;
	Lightning cmo;

	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	fmo = Lightning(SpawnPlayerMissile(LightningFloor));
	cmo = Lightning(SpawnPlayerMissile(LightningCeiling));
	if (fmo)
	{
		fmo.MissileDamage = fmo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
		fmo.ZigZagCount = 0;
		fmo.LightningLink = cmo;
		fmo.A_LightningZap();
	}
	if (cmo)
	{
		cmo.MissileDamage = cmo.MissileDamage * KRPGPlayerBase(Player).intelligence / 10;
		cmo.Tracer = none;	// mobj that it will track
		cmo.LightningLink = fmo;
		cmo.A_LightningZap();
	}
	PlaySound('MageLightningFire', CHAN_WEAPON);
	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//==========================================================================
//
//	WA_MStaffSpawn
//
//==========================================================================

final void WA_MStaffSpawn(float angle)
{
	EntityEx	mo;

	mo = EntityEx(Owner).SpawnPlayerMissileAngle(MageStaffFX2, angle);
	if (mo)
	{
		mo.Target = EntityEx(Owner);
		mo.MissileDamage = mo.MissileDamage * KRPGPlayerBase(Owner.Player).intelligence / 10;
		MageStaffFX2(mo).FindEnemy();
	}
}

//==========================================================================
//
//  WA_MStaffAttack
//
//==========================================================================

final void WA_MStaffAttack() self(Actor)
{
	float angle;

	if (!bIsPlayer)
	{
		return;
	}

	HexenWeapon Wpn = HexenWeapon(PlayerEx(Player).ReadyWeapon);

	angle = Angles.yaw;

	Wpn.WA_MStaffSpawn(angle);
	Wpn.WA_MStaffSpawn(angle - 5.0);
	Wpn.WA_MStaffSpawn(angle + 5.0);
	PlaySound('MageStaffFire', CHAN_WEAPON);
//	MWeapBloodscourge(Wpn).CShift = RGBA(255, 187, 4, 76);

	HexenWeapon(Wpn).DepleteAmmoEx(Wpn.FireMode);
}

//==========================================================================
//
//  WA_SnoutAttack
//
//==========================================================================

final void WA_SnoutAttack()
{
	int			damage;
	TAVec		angles;
	TVec		vforward;
	EntityEx	AimTarget;

	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = KRPGPlayerBase(Player).ReadyWeapon;

	damage = (3 + (P_Random() & 3)) * KRPGPlayerBase(Player).strength / 10;
	angles = Angles;
	AimTarget = AimLineAttack(vforward, angles, MELEERANGE);
	LineSpecialLevelInfo(Level).bPuffSpawned = false;
	LineAttack(vforward, MELEERANGE, damage, SnoutPuff);
	PlaySound((P_Random() & 1) ? 'PigActive2' : 'PigActive1', CHAN_VOICE);
	if (AimTarget)
	{
		KRPGPlayerBase(Player).AdjustPlayerAngle(AimTarget);
		if (LineSpecialLevelInfo(Level).bPuffSpawned)
		{
			// Bit something
			PlaySound('PigAttack', CHAN_VOICE);
		}
	}
}
