//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id: IntelligentActor.vc 1963 2009-03-06 23:26:20Z crimsonwizard $
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//==========================================================================
//
//  A_KRPGPain
//
// [FB] This one will become handy for things that change when monsters are
//	     damaged (i.e. Set Damage skins)
//
//==========================================================================

void A_KRPGPain()
{
	::A_KRPGPain();
	
	// [FB] Take the chance to get berserk
	if (bCanBerserk && (Health < (default.Health >> 1)) &&
		((1 + (P_Random() & 7)) > 5) && !GetCondition(ACSpellBerserk))
	{
		// [CW] TODO: this should be done by monster using spell item instead
		CastCondition(
			ACSpellBerserk,				// condition class
			false,						// not accumulative
			self,						// instigator (caster)
			1,							// spell level (should be calculated somehow?)
										// duration
			(1.0 + itof(P_Random() & 7) * 80.0) / 35.0 // FIXME: Should we determine it somehow else??
			);
	}
}

//==========================================================================
//
//	A_AttackLeap
//
//	Thrust towards Target.
//
//==========================================================================

final void A_AttackLeap(float ForceXY, float ForceZ)
{
	if (Target)
	{
		A_FaceTarget();
		Thrust(Angles.yaw, ForceXY);
		Velocity.z += ForceZ * 35.0;
	}
}

//==========================================================================
//
//	FireDemonSpecialMove
//
//==========================================================================

final bool FireDemonSpecialMove()
{
	float dist;
	float ang;

	// Float up and down
	float savedz = Origin.z;
	Origin.z += Level.Game.FloatBobOffsets[Special1];
	if (!TestMobjZ())
	{
		Origin.z = savedz;
	}
	Special1 = (Special1 + 2) & 63;

	// Insure it stays above certain height
	if (Origin.z < FloorZ + 64.0)
	{
		savedz = Origin.z;
		Origin.z += 2.0;
		if (!TestMobjZ())
		{
			Origin.z = savedz;
		}
	}

	if (true /*!Evade */) // FIXME
	{
		if (Origin.z - pDestination->z > Height * 8.0)
		{
			Velocity.z -= 2.5;
		}
	}

	// Strafe
	if (Special2 > 0)
	{
		Special2--;
	}
	else if (Target)
	{
		Special2 = 0;
		Velocity.x = 0.0;
		Velocity.y = 0.0;
		dist = DistTo(Target);
		if (dist < FIREDEMON_ATTACK_RANGE)
		{
			if (P_Random() < 30)
			{
				ang = atan2(Target.Origin.y - Origin.y,
					Target.Origin.x - Origin.x);
				if (P_Random() < 128)
					ang = AngleMod360(ang + 90.0);
				else
					ang = AngleMod360(ang - 90.0);
				Velocity.x = 8.0 * cos(ang) * 35.0;
				Velocity.y = 8.0 * sin(ang) * 35.0;
				Special2 = 3;	// strafe time
			}
		}

		return true;
	}

	return false;
}

//==========================================================================
//
//  A_KRPGIceGuyLook
//
//==========================================================================

final void A_KRPGIceGuyLook()
{
	float dist;
	float an;
	class<Actor> tp;

	A_LookAI();
	if (Random() < 0.25)
	{
		dist = Radius * (Random() * 2.0 - 1.0);
		an = AngleMod360(Angles.yaw + 90.0);

		if (Random() < 0.5)
			tp = IceGuyWisp2;
		else
			tp = IceGuyWisp1;
		Spawn(tp, Origin + vector(dist * cos(an), dist * sin(an), 60.0));
	}
}

//==========================================================================
//
//  A_KRPGIceGuyChase
//
//==========================================================================

final void A_KRPGIceGuyChase()
{
	float dist;
	float an;
	Actor A;
	class<Actor> tp;

	A_ChaseAI();
	if (Random() < 0.5)
	{
		dist = Radius * (Random() * 2.0 - 1.0);
		an = AngleMod360(Angles.yaw + 90.0);

		if (Random() < 0.5)
			tp = IceGuyWisp2;
		else
			tp = IceGuyWisp1;
		A = Spawn(tp, Origin + vector(dist * cos(an), dist * sin(an), 60.0));
		if (A)
		{
			A.Velocity = Velocity;
			A.Target = self;
		}
	}
}

//==========================================================================
//
//	A_SerpentChase
//
//==========================================================================

bool A_SerpentChase(bool FastChase, /*state AMeleeState, state AMissileState,*/
				bool NightmareFast, bool PlayActive, bool DontMove)
{
	return DoMove(false /*FastChase */, /*AMeleeState, none, */
		true /*NightmareFast */, PlayActive, DontMove);
}

//==========================================================================
//
//	A_SerpentChaseAI
//
//==========================================================================

final void A_SerpentChaseAI()
{
	DoMoveR = A_SerpentChase;
	MissileState = none;
	
	A_ChaseAI();
	
	MissileState = FindState('Missile');
}

//==========================================================================
//
//  A_SerpentWalk
//
//  Similar to A_Chase, only has a hardcoded entering of meleestate.
//
//==========================================================================

bool A_SerpentWalk(bool FastChase, /*state AMeleeState, state AMissileState,*/
				bool NightmareFast, bool PlayActive, bool DontMove)
{
	return DoMove(false /*FastChase */, /*FindState('Attack'), none, */
		true /*NightmareFast */, false /*PlayActive */, DontMove);
}

//==========================================================================
//
//	A_SerpentWalkAI
//
//==========================================================================

final void A_SerpentWalkAI()
{
	DoMoveR = A_SerpentWalk;
	MeleeState = FindState('Attack');
	MissileState = none;
	WalkState = none;
	
	A_ChaseAI();
	
	MeleeState = FindState('Melee');
	MissileState = FindState('Missile');
	WalkState = FindState('Walk');
}

//==========================================================================
//
//  A_SerpentCheckForAttackAI
//
//==========================================================================

final void A_SerpentCheckForAttackAI()
{
	if (!Target || !CanSense(Target))
	{
		return;
	}
	if (MissileState && !CheckMeleeRange())
	{
		SetState(FindState('Attack'));
		return;
	}
	if (CheckMeleeRange2())
	{
		SetState(FindState('Walk'));
	}
	else if (CheckMeleeRange())
	{
		if (P_Random() < 32)
		{
			SetState(FindState('Walk'));
		}
		else
		{
			SetState(FindState('Attack'));
		}
	}
}

//==========================================================================
//
//  A_SerpentHumpDecideAI
//
//  Decided whether to hump up or to missile attack
//
//==========================================================================

final void A_SerpentHumpDecideAI()
{
	// Since now Serpent may be in chase state without a Target, it needs
	// this extra check to prevent shooting dead enemies (and potential
	// crashes)
	if (Target)
	{
		::A_SerpentHumpDecide();
	}
}

//==========================================================================
//
//	A_BishopSpawnBlurAI
//
//==========================================================================

final void A_BishopSpawnBlurAI()
{
	Actor blur;

	if (!--Special1 || Special1 < 0)
	{
		Velocity.x = 0.0;
		Velocity.y = 0.0;
		
		if (P_Random() > 96 || !ConsiderAttack())
		{
			SetState(SeeState);
		}
	}
	blur = Spawn(BishopBlur, Origin);
	if (blur)
	{
		blur.Angles = Angles;
	}
}

//==========================================================================
//
//  A_WraithChaseAI
//
//==========================================================================

final void A_WraithChaseAI()
{
	float savedz = Origin.z;

	Origin.z += Level.Game.FloatBobOffsets[Special1];
	if (!TestMobjZ())
	{
		Origin.z = savedz;
	}
	Special1 = (Special1 + 2) & 63;
//  if (actor->FloorClip > 0)
//  {
//      SetState(S_WRAITH_RAISE2);
//      return;
//  }
	A_ChaseAI();
	A_WraithFX4();
}

//==========================================================================
//
//	DragonSeekAI
//
//==========================================================================

final void DragonSeekAI(float thresh, float turnMax)
{
	int dir;
	float dist;
	float delta;
	EntityEx tempTarget;
	int i;
	int bestArg;
	float bestAngle;
	float angleToSpot, angleToTarget;
	Actor A;
	EntityEx oldTarget;

	tempTarget = Tracer;

	if (!tempTarget)
	{
		return;
	}
	dir = FaceActor(Actor(tempTarget), delta);

	if (delta > thresh)
	{
		delta /= 2.0;

		if (delta > turnMax)
		{
			delta = turnMax;
		}
	}

	if (dir)
	{
		// Turn clockwise
		Angles.yaw = AngleMod360(Angles.yaw + delta);
	}
	else
	{
		// Turn counter clockwise
		Angles.yaw = AngleMod360(Angles.yaw - delta);
	}
	Velocity.x = Speed * cos(Angles.yaw);
	Velocity.y = Speed * sin(Angles.yaw);

	if (Origin.z + Height < tempTarget.Origin.z ||
		tempTarget.Origin.z + tempTarget.Height < Origin.z)
	{
		dist = DistTo2(tempTarget);
		dist = dist / Speed;

		if (dist < 1.0)
		{
			dist = 1.0;
		}
		Velocity.z = (tempTarget.Origin.z - Origin.z) / dist;
	}
	else
	{
		dist = DistTo2(tempTarget);
		dist = dist / Speed;
	}

	if (/*tempTarget.bShootable*/ Actor(tempTarget).CanBeAttacked() && P_Random() < 64)
	{
		// attack the destination mobj if it's attackable
		if (fabs(AngleMod180(Angles.yaw -
			atan2(tempTarget.Origin.y - Origin.y,
			tempTarget.Origin.x - Origin.x))) < 45.0 / 2.0)
		{
			oldTarget = Target;
			Target = tempTarget;
			SetDangerSource(Actor(Target));

			if (CheckMeleeRange())
			{
				Target.Damage(self, self, HITDICE(10));
				PlaySound('DragonAttack', CHAN_WEAPON);
			}
			else if (P_Random() < 128 && CheckMissileRange())
			{
				SpawnMissile(tempTarget, DragonFireball);
				PlaySound('DragonAttack', CHAN_WEAPON);
			}
			Target = oldTarget;
			SetDangerSource(Actor(Target));
		}
	}

	if (dist * 35.0 < 4.0)
	{
		// Hit the target thing
		if (tempTarget && P_Random() < 200)
		{
			bestArg = -1;
			bestAngle = 360.0;
			angleToTarget = atan2(tempTarget.Origin.y - Origin.y,
				tempTarget.Origin.x - Origin.x);
			for (i = 0; i < 5; i++)
			{
				if (!tempTarget.Args[i])
				{
					continue;
				}
				A = Actor(Level.FindMobjFromTID(tempTarget.Args[i], none));
				angleToSpot = atan2(A.Origin.y - Origin.y,
					A.Origin.x - Origin.x);
				delta = fabs(AngleMod180(angleToSpot - angleToTarget));

				if (delta < bestAngle)
				{
					bestAngle = delta;
					bestArg = i;
				}
			}
			if (bestArg != -1)
			{
				Tracer = Actor(Level.FindMobjFromTID(tempTarget.Args[bestArg],
					none));
			}
		}
		else
		{
			int count;
			// [FB] Don't loop forever!
			do
			{
				count++;
				i = (P_Random() >> 2) % 5;
			}
			while (!tempTarget.Args[i] || count > MONS_LOOK_LIMIT * 4);

			if (count <= MONS_LOOK_LIMIT * 4)
			{
				Tracer = Actor(Level.FindMobjFromTID(tempTarget.Args[i], none));
			}
		}
	}

	if (!bNeverUseRoutes)
		DoRoute();

	if (ITarget.Thing)
		pDestination = &ITarget.Thing.Origin;
	else if (ITarget.Action)
		pDestination = &ITarget.Pos;
	else if (NRoute)
		pDestination = &NRoute.Nodes[NRouteNodeIndex].Origin;
	else if (TaskLeader)
		pDestination = &TaskLeader.Origin;
	else
		pDestination = NULL;

	if ((pDestination || Target) && DoMoveR)
	{
		DoMoveR(false, /*MeleeState, MissileState,*/ false, false, ReactionTime > XLevel.Time);
	}
}

//==========================================================================
//
//	A_DragonFlightAI
//
//==========================================================================

final void A_DragonFlightAI()
{
	float angle;

	if (Target)
	{
		SetDangerSource(Actor(Target));

		if (/*!Target.bShootable*/ !Actor(Target).CanBeAttacked())
		{
			// enemy died
			// [CW] CHECKME!! perhaps needs fixing
			Target = none;
			bHateTarget = false;
			SetDangerSource(none);
			bInCombat = false;
			return;
		}

		if (CanSense(Actor(Target)) && P_Random() < 200)
		{
			angle = atan2(Target.Origin.y - Origin.y, Target.Origin.x - Origin.x);

			if (fabs(AngleMod180(Angles.yaw - angle)) < 45.0 / 2.0
				&& CheckMeleeRange())
			{
				Target.Damage(self, self, HITDICE(8));
				PlaySound('DragonAttack', CHAN_WEAPON);
			}
			else if (fabs(AngleMod180(Angles.yaw - angle)) <= 20.0)
			{
				SetState(MissileState);
				PlaySound('DragonAttack', CHAN_WEAPON);
			}
		}
	}
	else
	{
		if (LookForEnemy(true))
		{
			SetDangerSource(Actor(Target));
		}
		else
		{
			SetDangerSource(none);
		}
	}
	DragonSeekAI(4.0, 8.0);

	ConsiderChangeState();
}

//==========================================================================
//
//	A_DragonFlapAI
//
//==========================================================================

final void A_DragonFlapAI()
{
	A_DragonFlightAI();

	if (P_Random() < 240)
	{
		PlaySound('DragonWingflap', CHAN_BODY);
	}
	else
	{
		PlaySound(ActiveSound, CHAN_VOICE);
	}
}

//==========================================================================
//
//	A_MartyrFlight
//
//==========================================================================

final void A_MartyrFlight()
{
	A_SpawnItem(FireDemonMartyrFX2);
	A_JumpIfCloser(128.0, FindState('Explode'));
}


defaultproperties
{
	Behavior.ThreatReaction			= THREATREACTION__DETECT;
	Behavior.ThreatVicinity			= DEFAULT_THREAT_VICINITY;

	bCanJoinTaskGroup				= true;
	bCanBeTaskLeader				= true;

	Behavior.RouteRebuildRate		= 512;
	Behavior.RouteRebuildRateLow	= 1024;
	Behavior.LostFTargetTolerance	= 32;
	Behavior.FTargetPosChangeLimit	= 512.0;

	bNeverTarget = false;

	bNonScalable = false;
	bCombatant = true;
}
