//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id$
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class ActorTask : LinkedObject
	abstract;


enum
{
	ORDER__NONE,
	ORDER__STANDSTILL,			// stand idle, looking in one direction
	ORDER__STANDLOOK180,		// stand idle, turning left and right consequently
	ORDER__STANDLOOK360,		// stand idle, looking 360 degrees around
	ORDER__WALKTO,				// walk to a point on map
	ORDER__WALKPATH,			// walk along a path of nodes
	ORDER__PATROL2PT,			// walk continiously between two points
	ORDER__PATROLPATH,			// walk continiously along the path, first fw, then bw
	ORDER__PATROLLOOP,			// walk continiously along the path, interpreting it as a loop
	ORDER__FOLLOW,				// follow actor
	ORDER__ATTACKALL,			// havoc! attack everyone on map
	ORDER__ATTACKTHING,			// attack single target until its death
	ORDER__EVADETHING,			// evade thing while it is alive
	ORDER__EVADEPOS,			// stay away from some position
	ORDER__EVADEDIR,			// stay away from something in defined direction
	ORDER__WANDER,				// just wander around
	ORDER__USELINE,				// come closer and 'use' line
	ORDER__USETHING				// come closer and 'use' thing
};

bool						bSituative;

int							Order;
int							Priority;	// 0 is highest (unquestioned order)

float						Duration;	// 0.0 means unlimited duration
float						Time;

IntelligentActor::generic_target_t		Target;

bool									bTargetMustBeAlive;

TVec									NextPos;
bool									Direction;	// 0 = fw; 1 = bw
float									Radius;

bool									bTurnCW;
float									OriginalAngle;

IntelligentActor			Owner;


//==========================================================================
//
//	ConsiderCompleted
//
//==========================================================================

bool ConsiderCompleted()
{
	//
	// Completion by duration exceeded
	//
	if (Duration > 0.0 && Time <= 0.0)
	{
		switch (Order)
		{
		case ORDER__ATTACKALL:
			Owner.bIgnoreTeams = Owner.default.bIgnoreTeams;
			break;
		}
		return true;
	}

	bool bCompleted = false;

	//
	// Completion by reaching an aim
	//
	switch (Order)
	{
	case ORDER__NONE:
		bCompleted = true;
		break;
	case ORDER__ATTACKTHING:
	case ORDER__ATTACKALL:
	case ORDER__FOLLOW:
	case ORDER__EVADETHING:
		bCompleted = !Target.Thing || Target.Thing.IsDestroyed() || (bTargetMustBeAlive && Target.Thing.Health <= 0);
		break;
	case ORDER__WALKTO:
	case ORDER__WALKPATH:
	case ORDER__PATROL2PT:
	case ORDER__PATROLPATH:
	case ORDER__PATROLLOOP:
		bCompleted = (Length(Owner.Origin - Target.Pos) < IntelligentActor::ACCEPTABLE_WAYPOINT_VICINITY );
		break;
	case ORDER__WANDER:
		bCompleted = (Length(Owner.Origin - NextPos) < IntelligentActor::ACCEPTABLE_WAYPOINT_VICINITY );
		break;
	}

	//
	// Perform any actions necessary for completion on an objective
	//
	if (bCompleted)
	{
		switch (Order)
		{
		case ORDER__WALKPATH:
		case ORDER__PATROLPATH:
		case ORDER__PATROLLOOP:
			{
				// Execute the specials of any PatrolSpecials with the same TID
				// as the goal.
				Entity Ent;
				for (Ent = Owner.Level.FindMobjFromTID(Target.Thing.TID, none); Ent;
					Ent = Owner.Level.FindMobjFromTID(Target.Thing.TID, Ent))
				{
					if (Ent.Class != PatrolSpecial)
					{
						continue;
					}
					Owner.Level.ExecuteActionSpecial(Ent.Special, Ent.Args[0],
						Ent.Args[1], Ent.Args[2], Ent.Args[3], Ent.Args[4],
						NULL, 0, Owner);
				}

				//
				// Make actor wait some time at this point
				if (Target.Thing.IsA('KRPGPatrolPointBase'))
				{
					Owner.ReactionTime = 
						itof(Target.Thing.Args[KRPGPatrolPointBase::ARG_DELAY]) + Owner.XLevel.Time;
				}
				else if (Target.Thing.IsA('ScriptedPathNode'))
				{
					Owner.ReactionTime = 
						itof(Target.Thing.Args[ScriptedPathNode::ARG_DELAY]) + Owner.XLevel.Time;
				}
			}
			break;
		}
	}

	//
	// Some tasks need to update objective, if previous one was completed
	//
	if (bCompleted)
	{
		switch (Order)
		{
		case ORDER__ATTACKALL:
			Target.Thing = none;
			bCompleted = false;
			break;
		case ORDER__PATROL2PT:
			{
				TVec v = Target.Pos;
				Target.Pos = NextPos;
				NextPos = v;
				bCompleted = false;
				Owner.SetFinalTarget(none, Target.Pos, Target.Action);
				Owner.SetImmediateTarget(none, Target.Pos, Target.Action);
			}
			break;
		case ORDER__WALKPATH:
		case ORDER__PATROLPATH:
		case ORDER__PATROLLOOP:
			{
				PathNode PN;
				if (Direction)
					PN = PathNode(Target.Thing).BW;
				else
					PN = PathNode(Target.Thing).FW;

				if (!PN)
				{
					if (Order == ORDER__PATROLPATH)
					{
						Direction = !Direction;
						if (Direction)
							PN = PathNode(Target.Thing).BW;
						else
							PN = PathNode(Target.Thing).FW;
					}
					else if (Order == ORDER__PATROLLOOP)
					{
						if (Direction)
							PN = PathNode(Target.Thing).GetPathTail();
						else
							PN = PathNode(Target.Thing).GetPathHead();
					}
				}

				if (PN)
				{
					Target.Thing = PN;
					Target.Pos = PN.Origin;
					bCompleted = false;
					Owner.SetFinalTarget(Target.Thing,, Target.Action);
					Owner.SetImmediateTarget(Target.Thing,, Target.Action);
				}
			}
			break;
		}
	}

	return bCompleted;
}

//==========================================================================
//
//	Tick
//
//==========================================================================

void Tick(float deltaTime)
{
	if (Duration > 0.0)
	{
		Time -= deltaTime;
	}
}


defaultproperties
{
	Order = ORDER__NONE;
	Priority = 0;
	Duration = 0.0;
	Time = 0.0;
}
