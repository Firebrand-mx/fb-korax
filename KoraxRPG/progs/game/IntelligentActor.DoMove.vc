//**************************************************************************
//**
//**	    ##   ##   ####   ######     ##    ##     ##
//**	    ##  ##   ##  ##  ##   ##  ##  ##   ### ###
//**	    ## ##   ##    ## ##   ## ##    ##   #####
//**	    ######  ##    ## #####   ########   #####
//**	    ## ###   ##  ##  ## ###  ##    ##  ### ###
//**	    ##   ##   ####   ##  ### ##    ## ##     ##
//**
//**				   ######  #####   ######
//**	               ##   ## ##  ## ##    ##
//**	     		   ##   ## ##  ## ##   
//**	               #####   #####  ##  ####
//**	     		   ## ###  ##     ###   ##
//**	     		   ##  ### ##      ######
//**
//**    $Id: IntelligentActor.DoMove.vc 1963 2009-03-06 23:26:20Z crimsonwizard $
//**
//**    Copyright (C) 2004-2008 Korax Heritage Team
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//==========================================================================
//
//	[CW]
//
// IntelligentActor movement is controlled by DoMove method, which acts
// as a fully substitute for common Actor::DoChase. DoMove performs not only
// direct movement towards destination, but also various advanced techniques
// like evasive actions: strafing, backing etc.
//
// In DoMove following "anchors" are used:
// - EntityEx::Target is used as a thing which should be attacked when possible;
//   Target is set by CheckThreat method or, in traditional way, by calling
//   IsOkayToSwitchTarget from EntityEx::Damage; as the additional option, Target
//   will be set by issuing corresponding Attack order either from other parts of
//   the code or from the map script.
// - IntelligenceActor::Destination is used as a place the actor is trying to
//   reach; it may or may not point to EntityEx::Target's origin; Destination is
//   updated in A_ChaseAI every time it is called.
// - IntelligenceActor::DangerSource is used to make actor evade some particular
//   place or thing; DangerSource is set in CheckThreat method.
//
// IntelligentActor shall usually try to move towards Destination. Destination may
// be Target's origin or some other point on map (a path node, or else). If
// Destination is not defined, actor shall call common A_Wander method to simply
// roam around aimlessly. If actor has Target set, Destination is always set (in
// other words, if Destination is not set, we may be sure that there's no Target
// as well).
// Regardless of what Destination is, actor's movement may be affected by
// current situation; usually a presence of Target and/or DangerSource may change
// actor's behavior is particular way, like forcing him to perform an evasive
// action or other special move of tactical nature. Thus, some actors will try
// to keep distance with Target even though Target may be their Destination.
//
// NOTE: EntityEx::strafecount variable is used as a counter for most evasive
// actions (not only backing).
//
//==========================================================================

enum
{
	STRAFE_NONE,
	STRAFE_WALKSTRAIGHT,
	STRAFE_BACKPEDAL,
	STRAFE_SIDEMOVE,
	STRAFE_DODGE			// ?
};

float MoveDirFree;
float MoveDirZ;
int StrafeType;

//============================================================================
//
//  A_NormalDoMove
//
//============================================================================

bool A_NormalDoMove(bool FastChase, /*state AMeleeState, state AMissileState,*/
				bool NightmareFast, bool PlayActive, bool DontMove)
{
	return DoMove(false /* FastChase*/, /*AMeleeState, AMissileState,*/
		LineSpecialGameInfo(Level.Game).bNightmareFastChase /* NightmareFast*/, PlayActive, DontMove);
}

//============================================================================
//
//  A_FastDoMove
//
//============================================================================

bool A_FastDoMove(bool FastChase, /*state AMeleeState, state AMissileState,*/
				bool NightmareFast, bool PlayActive, bool DontMove)
{
	return DoMove(true /* FastChase*/, /*AMeleeState, AMissileState,*/
		true /* NightmareFast*/, PlayActive, DontMove);
}

//==========================================================================
//
//	DoMove
//
//==========================================================================

bool DoMove(bool FastChase, /*state AMeleeState, state AMissileState,*/
				bool NightmareFast, bool PlayActive, bool DontMove)
{
	float		delta;
	float		dist;
	float		ang;
	float		oldX;
	float		oldY;
	int			oldFloor;

	if (bInChase)
	{
		return false;
	}
	bInChase = true;

	//	Andy Baker's stealth monsters
	if (bStealth)
	{
		VisDir = -1;
	}

	if (ReactionCount)
	{
		ReactionCount--;
	}

	if (!IsOkayToTarget(Actor(Target)))
	{
		Target = none;
		SetDangerSource(none);
		bHateTarget = false;
		bInCombat = false;
	}

	//	Modify target threshold.
	if (Threshold)
	{
		if (!Target)
		{
			Threshold = 0;
		}
		else
		{
			Threshold--;
		}
	}

	if (NightmareFast && Level.Game.fastparm)
	{
		//	Monsters move faster in nightmare mode.
		StateTime *= 0.5;
		if (StateTime < 0.1)
		{
			StateTime = 0.1;
		}
	}

	//	Turn towards movement direction if not there yet.
	// killough 9/7/98: keep facing towards target if strafing or backing out
	if (Target && strafecount)
	{
		if (StrafeType != STRAFE_WALKSTRAIGHT)
		{
			A_FaceTarget();
		}
	}

	//	Do not attack twice in a row.
	if (bJustAttacked)
	{
		bJustAttacked = false;
		if (!IsFast() && !DontMove && !DoSpecialMove())
		{
			if (pDestination)
			{
				NewChaseDirEx(*pDestination, false);
			}
			else
			{
				A_Wander();
			}
		}
		bInChase = false;
		return true;
	}

	//
	//// Perform attack
	// [CW] An experiment to start attack states from IA::ConsiderChangeState instead -----
	// only do fast strafe here (aka boss-character strafe)
	//
	if (Target)
	{
		if (FastChase && !DontMove)
		{
			//	Strafe
			if (FastChaseStrafeCount > 0)
			{
				FastChaseStrafeCount--;
			}
			else
			{
				FastChaseStrafeCount = 0;
				Velocity.x = 0.0;
				Velocity.y = 0.0;
				dist = DistTo(Target);
				if (dist < 640.0)
				{
					if (P_Random() < 100)
					{
						ang = atan2(Target.Origin.y - Origin.y,
							Target.Origin.x - Origin.x);
						if (P_Random() < 128)
							ang = AngleMod360(ang + 90.0);
						else
							ang = AngleMod360(ang - 90.0);
						Velocity.x = 13.0 * cos(ang) * 35.0;
						Velocity.y = 13.0 * sin(ang) * 35.0;
						FastChaseStrafeCount = 3;	// strafe time
					}
				}
			}
		}

		// [CW] An experiment to start attack states from IA::ConsiderChangeState instead
		/*
		//	Check for melee attack.
		if (AMeleeState && CheckMeleeRange())
		{
			if (AttackSound)
			{
				PlaySound(AttackSound, CHAN_WEAPON);
			}
			SetState(AMeleeState);
			bInChase = false;
			return true;
		}

		//	Check for missile attack.
		if (AMissileState)
		{
			if (IsFast() || !MoveCount)
			{
				if (CheckMissileRange())
				{
					SetState(AMissileState);
					bJustAttacked = true;
					bInChase = false;
					return true;
				}
			}
		}
		*/
	}

	if (strafecount)
	{
//		print("strafecount = %i", strafecount);
		strafecount--;
	}

	//	Chase towards enemy.
	if (!DontMove && !DoSpecialMove() && (!FastChase || !FastChaseStrafeCount))
	{
		if (pDestination)
		{
			oldX = Origin.x;
			oldY = Origin.y;
			oldFloor = Sector->floor.pic;
			if (--MoveCount < 0 || !StepMoveEx())
			{
				NewChaseDirEx(*pDestination, false);
			}
			if (bCantLeaveFloorpic && Sector->floor.pic != oldFloor)
			{
				TryMove(vector(oldX, oldY, Origin.z), false);
				NewChaseDirEx(*pDestination, false);
			}
		}
		else
		{
			A_Wander();
		}
	}

	//	Make active sound.
	if (PlayActive && P_Random() < 3)
	{
		PlayActiveSound();
	}

	bInChase = false;
	return true;
}

//=============================================================================
//
// NewChaseDirEx
//
//=============================================================================

final void NewChaseDirEx(TVec Destination, optional bool bOpDir)
{	
	float deltax;
	float deltay;
	float deltaz;

	// killough 8/8/98: sometimes move away from target, keeping distance
	// Take advantage over an enemy without missiles, by keeping distance
	strafecount = 0;

	// Try to move away from a dropoff
	if (FloorZ - DropOffZ > MaxDropoffHeight &&
		Origin.z <= FloorZ && !bDropOff && !bOnMobj && !bFloat &&
		!Level.CompatDropOff)
	{
		float AvoidDeltaX;
		float AvoidDeltaY;
		// We call CheckDropoff here to determine if the
		// bounding box actually needs to be used below
		CheckDropOff(AvoidDeltaX, AvoidDeltaY);

		if (AvoidDeltaX || AvoidDeltaY)
		{
			// [Graf Zahl] I have changed TryMove to only apply this logic when
			// being called from here. bAavoidingDropoff activates the code that
			// allows monsters to move away from a dropoff. This is different from
			// MBF which requires unconditional use of the altered logic and therefore
			// forcing a massive change in the monster behavior to use this.

			// use different dropoff movement logic in TryMove
			bAvoidingDropoff = true;
			DO_NewChaseDirEx(AvoidDeltaX, AvoidDeltaY, 0.0);
			bAvoidingDropoff = false;
		
			// If moving away from dropoff, set movecount to 1 so that
			// small steps are taken to get monster away from dropoff.
			MoveCount = 1;
			return;
		}
	}

	// [CW] CHECKME not used for now
	/*
	if (bOpDir)
	{
		deltax = Origin.x - Destination.x;
		deltay = Origin.y - Destination.y;
		deltaz = Origin.z - Destination.z;
	}
	else
	{
	*/
		deltax = Destination.x - Origin.x;
		deltay = Destination.y - Origin.y;
		deltaz = Destination.z - Origin.z;
	/*
	}
	*/
	
	// Prepare a tactical move, if needed
	DO_TacticalMove(deltax, deltay, deltaz);	

	DO_NewChaseDirEx(deltax, deltay, deltaz);

	// If strafing, set movecount to strafecount so that old Doom
	// logic still works the same, except in the strafing part
	if (strafecount)
	{
		MoveCount = strafecount;
	}
}

//==========================================================================
//
//  DO_TacticalMove
//
//	[CW] Customize tactical move before calling DO_NewChaseDirEx
//
//==========================================================================

final void DO_TacticalMove(out float deltax, out float deltay, out float deltaz)
{
	if (DangerSource.Action /*&& bCanEvade*/)
	{
		StrafeType = STRAFE_NONE;
		float distToDanger = DistTo2(DangerSource.Thing);
		if (bEvadeMelee && distToDanger <= ACCEPTABLE_DANGERMELEE_VICINITY)
		{
			// Actor should evade enemy who is closing in ASAP; decide how
			if ((bCanBackpedal || MainGameInfo(Level.Game).bMonsterBacking) &&
				(MissileState || P_Random() < 128))
			{
				StrafeType = STRAFE_BACKPEDAL;
			}
			else if (bCanSidemove)
			{
				StrafeType = STRAFE_SIDEMOVE;
			}
			else
			{
				StrafeType = STRAFE_WALKSTRAIGHT;
			}
		}
		else if (MissileState)
		{
			if (bCanSidemove && P_Random() < 64)
			{
				StrafeType = STRAFE_SIDEMOVE;
			}
			else if ((bCanBackpedal || MainGameInfo(Level.Game).bMonsterBacking) && P_Random() < 64)
			{
				StrafeType = STRAFE_BACKPEDAL;
			}
		}
		else
		{
			if (bCanSidemove && P_Random() < 16)
			{
				StrafeType = STRAFE_SIDEMOVE;
			}
		}
		
		if (StrafeType)
		{
			switch (StrafeType)
			{
			case STRAFE_WALKSTRAIGHT:
			case STRAFE_BACKPEDAL:
				strafecount = P_Random() & 7 + 3;
				deltax = Origin.x - DangerSource.Thing.Origin.x;
				deltay = Origin.y - DangerSource.Thing.Origin.y;
				break;
			case STRAFE_SIDEMOVE:
				{
					strafecount = P_Random() & 3 + 3;
					TVec vec = Origin - DangerSource.Thing.Origin;
					VectorRotateAroundZ(&vec, Random() < 0.5 ? 90.0 : -90.0);
					deltax = vec.x;
					deltay = vec.y;
				}
				break;
			}
			
			if (bFloat && DangerSource.Thing.Origin.z - Origin.z <= MELEERANGE)
			{
				deltaz = Origin.z + Height * 0.5 - FloorClip -
					(DangerSource.Thing.Origin.z + DangerSource.Thing.Height * 0.5 -
					DangerSource.Thing.FloorClip);
			}
			else
			{
				deltaz = 0.0;
			}
			
			// FIXME - remove later
			//print("strafecount = %i", strafecount);
			//print("StrafeType = %i", StrafeType);
			//print("deltax = %f", deltax);
			//print("deltay = %f", deltay);
			//print("deltaz = %f", deltaz);
		}
	}
}

//==========================================================================
//
//  DO_NewChaseDirEx
//
//	[CW] an attempt to make movement free of MoveDir presets (any angle)
//
//==========================================================================

final void DO_NewChaseDirEx(float deltax, float deltay, float deltaz)
{	
	float oldang = MoveDirFree;
	float oldangz = MoveDirZ;
	MoveDirFree = atan2(deltay, deltax);
	MoveDirZ = bFloat ? atan(deltaz) : 0.0;

	if (!TryWalkEx())
	{
		//
		// TODO: CHECKME, work this out!
		// also check Z movement
		//
		if (P_Random() & 1)
		{
			MoveDirFree += 45.0 + Random() * 90.0;
		}
		else
		{
			MoveDirFree -= 45.0 + Random() * 90.0;
		}

		if (!TryWalkEx())
		{
			MoveDirFree = -oldang;
			TryWalkEx();
		}
	}

	if (!strafecount || StrafeType == STRAFE_WALKSTRAIGHT)
	{
		Angles.yaw = MoveDirFree;
	}
}

//==========================================================================
//
//  TryWalkEx
//
//  Attempts to move actor in its current (ob->moveangle) direction.
//  If blocked by either a wall or an actor returns FALSE.
//  If move is either clear of block only by a door, returns TRUE and sets.
//  If a door is in the way, an OpenDoor call is made to start it opening.
//
//==========================================================================

final bool TryWalkEx()
{
	if (!StepMoveEx())
	{
		return false;
	}
	MoveCount = P_Random() & 15;
	return true;
}

//==========================================================================
//
//  StepMoveEx
//
//  Move in the current direction, returns false if the move is blocked.
//
//  [CW] Contents used mostly from original StepMove;
//       Difference is in making MoveDir unused, use free movement angle
//       instead.
//
//==========================================================================

final bool StepMoveEx()
{
	float tryx, deltax, origx;
	float tryy, deltay, origy;
	float tryz, deltaz, origz;
	float maxmove;
	int steps;
	float xspeed;
	float yspeed;
	int i;
	bool try_ok;
	bool dropoff = false;

	line_t *ld;
	int good;

	if (bBlasted)
	{
		return true;
	}

	// Instead of yanking non-floating monsters to the ground,
	// let gravity drop them down, unless they're moving down a step.
	if (!bNoGravity && Origin.z > FloorZ && !bOnMobj)
	{
		if (Origin.z <= FloorZ + MaxStepHeight)
		{
			Origin.z = FloorZ;
		}
		else
		{
			return false;
		}
	}

	origx = Origin.x;
	origy = Origin.y;
	origz = Origin.z;
	deltax = Speed / 35.0 * cos(MoveDirFree);
	deltay = Speed / 35.0 * sin(MoveDirFree);
	deltaz = bFloat ? FloatSpeed / 35.0 * sin(MoveDirZ) : 0.0;
	tryx = origx + deltax;
	tryy = origy + deltay;
	tryz = origz + deltaz;

	// Like P_XYMovement this should do multiple moves if the step size is too large
	maxmove = Radius;
	steps = 1;

	if (maxmove > 0.0)
	{
		xspeed = fabs(deltax);
		yspeed = fabs(deltay);

		if (xspeed > yspeed)
		{
			if (xspeed > maxmove)
			{
				steps = 1 + ftoi(xspeed / maxmove);
			}
		}
		else
		{
			if (yspeed > maxmove)
			{
				steps = 1 + ftoi(yspeed / maxmove);
			}
		}
	}

	try_ok = true;
	tmtrace_t tmtrace;
	for (i = 1; i < steps; i++)
	{
		// killough 10/98: allow actors to drop off of taller ledges sometimes.
		// dropoff == 1 means only up to 128 high, and only if the target is
		// immediately on the other side of the line.

		if (bJumpDown && Target && !Target.IsFriend(self) &&
			DistTo(Target) < 144.0 && P_Random() < 235 &&
			!(tmtrace.FloorZ - tmtrace.DropOffZ > 128.0 ||
			  !Target || Target && Target.Origin.z > tmtrace.DropOffZ))
		{
			dropoff = true;
		}
		try_ok = TryMoveEx(&tmtrace, vector(origx + (deltax / itof(steps * i)), origy +
					(deltay / itof(steps * i)), tryz /*Origin.z*/), dropoff);

		if (!try_ok)
		{
			break;
		}
	}

	// killough 3/15/98: don't jump over dropoffs:
	if (try_ok)
	{
		// killough 10/98: allow actors to drop off of taller ledges sometimes.
		// dropoff == 1 means only up to 128 high, and only if the target is
		// immediately on the other side of the line.
		if (bJumpDown && Target && !Target.IsFriend(self) &&
			DistTo(Target) < 144.0 && P_Random() < 235 &&
			!(tmtrace.FloorZ - tmtrace.DropOffZ > 128.0 ||
			  !Target || Target && Target.Origin.z > tmtrace.DropOffZ))
		{
			dropoff = true;
		}
		try_ok = TryMoveEx(&tmtrace, vector(tryx, tryy, tryz /*Origin.z*/), dropoff);
	}

	if (!try_ok)
	{
		// open any specials
		if (bFloat && tmtrace.bFloatOk)
		{
			float savedz = Origin.z;

			// must adjust height
			if (Origin.z < tmtrace.FloorZ)
			{
				Origin.z += FloatSpeed * 2.0 * Level.Game.frametime;
			}
			else
			{
				Origin.z -= FloatSpeed * 2.0 * Level.Game.frametime;
			}

			// Check to make sure there's nothing in the way of the float
			if (TestMobjZ())
			{
				bInFloat = true;
				return true;
			}
			Origin.z = savedz;
		}
		if (bCanJump && !bFloat && tmtrace.bFloatOk && 
			(Origin.z <= FloorZ || bOnMobj) && !JumpTime)
		{
			if ((Target && (Target.FloorZ > (FloorZ + MaxStepHeight) && (Target.FloorZ /*tmtrace.FloorZ*/ - Origin.z <= 48.0)) ||
				Target && (Target.FloorZ - tmtrace.DropOffZ < (FloorZ + MaxStepHeight) && (Target.FloorZ - tmtrace.DropOffZ <= 48.0) /*tmtrace.FloorZ - Target.Origin.z <= 48.0)*/) /*||
				!Target*/) && (tmtrace.FloorZ - Origin.z <= 48.0 || tmtrace.FloorZ - tmtrace.DropOffZ <= 48.0))
			{
				float DeltaX;
				float DeltaY;
				TVec dir;
				TAVec ang;

				// [FB] Use CheckDropOff method to get line angles to use for jumping
				CheckDropOff(DeltaX, DeltaY);
				dir = Target.Origin - Origin;
				VectorAngles(&dir, &ang);

				if (DeltaX > 0.0)
				{
					Velocity.x += Speed / 35.0 * cos(-ang.yaw);
				}
				else
				{
					Velocity.x += Speed / 35.0 * cos(ang.yaw);
				}
				
				if (DeltaY > 0.0)
				{
					Velocity.y += Speed / 35.0 * sin(-ang.yaw);
				}
				else
				{
					Velocity.y += Speed / 35.0 * sin(ang.yaw);
				}
				Velocity.z = (JumpVelZ * 1.1) * 35.0;
				bOnMobj = false;
				JumpTime = 0.5;
				return true;
			}
		}

		if (!tmtrace.SpecHit.Num)
		{
			return false;
		}

		//MoveDir = DI_NODIR;

		// if the special is not a door that can be opened, return false
		//
		// killough 8/9/98: this is what caused monsters to get stuck in
		// doortracks, because it thought that the monster freed itself
		// by opening a door, even if it was moving towards the doortrack,
		// and not the door itself.
		//
		// killough 9/9/98: If a line blocking the monster is activated,
		// return true 90% of the time. If a line blocking the monster is
		// not activated, but some other line is, return false 90% of the
		// time. A bit of randomness is needed to ensure it's free from
		// lockups, but for most cases, it returns the correct result.
		//
		// Do NOT simply return false 1/4th of the time (causes monsters to
		// back out when they shouldn't, and creates secondary stickiness).

		good = 0;
		if (!bNoTrigger)
		{
			while (tmtrace.SpecHit.Num)
			{
				ld = tmtrace.SpecHit[tmtrace.SpecHit.Num - 1];
				tmtrace.SpecHit.Num = tmtrace.SpecHit.Num - 1;
				// if the special is not a door that can be opened
				// then return false
				if ((bCanUseWalls && LineSpecialLevelInfo(Level).ActivateLine(ld, self, 0, SPAC_Use)) ||
					(bActivatePushWall && LineSpecialLevelInfo(Level).ActivateLine(ld, self, 0, SPAC_Push)))
				{
					good |= ld == tmtrace.BlockingLine ? 1 : 2; //true;
				}
			}
		}
		return good && ((P_Random() >= 203) ^ (good & 1));
	}
	else
	{
		bInFloat = false;
	}

	if (!bFloat && !bNoGravity && !bDropOff && !bOnMobj)
	{
		if (Origin.z > FloorZ)
		{
			HitFloorType();
		}
		Origin.z = FloorZ;
	}
	return true;
}

//==========================================================================
//
//	DoSpecialMove
//
//	Returns TRUE if actor should skip normal movement
//
//==========================================================================

bool DoSpecialMove()
{
	// [CW] TODO: this is compatibility-only section; perhaps should be made in a more generic way
	if (bFireDemonSpecialMove)
	{
		return FireDemonSpecialMove();
	}
	return false;
}
